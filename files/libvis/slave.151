TITLE DS'S SLAVE

ZR=0	

A=1
B=2
C=3
D=4	;
E=5	;
F=6	;
G=7

T=10
U=11
V=12	;
W=13	;
X=14
Y=15
Z=16	

P=17

ZMD==0
PMD==1

VIDX==620	;BAT X DEVICE
VIDY==624	;BAT Y DEVICE
VIDT==630	;BAT TIME-OUT DEVICE
TTY==120	;TELETYPE
DIS==130	;340 SCOPE
IMX==574	;INPUT MULTIPLEXOR
OMX==570	;OUTPUT MULTIPLEXOR
CLR==420	;COLOR SCOPE - TIP-BREAKS
RBT==514	;ROBOT CONSOLE
PDP==20		;INTERPROCESSOR

IORSET=633550

PDCH==7		;INTERPROCESSOR CHANNEL
TTCH==7		;TTY CHANNEL
CLCH==3		;CLOCK CHANNEL
IXCH==1		;INPUT MULTIPLEXOR CHANNEL
DCCH==1		;DATA CHANNEL

LTTB==100	;LENGTH OF TTY OUT BUFFER (CHARACTER PER WORD)
LPDL==100	;LENGTH OF PDL

DDT=34000	;DDT'S STARTING ADDRESS


VISION==1	;ON TO ASSEMBLE VISION ROUTINES TOO

; UUO DEFINITIONS

PRN=001000,,0	;PRINT ASCIZ STRING
SAC=002000,,0	;SAVE ACS
UAC=003000,,0	;RESTORE ACS
STP=004000,,0	;TYPE MESSAGE AND DIE

DEFINE PRINT ARG
	PRN [ASCIZ /ARG/]
TERMIN

DEFINE STOP ARG
	STP [ASCIZ /ARG/]
TERMIN



	LOC 40+2*IXCH
	JSR IMXBK

	LOC 40+2*CLCH
	JSR CLOCK

	LOC 40+2*TTCH
	JSR TTYBK



; POT NUMBERS

;SWG,VRT,HRZ,ROL,YAW,TLT,EXT,ROT,GRP
;102,103,104,105,106,107,110,111,112

; OLD POT BOX
; 70, 71, 72, 73, 74, 75, 76, 77

; NEW POT BOX
; 36, 37, 24, 23, 25, 26, 27, 22


; CHANNELS OUT

;SWG,VRT,HRZ,ROL,YAW,TLT,EXT,ROT,GRP
;  2,  3,  4,  5,  6,  7, 10, 11, 12
LOC 100
XQ:	41.0	;FLOATING COORDINATES SUPPLIED BY DRIVER IN PDP10
YQ:	0
ZQ:	12.0
TLTQ:	0
EXTQ:	0
ROTQ:	1.5708
GRPQ:	2.0

CLBARM:	-1	;-1 TO ALLOW PDP10 TO CLOBBER XQ ETC. AND INSPECTS DLTS, 0 WHILE I'M PROCESSING
TIPBK:	0	;-1 IF TIPS ARE BROKEN
ZBK:	0	;Z AT LAST PLACE TIPS WHERE STILL BROKEN
TTYLCK:	0	;-1 IF TTY IS LOCKED
TYPOUT:	-1	;INTERPRETED AS TYPED CHARACTER, -1 OTHERWISE
TIPBTS:	0	;TIP AND WRIST BITS AT LAST BREAK
RBTSWT:	0	;PLACE ROBOT CONSOLE SWITCHES GET READ INTO
HYDLCK:	0	;-1 WHEN HYDRAULICS LOCKED
FEDBCK:	0	;-1 TO LIE TO OMX

LOC 120
SWGDS:	0	;CALCULATED DIFFERENCES (LAST OUT - FINAL DESIRED) [VLTAB-SWGO]
VRTDS:	0
HRZDS:	0
ROLDS:	0
YAWDS:	0
TLTDS:	0
EXTDS:	0
ROTDS:	0
GRPDS:	0

LOC 140
SWGDR:	0	;MEASURED DIFFERENCES (CURRENT IN - FINAL DESIRED) [SWGI-SWGO]
VRTDR:	0
HRZDR:	0
ROLDR:	0
YAWDR:	0
TLTDR:	0
EXTDR:	0
ROTDR:	0
GRPDR:	0

LOC 160
SWGOUT:	0	;OUT OF RANGE FLAGS, NORMALLY 0
VRTOUT:	0
HRZOUT:	0
ROLOUT:	0
YAWOUT:	0
TLTOUT:	0
EXTOUT:	0
ROTOUT:	0
GRPOUT:	0
LOC 200
SWGCLP:	0	;CLIPPING FLAGS, NORMALLY 0
VRTCLP:	0
HRZCLP:	0
ROLCLP:	0
YAWCLP:	0
TLTCLP:	0
EXTCLP:	0
ROTCLP:	0
GRPCLP:	0

LOC 220
MXTB2:	REPEAT 8,0	;NEW POT BOX GETS READ IN HERE (FOR USE BY PDP10 PROGRAM)

LOC 230
SWGGAN:	7000	;GAIN ON SWING SERVO
HRZGAN:	2000	;GAIN ON HORIZONTAL SERVO
SWGQ:	0	;SWING
HRZQ:	0	;HORIZONTAL
RDIMX:	-1	;READ NEW POT BOX (FOR PDP-10 PROGRAMS USE)
RDOMX:	0	;OUTPUT GOODIES TO OMX
TLTCMP:	0	;-1 TO COMPENSATE FOR TILT

LOC 240		;"ACCEPTABLE" ERRORS
SWGER:	120
VRTER:	40
HRZER:	40
ROLER:	7777
YAWER:	7777
TLTER:	60
EXTER:	40
ROTER:	120
GRPER:	400

LOC 260
MXTB:	REPEAT 12,0	;OLD POT BOX GETS READ IN HERE

SWGI:	0	;CURRENT POSITION READ (HAS TO BE DIRECTLY AFTER MXTB)
VRTI:	0
HRZI:	0
ROLI:	0
YAWI:	0
TLTI:	0
EXTI:	0
ROTI:	0
GRPI:	0

SWGO:	0	;FINAL DESIRED OUTPUT
VRTO:	0
HRZO:	0
ROLO:	0
YAWO:	0
TLTO:	0
EXTO:	0
ROTO:	0
GRPO:	0

SWGV:	0	;VELOCITY LIMITS (POT UNITS PER 60TH) OR -1
VRTV:	0
HRZV:	0
ROLV:	0
YAWV:	0
TLTV:	0
EXTV:	0
ROTV:	0
GRPV:	0

SWGA:	-1	;ACCELERATION LIMITS (POT UNITS PER 60TH PER 60TH) OR -1
VRTA:	-1
HRZA:	-1
ROLA:	-1
YAWA:	-1
TLTA:	-1
EXTA:	-1
ROTA:	-1
GRPA:	-1

SWGVO:	0	;CURRENTLY ALLOWED VELOCITY (MAGNITUDE)
VRTVO:	0
HRZVO:	0
ROLVO:	0
YAWVO:	0
TLTVO:	0
EXTVO:	0
ROTVO:	0
GRPVO:	0

SWGMN:	260	;LOWER LIMITS
VRTMN:	430
HRZMN:	100
ROLMN:	0
YAWMN:	0
TLTMN:	70
EXTMN:	1360
ROTMN:	1000
GRPMN:	500

SWGMX:	7620	;UPPER LIMITS
VRTMX:	7730
HRZMX:	7710
ROLMX:	7777
YAWMX:	7777
TLTMX:	6140
EXTMX:	4450
ROTMX:	6030
GRPMX:	7140

SWGC:	200	;CLIPPING LEVELS
VRTC:	100
HRZC:	200
ROLC:	77777
YAWC:	77777
TLTC:	200
EXTC:	200
ROTC:	200
GRPC:	400

FTAB:	REPEAT 11,0	;FINAL DESIRED - LAST OUT [SWGO-VLTAB] FROM LAST TIME WE INCREASED VELOCITY
			;GIVES US DIRECTION OF VELOCITY

VLTAB:	REPEAT 11,3777	;LAST VALUES OUTPUT TO ARM


SWGML:	3388.1	;MULTIPLICATIVE CONVERSION FACTOR FROM INCHES/RADIANS TO POT UNITS
VRTML:	-138.8
HRZML:	154.9
ROLML:	0
YAWML:	0
TLTML:	-864.5
EXTML:	-1064.0
ROTML:	-331.7
GRPML:	-921.0

SWGAD:	1856.0		;ADDITIVE CONVERSION FACTOR
VRTAD:	3904.0
HRZAD:	-4053.6
ROLAD:	0
YAWAD:	0
TLTAD:	2258.0
EXTAD:	2084.0
ROTAD:	2940.0
GRPAD:	2618.0

ANAME:	ASCIZ /SWG /
	ASCIZ /VRT /
	ASCIZ /HRZ /
	ASCIZ /ROL /
	ASCIZ /YAW /
	ASCIZ /TLT /
	ASCIZ /EXT /
	ASCIZ /ROT /
	ASCIZ /GRP /

XARM:	0	;INCHES AND RADIANS - STORAGE FOR XYARM FUNCTIONS
YARM:	0
ZARM:	0
TLTARM:	0
EXTARM:	0
ROTARM:	0
GRPARM:	0

RARM:	0	;HORIZONTAL AXIS

;MXTB1:	BLOCK 8	;SOME POT TABLE OR OTHER
;MXTBP:	0
;DISSOM:	0
;WDC:	0	;DC WORD COUNT

TIME:	0	;TIME
COUNT:	0	;TTY BUFFER COUNT
TTYOFF:	0	;-1 WHEN NOTHING WAITING IN TTY BUFFER
IMXPTR:	0
PIE:	3.1415926
NORST:	0	;NUMBER OF TIMES WRIST OPEN SEQUENTIALLY
LMOD:	-1	;0 OR >0 WHEN TIPS BROKE
NOCLP:	0	;NUMBER OF TIMES WE CLIPPED
QUITP:	0	;PDL POINTER WHEN IT OVERFLOWED
ULDBP:	0	;ASCIZ BYTE POINTER IN UUOS
NO:	0	;NUMBER ACCUMULATED FROM TTY
RBTCNS:	-1	;OUTPUT TO ROBOT CONSOLE IF -1
SAVA:	0	;SAVED A IN DISIN & REDIS
VRTBRK:	0	;VRTI WHEN TIPS BROKE - 400
TYI:	0	;LAST CHR READ FROM TTY

LG:	2.75	;OFFSET BETWEEN HORIZONTAL AND VERTICAL AXES
TLTLEN:	10.0	;ARM BOX + FINGERS FOR EXTEND = 0

OTPNTR:	LTTB-1	;TTY OUT BUFFER POINTER (INTERRUPT LEVEL)
INPNTR:	LTTB-1	;TTY OUT BUFFER POINTER (MAIN PROGRAM LEVEL)

SETL:	-7	;70 MICRO-SEC SETTLING TIME FOR IMAGE-SUCKER

SENSD:	0

SAV10:	0
SAV11:	0
SAV12:	0
SAV13:	0
SAV14:	0
SAV15:	0	
SAV16:	0

IFN VISION,[

NVDDK:	0	;LIMIT
NVDDKV:	0	;SUBSTITUTE VALUE

RSIZEA:	0	;1.0/SIZEA
TVN11:	0
TVN12:	0
TVN13:	0

DELT:	0

LSTHGH:	0		;LAST HIGH OR LOW ONE SEEN

TRANSX:	BLOCK 10	;RECORD OF TRANSITIONS FOUND
TRANSY:	BLOCK 10

LVL1:	0
LVL2:	0	;LEVELS OF TRANSITIONS

SPRDL:	0	;SPREAD ACTUALLY FOUND
STRAD:	0	;ARRAY
SZRAD:	0	;SIZE
FLTN:	0	;SIZE (FLOATING)
MXMNRT:	0	;MAX TO MIN RATIO
STRHGH:	0	;STARTED HIGH OR LOW

MXSQOF:	0.5	;MAX LINE SPREAD THAT IS REASONABLE
RATMIN:	1.6	;MINIMUM MAX TO MIN RATIO ALLOWED
FRCLVL:	0.16	;FRACTIONAL HALF DIFFERENCE IN LVL1 AND LVL2
CONTRA:	0.7	;CONTRACTION FACTOR FOR CIRCLES
EXPAN:	1.1	;EXPANSION FACTOR FOR CIRCLES


;NEW CRUFT FOR GETTING OFFSET FROM TRANSITIONS

LX1:	0	;START OF LINE 1
LY1:	0
LX2:	0	;END OF LINE 1
LY2:	0
LX3:	0	;START OF LINE 2
LY3:	0
LX4:	0	;END OF LINE 2
LY4:	0

LX21:	0	;LX2-LX1
LY21:	0	;LY2-LY1
LX43:	0	;LX4-LX3
LY43:	0	;LY4-LY3

LCRS1:	0	;LX1*LY2-LX2*LY1
LCRS2:	0	;LX3*LY4-LX4*LY3

IXS:	0	;SUM OF X FOR THREE INTERSECTIONS
IYS:	0
IX2S:	0	;SUM OF X SQUARED
IY2S:	0

XSER:	0
YSER:	0
XSSR:	0
YSSR:	0
RSER:	0
TSER:	0
NSER:	0
STSER:	0
SQTPI:	1.772
ANGL:	0
RADC:	0
MXRADC:	12000.0

MNN==200	;MAX PTS AROUND CIRCLE

XCN:	8200.0	;CENTRE OF VIDI IN FLOATING
YCN:	8200.0
VRAD:	7000.0	;RADIUS OF FIELD OF VIEW (OUT OF 8192.0)

WR3:	AWR(C)
WI3:	AWI(C)
WR4:	AWR(D)	;ADDR OF COS & SIN TABLES
WI4:	AWI(D)

FRAD:	0	;RADIUS (FLOATING)
DCEN:	0	;CENTRE (X,,Y)

RN:	0	;N	NUMBER OF POINTS ROUND CIRCLE
NN:	7	;RN-1
LN:	3	;LOG N
NO2:	4	;N/2
NO4:	2	;N/4
NO8:	1	;N/8
FLT:	8.0	;FLOATED N
TPN:	0.1	;2 PI /N

TWPI:	6.283185307

NVSCL:	1,,	;VIDI SCALE

]

PAT:		;PATCH AREA

START=1000

IFN VISION,[

LOC 750
XLKN:	0	;CENTRE OF CIRCLE (FLOATING)
YLKN:	0
NLKN:	0	;NUMBER OF POINTS (FIXED)
RLKN:	0	;RADIUS (FLOATING
OUTRNG:	0	;OUT OF RANGE FLAG
LOSTRC:	0	;LOST IT FLAG
RADCHN:	0	;-1 TO DECREASE RADIUS, 1 TO INCREASE IT

LOC 760
TVSWT:	-1	;>-1  WHILE I SCAN
VIDCON:	1	;VIDISECTOR CONO
TXORG:	0	;X ORIGIN IN LEFT HALF OF WORD
TYORG:	0	;Y ORIGIN IN LEFT-HALF OF WORD
TXINC:	0	;X INCREMENT IN LEFT-HALF OF WORD
TYINC:	0	;Y INCREMENT IN LEFT-HALF OF WORD
XCNT:TVN1:TCNT:	0	;NUMBER OF POINTS
PRCVID:	-1	;-1 TO PROCESS DATA (CORRELATION OR INVERSION)
TVCFLV:	0	;-1 TO ACT LIKE TVC RATHER THAN LINE-SCAN
NONDIM:	0	;NUMBER OF NON-DIMCUTOFF POINTS
DSKGEN:	0	;-1 TO PRODUCE PACKED DISK DATA
YCNT:	0	;IN VPATCH, NUMBER OF LINES TO BE SCANNED

LOC 1000
ROWC:ROWCI:VIDOUT:		;PLACE TO STUFF IMAGE DISSECTOR OUTPUT

LOC 3000
ROWCG:ROWCS:VIDPRC:		;PLACE FOR PROCESSED DATA (ALSO X,,Y IF TVCFLV=-1)

LOC 5000
DSKSTF:		;PACKED DATA TO BE OUTPUT TO DISK

START=6000

]
LOC START

GO:	CONO 420,400000	;SET DEVICE 420 FOR ROBOT SWITCHES
	CONO IORSET+CLCH	;START OF MAIN PROGRAM
	MOVE A,[JSR UUOH]
	MOVEM A,41	;SET UP UUO LOCATION MUNGED BY LOADER
	MOVE P,[-LPDL,,PDL-1]
	CONO TTY,TTCH	;ASSIGN TTY PIA
	CONO PDP,PDCH	;ASSIGN INTERPROCESSOR INTERRUPT PIA
	CONO IMX,0
	CONO OMX,0
	SETZM TIME	;RESET TIME
	SETZM COUNT	;RESET TTY OUT BUFFER COUNTER
	SETOM TTYOFF
	SETZM RDOMX
	SETZM HYDLCK
	SETZM TIPBK
	SETOM TTYLCK
;	MOVE A,[-1,,MXTB-1]	;FOR OLD IMPXR RUTS
;	MOVEM A,MXTBP
	MOVEI A,LTTB-1
	MOVEM A,OTPNTR	;SET UP TTY BUFFER POINTERS
	MOVEM A,INPNTR
	MOVE A,[-8,,MXTB-1]
	MOVEM A,IMXPTR

	CONO PI,12333	;ENABLE INTERRUPTS

	CONI IMX,A
	TLNN A,400000
	PRINT _PDP10 HAS IMX_
	PUSHJ P,TTYW
	TRNE A,100
	PRINT _IMX IN TEST MODE_
	PUSHJ P,TTYW
	CONI OMX,A
	TLNN A,400000
	PRINT _PDP10 HAS OMX_
	PUSHJ P,TTYW
	CONI VIDX,A
	SKIPA	;	TLNN A,400000
	PRINT _PDP10 HAS VIDI-SECTOR_
	PUSHJ P,TTYW

SLAVE:	MOVE P,[-LPDL,,PDL-1]	;RUN ARM AND EYE FROM PDP10 DRIVER
	PUSHJ P,IARM	;SET UP VELOCITY AND ACCELERATION LIMITS

RESLV:	SKIPL CLBARM
	PUSHJ P,SLVXYZ	;TIME TO MOVE ARM

	IFN VISION,[
	SKIPL TVSWT
	PUSHJ P,TVSCAN	;TIME TO DO A TVSCAN
	]

	JRST RESLV

IDL:	JRST IDL

SLVXYZ:	CONI OMX,A
	TLNN A,400000
	JRST 4,.+1	;PDP10 HAS OUTPUT MULTIPLEXOR
	MOVE A,XQ
	MOVE B,YQ
	MOVE C,ZQ
	MOVE D,TLTQ
	MOVE E,EXTQ
	MOVE F,ROTQ
	MOVE G,GRPQ
	SKIPN TIPBK	;NO POINT IN RUNNING XYARM IF TIPS BROKE
	PUSHJ P,XYARM
	SETOM RDOMX	;SWITCH ON OUTPUT TO ARM
	MOVE A,TIME
	CAML A,TIME	;WAIT FOR INTERUPT ROUTINES TO HAVE RUN ONCE
	JRST .-1
	SETOM CLBARM	;UNLOCK
	JRST PDPINT

IFN VISION,[

NBHD==4
SIZEA==15.
SIZEA1==SIZEA+NBHD-2

TVSCAN:	CONI VIDX,A
	SKIPA	;	TLNN A,400000
	JRST 4,.+1	;PDP10 HAS VIDI-SECTOR
	MOVE A,TVCFLV	;DISPATCH ON TVCFLV VALUE
	JRST @.+1(A)
	VSCANL		;TVCFLV=0 => LINE SCAN
	TVCSCN		;TVCFLV=1 => TVC BEHAVIOR
	TCIRC		;TVCFLV=2 => SCAN A CIRCLE
	TRCK		;TVCFLV=3 => ONE TRACKING STEP
	SEART		;TVCFLV=4 => SEARCH
	VPATCH		;TVCFLV=5 => SCAN A PATCH

VSCANL:	PUSHJ P,NVCALC
	PUSHJ P,LINSCN	;SCAN A LINE
	SKIPE DSKGEN
	PUSHJ P,DSKPCK	;PACK IN FORMAY FOR 'CAMERA'

TVDONE:	SETOM TVSWT	;INDICATE DONE
	DATAO 20,[200000,,0]	;DEASSIGN VIDI
	JRST PDPINT	;SEND INTERRUPT TO PDP-10

LINSCN:	PUSH P,F
	MOVE F,TCNT
	PUSH P,TXORG	;LINE SCAN
	PUSH P,TYORG
	PUSHJ P,SETL1	;MOVE DEFLECTION
	DATAO VIDT,[-40]
	PUSHJ P,NVCALD
	SKIPE PRCVID
	PUSHJ P,NVCALE
	SOS F
TVLOOP:	MOVE C,TXINC
	ADDM C,TXORG	;INCREMENT X
	MOVE D,TYINC
	ADDM D,TYORG	;INCREMENT Y
	PUSHJ P,SETL2	;WAIT FOR LAST POINT TO BE READ
	PUSHJ P,SETL1	;OUTPUT DEFLECTION FOR NEXT POINT
	DATAO VIDT,SETL	;START UP VIDI-PROCESSOR
	PUSHJ P,FLXVID	;PROCESS AND STORE AWAY LAST POINT
	SOJG F,TVLOOP
	PUSHJ P,SETL2
	PUSHJ P,FLXVID
	POP P,TYORG
	POP P,TXORG
	POP P,F
	SKIPE PRCVID
	PUSHJ P,CORRA0	;PERFORM CORRELATIONS
	JRST SETL1	;DEFLECT BACK TO FIRST POINT

VPATCH:	MOVE X,YCNT
	MOVEI A,VIDPRC-1
	HRLI A,1100	;9BIT BYTES
	MOVEM A,DSKBUF
	PUSHJ P,NVCALC
VPAT1:	SETZM E	;RESET INDEX TO VIDOUT
	PUSHJ P,LINSCN
	MOVE A,TXINC
	ADDM A,TYORG
	MOVE A,TYINC
	ADDM A,TXORG
	SKIPE DSKGEN
	PUSHJ P,VPAT2
	SOJG X,VPAT1
	JRST TVDONE

VPAT2:	MOVN A,XCNT
	HRLZS A	;AOBJN POINTER
	MOVE B,DSKBUF
	PUSHJ P,DSKPC1
	MOVEM B,DSKBUF	;SAVE BYTE POINTER
	POPJ P,

DSKPCK:	MOVE A,XCNT
	MOVEI B,DSKSTF
DSKPK:	SOS B	;A=#, B=BUFFER
	HRLI B,1100	;9 BIT BYTES
	MOVNS A
	HRLZS A
DSKPC1:	HRRZ C,VIDOUT(A)
	SUBI C,201
	IDPB C,B
	AOBJN A,DSKPC1
	POPJ P,

DSKBUF:	0

SETL1:	HLRZ C,TXORG	;OUTPUT X AND Y
	HLRZ D,TYORG
	DATAO VIDX,C
	DATAO VIDY,D
	CONO VIDX,@VIDCON
	POPJ P,

SETL2:	CONSZ VIDX,20000	;AWAIT  VIDI DONE
	JRST .-1
	CONSO VIDX,1000
	JRST .-1
	DATAI VIDX,A
	CONI VIDX,B
	LSH B,-13
	POPJ P,

FLXVID:	TRNE B,3	;PROCESS THE VALUE READ
	JRST TVB2	;DCF OR OVF ON
	MOVE B,A
	ANDI B,1777
	CAMG B,NVDDK	;TOO DARK
	AOSA NONDIM
TVB2:	MOVE A,NVDDKV	;SUBSTITUTE STANDARD VALUE
	MOVEM A,VIDOUT(E)	;STORE AWAY
	AOS E
	POPJ P,

NVCALC:	SETZM E
	MOVE A,TCNT	;CALCULATE DIM CUT OFF AND SUBSTITUTE
	CAILE A,2000
	MOVEI A,2000	;LIMIT IT
	MOVEM A,TCNT
	LDB B,[020300,,VIDCON]	;DCL
	LDB A,[000200,,VIDCON]	;CFL
	JUMPE B,TVCO3	;DCL=0
	CAIN B,7
	JRST TVCO6	;DCL=7
	IMUL B,[-100]
TVCO4:	ADDI B,1300
TVCO7:	MOVEM B,NVDDK	;CUTOFF VALUE

	MOVE A,[1.0]
	DPB B,[001200,,A]
	LSH B,-6
	DPB B,[330400,,A]
	TLO A,2
	MOVEM A,NVDDKV	;STANDARD SUBSTITUTE VALUE
	POPJ P,

TVCO3:	CAIN A,3
	MOVNI B,100	;CFL=3,DCL=0
	JRST TVCO4

TVCO6:	IMUL A,[-200]	;DCL=7 OVERFLOW ONLY
	MOVEI B,2000(A)
	JRST TVCO7

NVCALD:	MOVE A,TXINC	;CALCULATE SETTLING TIME NEEDED
	CAMGE A,TYINC
	MOVE A,TYINC
	HLRZS A
	CAIL A,1000
	JRST TVB5
	MOVNI B,4
	JRST TVB6

TVB5:	TLC A,232000
	FAD A,A
	LDB A,[330400,,A]
	MOVE B,SETLL-10.(A)
TVB6:	MOVEM B,SETL	;SETTLING TIME
	POPJ P,

NVCALE:	MOVEI A,SIZEA	;CALCULATE CORRELATION FACTORS
	PUSHJ P,FFLOAT
	MOVE B,[1.0]
	FDVR B,A
	MOVEM B,RSIZEA
	MOVE A,TVN1
	SOS A
	MOVEM A,TVN11
	SUBI A,SIZEA1
	MOVEM A,TVN12
	SUBI A,NBHD
	MOVEM A,TVN13
	SETZM NONDIM
	POPJ P,

SETV1:	HLRZ C,VIDPRC+1(E)	;OUTPUT X AND Y
	HRRZ D,VIDPRC+1(E)
	DATAO VIDX,C
	DATAO VIDY,D
	CONO VIDX,@VIDCON
	POPJ P,

TVCSCN:	PUSHJ P,TVCSUC
	JRST TVDONE

TVCSUC:	SETOM E		;SCAN AT GIVEN COORDINATES
	PUSHJ P,SETV1
	DATAO VIDT,[-40]
	PUSHJ P,NVCALC
	MOVNI A,4
	MOVEM A,SETL
	SOS TCNT
TVCLOP:	PUSHJ P,SETL2	;WAIT FOR LAST POINT TO BE READ
	PUSHJ P,SETV1	;OUTPUT DEFLECTION FOR NEXT POINT
	DATAO VIDT,SETL	;START IMAGE DISSECTOR
	PUSHJ P,FLXVID	;PROCESS AND STORE AWAY LAST POINT
	CAMGE E,TCNT
	JRST TVCLOP
	AOS TCNT
	PUSHJ P,SETL2
	PUSHJ P,FLXVID
	SKIPN PRCVID
	POPJ P,
	MOVE E,TCNT
	SOS E
	MOVE B,[1024.0]
	FDVRM B,VIDOUT(E)	;INVERT TO GET A VALUE PROPORTIONAL TO INTENSITY
	SOJGE E,.-1
	POPJ P,

;ROWCI IS NOW (LOCAL AVG [NBHD] LEFT)-(LOCAL AVG [NBHD] RIGHT) OF INTENSITY
;ROWCS IS NOW (LOCAL AVG [NBHD] LEFT)-(LOCAL AVG [NBHD] RIGHT) OF INTENSITY - (LOCAL AVG [SIZEA] OF INTENSITY)

;ENTRIES ORIGINALLY RAN FROM 0 TO TVN1-1
;NOW RUN FROM NBHD*2 TO TVN1+1-SIZEA

SAVWRK:	SETZM ROWC
	MOVE A,[ROWC,,ROWC+1]
	MOVE B,TVN1
	BLT A,ROWC-1(B)
	SETZM ROWCG
	MOVE A,[ROWCG,,ROWCG+1]
	MOVE B,TVN1
	BLT A,ROWCG-1(B)
	POPJ P,

;COMPUTE G=I-IBAR
CORRA0:	SKIPN NONDIM
	JRST SAVWRK	;ALL DIM-CUT-OFF
	MOVE A,TVN11	;COUNT-1 ... FIRST THING CALLED IN VPRED1
	HRLI T,233000
	MOVEI B,0
	MOVEI D,SIZEA-1	;WIDTH LOCAL AVG
CORRA1:	HLLM T,ROWC(A)	;CONVERT FIXED LEFT PART TO UNNORM FLT PT
	FADR B,ROWC(A)	;COLLECT MOVING AVG
	SOS A
	SOJG D,CORRA1	;GO UNTIL HAVE COLLECTED ONE FULL MOVING AVG

CORRA2:	HLLM T,ROWC(A)	;LOOP
	FADR B,ROWC(A)	;ADD NEW ELEMENT TO AVG
	MOVN C,B
	FMPR C,RSIZEA	;RECIPROCAL SIZEA
	FADR C,ROWC+SIZEA/2(A)	;CENTER ELEMENT
	MOVEM C,ROWCG(A)	;ROWCG(A)=ROWC(A+SIZEA/2)-S(ROWC(A)-ROWC(A+SIZEA))
	FSBR B,ROWC+SIZEA-1(A)	;FLUSH OLD ELEMENT FROM AVG
	SOJGE A,CORRA2

;COMPUTE SUM ON L-R AND INTENSITY
	SETZB B,C	;?
	MOVE A,TVN12
	ADDI A,NBHD-1	;?
	HRLI T,233000
	MOVEI D,NBHD-1
CORRA3:	FADR B,ROWCG(A)
	HRR T,ROWC+SIZEA/2(A)
	FADR C,T
	SOS A
	SOJG D,CORRA3

CORRA4:	FADR B,ROWCG(A)		;ADD TO HEAD
	MOVEM B,ROWCS+NBHD(A)	;ROWCS(A+NBHD)=S(ROWCG(A)-ROWCG(A+NBHD))
	FSBR B,ROWCG+NBHD-1(A)
	HRR T,ROWC+SIZEA/2(A)
	FADR C,T
	HLLM C,ROWCI+NBHD(A)	;L.ROWCI(A+NBHD)=S(ROWC(A+SIZEA/2)-ROWC(A+SIZEA/2+NBHD))
	HRR T,ROWC+NBHD-1+SIZEA/2(A)
	FSBR C,T
	SOJGE A,CORRA4

;COMPUTE L-R
	MOVE A,TVN13	;COUNT
CORRA5:	MOVE B,ROWCS+NBHD(A)
	FSBR B,ROWCS+NBHD*2(A)
	MOVEM B,ROWCS+NBHD*2(A)	;ROWCS(A+NBHD*2)=ROWCS(A+NBHD)-ROWCS(A+NBHD*2)
	MOVE C,ROWCI+NBHD(A)
	FSBR C,ROWCI+NBHD*2(A)
	MOVEM C,ROWCI+NBHD*2(A)	;ROWCI(A+NBHD*2)=ROWCI(A+NBHD)-ROWCI(A+NBHD*2)
	SOJGE A,CORRA5

	POPJ P,
;ALL THIS IS PIE-TRACKING CRUFT

;NEXT SETS UP NUMEROUS PARAMETERS DEPENDING ON N

SETNP:	MOVEM A,RN
	SOS A
	MOVEM A,NN
	AOS A
	LSH A,-1
	MOVEM A,NO2
	LSH A,-1
	MOVEM A,NO4
	LSH A,-1
	MOVEM A,NO8
	MOVE A,RN
	PUSHJ P,FFLOAT
	MOVEM A,FLT
	POPJ P,

INITS:	SETZM E		;INITIALISE TABLES
LOGNLP: AOS E
	ASH A,-1
	JUMPN A,LOGNLP	;FIND LOG<N>
	SOS E
	MOVEM E,LN	;GET LN
	MOVEI A,1
	ASH A,(E)	;N=2**LN

	PUSHJ P,SETNP	;GET N/2, N/4, N/8 ETC.
	HRLI F,C
	HRLI G,E

;GENERATE COS AND SIN TABLES
	MOVE A,TWPI
	FDVR A,FLT
	MOVEM A,TPN	;TPN=TWPI/FLT

	HRRZ F,WR4
	HRRZ G,WI4
	SETZB C,D

SSINT:		;FIRST EIGHT OF IT
	MOVE A,C
	PUSHJ P,SIN
	MOVEM A,@WI4
	MOVE A,C
	PUSHJ P,COS
	MOVEM A,@WR4
	FAD C,TPN
	AOS D
	CAMG D,NO8
	JRST SSINT

	MOVE D,NO8
	PUSHJ P,SETWIR
NO8AD:	MOVE A,@WR4
	MOVEM A,(C)
	MOVE A,@WI4
	MOVEM A,(B)
	AOS B
	AOS C
	SOJGE D,NO8AD
;COPY ONCE TO MAKE A QUADRANT

	MOVE D,NO4
	PUSHJ P,SETWIR
NO4AD:	MOVN A,@WR4
	MOVEM A,(B)
	MOVE A,@WI4
	MOVEM A,(C)
	AOS B
	AOS C
	SOJGE D,NO4AD
;COPY TO MAKE SEMI-CIRCLE

	MOVE D,NO2
	PUSHJ P,SETWIR
NO2AD:	MOVE A,@WR4
	MOVEM A,(B)
	MOVN A,@WI4
	MOVEM A,(C)
	AOS B
	AOS C
	SOJG D,NO2AD
;COPY TO MAKE FULL CYCLE
	MOVE A,RN
	AOS A
	POPJ P,

SETWIR:	HRRZ B,WR4
	HRRZ C,WI4
	ADD B,D		;WRS
	ADD C,D		;WIS
	POPJ P,

;               SETUP FOR VIDOT OF CIRCLE

LOOKC:	MOVE G,A	;ARRAY,X,Y,INCR
	HLRZ A,NVSCL
	PUSHJ P,FFLOAT
	FMPR A,D
	FMPR A,FLT
	FDVR A,TWPI
	MOVEM A,FRAD	;FRAD=HFLOAT<NVSCL>*D*FLT/TWPI
	HRLI G,C
	IMUL B,NVSCL
	IMUL C,NVSCL
	HLR B,C
	MOVEM B,DCEN	;CENTRE X,,Y

	MOVE C,RN	;POINTS AROUND CIRCUMFERENCE
	SOS C

LCRTC:	MOVE A,WI3@
	FMP A,FRAD
	PUSHJ P,FFIX
	HRL E,A
	MOVE A,WR3@
	FMP A,FRAD
	PUSHJ P,FFIX
	HRR E,A
	ADD E,DCEN	;FIXX<COS(T)*FRAD,,SIN(T)*FRAD)>+(XCEN,,YCEN)
	MOVEM E,G@
	SOJGE C,LCRTC

	JRST RESURN


OUTVIW:	MOVNS C
	FADR C,VRAD
	FSBR A,XCN	;X Y MAG - SKIPS IF IN VIEW
	FDVR A,C
	FMPR A,A
	FSBR B,YCN
	FDVR B,C
	FMPR B,B
	FADR A,B	;((&-XCN)*C/XRAD)^2 +((&-YCN)*C/YRAD)^2
	CAMGE A,[1.0]
	AOS (P)
	POPJ P,

RESURN:	MOVEI A,0
	POPJ P,

AVMXMN:	;GET AVERAGE, MAX AND MIN OF ARRAY
;ARRAY SIZE
	PUSH P,B
	PUSH P,D
	MOVNS B
	HRL A,B
	SETZM B
	MOVE C,[1000000000.0]
	MOVN D,C
AVXNLP:	FADR B,(A)
	CAMLE C,(A)
	MOVE C,(A)
	CAMGE D,(A)
	MOVE D,(A)
	AOBJN A,AVXNLP

	MOVE A,-1(P)	;ITEMS
	PUSHJ P,FFLOAT
	EXCH A,B
	FDVR A,B	;AVERAGE
	MOVE B,C	;MIN
	MOVE C,D	;MAX
	POP P,D
	SUB P,[1,,1]
	POPJ P,
LVLPRD:	;LEVEL PREDICATE LOW-SKIP 0 PLACES
	CAMG A,LVL1	;IN BETWEEN-SKIP 1 PLACE
	POPJ P,		;HIGH 	-SKIP 2 PLACES
	AOS (P)
	CAMLE A,LVL2
	AOS (P)
	POPJ P,

;SCANS ALONG ARRAY FINDS UP UPDOWNS AND DOWNUPS
LOWHGH:	PUSHJ P,STRTRN	;RECORD TRANSITION
HGHRUN:	MOVEM F,LSTHGH
	MOVE A,@G
	PUSHJ P,LVLPRD
	JRST HGHLOW	;DOWNWARD TRANSITION
	JRST HGHLOS	;UNCLEAR IF WE ARE GOING DOWN YET
	AOS F		;CARRY ON
	CAME F,SZRAD
	JRST HGHRUN
	POPJ P,		;RUN OUT

HGHLOS:	MOVE A,@G
	PUSHJ P,LVLPRD
	JRST HGHLOW	;DOWNWARD TRANSITION
	SKIPA		;CARRY ON
	JRST HGHRUN	;GO BACK UP
	AOS F
	CAME F,SZRAD
	JRST HGHLOS
	POPJ P,

HGHLOW:	PUSHJ P,STRTRN	;RECORD TRANSITION
LOWRUN:	MOVEM F,LSTHGH
	MOVE A,@G
	PUSHJ P,LVLPRD
	JRST .+3	;CARRY ON
	JRST LOWLOS	;UNCLEAR IF WE GO UP ALREADY
	JRST LOWHGH	;UPWARD TRANSITION
	AOS F
	CAME F,SZRAD
	JRST LOWRUN
	POPJ P,

LOWLOS:	MOVE A,@G
	PUSHJ P,LVLPRD
	JRST LOWRUN	;GO BACK TO LOW
	SKIPA		;CARRY ON
	JRST LOWHGH	;UPWARD TRANSITION
	AOS F
	CAME F,SZRAD
	JRST LOWLOS
	POPJ P,

GTTRAN:	MOVEM A,STRAD	;GET TRANSITIONS ARRAY,SIZE
	MOVEM B,SZRAD
	MOVE A,B
	PUSHJ P,FFLOAT
	MOVEM A,FLTN
	MOVE A,STRAD
	MOVE B,SZRAD
	PUSHJ P,STLVS	;SETUP LVL1 AND LVL2
	MOVE B,MXMNRT	;MAX TO MIN RATIO
	CAMG B,RATMIN	;IS IT GOOD ENOUGH
	POPJ P,		;NO
	MOVE B,SZRAD
	MOVE G,A
	HRLI G,F
	SETZM B
	MOVE F,SZRAD
REVRTF:	SOS F
	MOVEM F,LSTHGH
	MOVE A,@G
	PUSHJ P,LVLPRD
	JRST ISFRLW
	JRST REVRTF	;REVERSE SOME MORE
	JRST ISFRHG

FORGT:	CAIE B,6
	POPJ P,		;WRONG NUMBER OF TRANSITIONS
	PUSHJ P,GTNACN	;GET NEW CENTRE
	POPJ P,		;DIDNT INTERSECT
	MOVEM C,SPRDL
	CAMG C,MXSQOF	;ARE LINE INTERSECTIONS SPREAD TOO FAR APART
	AOS (P)
	POPJ P,

STLVS:	PUSH P,A
	PUSH P,B
	PUSH P,C
	PUSHJ P,AVMXMN	;STORE TRANSITION LEVELS -ARRAY,SIZE
	PUSH P,C
	FDVR C,B
	MOVEM C,MXMNRT	;RATIO OF MAX TO MIN
	POP P,C
	FADR A,B
	FADR A,C
	FDVR A,[3.0]	;(AVERAGE+MAX+MIN)/3.0
	FSBR B,C
	FMPR B,FRCLVL	;+- (MAX-MIN)/6.0
	FADR A,B
	MOVEM A,LVL1
	FSBR A,B
	FSBR A,B
	MOVEM A,LVL2
	POP P,C
	POP P,B
	POP P,A
	POPJ P,
STRTRN:	PUSH P,F	;STORE AWAY A TRANSITION
	MOVEM F,TRANSX(B)
	MOVE F,LSTHGH
	MOVEM F,TRANSY(B)
	POP P,F
	AOS B
	CAIN B,7	;LOSSAGE WE JUST STORED THE 6'TH TRANSITION
	SUB P,[1,,1]	;POP AN EXTRA LEVEL UP
	POPJ P,

ISFRLW:	SETZB F,STRHGH	;INDICATE WE STARTED LOW
	PUSHJ P,LOWRUN
	JRST FORGT


ISFRHG:	SETZM F
	SETOM STRHGH	;INDICATE WE STARTED HIGH
	PUSHJ P,HGHRUN
	JRST FORGT


INTRSC:	;INTERSECT LINES IN LX1,LY1 ETC.	SKIPS IF OK
	MOVE A,LY2
	FSBR A,LY1
	MOVEM A,LY21
	MOVE B,LX4
	FSBR B,LX3
	MOVEM B,LX43
	FMPR A,B
	MOVE B,LY4
	FSBR B,LY3
	MOVEM B,LY43
	MOVE C,LX2
	FSBR C,LX1
	MOVEM C,LX21
	FMPR B,C
	FSBR A,B
	MOVEM A,DELT

; DELT=(LY21=(LY2-LY1))*(LX43=(LX4-LX3))-(LY43=(LY4-LY3))*(LX21=(LX2-LX1))
	SKIPN A
	JRST LOSINS
	MOVE A,LX1
	FMPR A,LY2
	MOVE B,LX2
	FMPR B,LY1
	FSBR A,B
	MOVEM A,LCRS1
	FMPR A,LX43
	MOVE B,LX3
	FMPR B,LY4
	MOVE C,LX4
	FMPR C,LY3
	FSBR B,C
	MOVEM B,LCRS2
	FMPR B,LX21
	FSBR A,B
	FDVR A,DELT
; ((LCRS1=LX1*LY2-LX2*LY1)*LX43-(LCRS2=LX3*LY4-LX4*LY3)*LX21)/DELT
	MOVE B,LCRS1
	FMPR B,LY43
	MOVE C,LCRS2
	FMPR C,LY21
	FSBR B,C
	FDVR B,DELT
; ,(LCRS1*LY43-LCRS2*LY21)/DELT
	AOS (P)
	POPJ P,

LOSINS:	PRINT _LINES DONT INTERSECT_
	JRST TTYW
	POPJ P,

GTNACN:	SETZM IXS	;SKIPS IF OK
	SETZM IYS
	SETZM IX2S
	SETZM IY2S

	MOVEI U,0
	MOVEI V,1
	PUSHJ P,GTNBCN	;INTERSECT 0 & 1
	POPJ P,

	MOVEI U,0
	MOVEI V,2
	PUSHJ P,GTNBCN	;INTERSECT 0 & 2
	POPJ P,

	MOVEI U,1
	MOVEI V,2
	PUSHJ P,GTNBCN	;INTERSECT 1 & 2
	POPJ P,

	MOVE A,IXS
	FDVR A,[3.0]
	MOVEM A,IXS
	MOVE B,IYS
	FDVR B,[3.0]
	MOVEM B,IYS
; IXS=IXS/3.0, IYS=IYS/3.0
	MOVE A,IX2S
	FDVR A,[3.0]
	MOVE B,IXS
	FMPR B,B
	FSBR A,B
	MOVEM A,IX2S
	MOVE B,IY2S
	FDVR B,[3.0]
	MOVE C,IYS
	FMPR C,C
	FSBR B,C
	MOVEM B,IY2S
; IX2S=IX2S/3.0-IXS^2, IY2S=IY2S/3.0-IYS^2
	MOVE A,IXS
	MOVE B,IYS
	MOVE C,IX2S
	FADR C,IY2S
; IXS, IYS, IX2S+IY2S
	AOS (P)
	POPJ P,

GTNBCN:	PUSHJ P,GTNWCN
	POPJ P,
	FADRM A,IXS
	FADRM B,IYS
	FMPR A,A
	FMPR B,B
	FADRM A,IX2S
	FADRM B,IY2S
	AOS (P)
	POPJ P,

GOOST:	MOVE A,AWI(X)
	FADR A,AWI(Y)
	FMPR A,[0.5]
	MOVE B,AWR(X)
	FADR B,AWR(Y)
	FMPR B,[0.5]
; (AWI(X)+AWI(Y))/2.0,(AWR(X)+AWR(Y))/2.0
	POPJ P,

GTNWCN:	MOVE X,TRANSX(U)	;ONE END OF LINE 1	SKIPS IF OK
	MOVE Y,TRANSY(U)
	PUSHJ P,GOOST
	MOVEM A,LX1
	MOVEM B,LY1
	MOVE X,TRANSX(U)+3	;OTHER END OF LINE 1
	MOVE Y,TRANSY(U)+3
	PUSHJ P,GOOST
	MOVEM A,LX2
	MOVEM B,LY2
	MOVE X,TRANSX(V)	;ONE END OF LINE 2
	MOVE Y,TRANSX(V)
	PUSHJ P,GOOST
	MOVEM A,LX3
	MOVEM B,LY3
	MOVE X,TRANSX(V)+3	;OTHER END OF LINE 2
	MOVE Y,TRANSY(V)+3
	PUSHJ P,GOOST
	MOVEM A,LX4
	MOVEM B,LY4
	JRST INTRSC	;INTERSECT THEM

TRCK:	PUSHJ P,LUKN	;TRACKING STEP
	AOSA LOSTRC	;LOST THIS TIME
	SETZB A,LOSTRC	;WON THIS TIME
	CAIL A,5
	SETZM TVSWT	;LOST TOO OFTEN
	MOVE D,RLKN
	SKIPGE RADCHN
	FMPR D,CONTRA	;CONTRACT
	SKIPLE RADCHN
	FMPR D,EXPAN	;EXPAND
	MOVEM D,RLKN
	SOSGE TVSWT
	JRST TVDONE
	POPJ P,

TCIRC:	PUSHJ P,LUKNY	;SCAN A CIRCLE
	JRST TVDONE

LUKNY:	SETZM OUTRNG
	MOVE C,NLKN
	SKIPLE C
	CAILE C,MNN
	POPJ P,		;ARG !!
	PUSHJ P,MKCRNW	;MAKE CIRCLE;DO A OUTVIW, TVLLRG,INVRT - GO LOSLKN IF BAD
	MOVE A,XLKN
	MOVE B,YLKN
	MOVE C,RLKN
	PUSHJ P,OUTVIW	; OUTVIW<XLKN,YLKN,1.15>
	AOSA  OUTRNG	;OUT OF RANGE, BUT TRY ANYWAY ...
	SETZM OUTRNG	;IN RANGE
	MOVE A,YLKN
	PUSHJ P,FFIX
	MOVE C,A
	MOVE A,XLKN
	PUSHJ P,FFIX
	MOVE B,A
	MOVEI A,VIDPRC
	MOVE D,RLKN
	FMPR D,TWPI
	FDVR D,FLT
	PUSHJ P,LOOKC	; LOOKC<#AXR,,,RLKN*TWPI/FLT>

	MOVE A,RN
	MOVEM A,TCNT
	SETOM PRCVID
	JRST TVCSUC

LUKN:	PUSHJ P,LUKNY
LKNRX:	MOVEI A,VIDOUT
	MOVE B,RN
	PUSHJ P,GTTRAN	; GTTRAN<#AXR,NLKN>
	POPJ P,		;LOSSAGE
	FMPR A,RLKN	;X OFFSET
	FMPR B,RLKN	;Y OFFSET
	FADRB A,XLKN
	FADRB B,YLKN
	AOS (P)		;RETURN HAPPY WITH NEW X AND Y
	POPJ P,

MKCRNW:	CAMN C,RN	;...
	POPJ P,
	MOVE A,C
	JRST INITS	;INITIALISE TABLES

FLOUND:	MOVEM A,XLKN	;GET FAIR POSITION
	MOVEM B,YLKN
	MOVEM C,NLKN
	MOVEM D,RLKN
	PUSHJ P,LUKN
	POPJ P,

	MOVE D,RLKN
	FMPR D,CONTRA	;CONTRACT CIRCLE
	MOVEM D,RLKN
	PUSHJ P,LUKN
	POPJ P,

	MOVE D,RLKN
	FMPR D,CONTRA	;CONTRACT CIRCLE
	MOVEM D,RLKN
	PUSHJ P,LUKN
	POPJ P,

	AOS (P)		;SKIPS IF WE HUNG ON SUCCESSFULLY
	POPJ P,

SEART:	SKIPE TVSWT
	JRST SEARC2
	MOVE A,XLKN	;FIRST TIME ROUND
	MOVE B,YLKN
	MOVE C,NLKN
	MOVE D,RLKN
	MOVEM A,XSER
	MOVEM B,YSER
	MOVEM C,NSER
	MOVEM D,RSER
	MOVN A,[0.5]
	MOVEM A,TSER
	AOS TVSWT
; XSER=,YSER=,NSER=,RSER=,TSER=-0.5
	JRST SEART

SEARC2:	AOS LOSTRC
	PUSHJ P,SEARC1
	JRST TVDONE	;LOST
	POPJ P,		;NOT DONE
	SETZM LOSTRC	;WON
	JRST TVDONE


SEARC1:	MOVE A,TSER
	FADR A,[1.0]
	MOVEM A,TSER
	PUSHJ P,SQRT
	MOVEM A,STSER
; STSER=SQRT<TSER=TSER+1.0>
	MOVE A,STSER
	FDVR A,SQTPI
	FMPR A,RSER
	MOVEM A,RADC
	CAML A,MXRADC
	POPJ P,		;LOSSAGE
; RADC=STSER/SQTPI*RSER
	MOVE A,STSER
	FMPR A,SQTPI
	FMPR A,[2.0]
	MOVEM A,ANGL
; ANGL=STSER*SQTPI*2.0
	MOVE A,ANGL
	PUSHJ P,COS
	FMPR A,RADC
	FADR A,XSER
	MOVEM A,XSSR
	MOVE B,ANGL
	EXCH A,B
	PUSHJ P,SIN
	EXCH A,B
	FMPR B,RADC
	FADR B,YSER
	MOVEM B,YSSR
	MOVE C,RSER
	PUSHJ P,OUTVIW
; OUTVIW<XSSR=COS<ANGL>*RADC+XSER,YSSR=SIN<ANGL>*RADC+YSER,1.15>
	JRST SEARC1
	MOVE A,XSSR
	MOVE B,YSSR
	MOVE C,NSER
	MOVE D,RSER
	AOS (P)
	PUSHJ P,FLOUND
; FLOUND<XSSR,YSSR,NSER,RSER>
	POPJ P,		;NOT FOUND YET SKIPS ONCE
	AOS (P)
	POPJ P,		;WON IT SEEMS SKIPS TWICE

]
XYARM:	MOVEM A,XARM	;CONVERT FROM FLOATING COORDINATES TO POT UNITS
	MOVEM B,YARM
	MOVEM C,ZARM
	MOVEM D,TLTARM
	MOVEM E,EXTARM
	MOVEM F,ROTARM
	MOVEM G,GRPARM

	PUSHJ P,PSDPOL	;CONVERT TO PSEUDO POLAR COORDINATE SYSTEM
	MOVEM A,SWGQ
	MOVEM B,HRZQ
	FSBR F,A	;COMPENSATE ROTATE FOR SWING
	EXCH B,C

	FMPR A,SWGML	;CONVERT TO POT UNITS
	FMPR B,VRTML
	FMPR C,HRZML
	FMPR D,TLTML
	FMPR E,EXTML
	FMPR F,ROTML
	FMPR G,GRPML

	FADR A,SWGAD
	FADR B,VRTAD
	FADR C,HRZAD
	FADR D,TLTAD
	FADR E,EXTAD
	FADR F,ROTAD
	FADR G,GRPAD

î	PUSHJ P,FIXUP	;FIX AND STORE IN DESIRED FINAL OUTPUT LOCATIONS
	MOVEM A,SWGO
	MOVE A,B
	PUSHJ P,FIXUP
	MOVEM A,VRTO
	MOVE A,C
	PUSHJ P,FIXUP
	MOVEM A,HRZO
	MOVE A,D
	PUSHJ P,FIXUP
	MOVEM A,TLTO
	MOVE A,E
	PUSHJ P,FIXUP
	MOVEM A,EXTO
	MOVE A,F
	PUSHJ P,FIXUP
	MOVEM A,ROTO
	MOVE A,G
	PUSHJ P,FIXUP
	MOVEM A,GRPO

	MOVE A,XARM	;RESTORE ACS
	MOVE B,YARM
	MOVE C,ZARM
	MOVE D,TLTARM
	MOVE E,EXTARM
	MOVE F,ROTARM
	MOVE G,GRPARM

	POPJ P,

FIXUP:	PUSHJ P,FFIX
	SKIPGE A
	SETZM A
	CAILE A,7777
	MOVEI A,7777
	POPJ P,

PSDPOL:	PUSH P,C
	FMPR A,A	;GIVEN X,Y - CALCULATES THETA, R IN PSEUDO-POLAR SYSTEM
	FMPR B,B	;R=SQRT<X^2 +Y^2 -LG^2 >
	FADR A,B
	MOVE B,LG
	FMPR B,B
	FSBR A,B
	SKIPGE A
	MOVEI A,0	;IMPOSSIBLE POSITION
	PUSHJ P,SQRT
	MOVEM A,RARM	;HORIZONTAL AXIS

;TAN<SWING>=(R*Y+LG*X)/(X*R-Y*LG)=(X*Y+R*LG)/(X^2 -R^2 )=(X*Y+R*LG)/(LG^2 -Y^2 )
	FMPR A,YARM
	MOVE B,LG
	FMPR B,XARM
	FADR A,B
	MOVE B,XARM
	FMPR B,RARM
	MOVE C,YARM
	FMPR C,LG
	FSBR B,C
	FDVR A,B
	PUSHJ P,ATAN	;SWING
	MOVE B,RARM
	POP P,C
	SKIPE D		;TILT NON-ZERO ?
	SKIPN TLTCMP	;COMPENSTAE FOR TILT ?
	POPJ P,

	PUSH P,A
	MOVE A,D
	PUSHJ P,SIN
	FMPR A,TLTLEN
	FSBR B,A	;HRZ=R-TLTLEN*SIN<TILT>
	MOVE A,D
	PUSHJ P,COS
	FSBR A,[1.0]
	FMPR A,TLTLEN
	FADR C,A	;VRT=Z-TLTLEN*(1.0-COS<TILT>)
	POP P,A
	POPJ P,

WAITYP:	PUSHJ P,MARM	;SERVO POTS UNTIL CHARACTER TYPED
	SKIPN TYI
	JRST .-2
	SETZM TYI
	POPJ P,

CLARM:	MOVE P,[-LPDL,,PDL-1]	;CALIBRATE POTS
	PUSHJ P,IARM

	SETZM TYI
	PRINT _MOVE ARM TO POINT 1_
	PUSHJ P,WAITYP
	MOVE A,HRZI

	PRINT _MOVE ARM TO POINT 2_
	PUSHJ P,WAITYP
	SUB A,HRZI
	PUSHJ P,FFLOAT
	FDVR A,[18.0]
	MOVNM A,HRZML
	MOVE A,SWGI

	PRINT _MOVE ARM TO POINT 3_
	PUSHJ P,WAITYP
	SUB A,SWGI
	PUSHJ P,FFLOAT
	FDVR A,PIE
	FMPR A,[6.0]
	MOVEM A,SWGML

	MOVE A,HRZI
	PUSHJ P,FFLOAT
	MOVE B,[49.0]
	FMPR B,HRZML
	FSBR A,B
	MOVEM A,HRZAD

	MOVE A,SWGI
	PUSHJ P,FFLOAT
	MOVEM A,SWGAD

	PRINT _SET VERTICAL TO 0 INCHES_
	PUSHJ P,WAITYP
	MOVE A,VRTI
	PUSHJ P,FFLOAT
	MOVEM A,VRTAD
	MOVE A,VRTI

	PRINT _SET VERTICAL TO 24 INCHES_
	PUSHJ P,WAITYP
	SUB A,VRTI
	PUSHJ P,FFLOAT
	FDVR A,[24.0]
	MOVNM A,VRTML

	PRINT _SET TILT TO 0 DEGREES_
	PUSHJ P,WAITYP
	MOVE A,TLTI
	PUSHJ P,FFLOAT
	MOVEM A,TLTAD
	MOVE A,TLTI

	PRINT _SET TILT TO 90 DEGREES_
	PUSHJ P,WAITYP
	SUB A,TLTI
	PUSHJ P,FFLOAT
	FDVR A,PIE
	FMPR A,[2.0]
	MOVNM A,TLTML

	PRINT _SET EXTEND TO 0 INCHES_
	PUSHJ P,WAITYP
	MOVE A,EXTI
	PUSHJ P,FFLOAT
	MOVEM A,EXTAD
	MOVE A,EXTI

	PRINT _SET EXTEND TO 1 INCH_
	PUSHJ P,WAITYP
	SUB A,EXTI
	PUSHJ P,FFLOAT
	FDVR A,[1.0]
	MOVNM A,EXTML

	PRINT _SET ROTATE TO 0 DEGREES_
	PUSHJ P,WAITYP
	MOVE A,ROTI
	PUSHJ P,FFLOAT
	MOVEM A,ROTAD
	MOVE A,ROTI

	PRINT _SET ROTATE TO 360 DEGREES_
	PUSHJ P,WAITYP
	SUB A,ROTI
	PUSHJ P,FFLOAT
	FDVR A,PIE
	FDVR A,[2.0]
	MOVNM A,ROTML

	PRINT _SET GRIP TO 0 INCHES_
	PUSHJ P,WAITYP
	MOVE A,GRPI
	PUSHJ P,FFLOAT
	MOVEM A,GRPAD
	MOVE A,GRPI

	PRINT _SET GRIP TO 2 INCHES_
	PUSHJ P,WAITYP
	SUB A,GRPI
	PUSHJ P,FFLOAT
	FDVR A,[2.0]
	MOVNM A,GRPML

	PRINT _TYPE SPACE TO SEND ARM TO 40 BY 19_
PNT8:	SKIPN TYI
	JRST PNT8

	MOVE A,[40.0]
	MOVE B,[19.0]
	MOVE C,[24.0]
	MOVE D,[0]
	MOVE E,[1.0]
	MOVE F,[1.5708]
	MOVE G,[2.0]
	PUSHJ P,XYARM
	PRINT _WIN_
	JRST IDL

XOUTP:	PRINT __AXIS      ML    AD    MN    MX_
	MOVSI G,-11
XOUTP1:	PRINT _
	PRN ANAME(G)
	MOVE A,SWGML(G)
	MOVEI B,6	;BEFORE DECIMAL POINT
	MOVEI C,1	;AFTER DECIMAL POINT
	PUSHJ P,FLOUT
	MOVE A,SWGAD(G)
	MOVEI B,6
	MOVEI C,1
	PUSHJ P,FLOUT
	MOVE A,SWGMN(G)
	MOVEI B,5	;LENGTH OF FIELD
	PUSHJ P,FXOUT
	MOVE A,SWGMX(G)
	MOVEI B,5
	PUSHJ P,FXOUT
	PUSHJ P,TTYW
	AOBJN G,XOUTP1
	PRINT __
	JRST IDL

RECMNX:	CAMLE A,B
	EXCH A,B
	MOVEM A,SWGMN(T)
	MOVEM B,SWGMX(T)
	AOS T
	POPJ P,

OUTLIM:	MOVSI A,-11	;RESET ALL LIMITS
	MOVEI B,7777
OUTLI1:	SETZM SWGMN(A)
	MOVEM B,SWGMX(A)
	AOBJN A,OUTLI1
	POPJ P,


RLARM:	MOVE P,[-LPDL,,PDL-1]	;FIND LIMITS ON ALL AXES
	PUSHJ P,IARM
	PUSHJ P,OUTLIM
	SETZB T,TYI

	PRINT _MOVE SWING TO MIN_
	PUSHJ P,WAITYP
	MOVE A,SWGI

	PRINT _MOVE SWING TO MAX_
	PUSHJ P,WAITYP
	MOVE B,SWGI
	PUSHJ P,RECMNX

	PRINT _MOVE VERTICAL TO MIN_
	PUSHJ P,WAITYP
	MOVE A,VRTI

	PRINT _MOVE VERTICAL TO MAX_
	PUSHJ P,WAITYP
	MOVE B,VRTI
	PUSHJ P,RECMNX

	PRINT _MOVE HORIZONTAL TO MIN_
	PUSHJ P,WAITYP
	MOVE A,HRZI

	PRINT _MOVE HORIZONTAL TO MAX_
	PUSHJ P,WAITYP
	MOVE B,HRZI
	PUSHJ P,RECMNX

	ADDI T,2

	PRINT _MOVE TILT TO MIN_
	PUSHJ P,WAITYP
	MOVE A,TLTI

	PRINT _MOVE TILT TO MAX_
	PUSHJ P,WAITYP
	MOVE B,TLTI
	PUSHJ P,RECMNX

	PRINT _MOVE EXTEND TO MIN_
	PUSHJ P,WAITYP
	MOVE A,EXTI

	PRINT _MOVE EXTEND TO MAX_
	PUSHJ P,WAITYP
	MOVE B,EXTI
	PUSHJ P,RECMNX

	PRINT _MOVE ROTATE TO MIN_
	PUSHJ P,WAITYP
	MOVE A,ROTI
	
	PRINT _MOVE ROTATE TO MAX_
	PUSHJ P,WAITYP
	MOVE B,ROTI
	PUSHJ P,RECMNX

	PRINT _MOVE GRIP TO MIN_
	PUSHJ P,WAITYP
	MOVE A,GRPI

	PRINT _MOVE GRIP TO MAX_
	PUSHJ P,WAITYP
	MOVE B,GRPI
	PUSHJ P,RECMNX

	PRINT _WIN_
	JRST IDL

TEST:	MOVE P,[-LPDL,,PDL-1]	;SLAVE ARM TO OLD POT BOX
	PUSHJ P,IARM
	MOVEI B,60
	MOVEI C,1
	MOVEM B,VRTV
	MOVEM C,VRTA
	PUSHJ P,MARM
	JRST .-1

IARM:	PUSH P,A	;SETUP VELOCITY AND ACCELERATION DATA
	PUSH P,B
	PUSH P,C

	MOVEI B,100	;HORIZONTAL LIMITS
	MOVEI C,1
	MOVEM B,HRZV
	MOVEM C,HRZA

	MOVEI B,40	;SWING LIMITS
	MOVEI C,1
	MOVEM B,SWGV
	MOVEM C,SWGA

	MOVEI B,10	;VERTICAL LIMITS
	MOVEI C,1
	MOVEM B,VRTV
	MOVEM C,VRTA

	MOVSI A,-4	;HAND AXES
	MOVEI B,20
	MOVEI C,1
	MOVEM B,TLTV(A)
	MOVEM C,TLTA(A)
	AOBJN A,.-2

	POP P,C
	POP P,B
	POP P,A
	POPJ P,

MARM:	PUSH P,A	;COPY FROM POTS TO DESIRED OUTPUT LOCATIONS
	MOVE A,[MXTB,,SWGO]
	BLT A,HRZO
	MOVE A,[MXTB+3,,TLTO]
	BLT A,GRPO
	SETOM RDOMX
	POP P,A
	POPJ P,

FFIX:	PUSH P,B
	MULI A,400
	TSC A,A
	ASH B,-243(A)
	MOVE A,B
	POP P,B
	POPJ P,

FFLOAT:	TLC A,232000
	FADR A,A
	POPJ P,


;FLOATING POINT SQUARE ROOT FUNCTION	 - RE-ENTRANT

SQRT:	MOVMS A
	SKIPN A
	POPJ P,
	PUSH P,B
	PUSH P,C
	ASHC A,-33
	SUBI A,201
	ROT A,-1
	HRRZ C,A
	PUSH P,C	;SQ1
	LSH A,-43
	ASH B,-10
	FSC B,177(A)
	MOVE C,B
	FMP B,SQRT1(A)
	FAD B,SQRT2(A)	;LINEAR APPROXIMATION
	MOVE A,C	;THEN TWO ITERATIONS OF NEWTON-RAPHSON
	FDV A,B
	FAD B,A
	FSC B,-1
	MOVE A,C
	FDV A,B
	FADR A,B
	POP P,B		;SQ1
	FSC A,(B)
	POP P,C
	POP P,B
	POPJ P,

SQRT1:	0.8125
	0.578125
SQRT2:	0.302734
	0.421875

;FLOATING POINT ARCTANGENT	- RE-ENTRANT

ATAN:	PUSH P,B
	PUSH P,C
	MOVM B,A
	CAMG B,[0.4^-8 ]
	JRST TANDON	;SMALL ENOUGH SO THAT ATAN(X)=X
	PUSH P,A
	CAML B,[7.0^7 ]
	JRST ATANS4	;LARGE ENOUGH SO ATAN(X)=PI/2
	MOVN C,[1.0]
	CAMLE B,[1.0]	;1.0
	FDVM C,B
	PUSH P,B
	FMPR B,B
	MOVE C,[1.44863154]
	FADR C,B
	MOVE A,[-0.264768620]
	FDVM A,C
	FADR C,B
	FADR C,[3.31633543]
	MOVE A,[-7.10676005]
	FDVM A,C
	FADR C,B
	FADR C,[6.76213924]
	MOVE A,[3.70925626]
	FDVR A,C
	FADR A,[0.174655439]
	POP P,B
	FMPR A,B
	JUMPG B,ATANS5
	FADR A,[1.57079632]	;PI/2
	JRST .+2	;SKIPA
ATANS4:	MOVE A,[1.57079632]
ATANS5:	POP P,B
	SKIPGE B
	MOVNS A
TANDON:	POP P,C
	POP P,B
	POPJ P,

COS:	FADR A,[1.57079632]	;PI/2
SIN:	PUSH P,B
	PUSH P,C
	MOVE C,A	;SAVE A
	MOVMS A
	CAMG A,[0.019]
	JRST SNSN3	;SMALL ENOUGH, SO SIN(X)=X
	CAML A,[1.0^8 ]
	MOVEI A,0	;ARGUMENT TOO LARGE
	FDV A,[1.57079632]	;PI/2
	CAMG A,[1.0]
	JRST SNSN2	;SMALL ENOUGH NOT TO REQUIRE ARGUMENT REDUCTION
	MULI A,400	;FIX IT
	LSH B,-202(A)
	MOVEI A,200
	ROT B,3
	LSHC A,33
	FAD A,[0]	;FLOAT IT
	JUMPE B,SNSN2
	TLCE B,1000
	FSB A,[1.0]	;01,11
	TLCE B,3000
	TLNN B,3000
	MOVNS A		;01,10
SNSN2:	SKIPGE C
	MOVNS A
	MOVEM A,C
	FMPR A,A
	MOVE B,[0.00015148419]	;0
	FMP B,A
	FAD B,[-0.00467376557]	;-0.004362476
	FMP B,A
	FAD B,[0.07968967928]	;0.079487663
	FMP B,A
	FAD B,[-0.64596371106]	;-0.645920978
	FMP A,B
	FAD A,[1.57079632]	;PI/2
	FMPR A,C
SINX:	POP P,C
	POP P,B
	POPJ P,
	
SNSN3:	MOVE A,C
	JRST SINX


TTYBK:	0		;TTY INTERRUPT
	PUSH P,A
	PUSH P,B
	PUSH P,40
	PUSH P,UUOH
	CONSZ PDP,10	;IS IT OTHER PROCESSOR
	JRST PDPTLK	;YES
	CONSZ TTY,10
	JRST TYOINT
	CONSZ TTY,40
	JRST TYIINT
	JRST 4,0

TYIINT:	DATAI TTY,A	;INPUT
TYIINC:	ANDI A,177
	MOVE B,COUNT
	CAIGE B,LTTB	;BUFFER SPACE AVAILABLE ?
	PUSHJ P,TYO	;YES, ECHO IT
	MOVEM A,TYI
	CAIE A,^A	;UNLOCK KEYBOARD CHR
	SKIPL TTYLCK	;IS KEYBOARD LOCKED ?
	XCT TTAB(A)
	JRST OUT1

TYOINT:	SKIPLE COUNT	;OUTPUT
	JRST TYOIN1
	CONO TTY,200+TTCH
	SETOM TTYOFF
	JRST OUT1

TYOIN1:	MOVE A,OTPNTR
	DATAO TTY,TTYTAB(A)
	SOS COUNT
	SOSL OTPNTR
	JRST OUT1
	MOVEI A,LTTB-1
	MOVEM A,OTPNTR
	JRST OUT1

PDPTLK:	CONO PDP,10+PDCH	;TURN IT OFF
	SKIPGE A,TYPOUT
	JRST OUT1		;NO PSEUDO-CHARCTER WAITING
	SETOM TYPOUT
	JRST TYIINC	;INTERPRET IT AS THOUGH CAME FROM TELETYPE

OUT1:	POP P,UUOH	;RETURN FROM TTY BREAK
	POP P,40
	POP P,B
	POP P,A
	JRST 12,TTYBK@



PDPINT:	CONO PDP,20+PDCH	;INTERRUPT OTHER PROCESSOR
	POPJ P,

TYO:	PUSH P,B	;PRINT CHARACTER
	MOVE B,COUNT
	CAIL B,LTTB
	JRST .-2	;WAIT FOR BUFFER SPACE
	MOVE B,INPNTR
	MOVEM A,TTYTAB(B)	;STORE IN BUFFER
	AOS COUNT
	SOSL INPNTR	;UPDATE CIRCULAR BUFFER POINTER 
	JRST OUT
	MOVEI B,LTTB-1
	MOVEM B,INPNTR

OUT:	AOSN TTYOFF
	CONO TTY,10+TTCH
	POP P,B
	POPJ P,

TTYW:	SKIPL TTYOFF	;WAIT FOR OUTPUT BUFFER TO EMPTY
	JRST .-1
	POPJ P,


LOOK:	DATAO VIDX,B	;READ ONE INTENSITY VALUE
	DATAO VIDY,C
	CONO VIDX,@VIDCON
	DATAO VIDT,SETL
	CONSZ VIDX,20000
	JRST .-1
	CONSO VIDX,1000
	JRST .-1
	DATAI VIDX,A
	CONI VIDX,B
	LSH B,-11.
	POPJ P,

ALOOK:	PUSH P,B
	PUSH P,C
	SKIPLE SENSD
	EXCH B,C
	ASH B,2
	ASH C,2
	PUSHJ P,LOOK
	POP P,C
	POP P,B
	POPJ P,

CLOCK:	0		;CLOCK INTERRUPT OR PDL OVERFLOW
	PUSH P,A
	PUSH P,B
	PUSH P,C
	PUSH P,D
	PUSH P,40
	PUSH P,UUOH
	CONSZ 200000
	JRST PIDLOV
	AOS TIME	;INCREMENT TIME
	CONO 1000+CLCH
	SKIPL RDIMX	;SHOULD WE READ NEW POT BOX ?
	JRST NOIMX	;NO

	MOVE A,[-6,,22]	;READ NEW POT BOX
XPT1:	DATAO IMX,A
	CONO IMX,0
	CONSO IMX,10
	JRST .-1
	DATAI IMX,B
	ASH B,1
	MOVEM B,@MXTB2P-22(A)
	AOBJN A,XPT1

	MOVE A,[-2,,36]
XPT2:	DATAO IMX,A
	CONO IMX,0
	CONSO IMX,10
	JRST .-1
	DATAI IMX,B
	ASH B,1
	MOVEM B,@MXTB2Q-36(A)
	AOBJN A,XPT2

NOIMX:	MOVE A,[-23,,70]	;READ OLD POT BOX + ARM POTS
XINPUT:	DATAO IMX,A
	CONO IMX,0
	CONSO IMX,10
	JRST .-1
	DATAI IMX,B
	ASH B,1
	MOVEM B,MXTB-70(A)
	AOBJN A,XINPUT

	SKIPL RDOMX	;SHOULD WE OUTPUT TO OMX ?
	JRST TMOUT	;NO

	HRRZ A,SWGGAN
	HRLI A,33
	DATAO OMX,A	;SWING GAIN
	HRRZ B,HRZGAN
	HRLI B,34
	DATAO OMX,B	;HORIZONTAL GAIN

	SKIPN TIPBK
	SETOM LMOD
	DATAI CLR,A	;GET TIP-BREAK BITS
	TLNN A,40000	;SEE IF HYDRAULICS LOCKED
	SETZM HYDLCK
	TLNE A,40000
	SETOM HYDLCK	;YES
	TLZ A,400000
	TLNE A,4000
	TLO A,400000
	LSH A,-33.
	TRNN A,4
	SETZM NORST
	AOS B,NORST
	CAIL B,20	;WAS WRIST OPEN FOR 1/4 SECOND ?
	JRST RSTBK	;YES
	TRNN A,3	;DID TIPS TOUCH ?
	JRST NORM1	;NO
RSTBK:	MOVEM A,TIPBTS	;SAVE TIP-BREAK BITS
	AOS LMOD	;BREAK HAPPENED
	SETOM TIPBK

NORM1:	MOVSI A,-11
	CONO OMX,0
REOUT:	SKIPGE LMOD	;ARE WE IN TIP-BREAK MODE ?
	JRST REOUTK	;NO
	CAME A,[-10,,1]	;VERTICAL AXIS ?
	JRST UTRNG	;NO
	PUSH P,A
	SKIPE LMOD	;STILL IN TOUCH ?
	JRST NORML	;NO
	AOS LMOD
	
	HRRZ A,VRTI
	PUSHJ P,FFLOAT
	FSBR A,VRTAD
	FDVR A,VRTML
	MOVEM A,ZBK	;FLOATING Z AT BREAK

	MOVE A,VRTI
	SUBI A,300	;WATCH OUT IF POT-POLARITY EVER CHANGES !!!
	CAMGE A,VRTMN
	MOVE A,VRTMN
	MOVEM A,VRTO	;NEW DESTINATION
	MOVEM A,VRTBRK
	SETZM VRTVO	;TENDERLY !!
	SETZM SWGVO
	SETZM HRZVO
	POP P,A
	JRST REOUTK	;O.K. CONTINUE

NORML:	MOVE A,VRTBRK	;CONTINUE UPWARD MOTION
	SKIPGE TIPBK
	MOVEM A,VRTO	;MUNG IF NOT THERE YET
	SKIPN HYDLCK
	AOS LMOD
	MOVE A,LMOD
	CAIG A,1000	;TIME OUT
	SKIPN VRTDS	;NEAR DESTINATION ?
	MOVMS TIPBK	;YES, ALLOW PDP10 TO RESET THINGS AGAIN
	POP P,A

REOUTK:	CAMLE A,[-7,,2]	;SWG,VRT OR HRZ ?
	JRST HNDHOK	;NO
	SKIPE HYDLCK	;HYDRAULICS LOCKED ?
	JRST HYDLOS	;YES

HNDHOK:	SKIPGE SWGV(A)	;VELOCITY LIMITED ?
	JRST NVSND	;NO
	SKIPL SWGA(A)	;ACCELERATION LIMITED ?
	JRST AXCL	;YES -DO IT- AND RETURN TO VENTER
	MOVE B,SWGV(A)
	MOVEM B,SWGVO(A)	;SET VELOCITY TO MAX (NO ACC LIMIT)
	MOVEI D,0		;INDICATE WE CAME THIS WAY

VENTER:	MOVE B,SWGI(A)
	SUB B,SWGO(A)
	MOVEM B,SWGDR(A)	;CURRENT IN - FINAL DESIRED

	MOVE B,VLTAB(A)
	SUB B,SWGO(A)
	MOVEM B,SWGDS(A)	;LAST OUT -FINAL DESIRED

	MOVM C,B
	CAMLE C,SWGVO(A)	;WITHIN ONE STEP OF FINAL POSITION ?
	JRST NWTHIN		;NO
	MOVE C,SWGDS(A)
	XOR C,FTAB(A)
	JUMPL C,WTHIN		;SAME SIGN	FROM LAST ERROR

NWTHIN:	MOVE C,SWGVO(A)
	XCT PMNTAB(D)	;FIND SIGN OF INCREMENT
	MOVN C,SWGVO(A)
	ADDM C,VLTAB(A)	;INCREMENT VALUE TO BE OUTPUT
SEND:	MOVE B,VLTAB(A)
SEND1:	SUB B,SWGI(A)
	MOVM C,B
	CAML C,SWGC(A)	;CLIPPING NEEDED ?
	JRST SPNT	;YES CLIP IT
	SETZM SWGCLP(A)	;NO

NVOUT:	MOVE B,VLTAB(A)
	CAMG B,SWGMN(A)	;OUT OF LIMITS ?
	JRST SETTAB	;YES
	CAML B,SWGMX(A)
	JRST SETTAB	;YES
	SETZM SWGOUT(A)	;NO
	ASH B,1
	HRLI B,2(A)
	DATAO OMX,B	;OUTPUT VALUE TO OMX
UTRNG:	AOBJN A,REOUT

TMOUT:	SKIPN RBTCNS	;OUTPUT TO ROBOT CONSOLE LIGHTS ?
	JRST TMOUT2	;NO

	CONO RBT,17_3	;SELECT LIGHTS
	MOVEI A,0
	SKIPGE CLBARM
	TLO A,400000
	SKIPGE TVSWT
	TLO A,200000
	SKIPE TTYLCK
	TLO A,100000
	MOVE B,TIPBTS
	DPB B,[360300,,A]	;?
	SKIPE TIPBK
	TLO A,4000
	SKIPE HYDLCK
	TLO A,2000
	SKIPE RDOMX
	TLO A,1000
	MOVE B,TIME
	DPB B,[220600,,A]	;?

	SKIPE SWGCLP
	TRO A,400000
	SKIPE VRTCLP
	TRO A,200000
	SKIPE HRZCLP
	TRO A,100000
	SKIPE TLTCLP
	TRO A,40000
	SKIPE EXTCLP
	TRO A,20000
	SKIPE ROTCLP
	TRO A,10000
	SKIPE GRPCLP
	TRO A,4000

	SKIPE SWGDS
	TRO A,400
	SKIPE VRTDS
	TRO A,200
	SKIPE HRZDS
	TRO A,100
	SKIPE TLTDS
	TRO A,40
	SKIPE EXTDS
	TRO A,20
	SKIPE ROTDS
	TRO A,10
	SKIPE GRPDS
	TRO A,4

	MOVE ZR,A	;
	DATAO RBT,A	;OUTPUT TO LIGHTS
	ROT A,36.
	DATAI RBT,RBTSWT	;READ SWITCHES

TMOUT2:	POP P,UUOH	;RETURN FROM CLOCK BREAK
	POP P,40
	POP P,D
	POP P,C
	POP P,B
	POP P,A
	JRST 12,@CLOCK

HYDLOS:	SETZM SWGVO(A)	;HYDRAULICS LOCKED
	MOVE B,SWGI(A)
	MOVEM B,VLTAB(A)
	SUB B,SWGO(A)
	MOVEM B,SWGDR(A)	;(SWGI-SWGO)
	MOVEM B,SWGDS(A)	;(VLTAB-SWGO) ...
	JRST NVOUT

PMNTAB:	SKIPL B
	SKIPGE FTAB(A)

SETTAB:	SETOM SWGOUT(A)	;INDICATE OUT OF RANGE
	JRST UTRNG


NVSND:	MOVE B,SWGO(A)	;NO VELOCITY LIMITING
	MOVEM B,VLTAB(A)
	SUB B,SWGI(A)
;	MOVNM B,SWGDS(A)	;(VLTAB-SWGO) 0 REALLY ...
	MOVNM B,SWGDR(A)	;(SWGI-SWGO)
	JRST NVOUT

SPNT:	SKIPL B		;CLIPPING
	SKIPA B,SWGC(A)
	MOVN B,SWGC(A)
;	ASH B,-1	;ADD ONLY HALF CLIPPING VALUE
	ADD B,SWGI(A)
	MOVEM B,VLTAB(A)
	AOS NOCLP
	SETOM SWGCLP(A)	;SET CLIPPING FLAGS
	JRST NVOUT

WTHIN:	MOVE B,SWGO(A)	;WITHIN RANGE & AND MOVING IN RIGHT DIRECTION, SO GIVE IT FINAL VALUE
	MOVEM B,VLTAB(A)
	SKIPN FEDBCK
	JRST SEND
	SUB B,SWGI(A)	;OUTPUT=DESIRED+(DESIRED-IN)
	ADD B,SWGO(A)
	JRST SEND1


AXCL:	MOVEI D,1	;INDICATE PATH WE TOOK
;	SKIPE SWGCLP(A)	;ARE WE CLIPPING ?
;	JRST RDOWN	;YES, TIME TO REDUCE THE VELOCITY

	MOVE B,SWGVO(A)
	IMUL B,B
	MOVE C,SWGA(A)
	IMULI C,2
	IDIV B,C	;VEL^2 /(2*ACC)

	MOVE C,SWGO(A)
	SUB C,VLTAB(A)
	MOVMS C
	CAMG C,B
	JRST RDOWN	;TIME TO SLOW DOWN FOR SAFE DECELLERATION

	MOVE B,SWGO(A)
	SUB B,VLTAB(A)
	XOR B,FTAB(A)
	SKIPE SWGVO(A)
	JUMPL B,RDOWN	;THERE HAS BEEN A REVERSAL IN DESIRED MOTION

	MOVE C,SWGV(A)
	CAMGE C,SWGVO(A)
	JRST RDOWN	;VELOCITY EXCEEDS LIMIT

	CAMG C,SWGVO(A)
	JRST VENTER	;NO CHANGE

RUP:	MOVE B,SWGA(A)
	ADD B,SWGVO(A)
	CAMLE B,SWGV(A)
	MOVE B,SWGV(A)
	MOVEM B,SWGVO(A)	;INCREASE VELOCITY

	MOVE B,SWGO(A)
	SUB B,VLTAB(A)
	MOVEM B,FTAB(A)		;FINAL DESIRED - CURRENT OUT

	JRST VENTER

RDOWN:	MOVN B,SWGA(A)
	ADDB B,SWGVO(A)	;REDUCE VELOCITY
	SKIPGE B
	SETZM SWGVO(A)
	JRST VENTER

PIDLOV:	CONO 400000
	MOVEM P,QUITP
	MOVE P,[-LPDL,,PDL-1]
	PRINT _PDL OVERFLOW_
	JRST 10,.+1
	PUSHJ P,TTYW	;WAIT FOR TTY
	JRST DDT
IFN 0,[

IMXBK1:	0
	CONSO IMX,10
	JRST .-1
	MOVEM A,IMXPTR
	MOVEI A,100
	SOJG A,.
	SETZM A
	LDB A,[301400,,MXTB1+0]
	MOVEM A,MXTB+0
	LDB A,[141400,,MXTB1+0]
	MOVEM A,MXTB+1
	LDB A,[001400,,MXTB1+0]
	MOVEM A,MXTB+2
	LDB A,[301400,,MXTB1+1]
	MOVEM A,MXTB+3
	LDB A,[141400,MXTB1+1]
	MOVEM A,MXTB+4
	LDB A,[001400,,MXTB1+1]
	MOVEM A,MXTB+5
	LDB A,[301400,,MXTB1+2]
	MOVEM A,MXTB+6
	LDB A,[141400,,MXTB1+2]
	MOVEM A,MXTB+7
	MOVE A,[-8,,MXTB1-1]
	EXCH A,IMXPTR
	CONO IMX,37760+IXCH
	JRST 12,@IMXBK1

DCBK:	0
	MOVEM A,MXTBP
	AOS A,WDC
	TRNE A,1
	JRST DCBK1
	MOVE A,[-8,,MXTB-1]
	EXCH A,MXTBP
	CONO 764,21140
	CONO DC,4160+DCCH
DCBK1:	JRST 12,@DCBK

]

IMXBK:	0
	BLKI IMX,IMXPTR
	JRST REIMX
	JRST 12,@IMXBK

REIMX:	MOVEM A,SAVA
	MOVE A,[-8,,MXTB-1]
	MOVEM A,IMXPTR
	CONO IMX,12040
	MOVE A,SAVA
	JRST 12,@IMXBK

FLSHIO:	CONO DIS,100	;GIVE UP VARIOUS I/O DEVICES
	CONO IMX,0
	DATAO 20,[-1]
	POPJ P,

UUOH:	0		;UUOS
	PUSH P,A
	LDB A,[330300,,40]
	XCT UUOTAB(A)
	POP P,A
	JRST @UUOH

GETNO:	MOVE A,NO
	SETZM NO
	POPJ P,

;UUO ROUTINES


STOPU:	PUSHJ P,PRINTU
	PUSHJ P,TTYW
	JRST 4,.

PRINTU:	MOVSI A,440700	;PRINT ASCII STRING
	HRR A,40
	MOVEM A,ULDBP
PBACK:	MOVE A,COUNT
	CAIL A,LTTB
	POPJ P,		;NO SPACE IN OUTPUT BUFFER
	ILDB A,ULDBP
	SKIPN A
	POPJ P,		;TERMINATE ON 0
	CAIN A,"_	;GLITCH CHARACTER FOR CR & LF
	JRST LFCRR
	PUSHJ P,TYO
	JRST PBACK

LFCRR:	MOVEI A,15
	PUSHJ P,TYO
	MOVEI A,12
	PUSHJ P,TYO
	JRST PBACK


UACR:SACR:	EXCH T,SAV10
	EXCH U,SAV11
	EXCH V,SAV12
	EXCH W,SAV13
	EXCH X,SAV14
	EXCH Y,SAV15
	EXCH Z,SAV16
	POPJ P,

;TTY RUTEENS

NUMBER:	MOVE B,NO
	LSH B,3
	ADDI B,-60(A)
	MOVEM B,NO
	POPJ P,

IDL1:	MOVE P,[-LPDL,,PDL-1]
	JRST 10,IDL
FLOUT:	PUSH P,V
	PUSH P,OBASE
	MOVEI V,10.
	MOVEM V,OBASE
	SETOM NOPNT
	SETZM LDZRO
	MOVEM B,LNTFLD	;NUMBER, BEFORE DEC PT, AFTER DEC PT
	MOVEM C,LATFLD
	MOVE B,[0.5]
	FMPR B,[0.1]	;GENERATE ROUNDING QUANTITY
	SOJG C,.-1
	SETZM SNFLG
	JUMPGE A,POSFL
	MOVNS A
	SETOM SNFLG
	SOS LNTFLD

POSFL:	CAML A,[1.0^6 ]
	JFCL	;	JRST 4,.+1
	FADR A,B	;ADD IN ROUNDING QUANTITY
	MOVEM A,-1(P)	;UPDATE SAVED VALUE
	PUSHJ P,FFIX
	SKIPG LNTFLD
	JRST ZSOFLT
	PUSHJ P,DIGITS	;OUTPUT INTEGER PART
PSZRFT:	PUSH P,A
	MOVEI A,".
	PUSHJ P,TYO	;DECIMAL POINT
	POP P,A
	MOVE V,LATFLD	;RESTORE SECOND FIELD LENGTH
	JUMPE V,PNDXDC
	MOVEM V,LNTFLD
	SETOM LDZRO
	PUSHJ P,FFLOAT
	MOVNS A
	FADR A,-1(P)
GROWMN:	FMPR A,[10.0]
	SOJG V,GROWMN
	PUSHJ P,FFIX
	PUSHJ P,DIGIT
PNDXDC:	POP P,OBASE
	POP P,V
	POPJ P,

ZSOFLT:	PUSH P,A
	MOVEI A,"-	;MAY SCREW ALIGNMENT BUT ...
	SKIPE SNFLG
	PUSHJ P,TYO
	POP P,A
	JRST PSZRFT


NOTFRE:	SKIPE LDZRO	;LEADING SPACE OR ZERO GENERATOR
	SKIPA A,["0]
	MOVEI A," 	;SPACE
	JRST .+2
	PUSHJ P,TYO
	SOJGE C,.-1
	POPJ P,

FXOUT:	SETZM NOPNT
	SETZM LDZRO
	MOVEM B,LNTFLD
DIGIT:	SETZM SNFLG
DIGITS:	PUSH P,A
	PUSH P,B
	PUSH P,C
	PUSHJ P,DIGDOG
	MOVE A,OBASE
	CAIE A,10.
	JRST DIGEND
	SKIPN FRCNPT
	SKIPE NOPNT
	JRST DIGEND
	MOVEI A,".
	PUSHJ P,TYO
DIGEND:	POP P,C
	POP P,B
	POP P,A
	POPJ P,

DIGDOG:	MOVE C,LNTFLD
	JUMPGE A,PSHDIG
	SETOM SNFLG
	SOS C
	MOVNS A		;

PSHDIG:	IDIV A,OBASE	;ASSEMBLE NUMBER IN OUTPUT FORM
	ADDI B,260
	HRLM B,(P)	;STORE ONE DIGIT
	SOS C
	JUMPE A,FOOEAY
	PUSHJ P,PSHDIG	;MORE TO COME
POPDIQ:	HLRZ A,(P)	;SPEW ONE OF PDL
	JRST TYO

FOOEAY:	SKIPE LNTFLD
	PUSHJ P,NOTFRE	;LEADING BLANKS OR ZEROS
	SKIPE SNFLG
	PUSHJ P,PTMNS	;MINUS SIGN
	JRST POPDIQ

PTMNS:	MOVEI A,"-
	JRST TYO

FRCNPT:	0	;1 TO FORCE NO DECIMAL POINT
LNTFLD:	0
NOPNT:	0
LDZRO:	0
LATFLD:	0
SNFLG:	0

OBASE:	8
MXTB2P:	MXTB2+3	;POT POINTERS FOR NEW POT BOX
	MXTB2+7
	MXTB2+6
	MXTB2+0
	MXTB2+1
	MXTB2+2

MXTB2Q:	MXTB2+4
	MXTB2+5

SETLL:	-6?-10?-16?-26?-45?-70	;SETLLING TIME TABLE

UUOTAB:	REPEAT 8,JRST 4,1

TTAB:	REPEAT 200,SETOM TTYLCK

CONST:	CONSTANTS
VARIA:	VARIABLES

PDL:	BLOCK LPDL	;PUSH DOWN LIST

TTYTAB:	BLOCK LTTB	;OUTPUT TTY BUFFER

IFN VISION,[
AWR:	BLOCK MNN	;COSINE TABLE
AWI:	BLOCK MNN	;SINE TABLE
]



; UUOS

ZZ==.


	LOC UUOTAB+PRN_<-33>
	PUSHJ P,PRINTU

	LOC UUOTAB+SAC_<-33>
	PUSHJ P,SACR

	LOC UUOTAB+UAC_<-33>
	PUSHJ P,UACR

	LOC UUOTAB+STP_<-33>
	PUSHJ P,STOPU

	LOC ZZ

;  TTY COMANDS

ZZ==.

	LOC TTAB+40	;IGNORE SPACES
	JFCL

	LOC TTAB+"A	;CALIBRATE ARM
	JRST 10,CLARM

	LOC TTAB+^A	;UNLOCK KEYBOARD
	SETZM TTYLCK

	LOC TTAB+"C	;INVERT SENSE OF DISP
	SETCMM SENSD

	LOC TTAB+"D	;GO TO DDT
	JRST 10,DDT

	LOC TTAB+"I	;JRST TO GO
	JRST 10,GO

	LOC TTAB+^I	;^I JRST IDL
	PUSHJ P,IDL1

	LOC TTAB+"L	;FLUSH ALL I/O
	PUSHJ P,FLSHIO

	LOC TTAB+"O	;FLUSH ALL LIMITS
	PUSHJ P,OUTLIM

	LOC TTAB+"R	;GET LIMITS OF RANGE
	JRST 10,RLARM

	LOC TTAB+"S	;RUN SLAVE ARM FROM ITS
	JRST 10,SLAVE

	LOC TTAB+"T	;MOVE ARM TO POTS
	JRST 10,TEST

	LOC TTAB+"U	;SETZM NUMBER BUFF
	PUSHJ P,GETNO

	LOC TTAB+"X	;TYPE OUT PARAMETERS
	JRST 10,XOUTP

	LOC TTAB+^Z
	SETOM TTYLCK	;LOCK KEYBOARD

	LOC TTAB+"0	;INPUT NUMBERS INTO BUFF
	REPEAT 10,PUSHJ P,NUMBER

	LOC ZZ
	
	END

