
IF1 TITLE EYE-ARM-SLAVE CODE HAS BEEN INSERTED

;THIS CODE IS FOR USING EYE-ARM SLAVE RUNNING IN PDP6


P6=200000	;PLACE WHERE PAGE 0 OF PDP6 WILL APPEAR

P6STRT==6000	;PDP6 STARTING LOCATION FOR SLAVE

USRO==15	;CHANNEL NUMBER ON WHICH PDP6 IS OPEN	

SLVUSR:	SIXBIT /JBL   /
SLVNAM:	6,,(SIXBIT /DSK/)
	SIXBIT /SLAVE /
	SIXBIT /BIN   /


SLVLOD:	PUSHJ P,OPNSIX
	PUSHJ P,SLVTST
	JRST SLIVES	;SLAVE IS ALREADY RUNNING

	.SUSET [.SSNAME,,SLVUSR]	;MUST LOAD SLAVE
	.OPEN DSIC,SLVNAM
	STOP _SLAVE IS MISSING
	.RESET USRO,	;ZERO PDP6 CORE
	.CALL SLOAD
	STOP _PDP6 LOAD FAILED

	MOVE A,[JRST P6STRT]	;GET START
	MOVEM A,P6+41

	PUSHJ P,SLVTST	;IS IT RUNNING NOW?
	JRST SLIVES	;YES
	STOP _SLAVE IS DEAD
	POPJ P,

SLIVES:	STRIKE _SLAVE LIVES
	POPJ P,

SLOAD:	SETZ
	SIXBIT /LOAD/
	[USRO]
	SETZ [DSIC]

;SKIPS IF THE SLAVE DOESN'T RESPOND
SLVTST:	MOVEI A,^Z
	MOVEM A,TYPOUT
	.IPDP USRO,	;INTERRUPT PDP6
	MOVEI A,10
	.SLEEP A,
	SKIPL TYPOUT	;SEE IF HE ATE IT !
	AOS (P)	;SKIP
	POPJ P,


PDP6IP:	27,,(SIXBIT /USR/)	;20 BIT IN LEFT HALF SO .IPDP WILL WORK ON USRO CHANNEL
	0
	SIXBIT /PDP6  /

OPNSIX:	PUSH P,A
	.OPEN USRO,PDP6IP	;OPEN SIX AND APPEND FIRST 3 PAGES
	STOP _PDP6 NOT AVAILABLE_
	MOVE A,[002415,,500000]	;200000
	.CBLK A,
	STOP _PAGE APPEND FAILED_
	MOVE A,[002415,,501001]	;201000
	.CBLK A,
	STOP _PAGE APPEND FAILED_
	MOVE A,[002415,,502002]	;202000
	.CBLK A,
	STOP _PAGE APPEND FAILED_
IFDEF ARYROT .I  ARYROT=#OPNSIX
	POP P,A
	POPJ P,

CLSSIX:	PUSH P,A
	MOVE A,[002015,,100000]	;FLUSH PAGES FROM MY PAGE TABLE
	.CBLK A,
	.VALUE
	MOVE A,[002015,,101001]
	.CBLK A,
	.VALUE
	MOVE A,[002015,,102002]
	.CBLK A,
	.VALUE
	.UCLOSE USRO,
	POP P,A
	POPJ P,

ORDER:	SKIPL TYPOUT	;SEND LETTER TO SLAVE PROGRAM IN PDP6
	JRST .-1
	PRINT $!
	PUSHJ P,TYI
	CAIN A,^C
	POPJ P,
	MOVEM A,TYPOUT
	.IPDP USRO,	;INTERRUPT PDP6
	JRST ORDER

FREE:	MOVSI A,400000	;CAUSE PDP10 TO RELEASE DEVICES
	.IOTLSR A,
	DATAO 20,[-1]	;ALL !!!! UNTIL KNIGHT COMPLETES 24
;	DATAO 24,[620]	;VIDISECTOR
;	DATAO 24,[514]	;ROBOT CONSOLE
;	DATAO 24,[570]	;OUTPUT MULTIPLEXOR
;	DATAO 24,[574]	;INPUT MULTIPLEXOR
;	DATAO 24,[420]	;COLOR SCOPE
;	DATAO 24,[130]	;DISPLAY
;	DATAO 24,[340]	;MAG TAPE
	MOVEI A,0
	.IOTLSR A,
	POPJ P,

;CODE FOR FIELDING INTERRUPTS FROM THE PDP6

TVTSTW:
.I INTADR=#PDPON
	MOVE A,INTENA
	IORI A,IOCINT
	MOVNI B,1
	.SETM2 A,
	MOVEI A,30.*10.
WSLEEP:	.SLEEP A,
	POPJ P,

PDPON:	SKIPL NTRUPT	;BETTER BE RIGHT FLAVOUR INTERRUPT
	.VALUE
	HRRZ A,NTRUPT+1
	CAIN A,WSLEEP
	.DISMIS [TVTSTD]
	MOVE A,INTACS
	.DISMIS NTRUPT+1

TVTSTD:	MOVE A,INTENA
	MOVEI B,0
	.SETM2 A,
	POPJ P,

INTENA:	0	;MUST CONTAIN ENABLED INTERRUPTS
IOCINT==400	;IOC INTERRUPT BIT

;LOCATIONS OF INTEREST IN PDP6 CORE

;LOCATIONS FOR CONTROLLING ARM

XQ=P6+100
YQ=XQ+1
ZQ=XQ+2
TLTQ=XQ+3
EXTQ=XQ+4
ROTQ=XQ+5
GRPQ=XQ+6

CLBARM=XQ+7
TIPBK=XQ+10
ZBRK=XQ+11
TTYLCK=XQ+12
TYPOUT=XQ+13
TIPBTS=XQ+14
RBTSWT=XQ+15
HYDLCK=XQ+16
FEDBCK=XQ+17

SWGDS=P6+120
VRTDS=SWGDS+1
HRZDS=SWGDS+2
ROLDS=SWGDS+3
YAWDS=SWGDS+4
TLTDS=SWGDS+5
EXTDS=SWGDS+6
ROTDS=SWGDS+7
GRPDS=SWGDS+10

SWGDR=P6+140
VRTDR=SWGDR+1
HRZDR=SWGDR+2
ROLDR=SWGDR+3
YAWDR=SWGDR+4
TLTDR=SWGDR+5
EXTDR=SWGDR+6
ROTDR=SWGDR+7
GRPDR=SWGDR+10

SWGOUT=P6+160
VRTOUT=SWGOUT+1
HRZOUT=SWGOUT+2
ROLOUT=SWGOUT+3
YAWOUT=SWGOUT+4
TLTOUT=SWGOUT+5
EXTOUT=SWGOUT+6
ROTOUT=SWGOUT+7
GRPOUT=SWGOUT+10

SWGCLP=P6+200
VRTCLP=SWGCLP+1
HRZCLP=SWGCLP+2
ROLCLP=SWGCLP+3
YAWCLP=SWGCLP+4
TLTCLP=SWGCLP+5
EXTCLP=SWGCLP+6
ROTCLP=SWGCLP+7
GRPCLP=SWGCLP+10

MXTAB=P6+220

SWGGAN=P6+230
HRZGAN=SWGGAN+1
SWGQ=SWGGAN+2
HRZQ=SWGGAN+3
RDIMX=SWGGAN+4
RDOMX=SWGGAN+5
TLTCMP=SWGGAN+6

SWGER=P6+240
VRTER=SWGER+1
HRZER=SWGER+2
ROLER=SWGER+3
YAWER=SWGER+4
TLTER=SWGER+5
EXTER=SWGER+6
ROTER=SWGER+7
GRPER=SWGER+10

MXTB=P6+260

;LOCATION FOR CONTROLING VIDISECTOR

SXLKN=P6+750
SYLKN=SXLKN+1
SNLKN=SXLKN+2
SRLKN=SXLKN+3
SOUTRNG=SXLKN+4
LOSTRC=SXLKN+5
RADCHN=SXLKN+6

TVSWT=P6+760
VIDCON=TVSWT+1
TXORG=TVSWT+2
TYORG=TVSWT+3
TXINC=TVSWT+4
TYINC=TVSWT+5
TCNT=TVSWT+6
XCNT=TCNT
PRCVID=TVSWT+7
TVCFLV=TVSWT+10
NONDIM=TVSWT+11
DSKGEN=TVSWT+12
YCNT=TVSWT+13

VIDOUT=P6+1000
VIDPRC=P6+3000
DSKSTF=P6+5000
