;-*- Mode:LISP; Package:SYSTEM-INTERNALS -*-

;List all messages handled by each flavor starting from a given base
;flavor.  Lists each message only once, under the innermost flavor that has a method for it.
;If no file-name argument is given, output to the display.

(DEFVAR MESSAGES-ALREADY-DONE)

(DEFUN MESSAGE-REPORT (FLAVOR-NAME &OPTIONAL FILE-NAME)
  (LET ((STREAM (IF FILE-NAME (OPEN FILE-NAME '(:PRINT)) STANDARD-OUTPUT))
	(MESSAGES-ALREADY-DONE NIL)
	(FLAVOR-LIST (MESSAGE-REPORT-0 (LIST FLAVOR-NAME))))
    (FORMAT STREAM "~&")
    (DOLIST (FL FLAVOR-LIST)
      (MESSAGE-REPORT-1 FL STREAM))
    (AND FILE-NAME (FUNCALL STREAM ':CLOSE))))

;Cons up list of flavors to do, breadth-first order
(DEFUN MESSAGE-REPORT-0 (FLAVOR-LIST)
  (DOLIST (FL FLAVOR-LIST)
    (LET ((MORE-FLAVORS (REVERSE (FLAVOR-DEPENDED-ON-BY (GET FL 'FLAVOR)))))
      (DOLIST (F MORE-FLAVORS)
	(AND (MEMQ F FLAVOR-LIST) (SETQ MORE-FLAVORS (DELQ F MORE-FLAVORS))))
      (NCONC FLAVOR-LIST MORE-FLAVORS)))
  FLAVOR-LIST)

(DEFUN MESSAGE-REPORT-1 (FLAVOR-NAME STREAM
			 &AUX MSG METH (MESSAGES NIL)) ;ASSQ list of message and method
  (DOLIST (X (FLAVOR-METHOD-TABLE (GET FLAVOR-NAME 'FLAVOR)))
    (SETQ MSG (CAR X)
	  METH (IF (FBOUNDP 'FLAVOR-PACKAGE)		;New or old system?
		   (CADR (ASSQ NIL (CDDDR X)))
		   (CADDR X)))
    (COND ((AND METH (NOT (MEMQ MSG MESSAGES-ALREADY-DONE)))
	   (PUSH MSG MESSAGES-ALREADY-DONE)
	   (PUSH (CONS MSG METH) MESSAGES))))
  (SETQ MESSAGES (SORTCAR MESSAGES #'STRING-LESSP))
  (COND (MESSAGES
	 (FORMAT STREAM "Messages handled by flavor ~S~%" FLAVOR-NAME)
	 (DOLIST (X MESSAGES)
	   (LET ((ARGL (ARGLIST (CDR X))))
	     (OR (EQ (CAR ARGL) '&REST) (SETQ ARGL (CDR ARGL)))
	     (FORMAT STREAM "~3X:~29A ~:S~%" (CAR X) ARGL)))
	 (TERPRI STREAM))))
