	TITLE IFD

		;CRASH ANALYZER'S ASSISTANT

A=1
B=2
C=3
D=4
E=5
T=6
TT=7
U=10
TYOC=16
P=17

LOC 774000

SYMPTR=.-2

IFDINI:	-1

PDL:	-220,,.
	BLOCK 220

MMPPNT:	BLOCK 8		;ADDRESSES OF MMP PAGES, INDEXED BY TT

DEFINE BARF ARGS/
%%EXIT==.+1
JRST [
%%BARF==0
 IRP ARG,,[ARGS]
IFN %%BARF,[
	%%BARF==0
	%BARF ARG
].ELSE {
IFSE ARG,J,{
	%%BARF==1
	DEFINE %BARF ARGH
	 IF2 %%EXIT==ARGH
	 TERMIN
	.STOP
}
IFSE ARG,\,{
	PUSHJ P,CRLF
	.STOP
}
IFSE ARG,6,{
	%%BARF==1
	DEFINE %BARF ARGH
	 MOVE TT,ARGH
	 PUSHJ P,SIXOUT
	TERMIN
	.STOP
}
IFSE ARG,A,{
	%%BARF==1
	DEFINE %BARF ARGH
	 MOVE TT,ARGH
	 PUSHJ P,ASZOUT
	TERMIN
	.STOP
}
IFSE ARG,N,{
	%%BARF==1
	DEFINE %BARF ARGH
	 MOVE T,ARGH
	 PUSHJ P,OCTOUT
	TERMIN
	.STOP
}
IFSE ARG,D,{
	%%BARF==1
	DEFINE %BARF ARGH
	 MOVE T,ARGH
	 PUSHJ P,DECOUT
	TERMIN
	.STOP
}
	MOVEI TT,[ASCIZ\ARG\]
	PUSHJ P,ASZOUT
};.ELSE
TERMIN
	PUSHJ P,CRLF
	.VALUE
	JRST %%EXIT]
TERMIN

SIXOUT:	MOVEI T,0
	LSHC T,6
	ADDI T,40
	.IOT TYOC,T
	JUMPN TT,SIXOUT
	POPJ P,

ASZOUT:	HRLI TT,440700
ASZOU1:	ILDB T,TT
	JUMPE T,CPOPJ
	.IOT TYOC,T
	JRST ASZOU1

OCTOUT:	IDIVI T,8
	HRLM TT,(P)
	SKIPE T
	 PUSHJ P,OCTOUT
	HLRZ TT,(P)
	ADDI TT,"0
	.IOT TYOC,TT
	POPJ P,

DECOUT:	IDIVI T,10.
	HRLM TT,(P)
	SKIPE T
	 PUSHJ P,OCTOUT
	HLRZ TT,(P)
	ADDI TT,"0
	.IOT TYOC,TT
	POPJ P,

CRLF:	.IOT TYOC,[15]
	.IOT TYOC,[12]
	POPJ P,

;GIVEN A SYMBOL IN A, RETURNS ITS VALUE IN A, SKIPPING IF WIN.
AEVAL:	MOVE TT,SYMPTR
AEVAL1:	MOVE T,(TT)
	TLZ T,740000
	CAMN T,A
	 JRST AEVAL2
	AOBJP TT,.+1
	AOBJN TT,AEVAL1
	POPJ P,

AEVAL2:	MOVE A,1(TT)
POPJ1:	AOS (P)
CPOPJ:	POPJ P,

;PLAIN SYMBOLS
SYMS1:	IRPS SYM,,LUBLK:NMMP:PMRCM:NLOOSP:FLOOSP:LLOOSP:USRHI:TSYSM:MURUSR:HUSRAD:
SYM:	SQUOZE 0,SYM
TERMIN
NSYMS1==.-SYMS1

;INDEXED BY A
SYMS2:	IRPS SYM,,MEMBLT:MMSWP:MEMPNT:MMPPPP:MMMPG
SYM:	SQUOZE 0,SYM
TERMIN
NSYMS2==.-SYMS2

;INDEXED BY U
SYMS3:	IRPS SYM,,UNAME:JNAME:UPGMP:UPGCP:
SYM:	SQUOZE 0,SYM
TERMIN
NSYMS3==.-SYMS3

;BP,,MEMBLT(A)
SYMS4:	IRPS SYM,,MUR:MWC:MLU:MNUMB:MMMPX:MLO:
SYM:	SQUOZE 0,SYM
TERMIN
NSYMS4==.-SYMS4

;EVALUATE ALL THESE
GETSMT:	IRPS TABLE,,[SYMS1 SYMS2 SYMS3]INDEX,,[0 A U]
	 MOVSI B,-N!TABLE
	 MOVE A,TABLE(B)
	 PUSHJ P,AEVAL
	  .LOSE
	 HRLI A,INDEX
	 MOVEM A,TABLE(B)
	 AOBJN B,.-5
	TERMIN
	MOVSI B,-NSYMS4
GTSMT1:	MOVE A,SYMS4(B)
	PUSHJ P,AEVAL
	 .LOSE
	HRLZS A
	ADD A,MEMBLT
	MOVEM A,SYMS4(B)
	AOBJN B,GTSMT1
	POPJ P,

;convert mmp index in T to address in same
MMPFND:	PUSH P,A
	MOVE A,T
	TRNE T,1
	 BARF N,T, ODD MMP INDEX
	MOVE T,A
	IDIVI T,2000
	CAML T,NMMP
	 BARF N,A, TOO BIG MMP INDEX 
	ADD TT,MMPPNT(T)
	HRRZ T,TT
POPAJ:	POP P,A
	POPJ P,

IFD:	MOVE P,PDL
	.OPEN TYOC,[.UAO,,'TTY]
	 .LOSE 1400
	AOSE IFDINI
	 JRST GO2
	SKIPE 774000-2		;NON-ZERO IF DUMPED WITH DSKDMP, ZERO IF DUMPED WITH DDT
	 JRST GO0
	MOVE A,[-2,,774000-4]	;MAKE TS DDT HAPPY
	MOVEM A,774000-2
GO0:	.VALUE [ASCIZ\774000-2P\]
	PUSHJ P,GETSMT
	MOVN A,NMMP
	HRLZS A
GO1:	LDB T,@MMPPPP ;(A)
	AND T,PMRCM
	CAME T,@MMMPG
	 BARF MMMPG AND EXEUMP DISAGREE FOR PAGE #,N,A, OF MMP
	IMULI T,2000
	HRLI T,TT
	MOVEM T,MMPPNT(A)
	AOBJN A,GO1

;HAVING GOT VARIOUS INFORMATION, CHECK STUFF
GO2:	PUSHJ P,CKLOOS
	MOVE A,TSYSM
GO3:	SOJL A,GO4
	PUSHJ P,CKCP
	JRST GO3

GO4:	PUSHJ P,CKZR
	BARF DONE.
	.LOSE

;CHECK OUT LOOSE PAGE LIST
CKLOOS:	MOVE A,@FLOOSP
	SKIPGE E,@NLOOSP
	 BARF NLOOSP NEGATIVE
CKLOO1:	JUMPE E,CKLOO9
	CAML A,TSYSM
	 BARF N,A,MEMBLT INDEX TOO BIG IN LOOSE PAGE LIST
	SKIPE @MMSWP
	 BARF MMSWP NONZERO FOR LOOSE PAGE ,N,A
	LDB T,MUR
	CAME T,MURUSR
	 BARF N,T, NOT MURUSR IN LOOSE PAGE ,N,A
	LDB B,MLO
CKLOO3:	CAIN B,-1 ;PATCHABLE
	 .VALUE
	JUMPE B,CKLOO2
	SOJG E,[MOVE A,B ? JRST CKLOO1]
	CAIE B,0
	 BARF LOOSE PAGE LIST TOO LONG ,N,A, SHOULD BE LAST
	JRST CKLOO9

CKLOO2:	CAIE E,1
	 BARF LOOSE PAGE LIST TOO SHORT LAST PAGE IS ,N,A
CKLOO9:	CAME A,@LLOOSP
	 BARF LOOSE PAGE LIST LAST PAGE ,N,A, NOT SAME AS LLOOSP
	POPJ P,

;CHECK FOR LARGE BLOCKS (AT LEAST ONE PAGE) OF ZERO CORE
CKZR:	MOVEI A,0
CKZR1:	CAIL A,774000
	 POPJ P,
	SKIPE (A)		;SEARCH FOR ZERO
	 AOJA A,CKZR1
	MOVE B,A
	HRLI B,-2000
	SKIPN (B)		;SEE IF NEXT 1777 LOCS ZERO
	 AOBJN B,.-1
	JUMPGE B,CKZR2
	MOVEI A,1(B)		;NO, SCAN AGAIN AFTER THE NONZERO
	JRST CKZR1

CKZR2:	HRRZS B
CKZR3:	CAIL B,774000		;SEARCH FOR TERMINATING NONZERO
	 JRST CKZR4
	SKIPN (B)
	 AOJA B,CKZR3
CKZR4:	SOS B
	BARF LOCATIONS ,N,A, THROUGH ,N,B, INCLUSIVE ARE ZERO.
	MOVEI A,1(B)
	JRST CKZR1

;A HAS A MEM PAGE NUMBER, CHECK CIRC PNTRS.
;IF PAGE IS LOOSE, CHECK THAT IT IS ON LOOSE PAGE LIST.
CKCP:	MOVEI E,1000		;MAX LENGTH OF LIST
	MOVEI C,0		;RH # USERS, SIGN MMP SEEN
	LDB T,MUR
	CAME T,MURUSR
	 POPJ P,
	MOVE T,A
	ADD T,MEMPNT
	HRLI T,002200
	MOVE D,T
CKCP1:	SOJL E,CKCP2		;JUMP IF LIST TOO LONG
	PUSHJ P,CPBP		;NEXT CP
	 POPJ P,		;LOST
	CAMN T,D
	 JRST CKCP3		;DONE WHOLE LIST
	JRST .+1(B)		;CHECK TYPE
	AOJA C,CKCP1
	BARF J,CKCP1,MEM IN CP LIST TWICE PAGE=,N,A
	TLOE C,400000	;MMP SET FLAG
	 BARF MMP IN CP LIST TWICE PAGE=,N,A
	JRST CKCP1

CKCP2:	BARF CP LIST TOO LONG PAGE=,N,A
	POPJ P,

CKCP3:	MOVE T,A
	IMULI T,2000
	CAMGE T,@HUSRAD
	 POPJ P,		;SYS JOB PAGES HAVE NO MMP ENTRIES.
	TLZN C,400000
	 BARF NO MMP IN CP LIST PAGE=,N,A
	JUMPN C,CPOPJ		;JUMP IF NOT LOOSE
	PUSH P,A
	HRRZ A,@FLOOSP
	JUMPE A,CKCP5
CKCP4:	CAMN A,(P)
	 JRST [ POP P,A ? POPJ P, ]
	LDB A,MLO
	JUMPN A,CKCP4
CKCP5:	POP P,A
	BARF PAGE #,N,A, HAS NO USERS IN CP LIST BUT IS NOT ON LOOSE PAGE LIST
	POPJ P,

;T HAS BP TO A CIRCULAR POINTER.  RETURN BP TO NEXT CP IN T.
;ALSO RETURNS IN B 0 USER 1 MEM 2 MMP
;NON-SKIP RETURN IF INVALID
CPBP:	MOVEM T,LASTBP'
	LDB T,T
	CAIN T,0
	 BARF J,CPOPJ,CP CLOBBERED TO ZERO
	CAIN T,777777
	 BARF J,CPOPJ,ABS CP ENCOUNTERED
	MOVEM T,LASTCP'
	TRNE T,400000
	 JRST CPBP1
	IDIVI T,400
	MOVE U,T
	IMUL U,LUBLK
	CAML U,@USRHI
	 BARF J,CPOPJ,BAD CIRCULAR POINTER ,N,LASTCP, AT ,N,LASTBP
	ROT TT,-1
	ADD U,UPGCP
	ADD U,TT
	SKIPL TT
	 HRLI U,222200
	SKIPGE TT
	 HRLI U,002200
	MOVE T,U
	MOVEI B,0
	JRST POPJ1

CPBP1:	TRNE T,200000
	 JRST CPBP2
	TRZ T,400000
	IDIVI T,2000
	CAMGE T,NMMP
	 SKIPN MMPPNT(T)
	  BARF J,CPOPJ,MMP CP OUT OF BOUNDS
	ADD TT,MMPPNT(T)
	HRLI TT,002200
	MOVE T,TT
	MOVEI B,2
	JRST POPJ1

CPBP2:	TRZ T,600000
	CAML T,TSYSM
	 BARF J,CPOPJ,MEM CP OUT OF BOUNDS
	EXCH T,A
	LDB TT,MUR
	CAMN TT,MURUSR
	 JRST CPBP3
	MOVE A,T
	BARF MEM CP POINTS TO PAGE #,N,A, NOT USER
	POPJ P,

CPBP3:	ADD A,MEMPNT
	HRLI A,002200
	EXCH A,T
	MOVEI B,1
	JRST POPJ1

...LIT:	CONSTANTS

END IFD
