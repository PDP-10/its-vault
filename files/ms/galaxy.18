TITLE GALAXY

TS==0
TENF==TS

DEFINE CONC A,B
A!B!TERMIN

DEFINE PT A
	PTUUO [ASCIZ \A\]
TERMIN

IFE TENF,[
IRP X,,[FAD,FSB,FMP,FDV]
DEFINE	X!RI A,B
	X!R A,[(B)]
TERMIN
TERMIN
]

O=0
A=1
B=2
C=3
D=4
E=5
F=6
G=7
H=10
I=11
J=12
K=13
L=14
M=15
N=16
P=17

LPDL==100

NPTS==200.

ZZ==.
LOC 41
	JSR UUOH
LOC ZZ

MFLG==100

IFE TS,[
RON==1

DISON==1
DISRED==2
FDIS==4

CLKCHN==7
TTYCHN==5
DISCHN==6

DDT=34000

TTIL==100
TTOL==100

ADM==574	;A/D MULTIPLEXOR
ADMDON==10	;DONE FLAG
ADMPK==40	;PACK MODE
ADMSEQ==100	;SEQUENCING
ADMTST==200	;TEST MODE
ADMACC==SETZ	;ACCESSIBILITY
REPEAT 8,CONC NPB,\.RPCNT,==CONC 7,\.RPCNT,

ZZ==.
LOC 40+2*CLKCHN
	JSR CLKBRK

LOC 40+2*TTYCHN
	JSR TTYBRK

LOC 40+2*DISCHN
	BLKO DIS,DBLKO'
	JSR DISBRK
LOC ZZ
]

X:	BLOCK NPTS
Y:	BLOCK NPTS
Z:	BLOCK NPTS
R1:	BLOCK NPTS	;EFFECTIVE RADIUS FROM GAL 1
R2:	BLOCK NPTS	;EFFECTIVE RADIUS FROM GAL 2
U:	BLOCK NPTS
V:	BLOCK NPTS
W:	BLOCK NPTS

X1:	-250.0
X2:	250.0
Y1:	-250.0
Y2:	250.0
Z1:	0
Z2:	0
U1:	1.0
U2:	-1.0
V1:	0.5
V2:	-0.5
W1:	0
W2:	0
M1:	100.0
M2:	100.0
RAD1:	25.0
RAD2:	25.0
RAD21:	0
RAD22:	0
SRM1:	0
SRM2:	0
R:	0	;DISTANCE BETWEEN 1 AND 2

GO:	MOVEI P,PDL-1
	MOVEI A,3000
	MOVEM A,DISLPT
	MOVE A,[DISLPT,,DISLPT+1]
	BLT A,DISL+LDISL-1
IFN TS,[.DSTART [-LDISL,,DISL-1]
	.VALUE
]
IFE TS,[
	CONO PI,11577
	CONO 633550+CLKCHN
	MOVSI O,RON
	SETZM TTOCNT'
	MOVE A,TTOI
	MOVEM A,TTOP'
	MOVEM A,ITTOP'
	SETZM TTICNT'
	MOVE A,TTII
	MOVEM A,TTIP'
	MOVEM A,OTTIP'
	CONO TTY,3600+TTYCHN
	MOVEI A,1
	MOVEM A,ITIME'
	MOVNM A,TIME'
	CONI ADM,A
	TRNE A,ADMTST
	PT ADM TEST MODE SWITCH ON
	SKIPL A
	PT ADM INACCESSIBLE
	CONO ADM,0
	DATAO ADM,[NPB0]
	CONO PI,2200+1_<7-CLKCHN>+1_<7-DISCHN>+1_<7-TTYCHN>
]
	PUSHJ P,DPINIT	;INITIALIZE DISPLAY PARAMETERS
	MOVE A,M1
	PUSHJ P,SQRT
	MOVEM B,SRM1
	MOVE A,M2
	PUSHJ P,SQRT
	MOVEM B,SRM2
	MOVE A,RAD1
	FMPR A,A
	MOVEM A,RAD21
	MOVE A,RAD2
	FMPR A,A
	MOVEM A,RAD22
	PUSHJ P,INCIRC
	PUSHJ P,DISGEN

STEP:
IFE TS,[	PUSHJ P,POTCVT
	PUSHJ P,DPINIT
]	MOVE A,U1	;UPDATE X1,Y1,Z1,ETC.
	FADRB A,X1
	MOVE B,V1
	FADRB B,Y1
	MOVE C,W1
	FADRB C,Z1
	MOVE D,U2
	FADRB D,X2
	MOVE E,V2
	FADRB E,Y2
	MOVE F,W2
	FADRB F,Z2
	FSBRB A,D
	FSBRB B,E
	FSBRB C,F
	FMPR D,D
	FMPR E,E
	FMPR F,F
	FADR D,E
	FADR D,F	;R^2
	SKIPN E,R
	MOVE E,D
SQRTR:	MOVE F,E
	FDVRM D,E
	FADRM F,E
	FSC E,-1
	CAME F,E
	JRST SQRTR
	MOVEM E,R	;R
	FMPR F,D	;R^3
	MOVE G,A
	MOVE H,B
	MOVE I,C
	MOVN M,M2
	FDVR M,F
	FMPR A,M
	FMPR B,M
	FMPR C,M
	FADRM A,U1
	FADRM B,V1
	FADRM C,W1
	MOVE M,M1
	FDVR M,F
	FMPR G,M
	FMPR H,M
	FMPR I,M
	FADRM G,U2
	FADRM H,V2
	FADRM I,W2
	MOVE A,X1
	MOVE B,Y1
	MOVE C,Z1
	PUSHJ P,DISPT
	MOVEM H,DISGL1
	MOVE A,X2
	MOVE B,Y2
	MOVE C,Z2
	PUSHJ P,DISPT
	MOVEM H,DISGL2
	MOVSI N,-NPTS	;UPDATE X(N),Y(N),Z(N),...

UPDATE:	MOVE A,U(N)
	FADRB A,X(N)
	MOVE B,V(N)
	FADRB B,Y(N)
	MOVE C,W(N)
	FADRB C,Z(N)
	PUSHJ P,DISPT	;DISPLAY
	MOVEM H,DISLPT(N)
	MOVE D,A	;NEW RADII
	FSBR A,X1
	MOVE G,A
	FSBR D,X2
	MOVE J,D
	FMPR G,G
	FMPR J,J
	MOVE E,B
	FSBR B,Y1
	MOVE H,B
	FSBR E,Y2
	MOVE K,E
	FMPR H,H
	FMPR K,K
	MOVE F,C
	FSBR C,Z1
	MOVE I,C
	FSBR F,Z2
	MOVE L,F
	FMPR I,I
	FMPR L,L
	FADR G,H
	FADR G,I	;R1^2
	FADR G,RAD21
	FADR J,K
	FADR J,L	;R2^2
	FADR J,RAD22
	SKIPN H,R1(N)
	MOVE H,G
SQRTR1:	MOVE I,H
	FDVRM G,H
	FADRM I,H
	FSC H,-1
	CAME I,H
	JRST SQRTR1
	MOVEM H,R1(N)
	SKIPN K,R2(N)
	MOVE K,J
SQRTR2:	MOVE L,K
	FDVRM J,K
	FADRM L,K
	FSC K,-1
	CAME L,K
	JRST SQRTR2
	MOVEM K,R2(N)
	PUSHJ P,FORCE	;A/  X-X1
			;B/  Y-Y1
			;C/  Z-Z1
			;D/  X-X2
			;E/  Y-Y2
			;F/  Z-Z2
			;G/  R1^2
			;H/  R1
			;I/  R1
			;J/  R2^2
			;K/  R2
			;L/  R2
	FADRM A,U(N)
	FADRM B,V(N)
	FADRM C,W(N)
	AOBJN N,UPDATE
	JRST STEP

FORCE:	FMPR H,G
	MOVN M,M1
	FDVR M,H
	FMPR A,M
	FMPR B,M
	FMPR C,M
	FMPR K,J
	MOVN M,M2
	FDVR M,K
	FMPR D,M
	FMPR E,M
	FMPR F,M
	FADR A,D
	FADR B,E
	FADR C,F
	POPJ P,

INCIRC:	MOVSI N,-NPTS
	MOVEI K,1
INCIRL:	MOVE A,PHI(K)
	PUSHJ P,SIN
	MOVE H,A	;SIN PHI
	MOVE A,PHI(K)
	PUSHJ P,COS
	MOVE D,A
	MOVE F,A
	MOVE A,THETA(K)
	PUSHJ P,SIN
	MOVE E,A	;SIN THETA
	FMPR F,A	;COS PHI * SIN THETA
	MOVE A,THETA(K)
	PUSHJ P,COS
	FMPR D,A	;COS PHI * COS THETA
	MOVE G,A	;COS THETA

	MOVEI M,NPTS/8
	MOVSI A,(25.0)
	PUSHJ P,RCIRC
	MOVSI A,(50.0)
	PUSHJ P,RCIRC
	MOVSI A,(75.0)
	PUSHJ P,RCIRC
	MOVSI A,(100.0)
	PUSHJ P,RCIRC
	SOJGE K,INCIRL
	POPJ P,

RCIRC:	MOVEM A,RADIUS
	PUSHJ P,CIRCV0
	MOVE A,M
	TLC A,232000
	FAD A,A
	MOVE B,[6.2831853072]	;2*PI
	FDVR B,A
	MOVEM B,DALPHA
	SETZM ALPHA
	IMUL M,[-1,,]
CIRCLP:	PUSHJ P,CIRCPT
	AOBJP N,CPOPJ
	MOVE A,DALPHA
	FADRM A,ALPHA
	AOBJN M,CIRCLP
	POPJ P,

CIRCV:	MOVE A,RADIUS	;V = SQRT(M1)*R/(R^2+A^2)^(3/4))
CIRCV0:	MOVE L,SRM1(K)
	FMPR L,A
	FMPR A,A
	FADR A,RAD21(K)
	PUSHJ P,SQRT
	FMPR A,B
	PUSHJ P,SQRT
	FDVR L,B
	POPJ P,

CIRCPT:	MOVE A,ALPHA
	PUSHJ P,SIN
	MOVE I,A
	MOVE A,ALPHA
	PUSHJ P,COS
	MOVE J,A
	FMPR A,D
	MOVE B,I
	FMPR B,E
	FSBR A,B
	FMPR A,RADIUS
	FADR A,X1(K)
	MOVE B,J
	FMPR B,F
	MOVE C,I
	FMPR C,G
	FADR B,C
	FMPR B,RADIUS
	FADR B,Y1(K)
	MOVE C,J
	FMPR C,H
	FMPR C,RADIUS
	FADR C,Z1(K)
	MOVEM A,X(N)	;X0 + R * (COS PHI * COS THETA * COS ALPHA - SIN THETA * SIN ALPHA)
	MOVEM B,Y(N)	;Y0 + R * (COS PHI * SIN THETA * COS ALPHA + COS THETA * SIN ALPHA)
	MOVEM C,Z(N)	;Z0 + R * (SIN PHI * COS ALPHA)
	MOVN A,I
	FMPR A,D
	MOVE B,J
	FMPR B,E
	FSBR A,B
	FMPR A,L
	FADR A,U1(K)
	MOVN B,I
	FMPR B,F
	MOVE C,J
	FMPR C,G
	FADR B,C
	FMPR B,L
	FADR B,V1(K)
	MOVN C,I
	FMPR C,H
	FMPR C,L
	FADR C,W1(K)
	MOVEM A,U(N)	;U0 + V * (- COS PHI * COS THETA * SIN ALPHA - SIN THETA * COS ALPHA)
	MOVEM B,V(N)	;V0 + V * (- COS PHI * SIN THETA * SIN ALPHA + COS THETA * COS ALPHA)
	MOVEM C,W(N)	;W0 + V * (- SIN PHI * SIN ALPHA)
	POPJ P,

PHI:	0	;CONE ANGLE OF SPIN AXIS
	0
THETA:	0	;AZIMUTHAL ANGLE OF SPIN AXIS
	0
RADIUS:	0
ALPHA:	0	;ANGLE ABOUT SPIN AXIS
DALPHA:	0

LENGTH:	FMPR A,A
	FMPR B,B
	FMPR C,C
	FADR A,B
	FADR A,C
SQRT:	MOVSI B,(1.0)
SQRT0:	MOVE C,B
	FDVRM A,B
	FADRM C,B
	FSC B,-1
	CAME C,B
	JRST SQRT0
	POPJ P,

LOG:	JUMPE A,LOGOF0	;LOG BASE 2
	MOVEM B,LOGT1'
	ASHC A,-33
	FSC A,233
	FSBRI A,(128.5)
	MOVEM A,LOGT2'
	ASH B,-10
	TLC B,200000
	MOVE A,B
	FADR A,[0.707106781187]	;SQRT 2
	FSBR B,[0.707106781187]
	FDVR B,A
	MOVEM B,LOGT3'
	FMPR B,B
	MOVE A,[0.5989786496]
	FMPR A,B
	FADR A,[0.9614706323]
	FMPR A,B
	FADR A,[2.8853912903]
	FMPR A,LOGT3
	FADR A,LOGT2
	SKIPA B,LOGT1
LOGOF0:	MOVSI A,400000
	POPJ P,

LOG10:	PUSHJ P,LOG
	FMPR A,[0.3010299957]	;LOG10 2
	POPJ P,

LN:	PUSHJ P,LOG
	FMPR A,[0.69314718056]	;LN 2
	POPJ P,

TENEXP:	FMPR A,[2.302585093]
EXP:	PUSHJ P,EXPM1
	FADRI A,(1.0)
	POPJ P,

T==B

EXPM1:	MOVM T,A
	CAMG T,[154400,,]
	POPJ P,
	FSC A,-1
	PUSHJ P,EXPM1
	MOVE T,A
	FADRI T,(2.0)
	FMPR A,T
	POPJ P,

COS:	FADR A,[1.57079632679]	;PI/2
SIN:	MOVM T,A		;ARG IN A
	CAMG T,[.00017]
	POPJ P,		;SIN X = X, WITHIN 27 BITS
	FDVRI A,(-3.0)
	PUSHJ P,SIN	;SIN -X/3
	FMPR T,T
	FSC T,2
	FADRI T,(-3.0)
	FMPRB A,T	;SIN X = 4(SIN -X/3)^3-3(SIN -X/3)
	POPJ P,		;SIN IN A, SIN OR ABS(SIN) IN T

UUOH:	0
	PUSH P,UUOH
	PUSH P,A
	LDB A,[331100,,40]
	JUMPE A,ZUUO
	JRST 4,DDT
ZUUO:	LDB A,[270400,,40]
	JRST .+1(A)
	JRST 4,DDT
	JRST .PTUUO
	JRST .EPTUUO
	REPEAT 15,JRST 4,DDT

.EPTUUO:	IFE TS,CONO PI,1000+1_<7-CLKCHN>+1_<7-DISCHN>
.PTUUO:	PUSH P,B
	HRRZ B,40
	PUSHJ P,PRINT
	POP P,B
POPAJ:	POP P,A
	POPJ P,

PTUUO=1_27
EPTUUO=2_27

IFE TS,[
DISBRK:	0
	CONO DIS,100
	TRZE O,FDIS
	TRZ O,DISON
	JRST 12,@DISBRK

CLKBRK:	0
	CONSZ 200000
	JRST PDLOV
	CONSO 1000
	JRST 4,DDT
	PUSH P,A
	PUSH P,B
	PUSH P,N
	PUSHJ P,INPOTS
	POP P,N
	POP P,B
	AOSGE TIME
	JRST CLKBK2
;	TLNE O,RON
	JRST .+3
	TRZN O,DISRED
	AOSA A,ITIME
	MOVE A,ITIME
	MOVNM A,TIME
	TRO O,FDIS
CLKBK3:	MOVE A,[-LDISL,,DISL-1]
	MOVEM A,DBLKO
	CONO DIS,100+TTYCHN_3+DISCHN
CLKBK1:	POP P,A
	CONO 1000+CLKCHN
	JRST 12,@CLKBRK

CLKBK2:	CONSZ DIS,77
	JRST CLKBK1
	JRST CLKBK3

PDLOV:	JUMPGE P,PSCREW
	EPTUUO [ASCIZ \
RANDOM PDL OV\]
	JRST 10,.
PSCREW:	EPTUUO [ASCIZ \
PCE\]
	JRST 10,.

TTYBRK:	0
	CONSO DIS,400
	JRST TTYB1
	TRZE O,FDIS
	TRZ O,DISON
	CONO DIS,100
TTYB1:	CONSZ DIS,5000
	CONO DIS,200+TTYCHN_3+DISCHN
	PUSH P,A
TTI:	CONSO TTY,40
	JRST TTO
	DATAI TTY,A
	ANDI A,177
	CAIN A,^D
	JRST DDT
	CAIN A,^R
	JRST TRON.
	HRRM A,TTI1
	MOVEI A,5*TTIL
	CAMG A,TTICNT
	JRST PQ
TTI1:	MOVEI A,.
	IDPB A,TTIP
	AOS TTICNT
	MOVE A,TTIP
	CAMN A,TTIPND
	HRRI A,TTIBUF-1
	HRRM A,TTIP
TTO:	CONSZ TTY,20
	JRST POPAR
	SOSGE TTOCNT
	JRST NMTTO
	MOVE A,TTOP
	CAMN A,TTOPND
	HRRI A,TTOBUF-1
	HRRM A,TTOP
	ILDB A,TTOP
	DATAO TTY,A
POPAR:	POP P,A
	JRST 12,@TTYBRK
NMTTO:	SETZM TTOCNT
	CONO TTY,200+TTYCHN
	JRST POPAR

PQ:	MOVEI A,7
	DPB A,ITTOP
	JRST TTO

TRON.:	TLO O,RON
	JRST TTO

TTIBUF:	BLOCK TTIL
TTOBUF:	BLOCK TTOL

TTOPND:	10700,,TTOBUF+TTOL-1
TTOI:	10700,,TTOBUF-1
TTIPND:	10700,,TTIBUF+TTIL-1
TTII:	10700,,TTIBUF-1
]

NPRNT:	IDIVI A,10
	HRLM B,(P)
	SKIPE A
	PUSHJ P,NPRNT
	HLRZ A,(P)
	TROA A,60

TYOCR:	MOVEI A,15

TYO:	IFE TS,[
	PUSH P,A
	MOVEI A,5*TTOL
	CAMG A,TTOCNT
	JRST .-1
	MOVE A,ITTOP
	CAMN A,TTOPND
	HRRI A,TTOBUF-1
	HRRM A,ITTOP
	MOVE A,(P)
	ANDI A,177
	CAIGE A,40
	JRST CNTRL
TYO1:	POP P,A
TYO2:	IDPB A,ITTOP
	SKIPG TTOCNT
	AOSA TTOCNT
	AOSA TTOCNT
	CONO PI,4000\1_<7-TTYCHN>
	POPJ P,

CNTRL:	CAIN A,15
	JRST CRLF
CLF:	CAIE A,12
	CAIN A,11
	JRST TYO1
	MOVEI A,"^
	PUSHJ P,TYO
	MOVE A,(P)
	TRO A,100
	PUSHJ P,TYO
	JRST POPAJ

CRLF:	PUSHJ P,TYO2
	MOVEI A,12
	PUSHJ P,TYO
	JRST POPAJ
]
PRINT:	TLOA B,440700
PR1:	PUSHJ P,TYO
	ILDB A,B
	JUMPN A,PR1
	POPJ P,

IFE TS,[
TYI:	SKIPG TTICNT
	JRST .-1
	MOVE A,OTTIP
	CAMN A,TTIPND
	HRRI A,TTIBUF-1
	HRRM A,OTTIP
	ILDB A,OTTIP
	SOS TTICNT
	JRST TYO
]
NUMGTE:	MOVEI A,"=
	PUSHJ P,TYO

NUMGET:	MOVEI B,0
	TLZA O,MFLG
SETMI.:	TLO O,MFLG
NUMG1:	PUSHJ P,TYI
	CAIN A,"-
	JRST SETMI.
	CAIL A,"0
	CAILE A,"9
	JRST NUMG2
	LSH B,3
	ADDI B,-"0(A)
	JRST NUMG1
NUMG2:	TLNE O,MFLG
	MOVNS B
	CAIE A,177
	POPJ P,
	MOVEI A,"?
	PUSHJ P,TYO
	JRST NUMGET
IFE TS,[
INPOTS:	CONO ADM,ADMSEQ
	MOVSI N,-7
INPOTL:	CONSO ADM,ADMDON
	JRST .-1
	DATAI ADM,A
	SUBI A,2000
	TLC A,212000	;10 BIT FRACTION * 1/64
	FAD A,A
	MOVE B,POT0(N)
	FMPRI B,(.984375)	;63/64
	FADR A,B
	MOVEM A,POT0(N)
	AOBJN N,INPOTL
	POPJ P,

POT0:	BLOCK 8

POTCVT:	MOVE A,POT0
	FSC A,15
	MOVEM A,XOFF
	MOVE A,POT0+1
	FSC A,15
	MOVEM A,YOFF
	MOVE A,POT0+2
	FSC A,15
	MOVEM A,ZOFF
	MOVE A,POT0+3
	FSC A,3
	PUSHJ P,EXP
	MOVEM A,SCALE
	MOVE A,POT0+4
	FMPR A,[6.2831853072]	;2*PI
	MOVEM A,TH
	MOVE A,POT0+5
	FMPR A,[6.2831853072]
	MOVEM A,PH
	MOVE A,POT0+6
	FMPR A,[6.2831853072]
	MOVEM A,PS
	POPJ P,
]

DISPT:	MOVE I,[SROT,,D]		;DISPLAY PT AT A,B,C
	BLT I,I
	FMPR D,A
	FMPR E,B
	FMPR F,C
	FADR D,E
	FADR D,F
	FADR D,DISPX
	JUMPL D,NODIS
	CAML D,[1024.0]
	JRST NODIS
	FMPR G,A
	FMPR H,B
	FMPR I,C
	FADR G,H
	FADR G,I
	FADR G,DISPY
	JUMPL G,NODIS
	CAML G,[1024.0]
	JRST NODIS
	MULI D,400
	MULI G,400
	LSH E,-243(D)
	LSH H,-221(G)
	HRRI H,22000(E)
	TLO H,220000
CPOPJ:	POPJ P,

NODIS:	MOVEI H,20000
	POPJ P,

TH:	0	;EULER ANGLES OF ROTATION
PH:	0
PS:	0

SCALE:	1.0
ROTAT:	1.0	;COS TH * COS PH * COS PS - SIN TH * SIN PS
	0.0	;- SIN TH * COS PS - COS TH * SIN PS * COS PH
	0.0	;COS TH * SIN PH
	0.0	;SIN TH * COS PH * COS PS + COS TH * SIN PS
	1.0	;COS TH * COS PS - SIN TH * SIN PS * COS PH
	0.0	;SIN TH * SIN PH
XOFF:	0	;X OFFSET
YOFF:	0	;Y OFFSET
ZOFF:	0	;Z OFFSET
XORG:	512.0	;SCOPE COORDS OF 0,0,0 (AFTER OFFSET)
YORG:	512.0

SROT:	BLOCK 6	;SCALED ROTATE
DISPX:	512.0	;X DISPLACEMENT
DISPY:	512.0	;Y DISPLACEMENT

DPINIT:	MOVE A,TH
	PUSHJ P,SIN
	MOVEM A,SINTH'
	MOVE A,TH
	PUSHJ P,COS
	MOVEM A,COSTH'
	MOVE A,PH
	PUSHJ P,SIN
	MOVEM A,SINPH'
	MOVE A,PH
	PUSHJ P,COS
	MOVEM A,COSPH'
	MOVE A,PS
	PUSHJ P,SIN
	MOVEM A,SINPS'
	MOVE A,PS
	PUSHJ P,COS
	MOVEM A,COSPS'
	MOVE A,COSTH
	FMPR A,COSPH
	FMPR A,COSPS
	MOVE B,SINTH
	FMPR B,SINPS
	FSBR A,B
	MOVEM A,ROTAT
	MOVE A,SINTH
	FMPR A,COSPS
	MOVE B,COSTH
	FMPR B,SINPS
	FMPR B,COSPH
	FADR A,B
	MOVNM A,ROTAT+\1
	MOVE A,COSTH
	FMPR A,SINPH
	MOVEM A,ROTAT+2
	MOVE A,SINTH
	FMPR A,COSPH
	FMPR A,COSPS
	MOVE B,COSTH
	FMPR B,SINPS
	FADR A,B
	MOVEM A,ROTAT+3
	MOVE A,COSTH
	FMPR A,COSPS
	MOVE B,SINTH
	FMPR B,SINPS
	FMPR B,COSPH
	FSBR A,B
	MOVEM A,ROTAT+4
	MOVE A,SINTH
	FMPR A,SINPH
	MOVEM A,ROTAT+5
	MOVE I,[ROTAT,,D]
	BLT I,I
	FMPR D,SCALE
	FMPR E,SCALE
	FMPR F,SCALE
	FMPR G,SCALE
	FMPR H,SCALE
	FMPR I,SCALE
	MOVE A,[D,,SROT]
	BLT A,SROT+5
	FMPR D,XOFF
	FMPR E,YOFF
	FMPR F,ZOFF
	FADR D,E
	FADR D,F
	FADR D,XORG
	MOVEM D,DISPX
	FMPR G,XOFF
	FMPR H,YOFF
	FMPR I,ZOFF
	FADR G,H
	FADR G,I
	FADR G,YORG
	MOVEM G,DISPY
	POPJ P,

DISGEN:	MOVE A,X1
	MOVE B,Y1
	MOVE C,Z1
	PUSHJ P,DISPT
	MOVEM H,DISGL1
	MOVE A,X2
	MOVE B,Y2
	MOVE C,Z2
	PUSHJ P,DISPT
	MOVEM H,DISGL2
	MOVSI N,-NPTS
DISG0:	MOVE A,X(N)
	MOVE B,Y(N)
	MOVE C,Z(N)
	PUSHJ P,DISPT
	MOVEM H,DISLPT(N)
	AOBJN N,DISG0
	POPJ P,

CONSTANTS
VARIABLES

PATCH:	BLOCK 1000

DISL:	20117
DISGL1:	221000,,23000
DISGL2:	221000,,23000
DISLPT:	BLOCK NPTS
	3000
LDISL==.-DISL

PDL:	BLOCK LPDL

END	GO

