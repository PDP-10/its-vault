TITLE	LIFE

TS==1
DDTF==0

IFN TS,[
TYIC==1
TYOC==2
DKIC==3
DKOC==4
]
IFE TS,[PTQCHN==3
TTYCHN==4
FLGCHN==5
DISCHN==6
APRCHN==7
APROFF==1000\1_<7-APRCHN>
APRON==2000\1_<7-APRCHN>
.OPER=42000,,
.CALL=43000,,
.NDIS=.OPER 71
.VALUE=.CALL 4,
.OPEN=41000,,
.CORE=.CALL 6,
]
F=0
A=1
B=2
C=3
D=4
E=5
X=6
Y=7
J=10
K=11
I=12
N=13
R=14
S=15
T=16
P=17

LHTL==11.+TS
HTL==1_<LHTL>
DLL==HTL_<TS-1>
MFSL==HTL/9
FSK==5	;#WDS/PT

ISCALE==20

LPDL==20

CRLF==15_7\12

LOC 41
	JSR UUOH
IFE TS,[
LOC 40+2*PTQCHN
	JSR PTQBRK
LOC 40+2*TTYCHN
	JSR TTYBRK
LOC 40+2*FLGCHN
	JSR FLGBRK
LOC 40+2*DISCHN
	BLKO DIS,DISPTR
	JSR DISBRK
LOC 40+2*APRCHN
	JSR APRBRK
]IFE TS,LOC 64
IFN TS,[
LOC 100
PATCH:	BLOCK 100
]

SCALE:	ISCALE
SCSHIF:	0
XOFF:	0
YOFF:	0
MARGIN:	0
PDL:	BLOCK LPDL

IFN TS,[
FSEXP:	MOVEI T,NBLKS
	.CORE 1(T)
NOCORE:	.VALUE 0
	AOS FSEXP
	LSH T,10.
	HRLI T,(MOVE T,)
	MOVEM T,FSP
	MOVEM I,IM'
	MOVEI I,2000/FSK-1
	ADDI T,FSK
	MOVEM T,-FSK(T)
	SOJG I,.-2
	MOVE I,[JRST FSEXP]
	MOVEM I,(T)
	MOVE I,IM
]
FSP:	MOVE T,FS
	EXCH T,FSP
	POPJ P,

;FS:	BLOCK FSK*FSL
;	JRST FSEXP
;	0
;	0

HT:	BLOCK HTL
EHT1:	BLOCK HTL+1	;LIST OF NON-ZERO HT ENTRIES
EHT2:	BLOCK HTL+1

DLIST:	20157
	BLOCK DLL
EDL1:	BLOCK 16
EDLIST:	3000

KTAB:	BLOCK 22
NTAB:	BLOCK 22

SCOBUF:	BLOCK 16	;SCOPE COMMENT BUFFER
IFE TS,[
TTYBRK:	0
	MOVEM A,TTYAM'
	CONSO TTY,40
	JRST TTO
TTI:	DATAI TTY,A
	ANDI A,177
	IDPB A,TTIP
	AOS TTICNT
	CAIE A,15
	JRST TTI1
	MOVE A,TTIP
	CAMN A,TTIPND
	HRRI A,TTIBUF-1
	HRRM A,TTIP
	MOVEI A,12
	IDPB A,TTIP
	AOS TTICNT
TTI1:	MOVE A,TTIP
	CAMN A,TTIPND
	HRRI A,TTIBUF-1
	HRRM A,TTIP
TTO:	CONSZ TTY,20
	JRST TTYBK1
	SOSGE TTOCNT
	JRST TTO1
	MOVE A,TTOP
	CAMN A,TTOPND
	HRRI A,TTOBUF-1
	HRRM A,TTOP
	ILDB A,TTOP
	DATAO TTY,A
TTYBK1:	MOVE A,TTYAM
	JRST 12,@TTYBRK

TTO1:	SETZM TTOCNT
	CONO TTY,200\TTYCHN
	JRST TTYBK1

TTIL==10
TTOL==10

TTICNT:	0
TTOCNT:	0

TTIBUF:	BLOCK TTIL
TTOBUF:	BLOCK TTOL

TTOPND:	10700,,TTOBUF+TTOL-1
TTOP:	10700,,TTOBUF-1
TTIPND:	10700,,TTIBUF+TTIL-1
TTIP:	10700,,TTIBUF-1
OTTIP:	10700,,TTIBUF-1
ITTOP:	10700,,TTOBUF-1

TYI:	SKIPGE TYIF
	JRST PTYI
	SKIPG TTICNT
	JRST .-1
	MOVE A,OTTIP
	CAMN A,TTIPND
	HRRI A,TTIBUF-1
	HRRM A,OTTIP
	ILDB A,OTTIP
	SOS TTICNT
TYO:	PUSH P,A
	MOVEI A,5*TTOL
	CAMG A,TTOCNT
	JRST .-1
	MOVE A,ITTOP
	CAMN A,TTOPND
	HRRI A,TTOBUF-1
	HRRM A,ITTOP
	MOVE A,(P)
	ANDI A,177
	CAIGE A,40
	JRST CNTRL
TYO1:	POP P,A
TYO2:	IDPB A,ITTOP
	SKIPG TTOCNT
	AOSA TTOCNT
	AOSA TTOCNT
	CONO PI,4000\1_<7-TTYCHN>
	POPJ P,

CNTRL:	CAIE A,15
	CAIN A,12
	JRST TYO1
	MOVEI A,"^
	PUSHJ P,TYO2
	MOVE A,(P)
	ADDI A,100
	PUSHJ P,TYO
	POP P,A
	POPJ P,

FLGBRK:	0
	CONSZ DIS,400
	JRST FLGBK1
	CONO DIS,200\FLGCHN_3\DISCHN
	JRST 12,@FLGBRK
FLGBK1:	CONO DIS,0
	JRST 12,@FLGBRK

DISBRK:	0
	CONO DIS,0
	JRST 12,@DISBRK

APRBRK:	0
	CONSO APR,1000
	JRST APRBK1
	SKIPL E
	AOS TIME
	CONSZ DIS,7
	JRST APRBK1
	MOVEM A,DISPTR
	MOVE A,DISIPT
	EXCH A,DISPTR
	CONO DIS,100\FLGCHN_3\DISCHN
APRBK1:	CONO APR,403440+APRCHN
	JRST 12,@APRBRK

DISIPT:	-1,,EDLIST-1
DISPTR:	-1,,EDLIST-1
PTQBRK:	0
	MOVEM A,PTQAM'
	CONSO PTR,10
	JRST PTO
PTI:	DATAI PTR,A
	JUMPE A,PTQBK1
	IDPB A,PTIP
	AOS A,PTICNT
	CAIL A,5*PTIL
	CONO PTR,0
	MOVE A,PTIP
	CAMN A,PTIPND
	HRRI A,PTIBUF-1
	HRRM A,PTIP
PTO:	CONSZ PTP,20
	JRST PTQBK1
	SOSGE PTOCNT
	JRST PTO1
	MOVE A,PTOP
	CAMN A,PTOPND
	HRRI A,PTOBUF-1
	HRRM A,PTOP
	ILDB A,PTOP
	DATAO PTP,A
PTQBK1:	MOVE A,PTQAM
	JRST 12,@PTQBRK

PTO1:	SETZM PTOCNT
	CONO PTP,PTQCHN
	JRST PTQBK1

PTROFF:	SETZM TYIF
	CONO PTR,400
	JRST TYI

PTIL==20
PTOL==20

PTICNT:	0
PTOCNT:	0

PTIBUF:	BLOCK PTIL
PTOBUF:	BLOCK PTOL

PTOPND:	10700,,PTOBUF+PTOL-1
PTOP:	10700,,PTOBUF-1
PTIPND:	10700,,PTIBUF+PTIL-1
PTIP:	10700,,PTIBUF-1
OPTIP:	10700,,PTIBUF-1
IPTOP:	10700,,PTOBUF-1

PTYI:	SKIPLE PTICNT
	JRST PTYI1
	CONSO PTR,7
	CONO PTR,10+PTQCHN
	MOVSI A,1
	SOJL A,PTROFF
	SKIPG PTICNT
	JRST .-2
PTYI1:	MOVE A,OPTIP
	CAMN A,PTIPND
	HRRI A,PTIBUF-1
	HRRM A,OPTIP
	ILDB A,OPTIP
	SOS PTICNT
	POPJ P,

PTYO:	PUSH P,A
	MOVEI A,5*PTOL
	CAMG A,PTOCNT
	JRST .-1
	MOVE A,IPTOP
	CAMN A,PTOPND
	HRRI A,PTOBUF-1
	HRRM A,IPTOP
PTYO1:	POP P,A
PTYO2:	IDPB A,IPTOP
	SKIPG PTOCNT
	AOSA PTOCNT
	AOSA PTOCNT
	CONO PI,4000+1_<7-PTQCHN>
	POPJ P,

IFN DDTF,[
PWAT:	SKIPLE PTOCNT
	JRST .-1
	JRST 34000
]
].TYOI=1_33
.DKOI=2_33

UUOH:	0
	MOVEM A,AM'
	LDB A,[331100,,40]
	CAIN A,.TYOI_-33
	JRST ATYOI
	CAIN A,.DKOI_-33
	JRST ADKOI
	JRST 4,.
IFE TS,FSEXP=.-1

ATYOI:	LDB A,[70700,,40]
	SKIPE A
IFN TS,	.IOT TYOC,A
IFE TS,	PUSHJ P,TYO
	LDB A,[700,,40]
IFN TS,	.IOT TYOC,A
IFE TS,	PUSHJ P,TYO
UUEXIT:	MOVE A,AM
	JRST 2,@UUOH

ADKOI:	LDB A,[70700,,40]
	SKIPE A
IFN TS,	.IOT DKOC,A
IFE TS,	PUSHJ P,PTYO
	LDB A,[700,,40]
IFN TS,	.IOT DKOC,A
IFE TS,	PUSHJ P,PTYO
	JRST UUEXIT
GO:	SETZM TYIF'
	SETOM DISF'
	SETOM LUPTF'
	MOVEI N,0
	MOVSI F,200000
	MOVEI P,PDL-1
IFN TS,[.OPEN TYIC,[SIXBIT \   TTY\]
	.VALUE
	.OPEN TYOC,[SIXBIT \  !TTY\]
	.VALUE
REQ0:]
IFE TS,[CONO APR,603440+APRCHN
	CONO PI,12200\1_<7-APRCHN>\1_<7-FLGCHN>\1_<7-DISCHN>\1_<7-TTYCHN>\1_<7-PTQCHN>
	CONO TTY,3600\TTYCHN
	CONO PTP,PTQCHN
REQ0:	CONO PTR,0
]
REQ:	SKIPE TYIF
	JRST GOI
	.TYOI CRLF
	.TYOI "_
	JRST GOI

GO1:	PUSHJ P,DISGO
GOI:	PUSHJ P,TYI
	CAIN A," 
	JRST SPACE
	SETZM PFLAG
	MOVNI E,1
	CAIN A,"E
	JRST EDIT
	CAIN A,"R
	JRST READA
	CAIN A,"W
	JRST WRITE
IFN TS,[CAIN A,"I
	JRST FILEI
	CAIN A,"O
	JRST FILEO
	CAIN A,"C
	JRST CLOSE
]	CAIN A,"K
	JRST KPRINT
	CAIN A,"T
	JRST TPRINT
	CAIN A,"S
	JRST SPRINT
	CAIN A,"Z
	JRST ZPRINT
	MOVEI B,1
	CAIN A,"X
	JRST SETXF
	CAIN A,"Y
	JRST SETYF
	CAIN A,"D
	JRST IN
	CAIN A,^S
IFN TS,	.VALUE
IFE TS,[
IFN DDTF,JRST PWAT
IFE DDTF,JRST QUEST
]	CAIN A,^D
	JRST SDISF
	CAIN A,^E
	JRST CDISF
	CAIN A,^O
	JRST ZLUPTF
	CAIN A,^P
	JRST SLUPTF
	CAIN A,^Q
	JRST CLUPTF
	CAIE A,15
	CAIN A,12
	JRST GOI
	CAIN A,14
	JRST GOI
	CAIN A,";
	JRST COMMNT
	CAIN A,":
	JRST SCOMNT
	CAIN A,"P
	JRST PROCED
	PUSHJ P,NUMGT0
	JRST QUEST
;	CAIN A,^T
;	JRST TORSET
	CAIN A,^F
	JRST SETFRM
	CAIN A,"S
	JRST SETSCL
	CAIN A,"X
	JRST SETXF
	CAIN A,"Y
	JRST SETYF
	CAIN A,",
	JRST COMMA
	CAIN A,"R
	JRST READ
	CAIE A,"P
	JRST QUEST
	JUMPGE B,GO1A
	ADD B,K
	MOVNS B
	JUMPGE B,GO1A
	MOVEI B,0
GO1A:	SOSGE E,B
	JRST DISGN0
	SETOM PFLAG	;FALLS THRU
STEP:	PUSHJ P,FRAMES	;FALLS IN
IFN TS,	.LISTEN A,
IFE TS,	MOVE A,TTICNT
	JUMPN A,TTYRD
	JUMPE N,KPRINT
	SETZB I,N
STEP1:	SKIPN J,EHT1(I)
	JRST STEP2
	MOVE J,(J)
STEP1A:	SKIPN X,4(J)
	JRST STEP1B
	MOVE A,1(J)
	MOVE C,X
	ADDI C,10
	ASH C,-4
	MOVE D,X
	LSH D,4
	ADD C,D
	ADDM C,3(J)
	IORM F,2(J)
	ADD C,X
	HRRI A,1(A)
	PUSHJ P,UPDATE
	HRRI A,-2(A)
	PUSHJ P,UPDATE
	MOVE C,X
	ADD C,[1_37]
	ASH C,-40
	JUMPE C,STEP1C
	SUB A,[11,,]
	PUSHJ P,UPDATE
	HRRI A,1(A)
	PUSHJ P,UPDATE
	HRRI A,1(A)
	PUSHJ P,UPDATE
	LSH X,40
	JUMPE X,STEP1B
	MOVE C,X
	ADD A,[22,,]
	PUSHJ P,UPDATE
	HRRI A,-1(A)
	PUSHJ P,UPDATE
	HRRI A,-1(A)
STEP1D:	PUSHJ P,UPDATE
STEP1B:	SKIPE J,(J)
	SKIPGE 2(J)
	AOJA I,STEP1
	JRST STEP1A

STEP1C:	LSH X,40
	JUMPE X,STEP1B
	MOVE C,X
	ADD A,[11,,]
	PUSHJ P,UPDATE
	HRRI A,1(A)
	PUSHJ P,UPDATE
	HRRI A,1(A)
	JRST STEP1D
STEP2:	PUSH R,J	;0
LUPCHK:	SKIPGE LUPTF
	JRST LUPCKE
	SETZB I,N
LUPCA:	SKIPN J,EHT2(I)
	JRST LUPCE
LUPCB:	SKIPN J,(J)
	AOJA I,LUPCA
	SKIPGE B,2(J)
	AOJA B,.+2
	TLZ B,200000
	ADD B,3(J)
	JUMPE B,LUPCB
LUPCL:	MOVEI A,0
	LSHC A,4
	ADD N,LUPCT(A)
	JUMPN B,LUPCL
	JRST LUPCB

LUPCT:	0
	1_0
	1_4
	1_10
	1_14
	1_20
	1_24
	1_30
	1_34
	1_40

LUPCE:	MOVE A,K
	SKIPG LUPTF
	JRST LUPCKS
	MOVE D,K
	MOVEI C,0
LUPCKL:	JUMPE D,LUPCKS
	CAMN N,NTAB(C)
	JRST LUPCKM
LUPCKA:	LSH D,-1
	AOJA C,LUPCKL

LUPCKM:	SUB A,KTAB(C)
	MOVE X,A
	.TYOI CRLF
	PUSHJ P,DECP
	.TYOI "P_7\"?
IFN TS,	.IOT TYIC,B
IFE TS,[PUSHJ P,TYI
	MOVE B,A
]	MOVE A,K
	CAIE B,"Y
	JRST LUPCKA
	SETZM DISF
	SETZM LUPTF
	SETZM TYIF
	SOS E,X

LUPCKS:	ANDCAI A,1(A)
	FAD A,A
	LDB B,[330500,,A]
	MOVEM N,NTAB-7(B)
	MOVEM K,KTAB-7(B)
LUPCKE:	SETZB I,N
	MOVEI R,EHT2-1
	MOVEI X,EHT1-1
STEP2A:	SKIPN J,EHT2(I)
	AOJA K,DONE
STEP2B:	SKIPN S,(J)
	JRST STEP2C
STEP2D:	SKIPGE T,2(S)	;ONNESS
	AOJA T,STEP2E
	TLZN T,200000
	JRST UNREF
STEP2E:	SKIPN A,3(S)	;COUNT
	JRST CNT0
	MOVE B,A
	LSH B,-1	;2 BIT
	MOVE C,B
	LSH C,-1	;4 BIT
	AND A,[42104210421]
	IOR A,T
	AND B,A
	ANDCMB B,C
	MOVEM C,2(S)
	ANDCM C,T
	ANDCM T,B
	SUB C,T
	JUMPE C,UNREF1
	MOVEM C,4(S)
	TRO F,3
	IDIVI B,17
	ADD N,C
	MOVE J,S
	JRST STEP2B

UNREF:	SKIPN 3(S)
	JRST GC0
	MOVE B,T
UNREF1:	SETZM 4(S)
	TRO F,2
	IDIVI B,17
	ADD N,C
	MOVE J,S
	JRST STEP2B

CNT0:	JUMPE T,GC0
	SETZM 2(S)
	MOVNM T,4(S)
	TRO F,3
	MOVE J,S
	JRST STEP2B

GC0:	MOVE T,S
	MOVE S,(S)
	MOVEM S,(J)
GC:	EXCH T,FSP
	MOVEM T,@FSP
	JUMPN S,STEP2D
STEP2C:	TRZE F,2
	PUSH R,EHT2(I)
	TRZE F,1
	PUSH X,EHT2(I)
	AOJA I,STEP2A

DONE:	SETZM 1(R)
	SETZM 1(X)
	SKIPL DISF
	JUMPG E,DONE1
	PUSHJ P,DISGEN
	PUSHJ P,DISGO
DONE1:	SOJGE E,STEP
	AOSE PFLAG
	JRST GOI
	JRST REQ
FRAMES:	MOVEI A,	;FRAMES OF FILM PER TICK OF LIFE
	JUMPE A,FRAM2
	MOVEI B,1
	.NDIS B,
	JFCL
FRAM1:	PUSHJ P,FROPEN
NNDISS:	MOVEI B,1	;NUMBER TO .NDIS (CYCLES OF DISPLAY)
	.NDIS B,
	JFCL
	MOVNI B,1
	.NDIS B,	;HANGS UNTIL DISPLAY IS DONE
	.VALUE
	PUSHJ P,FROPEN
	SOJG A,FRAM1
	HRLOI B,377777
	.NDIS B,	;START DISPLAY UP AGAIN
	.VALUE
FRAM2:	POPJ P,

LEADER:	MOVEI P,PDL-1
	MOVEI A,310	;5 FEET
	PUSHJ P,FROPEN
	PUSHJ P,FROPEN
	SOJG A,.-2
	JRST .

;CCW = FWD = 0, 100, 300, 200, 0, ...
FROPEN:	PUSH P,A
	PUSH P,B
	MOVEI A,62	;25. COMPLETE CYCLES = 1/2 REVOLUTION
OPEN2:	MOVEI B,100
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,1400	;1400 IN PDP-10 DROPS STEP OCCASIONALLY
	SOJG B,.
	MOVEI B,200
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,1400
	SOJG B,.
	SOJG A,OPEN2
	POP P,B
	POP P,A
	POPJ P,

MOTOR:	0	;LAST DATAO TO MOTOR
EDIT:	MOVEI I,0	;PUT WORLD IN INITIAL DATA FORMAT
	MOVEI R,EHT2-1
	TRZ F,1
EDIT1A:	SKIPN J,EHT2(I)
	JRST INPUT2
EDIT1B:	SKIPN S,(J)
	JRST EDIT1C
EDIT1D:	SKIPE T,2(S)
	JRST EDIT1E
	MOVE T,S
	MOVE S,(S)
	MOVEM S,(J)
	EXCH T,FSP
	MOVEM T,@FSP
	JUMPN S,EDIT1D
EDIT1C:	TRZE F,1
	PUSH R,EHT2(I)
	AOJA I,EDIT1A

EDIT1E:	SETZM 3(S)
	MOVEM T,4(S)
	TRO F,1
	MOVE J,S
	JRST EDIT1B

;P PICK POINTS, M MOVE, C COPY, D DELETE, R ROTATE, F FLIP. T TIME STOP, S, X, Y
SETFRM:	HRRM B,FRAMES
	JRST GOI

;TORSET:	MOVEI A,1
;	LSH A,(B)
;	SOS A
;	HRLS A
;	MOVEM A,TORSIZ
;	JRST GOI

SETSCL:	JUMPLE B,SETSC2
	MOVEM B,SCALE
	JRST DISGN0
SETXF:	ADDM B,XOFF
	JRST DISGN0
SETYF:	ADDM B,YOFF
	JRST DISGN0
SETSC2:	HRRZM B,SCSHIF
	JRST DISGN0

SETXYF:	ADDM X,XOFF
	ADDM Y,YOFF
	JRST DISGN0

PROCED:	MOVSI B,1
	SETOM DISF
	JRST GO1A

SPACE:	AOSE PFLAG'
	AOJA E,STEP
	MOVNI A,1(E)
	SUB A,K
	PUSHJ P,SDECP
	.TYOI "P_7\"?
	MOVNI E,1
	JRST REQ

SDISF:	SETOM DISF
	JRST GOI

CDISF:	SETZM DISF
	JRST GOI

ZLUPTF:	SETOM LUPTF
	JRST GOI

SLUPTF:	SETZM LUPTF
	AOSA LUPTF

CLUPTF:	SETZM LUPTF
	JRST GOI

COMMA:	MOVE X,B
	PUSHJ P,NUMGET
	JRST QUEST
	MOVE Y,B
	CAIN A,"Z
	JRST SETXYF

QUEST:	.TYOI "?
TTYRD:
IFN TS,[SKIPN TYIF
	JRST GOI
]	SETZM TYIF
IFN TS,	SOS RCNT
	JRST GOI

KPRINT:	.TYOI CRLF
	MOVE A,K
	PUSHJ P,DECP
	.TYOI 40
	MOVE A,N
	PUSHJ P,DECP
	.TYOI CRLF
	JRST GOI

SPRINT:	.TYOI CRLF
	MOVE A,SCALE
	PUSHJ P,DECP
	HRRE A,SCSHIF
	JUMPE A,SPRIN1
	.TYOI "_
	PUSHJ P,SDECP
SPRIN1:	.TYOI CRLF
	JRST GOI

ZPRINT:	.TYOI CRLF
	MOVE A,XOFF
	PUSHJ P,SDECP
	.TYOI ",
	MOVE A,YOFF
	PUSHJ P,SDECP
	.TYOI CRLF
	JRST GOI

TPRINT:	.TYOI CRLF
IFN TS,[.SUSET [.RRUNT,,A]
	SUB A,TIME'
	IDIVI A,6_12.
]
IFE TS,[MOVE A,TIME
	IDIVI A,6
]
	PUSHJ P,DECP
	.TYOI CRLF
	JRST GOI
DISGO:	MOVEM D,PTDEND
	PUSH D,[221747,,61617]
	MOVE A,K
	MOVEI C,5
	MOVE I,[600,,(D)]
	PUSHJ P,DISNUM
	MOVEI A,37
	IDPB A,I
	ADD D,[1,,1]
	PUSH D,[20015]
	PUSH D,[220000,,61617]
	MOVE I,[600,,(D)]
	MOVE A,N
	MOVEI C,5
	PUSHJ P,DISNUM
	MOVEI A,34	;340 CR
	MOVE B,[440700,,SCOBUF]
DISGO2:	IDPB A,I
	ILDB A,B
	JUMPN A,DISGO2
	MOVEI A,37
	MOVEI B,5
	IDPB A,I
	SOJG B,.-1
DISGO1:
IFN TS,[.DSTART [DLIST-EDLIST-2,,DLIST-1]
	.VALUE 
]
IFE TS,[MOVE A,[DLIST-EDLIST-2,,DLIST-1]
	MOVEM A,DISIPT
	CONO PI,APROFF
	CONO DIS,0
	MOVEM A,DISPTR
	CONO DIS,100\FLGCHN_3\DISCHN
	CONO PI,APRON
]	POPJ P,

DISG0:	SETZM 1(R)
	MOVE A,[EHT2,,EHT1]
	BLT A,EHT1-EHT2+1(R)
	SETZM PFLAG
IFN TS,	.SUSET [.RRUNT,,TIME]
IFE TS, SETZM TIME'
DISGN0:	PUSH P,[GO1]
DISGEN:
IFN TS,	.DSTOP
IFE TS,[MOVEI A,[3000]-1
	HRROM A,DISIPT
]	MOVEI I,0
	MOVE D,[DLIST-EDL1+1+5,,DLIST]
DISG1:	SKIPN J,EHT2(I)
	POPJ P,
	MOVE J,(J)
DISG1A:	MOVE A,1(J)
	SKIPE T,2(J)
	PUSHJ P,DISADW
DISG1B:	SKIPE J,(J)
	JRST DISG1A
	AOJA I,DISG1

DISNM1:	SOJLE C,CPOPJ
	SKIPN A
	SKIPA B,[20]
DISNUM:	IDIVI A,10.
	HRLM B,(P)
	PUSHJ P,DISNM1
	HLRZ A,(P)
	TRC A,60
	IDPB A,I
	POPJ P,

DISADW:	JUMPG D,CPOPJ
	HRRE C,A
	ADD C,YOFF
	IMUL C,SCALE
	ASH C,@SCSHIF
	ADDI C,1000
	TDNE C,[777777,,776000]
	POPJ P,
	MOVSI C,220000(C)
	HLRE B,A
	ADD B,XOFF
	IMUL B,SCALE
DISAD1:	TLNE T,40000
	PUSHJ P,DISADX
	JUMPG D,CPOPJ
	LSH T,4
	ADD B,SCALE
	JUMPN T,DISAD1
	POPJ P,

DISADX:	MOVE S,B
	ASH S,@SCSHIF
	ADDI S,1000
	TDNE S,[777777,,776000]
	POPJ P,
	HRRI C,22000(S)
	PUSH D,C
	POPJ P,

DISAD:	JUMPG D,CPOPJ
	HLRE B,A
	HRRE C,A
	ADD B,XOFF
	ADD C,YOFF
	IMUL B,SCALE
	IMUL C,SCALE
	ASH B,@SCSHIF
	ASH C,@SCSHIF
	ADDI B,1000
	ADDI C,1000
	HRL B,C
	TDZE B,[776000,,776000]
	POPJ P,
	TDO B,[220000,,22000]
	PUSH D,B
	POPJ P,

PTDEND:	DLIST-EDL1+1+5,,DLIST	;END OF PT MODE DISPLAY LIST
COMMNT:	PUSHJ P,TYI
	SKIPGE TYIF
	.TYOI (A)
	CAIE A,15
	JRST COMMNT
	SKIPGE TYIF
	.TYOI 12
	JRST GOI

SCOMNT:	MOVE B,[440700,,SCOBUF]
SCOMN1:	PUSHJ P,TYI
	CAIN A,15
	MOVEI A,0
	IDPB A,B
	JUMPN A,SCOMN1
	JRST DISGN0

READA:	MOVEI B,1
READ:	SETCMB A,TYIF
	JUMPE A,REQ0
	JUMPLE B,UNREAD
READ1:
IFN TS,	AOS RCNT
	SOJLE B,GOI
READ2:	PUSHJ P,TYI
	CAIE A,";
	CAIN A,":
	JRST RCOM
	CAIE A,"R
	JRST READ2
	JRST READ1

IFE TS,	UNREAD==QUEST
IFN TS,[
UNREAD:	XCT OPEN
	JRST QUEST
	ADD B,RCNT
	SETZM RCNT
	JUMPG B,READ1
	JRST QUEST
]
RCOM:	PUSHJ P,TYI
	CAIE A,15
	JRST RCOM
	JRST READ2

WRITE:	.DKOI "E
	MOVE A,K
	PUSHJ P,WDECP
	.DKOI "G
	MOVE A,SCALE
	PUSHJ P,WDECP
	.DKOI "S
	HRRE A,SCSHIF
	PUSHJ P,WDECP
	.DKOI "S_7\";
	.DKOI CRLF
	MOVEI I,0
W1:	SKIPN J,EHT2(I)
	JRST WDONE
	MOVE J,(J)
W1A:	SKIPN Y,2(J)
	JRST W1B
	MOVE X,1(J)
W1D:	TLNN Y,40000
	JRST W1C
	HLRE A,X
	SUB A,XOFF
	PUSHJ P,WDECP
	.DKOI ",
	HRRE A,X
	SUB A,YOFF
	PUSHJ P,WDECP
	.DKOI CRLF
W1C:	LSH Y,4
	ADD X,[1,,]
	JUMPN Y,W1D
W1B:	SKIPE J,(J)
	JRST W1A
	AOJA I,W1

WDONE:	.DKOI "E_7\"R
	.DKOI CRLF
	JRST GOI

WDECP:	JUMPGE A,WDECP1
	.DKOI "-
	MOVNS A
WDECP1:	IDIVI A,10.
	HRLM B,(P)
	SKIPE A
	PUSHJ P,WDECP1
	HLRZ A,(P)
	.DKOI "0(A)
	POPJ P,

IFN TS,[
CLOSE:	.CLOSE DKOC,
	JRST GOI

FILEI:	TDZA I,I
FILEO:	MOVEI I,4
	SKIPN TYIF
	.TYOI " 
FIN1:	PUSHJ P,SYLRD
	JUMPE B,OPENIO
	MOVEM B,NAME2(I)
	JUMPL A,OPENIO
	PUSHJ P,SYLRD
	JUMPE B,OPENIO
	EXCH B,NAME2(I)
	MOVEM B,NAME1(I)
	JUMPGE A,FIN1
OPENIO:	CAMN A,[15-40]
	XCT OPEN(I)
	JRST QUEST
	JUMPN I,GOI
	SETZM RCNT'
	JRST GOI

ISPACE:	JUMPN B,CPOPJ
SYLRD:	MOVEI B,0
	MOVE C,[440600,,B]
SYLRD1:	PUSHJ P,TYI
	SUBI A,40
	JUMPL A,CPOPJ
	JUMPE A,ISPACE
	CAIN A,":-40
	JRST ICOLON
	CAIN A,";-40
	JRST ISEMI
	CAIN A,137
	JRST IRUB
	TLNE C,770000
	IDPB A,C
	JRST SYLRD1

IRUB:	.IOT TYOC,["?]
	JRST SYLRD


ISEMI:	MOVEM B,SYSNAM
	.SUSET [.SSNAME,,SYSNAM]
	MOVSI B,(SIXBIT \DSK\)
ICOLON:	HLRM B,DEVICE(I)
	JRST SYLRD

OPEN:	.OPEN DKIC,.+1
DEVICE:	SIXBIT \   DSK\
NAME1:	SIXBIT \LIFE\
NAME2:	SIXBIT \DATA\
	.OPEN DKOC,.+1
	SIXBIT \  !DSK\
	SIXBIT \LIFE\
	SIXBIT \SAVED\
SYSNAM:	SIXBIT \MS\
]
INIT:	PUSHJ P,TYI
	CAIE A,15
	JRST QUEST
IFN TS,[.CORE NBLKS
	.VALUE
	SKIPGE TYIF
]	PUSHJ P,TYI
	MOVEI I,FSL
	MOVE T,[MOVE T,FS]
	MOVEM T,FSP
	ADDI T,FSK
	MOVEM T,-FSK(T)
	SOJG I,.-2
	MOVE I,[JRST FSEXP]
	MOVEM I,(T)
IFN TS,[MOVEI I,NBLKS
	HRRM I,FSEXP
]	SETZM HT
	MOVE I,[HT,,HT+1]
	BLT I,HT+HTL-1
	SETZM KTAB
	MOVE A,[KTAB,,KTAB+1]
	BLT A,KTAB+21
	SETZM KTAB
	MOVE A,[KTAB,,KTAB+1]
	BLT A,KTAB+21
	MOVEI R,EHT2-1
	MOVEI J,3000
	MOVEM J,DLIST+1
	MOVE D,[DLIST+1,,DLIST+2]
	BLT D,EDLIST
	PUSHJ P,DISGO1
	MOVE D,[DLIST-EDLIST+1,,DLIST]
	SETZB K,SCSHIF
	MOVEI B,ISCALE
	MOVEM B,SCALE
	SETZM MARGIN
	MOVEI N,0
	POPJ P,

IN:	PUSHJ P,INIT
	SETZB X,Y
INPUT:	MOVNM X,XOFF
	MOVNM Y,YOFF
INPUT0:	SKIPGE TYIF
	JRST INPUT1
	SETZM 1(R)
	PUSHJ P,DISGEN
	PUSH D,[220764,,60770]
	PUSH D,[173737,,20000]
	PUSHJ P,DISGO
	MOVN X,XOFF
	MOVN Y,YOFF
INPUT1:	PUSHJ P,TYI
	CAIN A,177
	JRST RUB
	CAIN A,"-
	JRST IN1
	MOVEI B,1
	CAIL A,"0
	CAILE A,"9
	JRST IN2
IN1:	PUSHJ P,NUMGT0
	JRST INRUB
IN2:	MOVMM B,COUNT'
	CAIN A,",
	JRST XSET
	CAIN A," 
	JRST SPACES
	CAIN A,".
	JRST DOTS
	CAIN A,"D
	JRST DELES
	CAIN A,15
	JRST NLINES
	CAIN A,"_
	JRST BACK
	CAIN A,"^
	JRST UP
	CAIN A,12
	JRST DOWN
	CAIGE A,40
	JRST INPUT1
	CAIN A,"X
	JRST SETXOF
	CAIN A,"Y
	JRST SETYOF
	CAIN A,"S
	JRST SETSC
	CAIE A,":
	CAIN A,";
	JRST INCOM
	CAIN A,"G
	JRST GENSTO
	CAIN A,"M
	JRST MSET
	CAIE A,"E
	JRST INRUB
	JRST DISG0

DOTS:	JUMPL B,MDOT
	JUMPE B,0DOT
DOT:	PUSHJ P,STORE0
	SOSLE COUNT
	AOJA X,DOT
	AOJA X,INPUT

0DOT:	PUSHJ P,STORE0
	JRST INPUT

MDOT:	PUSHJ P,STORE0
	SOSLE COUNT
	SOJA X,MDOT
	SOJA X,INPUT

DELES:	JUMPL B,MDELE
	JUMPE B,0DELE
DELE:	PUSHJ P,DELE0
	SOSLE COUNT
	AOJA X,DELE
	AOJA X,INPUT

0DELE:	PUSHJ P,DELE0
	JRST INPUT

MDELE:	PUSHJ P,DELE0
	SOSLE COUNT
	SOJA X,MDELE
	SOJA X,INPUT

SPACES:	ADD X,B
	JRST INPUT

UP:	ADD Y,B
	JRST INPUT

NLINES:	IFN TS,SKIPGE TYIF
	PUSHJ P,TYI
	MOVE X,MARGIN
DOWN:	SUB Y,B
	JRST INPUT

BACK:	SUB X,B
	JRST INPUT

RUB:	SKIPL TYIF
	.TYOI "#
	SOS X
RUBOUT:	PUSHJ P,DELE0
	JRST INPUT

XSET:	MOVE X,B
	PUSHJ P,NUMGET
	JRST INRUB
	MOVE Y,B
	CAIN A,15
	JRST XYSTOI
	CAIE A,40
	CAIN A,11
	JRST XYSTO
	CAIN A,"R
	JRST XYDEL
	CAIN A,"C
	JRST INPUT
	CAIN A,"V
	JRST XYSET
	CAIN A,"Z
	JRST ZSET
	CAIN A,".
	JRST .LINE
	CAIN A,"D
	JRST DLINE
	CAIE A,":
	CAIN A,";
	JRST XYSTOC
INRUB:	SKIPGE TYIF
	JRST INPUT2
	.TYOI "?
	JRST INPUT2

XYSTOC:	PUSHJ P,TYI
	CAIE A,15
	JRST XYSTOC
XYSTOI:	IFN TS,SKIPGE TYIF
	PUSHJ P,TYI
XYSTO:	SUB X,XOFF
	SUB Y,YOFF
	PUSHJ P,STORE0
INPUT2:	MOVN X,XOFF
	MOVN Y,YOFF
	JRST INPUT0

XYSET:	SUB X,XOFF
	SUB Y,YOFF
	MOVNM X,XOFF
	MOVNM Y,YOFF
	JRST INPUT0

XYDEL:	SUB X,XOFF
	SUB Y,YOFF
	PUSHJ P,DELE0
	JRST INPUT2

.LINE:	MOVEI A,STORE0
LINE:	HRRM A,LINEL
	MOVM B,X
	MOVM C,Y
	CAMGE B,C
	EXCH B,C
GCD:	MOVE A,B
	IDIV A,C
	EXCH B,C
	JUMPN C,GCD
	MOVEM B,COUNT
	IDIV Y,B
	MOVEM Y,YM'
	IDIV X,B
	MOVEM X,XM'
	MOVN X,XOFF
	MOVN Y,YOFF
LINEL:	PUSHJ P,(J)
	ADD X,XM
	ADD Y,YM
	SOSL COUNT
	JRST LINEL
	JRST INPUT2

DLINE:	MOVEI A,DELE0
	JRST LINE

SETXOF:	ADDM B,XOFF
	JRST INPUT2
SETYOF:	ADDM B,YOFF
	JRST INPUT2

ZSET:	ADDM X,XOFF
	ADDM Y,YOFF
	JRST INPUT2

SETSC:	JUMPLE B,SETSH
	MOVEM B,SCALE
	JRST INPUT0
SETSH:	HRRZM B,SCSHIF
	JRST INPUT0

GENSTO:	MOVE K,B
	JRST INPUT0

MSET:	MOVEM X,MARGIN
	JRST INPUT1

INCOM:	PUSHJ P,TYI
	CAIE A,15
	JRST INCOM
	IFN TS,SKIPGE TYIF
	PUSHJ P,TYI
	JRST INPUT1
IFN TS,[
TYI:	SKIPGE TYIF
	JRST DKI
	.IOT TYIC,A
	POPJ P,

DKI:	.IOT DKIC,A
	POPJ P,
]
IFE TS,	DKI=PTYI

SDECP:	JUMPGE A,DECP
	MOVNS A
	.TYOI "-
DECP:	IDIVI A,10.
	HRLM B,(P)
	SKIPE A
	PUSHJ P,DECP
	HLRZ A,(P)
	.TYOI "0(A)
CPOPJ:	POPJ P,

NUMGET:	PUSHJ P,TYI
NUMGT0:	MOVEI B,0
	CAIN A,"-
	TROA F,1
	TRZA F,1
NUMGT1:	PUSHJ P,TYI
	CAIL A,"0
	CAILE A,"9
	JRST NUMGT2
	IMULI B,10.
	ADDI B,-"0(A)
	JRST NUMGT1
NUMGT2:	CAIE A,177
	AOS (P)
	TRZN F,1
	POPJ P,
	MOVNS B
	SKIPN B
	MOVNI B,1
	POPJ P,

UPDATE:	MOVE B,A
	IMUL B,[345671234563]
	TSC B,B
	ANDI B,HTL-1
	SKIPN T,HT(B)
	JRST PLACE
UPDAT1:	CAMN A,1(T)
	JRST FOUND
	MOVE B,T
	SKIPE T,(B)
	JRST UPDAT1
	JRST PLACE1

FOUND:	IORM F,2(T)
	ADDM C,3(T)
	POPJ P,

PLACE:	ADDI B,HT
	PUSH R,B
PLACE1:	PUSHJ P,FSP
	MOVEM T,(B)
	SETZM (T)
	MOVEM A,1(T)
	SETOM 2(T)
	MOVEM C,3(T)
	POPJ P,

STORE0:	MOVE A,Y
	HRL A,X
STORE:	HLRE B,A
	ADD B,[11,,]
	IDIVI B,11
	IMULI B,11
	HRLZS B
	HRR B,A
	IMULI C,-4
	MOVEI I,200000
	LSH I,20(C)
	MOVE S,B
	IMUL B,[345671234563]
	TSC B,B
	ANDI B,HTL-1
	SKIPN T,HT(B)
	AOJA N,SPLACE
STORE1:	CAMN S,1(T)
	JRST SFOUND
	MOVE B,T
	SKIPE T,(B)
	JRST STORE1
	AOJA N,SPLAC1

SFOUND:	TDOE I,2(T)
	POPJ P,
	MOVEM I,2(T)
	MOVEM I,4(T)
	AOJA N,CPOPJ

SPLACE:	ADDI B,HT
	PUSH R,B
SPLAC1:	PUSHJ P,FSP
	MOVEM T,(B)
	SETZM (T)
	MOVEM S,1(T)
	MOVEM I,2(T)
	SETZM 3(T)
	MOVEM I,4(T)
	POPJ P,

DELE0:	MOVE A,Y
	HRL A,X
DELETE:	HLRE B,A
	ADD B,[11,,]
	IDIVI B,11
	IMULI B,11
	HRLZS B
	HRR B,A
	IMULI C,-4
	MOVEI I,200000
	LSH I,20(C)
	MOVE S,B
	IMUL B,[345671234563]
	TSC B,B
	ANDI B,HTL-1
	ADDI B,HT
DEL1:	SKIPN T,(B)
	POPJ P,
	CAMN S,1(T)
	JRST DF
	MOVE B,T
	JRST DEL1

DF:	TDCN I,2(T)
	POPJ P,
	JUMPE I,DF1
	MOVEM I,2(T)
	MOVEM I,4(T)
	SOJA N,CPOPJ
DF1:	MOVE S,(T)
	MOVEM S,(B)
	EXCH T,FSP
	MOVEM T,@FSP
	JUMPN S,DF2
	TLNE B,-1
DF2:	SOJA N,CPOPJ
	MOVE C,R
DF3:	CAMN B,(C)
	SOJA N,DF4
	SUB C,[1,,1]
	JUMPGE C,DF3
	.VALUE
DF4:	POP R,(C)
	POPJ P,

;TORSIZ:	-1,,-1		;SIZE OF LIFE TORUS

CONSTANTS
VARIABLES

FS:	BLOCK FSK*MFSL

NBLKS==<.+1777>_<-10.>
MEMTOP==NBLKS_10.
IFE TS,[
MEMTOP==40000-DDTF*10000
IFN DDTF,PATCH=MEMTOP
]
FSL==<MEMTOP-FS>/FSK-1

END	GO
