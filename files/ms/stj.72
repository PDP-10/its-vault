TITLE	SUPERIOR TEXT JUSTIFIER

.MLLIT==1

TYOC==1
TYIC==2
OUTC==3
INPC==4

F=0
A=1
B=2
C=3
D=4

K=5

L=6

I=7
S=10
T=11
M=12
N=13

W=14
R=15
Q=16
P=17

NOFS==1	;NO OUTPUT FILE SPECIFIED
DPT==2	;DECIMAL POINT

LJUST==4
RJUST==10
CENTER==20
JUST==LJUST\RJUST\CENTER

PERSUP==40	;SUPPRESS PERIOD

FLAB==100	;FIRST LINE AFTER BREAK

PERIOD==40
DSP==200
EOL==100	;FIRST CHARACTER AFTER LINE

LPDL==100
BUFL==1000
LOBUF==BUFL*4

ZZ==.
LOC 41
	JSR UUOH
	JSR INT
LOC ZZ

DEFINE	CONC A,B
A!B!TERMIN

DEFINE	ETYPEL A/
	EPRINT [ASCIZ \A
\]
TERMIN

DEFINE	TYPEL A/
	TPRINT [ASCIZ \A
\]
TERMIN

DEFINE	TYPE A/
	TPRINT [ASCIZ \A\]
TERMIN

DEFINE	FIX A
	FADRI A,233400
	TLZ A,777400
TERMIN
UUOMIN==1
UUOMAX==UUOMIN-1

DEFINE	UUODEF UUONAM,UUOCAL
	UUOMAX==UUOMAX+1
	UUONAM==UUOMAX_33
		UUOCAL A!UUONAM
TERMIN

DEFINE	PJP A\
	PUSHJ P,A
TERMIN

UUODSP:	IRP A,,[[DBP,JRST],[DPBD,JRST],[LDBD,JRST],[PRINT,PJP],[TPRINT,PJP],[EPRINT,PJP],[6TYPE,PJP]]
		UUODEF A
	TERMIN

UUOH:	0
	MOVEM A,UUOA
	MOVEM B,UUOB
	LDB A,[270400,,40]
	LDB B,[331100,,40]
	CAIL B,UUOMIN
	CAILE B,UUOMAX
	.VALUE [ASCIZ \UUOH/Q-1/\]
	XCT UUODSP-UUOMIN(B)
	MOVE A,UUOA
	MOVE B,UUOB
	JRST 2,@UUOH

UUOA:	0
UUOB:	0

ADBP:	MOVE A,UUOA	;DECREMENT BYTE POINTER
	MOVE B,UUOB
ADBP0:	PUSH P,@40
	LDB A,[301400,,(P)]
	IMULI A,101
	ANDI A,7777
	CAIGE A,4400
	JRST ADBP1
	SOS (P)
	LDB B,[600,,A]
	LSH B,6
	IDIV A,B
	MOVE A,B
ADBP1:	DPB A,[301400,,(P)]
	MOVE A,UUOA
	MOVE B,UUOB
	POP P,@40
	JRST 2,@UUOH

ADPBD:	SKIPA A,[DPB_<-33>]	;DPB AND DECREMENT
ALDBD:	MOVEI A,LDB_<-33>	;LDB AND DECREMENT
	DPB A,[331100,,40]
	MOVE A,UUOA
	MOVE B,UUOB
	XCT 40
	MOVEM A,UUOA
	MOVEM B,UUOB
	JRST ADBP0

APRINT:	HRRZ B,40
	TLOA B,440700
APRIN1:	PUSHJ P,(W)
APRIN:	ILDB A,B
	JUMPN A,APRIN1
CPOPJ:	POPJ P,

ATPRINT:	PUSH P,W
	MOVEI W,TYO
	PUSHJ P,APRINT
	POP P,W
	POPJ P,

AEPRINT:	PUSHJ P,ATPRINT
	.VALUE

A6TYPE:	PUSH P,W
	MOVEI W,TYO
	PUSHJ P,6PRIN1
	POP P,W
	POPJ P,

6PRIN1:	HRRZ B,40
	TLOA B,440600
6P1A:	PUSHJ P,6OUT
	TLNN B,770000
	POPJ P,
	ILDB A,B
	JUMPN A,6P1A
	POPJ P,

6OUT:	ADDI A,40
	JRST (W)

TYO:	.IOT TYOC,A
	POPJ P,

TYI:	.IOT TYIC,A
	POPJ P,

QUIT:	TYPEL
	TYPEL QUIT?
	JRST GO0

INT:	0
	0
	EXCH A,INT
	SKIPL A
	.VALUE
	MOVEI A,TYIC
	.ITYIC A,
	JRST .+3
	CAIN A,^G
	JRST QUIT
	MOVE A,INT
	.DISMI INT+1

GO:	MOVE P,[-LPDL,,PDL-1]
	.OPEN TYOC,[SIXBIT \  !TTYSTJ   OUTPUT\]
	.VALUE
	TYPE STJ.
	6TYPE [.FNAM2]
	.OPEN TYIC,[SIXBIT \   TTYSTJ   INPUT\]
	ETYPEL TTY INPUT OPEN FAILED
	.SUSET [.RSNAM,,ISNAM]
	.SUSET [.RSNAM,,OSNAM]
	.SUSET [.SMSK2,,[1_TYIC]]
	.SUSET [.SMASK,,[SETZ 220400]]
GO0:	.SUSET [.SPICLR,,[-1]]
	MOVE P,[-LPDL,,PDL-1]
	TYPEL
AGO:	.CLOSE INPC,
	.CLOSE OUTC,
	TYPE _
	PUSHJ P,RDLINE	;READ FILE SPECIFICATIONS
	PUSHJ P,OPNFLS	;OPEN FILES
	JRST AGO	;OPEN FAILED
STJ:	PUSHJ P,TXTWRT	;START TEXT JUSTIFICATION
	JRST AGO

RDL0:	TYPEL
RDLINE:	MOVE C,[10700,,TTYBUF-1]
RDL1:	PUSHJ P,TYI
	CAIN A,177
	JRST RDRUB
	CAIN A,^G
	JRST RDLINE
	IDPB A,C
	CAIL A,40
	JRST RDL1
	MOVEI A,0
	DPB A,C
	POPJ P,

RDRUB:	CAMN C,[10700,,TTYBUF-1]
	JRST RDL0
	LDBD A,C
	PUSHJ P,TYO
	JRST RDL1

TTYBUF:	BLOCK 100

OPNFLS:	MOVE B,[10700,,TTYBUF-1]
	MOVEI R,[ILDB A,B
		POPJ P,]
OPNFLL:	ILDB A,B	;FIND _
	JUMPE A,OPNFL1
	CAIE A,"_
	JRST OPNFLL
	TRZ F,NOFS
OPNIFL:	TDZA I,I
OPNFL:	MOVE B,[10700,,TTYBUF-1]
	PUSHJ P,GETFIL
	JRST OPNI1
	MOVEM D,IFNAM1(I)
	MOVEM D,OFNAM1
OPNIL:	DBP B
	PUSHJ P,GETFIL
	JRST OPNI1
	MOVEM D,IFNAM2(I)
	JRST OPNIL

OPNFL1:	MOVE B,[10700,,TTYBUF-1]
	TRO F,NOFS
	JRST OPNIFL

OPNI1:	.SUSET SNAMP(I)
OPNO1A:	XCT IOPEN(I)
	JRST OPNF
	JUMPN I,POPJ1
OPNOFL:	MOVEI I,6	;OPEN OUTPUT FILE
	TRNE F,NOFS
	JRST OPNO1A
	CAIGE A,40
	JRST OPNFL
	TYPEL ?
	POPJ P,

GETFIL:	PUSHJ P,GETSYL
	CAIN A,":
	JRST GETFLD
	CAIN A,";
	JRST GETFLS
	JUMPE D,GETFL1
POPJ1:	AOS (P)
	POPJ P,

GETFL1:	CAIE A,"_
	CAIGE A,40
	POPJ P,
	JRST GETFIL

GETFLD:	HLRM D,IFILE(I)
	HLRM D,OFILE
	JRST GETFIL

GETFLS:	MOVEM D,ISNAM(I)
	MOVEM D,OSNAM
	JRST GETFIL

OPNF:	.OPEN [SIXBIT \   ERR     !\]
	ETYPEL ERR OPEN FAILED
OPNF1:	.IOT A
	.IOT TYOC,A
	CAIE A,12
	JRST OPNF1
OPNF2:	HRLZ T,IFILE(I)
	6TYPE T
	TYPE : 
	6TYPE ISNAM(I)
	TYPE ; 
	6TYPE IFNAM1(I)
	TYPE  
	6TYPE IFNAM2(I)
	TYPEL  ?
	POPJ P,

IOPEN:	.OPEN INPC,IFILE
IFILE:	SIXBIT \   DSK\
IFNAM1:	SIXBIT \STJ\
IFNAM2:	SIXBIT \MEMO\
ISNAM:	0
SNAMP:	.SSNAM,,ISNAM
	.OPEN OUTC,OFILE
OFILE:	SIXBIT \  !DSK\
OFNAM1:	SIXBIT \STJ\
OFNAM2:	SIXBIT \(MEMO)\
OSNAM:	0
	.SSNAM,,OSNAM

INPUT:	.IOT INPC,A
	SKIPL A
	POPJ P,
	PUSHJ P,BREAK	;EOF
	JRST AGO

OUTPUT:	.IOT OUTC,A
	CAIE A,^J
	POPJ P,
	PUSH P,L
	MOVE L,LINEH
	FDVR L,LFH
	FIX L
	SOJLE L,POPLJ
	.IOT OUTC,A
	SOJG L,.-1
POPLJ:	POP P,L
	POPJ P,

NUMGET:	PUSHJ P,(R)
NUMGT0:	SETZB M,N
	TRZA F,DPT
NUMG:	PUSHJ P,(R)
	CAIE A," 
	CAIN A,^I
	JRST NUMG
	JRST NUMGT2

NUMGPT:	TROE F,DPT
	JRST NUMGP2
	MOVSI M,(1.0)
NUMGT1:	PUSHJ P,(R)
NUMGT2:	CAIL A,"0
	CAILE A,"9
	JRST NUMGB
	IMULI N,10.
	ADDI N,-"0(A)
	FMPRI M,(10.0)
	JRST NUMGT1

NUMGB:	CAIN A,".
	JRST NUMGPT
	FSC N,233
	FDVR N,M
	POPJ P,

NUMGP2:	;2 DECIMAL POINTS?

TXTWRT:	SETZM BUF
	MOVE A,[BUF,,BUF+1]
	BLT A,@MEMTOP
	TRNN F,JUST
	SKIPA B,[10700,,BUF-1]
	MOVE B,[1100,,BUF-1]
	MOVEI R,INPUT
	MOVEI W,OUTPUT
	JRST TXTRD

TXTRDL:	MOVEI A,^M
	IDPB A,B
	MOVEI A,^J
TXTRDI:	IDPB A,B
TXTRD:	PUSHJ P,(R)
TXTRD0:	CAIE A,^J
	CAIN A,^L
	JRST TXTRDL	;LINE FEED AT BEGINNING OF LINE
	TRNN F,JUST
	JRST TXTRDB
	CAIE A,^I
	CAIN A," 
	JRST SPECIN
TXTRDB:	CAIE A,".
	JRST TXTRD2
	PUSHJ P,GETSYL	;COMMAND
	PUSHJ P,GETCOM
	SKIPGE D,COMDSP(C)	;C HAS COMMAND INDEX
	PUSHJ P,BREAK
	TLNE D,200000
	JRST COMNUM
	TLNE D,100000
	JRST (D)
	PUSHJ P,(D)
	JRST NXTLIN

COMNUM:	PUSHJ P,NUMGET
	TLNE D,100000
	MOVNS N
	MOVEM N,(D)
NXTLIN:	PUSHJ P,NXTLN
	JRST TXTRD0

NXTLN:	CAIN A,^M
	JRST NXTLN1
	PUSHJ P,(R)
	JRST NXTLN

NXTLN1:	PUSHJ P,(R)
	CAIE A,^J
	CAIN A,^L
	JRST (R)
	POPJ P,

TXTRDQ:	PUSHJ P,(R)
	TRO A,400
	IDPB A,B
TXTRD1:	PUSHJ P,(R)
TXTRD2:	CAMN A,QCHAR
	JRST TXTRDQ
	CAMN A,DUMMYC
	MOVEI A,400+" 
	IDPB A,B
TXTRD3:	CAIE A,^M
	JRST TXTRD1
	PUSHJ P,(R)
	CAIE A,^J
	CAIN A,^L
	JRST TXTRDI
	MOVEI T,^J
	IDPB T,B
	JRST TXTRD0

BREAK:	PUSH P,A
	MOVEI A,0
	IDPB A,B
	PUSHJ P,TXTOUT
BREAK1:	TRNN F,JUST
	SKIPA B,[10700,,BUF-1]
	MOVE B,[1100,,BUF-1]
	SETZM SPIN
	SETZM FLIN
	POP P,A
	POPJ P,

TXTOUT:	MOVE L,PPAGEW
	FSBR L,LPAGEW
	FSC L,-1
	MOVEM L,PIND
	TRNN F,JUST
	JRST TXTO1
	MOVE B,[1100,,BUF-1]
	TRO F,FLAB
	MOVE C,[-LOBUF,,OBUF-1]
	SETOM BKUPC
TXTO0:	PUSHJ P,INDENT	;DO INDENTATION
	MOVEM C,CIND
	SKIPA L,LIND
TXTOL:	PUSH C,A
TXTOLA:	ILDB A,B
	JUMPE A,TXTOE
	SKIPGE T,(C)
	JRST TXTOL3
	CAIE A,".
	CAIN A,":
	JRST PERSET
	CAIE A,"?
	CAIN A,"!
	JRST PERSET
	TLNN T,PERIOD
	JRST TXTOL3
	CAIE A,""
	CAIN A,"'
	JRST PERSET
	CAIE A,")
	CAIN A,"]
PERSET:	TLO A,PERIOD
TXTOL3:	CAIE A,^M
	CAIN A,^J
	JRST TXTOL0
	CAIE A," 
	CAIN A,^L
	JRST TXTOL0
TXTOL4:	TRZ A,777600
	SKIPGE T,CHARW(A)
	PUSHJ P,(T)
	FADR L,T
	CAMG L,LPAGEW
	JRST TXTOL
	SKIPGE BKUPC
	JRST TXTOE
TXTOE1:	TRNE F,CENTER
	JRST TXTOC
	TRZ F,FLAB
TXTOC1:	PUSHJ P,TXTOP
	LDB A,B
	MOVE C,[-LOBUF,,OBUF-1]
	MOVE B,BKUPB
	JUMPN A,TXTO0
	POPJ P,

TXTOE:	CAMN C,CIND
	POPJ P,
	HRRZM C,BKUPC
	MOVEM B,BKUPB
	MOVEM L,BKUPL
	JRST TXTOE1

TXTOC:	MOVNS L
	FADR L,FLIN
	FADR L,LPAGEW
	FSC L,-1
	FADRI L,233400
	FSBRI L,233400
	CAMG L,FLIN
	JRST TXTOC1
	MOVEM L,FLIN
	JRST TXTOUT

TXTOP:	MOVSI T,EOL	;WRITE OUT LINE
	IORM T,@BKUPC
	TRNE F,RJUST
	PUSHJ P,TXTOAD
	MOVE T,[4400,,OBUF-1]
TXTOPL:	ILDB A,T
	PUSHJ P,(W)
	TLZE A,DSP
	JRST .-2
	TLNN A,EOL
	JRST TXTOPL
CRLF:	MOVEI A,^M
	PUSHJ P,(W)
	MOVEI A,^J
	JRST (W)

TXTOL0:	JUMPL T,TXTOLA	;CR OR LF, SO CHANGE TO SPACE
	AOS NBRKS
	HRRZM C,BKUPC
	MOVEM B,BKUPB
	MOVEM L,BKUPL
	MOVE A,[400000,," ]
	TLNE T,PERIOD
	TLOA A,DSP
	JRST TXTOL4
	AOS NDBRKS
	JRST TXTOL4

	-1
OBUF:	BLOCK LOBUF
BKUPC:	0	;PLACE TO BACK UP C TO
BKUPB:	0	;PLACE TO BACK UP B TO
BKUPL:	0	;LINE LENGTH
NBRKS:	0	;# BREAKS
NDBRKS:	0	;# DOUBLE BREAKS

CIND:	0	;C AFTER INDENT

TXTOAD:	;ADJUST SPACES

INDENT:	MOVE L,INDW
	TRNE F,FLAB
	FADR L,FLIN
	TRNN F,FLAB
	FADR L,SPIN
	MOVEM L,LIND
	FADR L,PIND
	JUMPGE L,INDENP
	MOVNS L
	SKIPA T,[400000,,^H]
INDENP:	MOVE T,[400000,," ]
	FIX L
	JUMPE L,CPOPJ
	PUSH C,T
	SOJG L,.-1
	POPJ P,

IND:	MOVE L,INDW	;INDENT FEATURE
IND1:	SKIPGE T,CHARW(A)
	PUSHJ P,(T)
	FADR L,T
	PUSHJ P,(R)
	CAIE A,^I
	CAIN A," 
	JRST IND1
	POPJ P,

FLININ:	PUSHJ P,IND	;FIRST LINE INDENT FEATURE
	FSBR L,INDW
	MOVEM L,FLIN
	JRST TXTRD2

SPECIN:	PUSHJ P,IND	;SPECIAL INDENT FEATURE
	FSBR L,INDW
	MOVEM L,SPIN
	JRST TXTRD2

FLIN:	0.0
SPIN:	0.0
PIND:	0.0	;PHYSICAL INDENT [(PPAGEW-LPAGEW)/2]

TXTO1:	MOVEI A,0	;NO FILL OUTPUT
	IDPB A,B
	MOVEI L,1
	SKIPA B,[10700,,BUF-1]
TXTO1A:	PUSHJ P,(W)
TXTO1B:	ILDB A,B
	JUMPE A,CPOPJ
	CAIN A,^M
	JRST TXTO1C
	CAIN A,^J
	JRST TXTO1A
	CAIN L,1
	PUSHJ P,TXTO1I
	CAIN A,^I
	JRST TXTO1T
	SKIPGE T,CHARW(A)
	PUSHJ P,(T)
	FADR L,T
	JRST TXTO1A

TXTO1C:	MOVEI L,1	;CR
	JRST TXTO1A

TXTO1T:	PUSHJ P,TAB
	FADR L,T
	FIX T
	JUMPE T,TXTO1B
	MOVEI A," 
	PUSHJ P,(W)
	SOJG T,.-1
	JRST TXTO1B

TXTO1I:	PUSH P,A
	MOVE L,INDW
	TRNE F,FLAB
	FADR L,FLIN
	MOVEM L,LIND
	FADR L,PIND
	JUMPGE L,TXTO1S
	MOVNS L
	SKIPA A,[^H]
TXTO1S:	MOVEI A," 
	FIX L
	JUMPE L,TXTO1L
	PUSHJ P,(W)
	SOJG L,.-1
TXTO1L:	MOVE L,LIND
	POP P,A
	POPJ P,


GETSYL:	MOVEI D,0
	MOVE C,[440600,,D]
	JRST GSYL0

GSYL1:	CAIE A,";
	CAIN A,":
	POPJ P,
	CAIE A,",
	CAIN A,"_
	POPJ P,
	SUBI A,40
	TLNE C,770000
	IDPB A,C
GSYL0:	PUSHJ P,(R)
GSYL0A:	JUMPL A,CPOPJ
	PUSHJ P,CNVTUC
	CAILE A,40
	JRST GSYL1
	POPJ P,

CNVTUC:	CAIL A,140	;CONVERT TO UPPER CASE
	SUBI A,40
	POPJ P,

GETCOM:	MOVSI C,-NCOMS	;GET COMMAND
GTCOML:	CAMN D,COMTAB(C)
	POPJ P,
	AOBJN C,GTCOML
	6TYPE D
	TYPEL :  NO SUCH COMMAND
	POPJ P,

QCHAR:	0	;QUOTE CHARACTER

DUMMYC:	0	;DUMMY SPACE

NCOMS==0
DEFINE	COMDEF COMNAM,COMDSP
	SIXBIT \COMNAM\
IF2,	CONC CD,\NCOMS,==COMDSP
NCOMS==NCOMS+1
TERMIN

COMTAB:	COMDEF ,[500000,,QUOTE]
	COMDEF BREAK,[400000,,CPOPJ]
	COMDEF C,CPOPJ
	COMDEF COMMEN,CPOPJ
	COMDEF SP,[400000,,ASP]
	COMDEF SPACE,[400000,,ASPACE]
	COMDEF CENTER,[400000,,ACENTER]
	COMDEF RIGHT,
	COMDEF PPAGEL,[600000,,PPAGEL]
	COMDEF PSIZE,[600000,,PPAGEL]
	COMDEF PPAGEW,[600000,,PPAGEW]
	COMDEF PAPERW,[600000,,PPAGEW]
	COMDEF LPAGEL,[600000,,LPAGEL]
	COMDEF PL,[600000,,LPAGEL]
	COMDEF PAPER,[600000,,LPAGEL]
	COMDEF LPAGEW,[600000,,LPAGEW]
	COMDEF L,[600000,,LPAGEW]
	COMDEF LINE,[600000,,LPAGEW]
	COMDEF NJ,[400000,,ANJ]
	COMDEF NOJUST,[400000,,ANOJUST]
	COMDEF NF,[400000,,ANF]
	COMDEF NOFILL,[400000,,ANOFILL]
	COMDEF AD,[400000,,AAD]
	COMDEF ADJUST,[400000,,AADJUST]
	COMDEF FI,[400000,,AFI]
	COMDEF FILL,[400000,,AFILL]
	COMDEF QUOTE,[AQUOTE]
	COMDEF DUMMY,[ADUMMY]
	COMDEF SS,[400000,,ASS]
	COMDEF SINGLE,[400000,,ASINGLE]
	COMDEF DS,[400000,,ADS]
	COMDEF DOUBLE,[400000,,ADOUBLE]
	COMDEF INDENT,[600000,,INDW]
	COMDEF UNDENT,[700000,,FLIN]
	COMDEF PAGE,[400000,,APAGE]
	COMDEF TABSET,
	COMDEF TABCLR,
	COMDEF INSERT,
	COMDEF TRANS,
	COMDEF END,[500000,,AGO]

;400000==>BREAK BEFORE PROCESSING COMMAND
;200000==>GOBBLE NUMBER, 100000==>MINUS
;100000==>JUMP TO ROUTINE

COMDSP:	REPEAT NCOMS,CONC CD,\.RPCNT,
	CPOPJ	;FOR NO SUCH COMMAND, IGNORE COMMAND LINE
ASP:
ASPACE:	PUSHJ P,NUMGT0	;OUTPUT LINEFEEDS
	FMPR N,LINEH
	FDVR N,LFH
	FIX N
	PUSH P,A
	MOVEI A,400\^J
	PUSHJ P,(W)
	SOJG N,.-1
	POP P,A
	POPJ P,

QUOTE:	CAIE A," 
	CAIN A,^I
	JRST FLININ
	CAIN A,^J
	JRST QLF
	CAIN A,^M
	JRST QCR
	JRST TXTRD2

ANOJUST:
ANJ:	TRZ F,RJUST
	TRNN F,LJUST
	MOVE B,[10700,,BUF-1]
	POPJ P,

ANOFILL:
ANF:	TRZE F,JUST
	MOVE B,[10700,,BUF-1]
	POPJ P,

AADJUST:
AAD:	TRON F,JUST
	MOVE B,[1100,,BUF-1]
	POPJ P,

AFILL:
AFI:	TRZE F,RJUST
	POPJ P,
	TRON F,LJUST
	MOVE B,[1100,,BUF-1]
	POPJ P,

AQUOTE:	PUSHJ P,(R)
	MOVEM A,QCHAR
	POPJ P,

ADUMMY:	PUSHJ P,(R)
	MOVEM A,DUMMYC
	POPJ P,

ASINGLE:
ASS:	MOVSI T,(1.0)
	MOVEM T,LINEH
	POPJ P,

ADOUBLE:
ADS:	MOVSI T,(2.0)
	MOVEM T,LINEH
	POPJ P,

ACENTER:	MOVE B,[1100,,BUF-1]
	PUSHJ P,LINRD
	PUSH P,A
	PUSH P,F
	TRZ F,RJUST\LJUST
	TRO F,CENTER\PERSUP
	MOVEI A,0
	IDPB A,B
	PUSHJ P,TXTOUT
	POP P,F
	JRST BREAK1

LINRD:	PUSHJ P,(R)
	CAIN A,^M
	POPJ P,
	CAIN A,^J
	JRST LINRD
	TRO A,400
	IDPB A,B
	JRST LINRD
APAGE:	PUSHJ P,NUMGT0
	FIX N
	PUSH P,A
	MOVEI A,^L
	PUSHJ P,(W)
	SOJG N,.-1
	POP P,A
	POPJ P,

IF2,[	REPEAT 40,CONC CW,\.RPCNT,==2.0
	CONC CW,\^H,==SETZ BKSPC
	CONC CW,\^I,==SETZ TAB
	CONC CW,\^J,==SETZ LF
	CONC CW,\^L,==SETZ FF
	CONC CW,\^M,==SETZ CAR
	REPEAT 140,CONC CW,\.RPCNT+40,==1.0
	CONC CW,\" ,==SETZ SPACE
]
CHARW:	REPEAT 200,CONC CW,\.RPCNT	;CHARACTER SIZES
TABS:	RADIX 10.
	REPEAT 100,CONC \8*.RPCNT,.0,
	RADIX 8
PPAGEL:	60.0	;PHYSICAL PAGE LENGTH
PPAGEW:	120.0	;PHYSICAL PAGE WIDTH [PHYSICAL LINE LENGTH]
LPAGEL:	50.0	;LOGICAL PAGE LENGTH
LPAGEW:	65.0	;LOGICAL PAGE WIDTH [LOGICAL LINE LENGTH]
LINEH:	1.0	;SIZE OF LINE (2 FOR DOUBLE SPACE)
LFH:	1.0	;SIZE OF LINE FEED
INDW:	0.0	;INDENT FROM LOGICAL PAGE'S LEFT MARGIN
LIND:	0.0	;TOTAL LOGICAL INDENTATION (INDW+SPIN)

TRANST:	REPEAT 200,.RPCNT

TAB:	MOVSI T,-100
	CAML L,TABS(T)
	AOBJN T,.-1
	MOVE T,TABS(T)
	FSBR T,L
	POPJ P,

BKSPC:	MOVSI T,(-1.0)
	CAMGE L,[1.0]
	MOVN T,L
	POPJ P,

SPACE:	MOVSI T,(1.0)
	TLNE A,DSP
	MOVSI T,(2.0)
	POPJ P,
PDL:	BLOCK LPDL

MEMTOP:	MEMT-1

CONSTANTS
VARIABLES

BUF:	BLOCK BUFL

MEMT=<<.+1777>_<-12>>_12
END GO
