;;; -*- LISP -*-

;;;; PARSE SUDS FILES

;;; *** What are BODY-BITs and POINT-BITs
;;; *** MACROS

(DECLARE (SPECIAL FILEI FILEO)
	 (*LEXPR GPRINT))

(defun suds-print (a b) nil)


(DEFMACRO DEFSUDSASCIZ (NAME)
  `(DEFUN ,NAME ()
     (SUDS-PRINT (LIST ',NAME (SUDS-ASCIZ FILEI))
	    FILEO)))


(DEFMACRO SUDS-HRRE (X)
  `(LET ((Z (LDB #o0022 ,X)))
     (COND ((= 0 (LOGAND #o400000 Z)) Z)
	   (T (LOGIOR #.(LSH #o777777 18.) Z)))))

(DEFMACRO SUDS-HLRE (X)
  `(LET ((Z (LDB #o2222 ,X)))
     (COND ((= 0 (LOGAND #o400000 Z)) Z)
	   (T (LOGIOR #.(LSH #o777777 18.) Z)))))

(DEFUN SUDS-SIXBIT (NUMBER)
  (DO ((N NUMBER (LSH N -6))
       (A NIL))
      ((ZEROP N) (IMPLODE A))
    (SETQ A (CONS (+ #o40 (LOGAND N #o77)) A))))

(DEFUN SUDS-ASCIZ (FILE)
  (SUDS-ASCIZ-1 (IN FILE) FILE))

(DEFUN SUDS-ASCIZ-1 (W0 FILE)
       (DO ((IN W0 (IN FILE))
	    (LS NIL (CONS IN LS)))
	   ((= 0 (LDB #o0107 IN))
	    (PNPUT (NREVERSE (CONS IN LS)) NIL))
	   ))


;;; Different for TOPS-20
(DEFUN SUDS-FILENAME (FNAME EXT PPN)
  (SUDS-PRINT (LIST 'SUDS-FILENAME
	       (SUDS-SIXBIT FNAME)
	       (SUDS-SIXBIT EXT)
	       (SUDS-SIXBIT PPN))
	 FILEO))

(DEFUN SUDS-PROPERTY-LIST (FILEI)
  (DO ((W1 (IN FILEI) (IN FILEI))
       (PL NIL))
      ((= W1 0) (CONS 'PROPERTIES (NREVERSE PL)))
    (PUSH (LIST (SUDS-ASCIZ-1 W1 FILEI)	;value
		(SUDS-ASCIZ FILEI)	;property (=0 -> text only)
		(LIST 'SIZE (IN FILEI))	;size
		(LIST 'LOC    (SUDS-XY (IN FILEI)))
		(LIST 'OFFSET (SUDS-XY (IN FILEI))))
	  PL)))


(DEFUN SUDS-XY (X)
       (LET* ((Y (LOGCLR #o1000001 X))
	      (L (SUDS-HLRE Y))
	      (R (SUDS-HRRE Y)))
	    (LIST (// (LOGAND L -2) 2)
		  (// (LOGAND R -2) 2)
		  (LOGAND X #o1000001)
		  )))

;;; POINT ID IS GENERATED FOR POINTS OR XWD PIN ID,BID FOR PINS
(DEFUN SUDS-POINT-ID (W)
  (COND ((ZEROP W) NIL)
	(T 
	 (LET ((L (SUDS-HLRE W))
	       (R (SUDS-HRRE W)))
	      (LIST 'SUDS-POINT-ID L R)))))

(DEFUN SUDS-BODY-ID (W)
       (LIST 'SUDS-BODY-ID W))

(DEFUN SUDS-CARD-LOC-ID (W)
  (LIST 'SUDS-CARD-LOCATION
	(LDB #o4004 W)				;N
	(LDB #o3305 W)				;L
	(LDB #o3003 W)				;X
	(LDB #o2206 W)				;N
	))



(DEFUN SUDS-DUMP-FILE (FILENAME CNT)
  (LET ((FILEI (OPEN FILENAME '(IN BLOCK FIXNUM))))
       (SUDS-DUMP FILEI CNT)
       (CLOSE FILEI)))

(DEFUN SUDS-DUMP (FILEI CNT)
  (DO ((I 1 (1+ I)))
      ((> I CNT))
    (SUDS-DUMP-1 (IN FILEI))))

(DEFUN SUDS-DUMP-1 (W)
    (FORMAT T '|~%~14o  ~7O,,~7O  ~14d.  |
	    W (SUDS-HLRE W) (SUDS-HRRE W) W)
    ;; ascii
    (DO ((J -29. (+ J 7))
	 (C   0.))
	((> J 0))
      (SETQ C (LOGAND (LSH W J) #o177))
      (TYO (COND ((OR (< C #/ ) (= C #o177)) #/.)
		 (T                          C))))
    (TYO #/ )
    ;; sixbit
    (DO ((J -30. (+ J 6)))
	((> J 0))
      (TYO (+ 40. (LOGAND (LSH W J) #o77)))))


;;;; SUDS Version, Nomenclature, Board, and Libraries

;;; VERSION # (OLDEST VERSION DOES NOT HAVE THIS, BUT STARTS WITH A BODY NAME)
;;; 				;IOVER = 27
;;; ASCIZ /NOMENCLATURE TYPE/
;;; ASCIZ /BOARD TYPE/
;;; 
;;; --------
;;; !	ASCIZ/TYPE NAMES OF LIBRARY BODIES ACTUALLY USED IN THIS DWG/
;;; --------
;;; 0
;;; --------
;;; !	FILENAME
;;; !	EXT,,BITS	------- LIBRARIES
;;; !	PPN
;;; --------
;;; 0

(DEFUN SUDS-IOVERSION ()
  (SUDS-PRINT
   (LET ((X (IN FILEI)))
	(LIST 'SUDS-IOVERSION (SUDS-HLRE X) (SUDS-HRRE X)))
   FILEO))

(DEFSUDSASCIZ SUDS-NOMENCLATURE)

(DEFSUDSASCIZ SUDS-BOARD-TYPE)

(DEFUN SUDS-LIBRARY-BODIES ()
    (DO ((W0 (IN FILEI) (IN FILEI))
	 (LIST NIL))
	((ZEROP W0) (SUDS-PRINT (CONS 'SUDS-BODIES-USED LIST) FILEO))
      (PUSH (SUDS-ASCIZ-1 W0 FILEI) LIST)))

(DEFUN SUDS-LIBRARY-FILES ()
    (DO ((W0 (IN FILEI) (IN FILEI)))
	((ZEROP W0))
      (SUDS-FILENAME W0 (IN FILEI) (IN FILEI))))


;;;; SUDS Body Definitions

;;; --------
;;; !	ASCIZ STRING NAME OF BODY DEF
;;; !	ASCIZ /PACKAGE NAME/	;DEFAULT VALUE FROM DIPS.DIP FILE
;;; !	BITS,,
;;; !	X,Y INITIAL BODY LOC OFFSET
;;; !	X,Y INITIAL BODY LOC CHAR OFFSET (400000 IF USING DEFAULT)
;;; !	--------
;;; !	!	loc of pin (x,y)
;;; !	!	bits,,pinid (pinid is generated by program)
;;; !	!	pin pos,,pin name (default)
;;; !	--------
;;; !	400000
;;; !	--------
;;; !	!	POINTS DESCRIBING LINES IN TYPE (LOW ORDER BIT =1 IF VISIBLE LINE TO THIS POINT)
;;; !	--------
;;; !	400000
;;; !	--------
;;; !	!	ASCIZ VALUE TEXT
;;; !	!	ASCIZ PROPERTY NAME TEXT (0 IF TEXT ONLY)
;;; !	!	TEXT SIZE (0 IF NOT NORMALLY DISPLAYED)
;;; !	!	TEXT LOCATION
;;; !	!	CONSTANT OFFSET
;;; !	--------
;;; !	 0
;;; --------
;;; 0

(DEFUN SUDS-BODY-DEFINITIONS ()
  (DO ((W0 (IN FILEI) (IN FILEI)))
      ((ZEROP W0))
      (SUDS-PRINT
       (LIST 'SUDS-BODY-DEFINITION
	     (SUDS-ASCIZ-1 W0 FILEI)		;body name
	     (SUDS-ASCIZ FILEI)			;dip type
	     (LIST 'BODY-BITS (IN FILEI))	;bits, unused
	     (LIST 'BODY-LOC-OFFSET      (SUDS-XY (IN FILEI)))
	     (LIST 'BODY-LOC-CHAR-OFFSET (SUDS-XY (IN FILEI)))
	     ;; pin location, pin ids, pin pos, pin name
	     (DO ((W1 (IN FILEI) (IN FILEI))
		  (PS NIL))
		 ((= W1 #o400000) (CONS 'PIN-STUFF (NREVERSE PS)))
		 (PUSH (LIST 'PIN-SHIT (SUDS-XY W1) (IN FILEI) (IN FILEI))
		       PS))
	     ;; points describing lines in body LSB=1 -> visible line
	     (DO ((W1 (IN FILEI) (IN FILEI))
		  (PL NIL))
		 ((= W1 #o400000) (CONS 'LINES (NREVERSE PL)))
		 (PUSH (SUDS-XY W1) PL))
	     ;; properties
	     (SUDS-PROPERTY-LIST FILEI))
       FILEO))
  NIL)



;;;; SUDS Macro Definitions

;;; --------
;;; !	ASCIZ/MACRO NAME/
;;; !	BYTE(9)MACRO BODY ENDING WITH 0 BYTE
;;; --------
;;; 0

(DEFUN SUDS-MACRO-DEFINITIONS ()
  (DO ((W0 (IN FILEI) (IN FILEI)))
      ((ZEROP W0))
    (SUDS-PRINT
     (LIST 'SUDS-MACRO
	   (SUDS-ASCIZ-1 W0 FILEI)
		 ;;; *** just gobble for now
	   (DO ((W1 (IN FILEI) (IN FILEI)))
	       ((= 0 (LOGAND W1 #o777)))))
     FILEO))
  NIL)


;;;; SUDS Body Instances

;;; --------
;;; !	LOC OF BODY
;;; !	ORIENTATION+400000(IF LOCATION FOLLOWS)
;;; !		BYTE(4)N(5)L(3)X(6)N,L(12)N
;;; !		X,Y CONSTANT OFFSET OF LOCATION
;;; !		X,Y CHAR OFFSET OF LOCATION
;;; !	XWD BODY BITS , BODY ID (GENERATED)
;;; !	NAME OF TYPE USED TO GENERATE BODY (IN ASCIZ)
;;; !	--------
;;; !	!	ASCIZ VALUE TEXT
;;; !	!	ASCIZ PROPERTY NAME TEXT (0 FOR TEXT ONLY) (THESE ARE PROPERTIES WHICH DIFFER FROM DEFINITION)
;;; !	!	TEXT SIZE
;;; !	!	TEXT LOCATION
;;; !	!	CONSTANT OFFSET
;;; !	--------
;;; !	 0
;;; --------
;;; 400000

(DEFUN SUDS-BODIES ()
  (DO ((W0 (IN FILEI) (IN FILEI)))
      ((= W0 #o400000))
    (SUDS-PRINT
     (LIST 'SUDS-BODY-INSTANCE
	   (LIST 'BODY-LOC (SUDS-XY W0))
	   (LET ((W1 (IN FILEI)))		;BYTE (4)N(5)L(3)X(6)N,L(12)N
		(COND ((NOT (= 0 (LOGAND W1 #o400000)))
		       (LIST 'ORIENTATION
			     W1
			     (IN FILEI)
			     (SUDS-XY (IN FILEI))
			     (SUDS-XY (IN FILEI))))
		      (T
		       (PRINT '***SUDS-ORIENTATION*****)
		       (PRINT W1)
		       (SUDS-DUMP FILEI 20)
		       (BREAK ORIENTATION)
		       (LIST 'ORIENTATION W1))))
	   (LIST 'BODY-BITS-ID (IN FILEI))
	   (LIST 'BODY-NAME (SUDS-ASCIZ FILEI))
	   (SUDS-PROPERTY-LIST FILEI)
	   )
     FILEO))
  NIL)


;;;; SUDS Points

;;; --------
;;; !	LOC OF POINT
;;; !	POINT ID (GENERATED)
;;; !	ID OF DOWN(0 IF NONE)
;;; !	ID OF UP(0 IF NONE)
;;; !	ID OF LEFT(0 IF NONE)
;;; !	ID OF RIGHT(0 IF NONE)
;;; !	XWD BITS,PIN #
;;; !	SIZE OF TEXT (0 IF NONE)
;;; !		X,Y CONSTANT OFFSET FROM POINT LOC
;;; !		ASCIZ TEXT(IF ANY)
;;; !	BYTE (4)N(5)L(3)X(6)N,L,L,N------\  IF CPIN ON IN BITS
;;; !	X,Y CONSTANT OFFSET--------------/	(6)L,L,N REPLACED BY (6)L(12)4000+N FOR "U" PIN
;;; --------
;;; 400000


(DEFUN SUDS-POINTS ()
  (DO ((W0 (IN FILEI) (IN FILEI)))
      ((= W0 #o400000))
    (SUDS-PRINT
      (LET ((BODY-BITS 0))
	(LIST 'SUDS-POINT
	      (LIST 'POINT-LOC (SUDS-XY W0))
	      (SUDS-POINT-ID (IN FILEI))
	      (LIST 'ID-DOWN-UP-LEFT-RIGHT
		    (SUDS-POINT-ID (IN FILEI))
		    (SUDS-POINT-ID (IN FILEI))
		    (SUDS-POINT-ID (IN FILEI))
		    (SUDS-POINT-ID (IN FILEI)))
	      (PROGN (SETQ BODY-BITS (IN FILEI))
		     (LIST 'BIT-PIN (SUDS-HLRE BODY-BITS) (SUDS-HRRE BODY-BITS)))
	      (LET ((TEXT-SIZE (IN FILEI)))
		(COND ((ZEROP TEXT-SIZE)
		       '(TEXT *NONE*))
		      (T
		       (LIST 'TEXT
			     TEXT-SIZE
			     (SUDS-XY (IN FILEI))
			     (SUDS-ASCIZ FILEI)))))
	      (AND NIL '****
		   (LIST 'ETC (IN FILEI) (IN FILEI)))))
      FILEO))
  NIL)


;;;; SUDS Sets

;;; --------
;;; !	LOC OF SET CENTER
;;; !	--------
;;; !	!	BODY ID
;;; !	--------
;;; !	0
;;; !	--------
;;; !	!	POINT ID
;;; !	--------
;;; !	0
;;; --------
;;; 400000
;;; 


(DEFUN SUDS-SETS ()
  (DO ((W0 (IN FILEI) (IN FILEI)))
      ((= W0 #o400000))
    (SUDS-PRINT
     (LIST (LIST 'SET-LOC (IN FILEI))
	   (DO ((W1 (IN FILEI) (IN FILEI))
		(BL NIL))
	       ((ZEROP W1) (CONS 'BODY-IDS (NREVERSE BL)))
	       (PUSH (SUDS-BODY-ID W1) BL))
	   (DO ((W1 (IN FILEI) (IN FILEI))
		(PL NIL))
	       ((ZEROP W1) (CONS 'POINT-IDS (NREVERSE PL)))
	       (PUSH (SUDS-POINT-ID W1) PL)))
     FILEO)
    ))


;;;; SUDS Strings and Things

;;; ASCIZ STRING DRAWN BY
;;; ASCIZ STRING TITLE LINE 1
;;; ASCIZ STRING TITLE LINE 2
;;; BYTE(4)N(5)L(3)X(6)N(18)0	CARD LOC FOR WHOLE DWG
;;; ASCIZ REVISION STRING
;;; ASCIZ MODULE STRING
;;; asciz variable string
;;; asciz prefix string
;;; ASCIZ PROJECT STRING
;;; ASCIZ PAGE STRING
;;; ASCIZ OF STRING
;;; ASCIZ SITE LINE 1 STRING
;;; ASCIZ SITE LINE 2 STRING
;;; DEC,<
;;; ASCIZ NEXT HIGHER ASSEMBLY NUMBER STRING
;;; 	0	OR	DRNNAM
;;; 			DRNEXT,,DRNDAT
;;; 			DRNPPN
;;; 	0	OR	CHKNAM
;;; 			CHKEXT,,CHKDAT
;;; 			CHKPPN
;;; 	0	OR	ENGNAM
;;; 			ENGEXT,,ENGDAT
;;; 			ENGPPN
;;; >;DEC
;;; 
;;; 0


(DEFSUDSASCIZ SUDS-DRAWN-BY)
(DEFSUDSASCIZ SUDS-TITLE1)
(DEFSUDSASCIZ SUDS-TITLE2)

(DEFUN SUDS-CARD-LOC ()
       (SUDS-PRINT (SUDS-CARD-LOC-ID (IN FILEI)) FILEO))

(DEFSUDSASCIZ SUDS-REVISION)
(DEFSUDSASCIZ SUDS-MODULE)
(DEFSUDSASCIZ suds-variable-string)
(DEFSUDSASCIZ suds-prefix-string)
(DEFSUDSASCIZ SUDS-PROJECT)
(DEFSUDSASCIZ SUDS-PAGE)
(DEFSUDSASCIZ SUDS-PAGE-OF)
(DEFSUDSASCIZ SUDS-SITE-LINE1)
(DEFSUDSASCIZ SUDS-SITE-LINE2)

(DEFUN SUDS-DEC-STUFF ()
       NIL)


;;;; Parse the .DRW file

(DEFUN PARSE-SUDS-FILE (FILENAME-IN FILENAME-OUT)
  (LET ((FILEI (OPEN FILENAME-IN  '(IN BLOCK FIXNUM)))
	(FILEO (OPEN FILENAME-OUT '(OUT BLOCK ASCII)))
	(PRINLEVEL   NIL)
	(PRINENDLINE NIL))
    (DECLARE (SPECIAL PRINLEVEL PRINENDLINE))
    (SUDS-IOVERSION)
    (SUDS-NOMENCLATURE)
    (SUDS-BOARD-TYPE)
    (SUDS-LIBRARY-BODIES)
    (SUDS-LIBRARY-FILES)
    (SUDS-BODY-DEFINITIONS)
    (SUDS-MACRO-DEFINITIONS)
    (SUDS-BODIES)
    (SUDS-POINTS)
    (SUDS-SETS)
    (SUDS-DRAWN-BY)
    (SUDS-TITLE1)
    (SUDS-TITLE2)
    (SUDS-CARD-LOC)
    (SUDS-REVISION)
    (SUDS-MODULE)
    (SUDS-VARIABLE-STRING)
    (SUDS-PREFIX-STRING)
    (SUDS-PROJECT)
    (SUDS-PAGE)
    (SUDS-PAGE-OF)
    (SUDS-SITE-LINE1)
    (SUDS-SITE-LINE2)
    (SUDS-DEC-STUFF)

    (COND ((NOT (= 0 (IN FILEI)))
	   (PRINT '|;;; *** DID NOT READ ZERO WORD|)))
    (FORMAT T '|~%;;; FILE LENGTH = ~D, CURRENT FILEPOS = ~D|
	    (LENGTHF FILEI)
	    (FILEPOS FILEI))

    (CLOSE FILEI)
    (CLOSE FILEO)
    ))

;;;; DIPS.DIP	FILE FORMAT

;;; 	A DIP's properties are arranged in a hierarchy of increasing
;;; 	specification, which is terminated in a unique part number. (?)
;;; 	At each level, a further property may be specified which
;;; 	may assume a set of values.  Sub-properties may appear under each
;;; 	property value, until the part is completely specified. The depth
;;; 	of the tree, and the sub-property's name at the next level are fixed
;;; 	by the DIP's property name list. For example:
;;; 
;;; 	TTLDLY	Delay	Drive	Part-number
;;; 		10 ns	4	TTLDLY-10-4
;;; 			10	TTLDLY-10-10
;;; 		20 ns	10	TTLDLY-20-10
;;; 		 ...



;;; -<DIP VERSION #>		;DIPVER = 5
;;; --------
;;; !	# OF PINS
;;; !	ASCIZ/DIPNAME/
;;; !	ASCIZ/PACKAGE NAME/	;If any
;;; !	------------
;;; !	!	ASCIZ /property names/		;In order of greater specificity
;;; !	------------
;;; !	0
;;; !	------------				;Sub-property values correspond to sequence
;;; !	!					; of property names above
;;; !	!	ASCIZ /property value/		;Most significant property
;;; !	!	0 ,, <property value bits>
;;; !	!	------------		
;;; !	!	!	ASCIZ /sub-property/	;Next most significant property
;;; !	!	!	0 ,, <property value bits>
;;; !	!	!		... (subtree) ...
;;; !	!	------------
;;; !	!	0
;;; !	------------
;;; !	0
;;; !	------------
;;; !	!	0 ,, PIN NAME
;;; !	!	BITS ,, PS #
;;; !	!	HI ,, LOW   LOADING	;SIGNED HALFWORDS, IN .01 MA UNITS
;;; !	!	SIXBIT/USE/		;FOR POWER PINS, HI IS REPLACED
;;; !	!				; BY VOLTAGE IN .01 VOLT UNITS
;;; !	!				; LOW BY SUPPLY CURRENT IN .01 MA UNITS
;;; !	!	SECT BITS,,1ST SECT PIN #
;;; !	------------
;;; --------
;;; 0
