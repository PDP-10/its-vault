TITLE PATERSON'S WORM

A=1
B=2
C=3
D=4
E=5
F=6
G=7
R=10
S=11
T=12
U=13
X=14
Y=15
Z=16
P=17

;WORM EATS IN PATH ALONG SEGMENTS ON TRIANGULAR GRAPH PAPER
;+X, +Y AXES MOVED TOGETHER TO MAKE 60 DEGREE ANGLE, THEN X = -Y ADDED
;AT ANY NODE, THE SIX SEGMENTS (SPOKES) MEETING THERE HAVE TWO CODES
;THE ARRIVAL/EXIT CODE IS 0 FOR +X, 1 TO 5 CW SO 5 = +Y
;THE SPOKE USAGE CODE IS 1 FOR +X, 2 TO 40 CCW SO 40 = +X, -Y
;<RULCOD> SPECIFIES A (PRESUMABLY DISTINCT) SET OF WORM FEEDING RULES
;4000 BIT SELECTS PREFERENCE IN A DESERT (UNVISITED FEEDING GROUND)
;3000, 600, 140 & 30 FIELDS SELECT PATH CROSSING RULES
;60 FIELD SELECTS RULE AT FIRST RETURN TO ORIGIN
;1 BIT SELECTS RULE AT SECOND RETURN TO ORIGIN
;<RULCOD> = 0 IS ORIGINAL SET OF RULES

TYOCHN==1
TYICHN==2
IPLCHN==3

DEFINE BARF A
	MOVE R,[440700,,[ASCII \A!$\]]
	PUSHJ P,TYPMES
TERMIN

;CTRL-D COMPLEMENT 340 USE
;<ARG>CTRL-F .NDIS OF <ARG>
;SPACE DO NEXT RULE
;A DO CURRENT RULE AGAIN
;C TRY TO CONTINUE (LOSES AFTER L OR N)
;<ARG>L SET MAX PATH LENGTH TO <ARG>*2000 SEGMENTS
;<ARG>M SET VECTOR SCALE TO <ARG>
;<ARG>N SET MAX NODES VISITED TO <ARG>*2000
;P COMPLEMENT DISPLAYING OF PARAMETER (RULE CODE)
;<ARG>R SET CURRENT RULE TO <ARG> AND DO IT
;<ARG>S STOP MAKING PATH
;T TYPE PARAMETERS (RULE CODE, PATH LENGTH, NODES VISITED)
;<ARG>U DO <ARG>TH UNIQUE RULE CODE, NEXT CODE IF <ARG> = 0
;<ARG>V SET VECTOR LENGTH TO <ARG>*2 340 UNITS
;W WRITE OUT PATH ON PLOTTER
;<ARG>X SET X OF ORIGIN TO <ARG>
;<ARG>Y SET Y OF ORIGIN TO <ARG>
;<ARG>Z .SLEEP OF <ARG> AFTER ADDING EACH VECTOR
BEG:	MOVEI P,PDL-1
	.OPEN TYOCHN,[1,,(SIXBIT \TTY\)]
	.VALUE
	.OPEN TYICHN,[0,,(SIXBIT \TTY\)]
	.VALUE
INIT:	CLEARM FINISH
	MOVE A,RULCOD	;SET UP 340 CHARACTERS
	DPB A,[140300,,DISNUM]
	LSH A,-3
	DPB A,[220300,,DISNUM]
	LSH A,-3
	DPB A,[300300,,DISNUM]
	LSH A,-3
	DPB A,[360300,,DISNUM]
	CLEARM LENGTH
	MOVE A,[403000,,403000]
INIT1:	MOVEM A,DISBUF
	HRR A,INIT1
	HRL A,INIT1
	AOS A
INIT2:	BLT A,DISBUF+MAXLEN/2+10-1
	CLEARM NNODES
	SETOM NODES
	MOVE A,[NODES,,NODES+1]
INIT3:	BLT A,NODES+MAXNOD-1
	MOVE A,[1,,400400]
	MOVEM A,NODES	;WORKS ONLY TO NODES TABLE OF 100 BLOCKS
	AOS NNODES
	MOVEI A,401400
	HRLI G,442200
	HRR G,INIT1
	MOVE B,VECTAB
	IDPB B,G
USE340:	.DSTAR PNTRL
	JFCL
	AOS LENGTH
	MOVEI B,3
;<A> = CURRENT NODE, <B> = CURRENT APPROACH DIRECTION CODE
LOOP:	LDB C,[111100,,A]	;GET NODE STATE
	IMUL C,A	;HASH CODE
GSTAT:	ANDI C,MAXNOD-1
	SKIPG D,NODES(C)
	AOJA D,NEWNOD	;ZEROS D
	HRRZS D
	CAME A,D
	AOJA C,GSTAT
	HLRZ D,NODES(C)	;PICK UP STATE
	DPB D,[360600,,D]	;6 BIT ROT
	ROT D,(B)	;MAKE ARRIVAL FROM +X (CODE 0)
	ANDI D,77
MATCH:	MOVEI E,	;GET MATCH IN RULES TABLE
GRULE:	SKIPG F,RULES(E)
	.VALUE		;CURRENT NODE STATE NOT IN RULES TABLE
	CAIE D,(F)
	AOJA E,GRULE
;	AOS RULUSE(E)	;UPDATE RULE USAGE COUNT
	HLRZS F		;EXIT DIRECTION
	CAIN F,7
	JRST DONE
	ADD F,B		;GET TRUE, UN-ROTED EXIT CODE
	CAIL F,6	;MOD 6
	SUBI F,6
	MOVE B,BITS(B)	;UPDATE CURRENT NODE
	IOR B,BITS(F)
	IORM B,NODES(C)
	MOVE B,VECTAB(F)
	IDPB B,G
	AOS B,LENGTH
	CAML B,MAXL
	JRST P2LONG	;PATH TOO LONG
	XCT JUMPS(F)	;UPDATE NODE STATE
	MOVEI B,3(F)	;UPDATE OLD EXIT TO NEW ARRIVAL
	CAIL B,6
	SUBI B,6
	SKIPE T,SLEEP
	.SLEEP T,
	AOS T,TIMER
	TRNE T,7	;LISTEN ONLY OCCASIONALLY
	JRST LOOP
	.LISTEN T,
	SKIPE T
	PUSHJ P,TYI
	JRST LOOP

NEWNOD:	MOVEM A,NODES(C)	;CREATE A NEW NODE
	AOS T,NNODES
	CAMGE T,MAXN
	JRST MATCH
BARF [NODES> ]		;TOO MANY NODES
	PUSHJ P,TYPAR1
	JRST LISTEN

P2LONG:	BARF [PATH> ]	;PATH TOO LONG
	PUSHJ P,TYPAR1
	JRST LISTEN
DONE:	SETOM FINISH
	HRRZ A,BIGLEN
	CAML A,LENGTH
	JRST DONE1
	HRR A,LENGTH
	HRL A,RULCOD
	MOVEM A,BIGLEN	;UPDATE BIGGEST LENGTH
DONE1:	PUSHJ P,TYPAR1
LISTEN:	PUSHJ P,TYI
	JRST .-1

TYI:	.IOT TYICHN,T
	CAIL T,"0
	CAILE T,"7
	JRST DISPAT	;DISPATCH ON COMMAND CHARACTER
	MOVE U,CURNUM	;ACCUMULATE NUMBER
	LSH U,3
	ADDI U,-"0(T)
	MOVEM U,CURNUM
	POPJ P,

DISPAT:	MOVEI U,
	EXCH U,CURNUM	;U HAS CURRENT ARGUMENT
	CAIN T,4
	JRST CTRLD
	CAIN T,6
	JRST CTRLF
	CAIN T,40	;SPACE
	JRST NEXT
	CAIN T,"A
	JRST AGAIN
	CAIN T,"C
	JRST CONTIN
	CAIN T,"L
	JRST SETLEN
	CAIN T,"M
	JRST SETMAG
	CAIN T,"N
	JRST SETNOD
	CAIN T,"P
	JRST COMPAR
	CAIN T,"R
	JRST SETRUL
	CAIN T,"S
	JRST STOP
	CAIN T,"T
	JRST TYPPAR
	CAIN T,"U
	JRST UNIQUE
	CAIN T,"V
	JRST SETVEC
	CAIN T,"W
	JRST PLOT
	CAIN T,"X
	JRST SETX
	CAIN T,"Y
	JRST SETY
	CAIN T,"Z
	JRST SETSLP
TYIERR:	MOVEI T,"?
	JRST TYO

CTRLD:	MOVE T,[<.DSTAR PNTRL>#.DCLOSE]	;COMPLEMENT 340 USE
	XORB T,USE340
	XCT T
	POPJ P,

CTRLF:	SKIPN U		;DISPLAY ARG FRAMES
	HRLOI U,377777
	.NDIS U,
	JFCL
	POPJ P,

AGAIN:	POP P,T		;DO CURRENT RULE AGAIN
	JRST INIT

CONTIN:	SKIPE FINISH	;TRY TO CONTINUE
	JRST TYIERR
	POP P,T
	JRST LOOP

SETMAG:	DPB U,[040200,,DIS1]	;SET VECTOR SCALE (MAGNIFICATION)
	POPJ P,

COMPAR:	MOVEI T,PNTRL+2	;COMPLEMENT DISPLAYING OF PARAMETERS
	XORM T,PNTRL+1
	POPJ P,

SETRUL:	ANDI U,7777	;SET CURRENT RULE, AND DO IT
	MOVEM U,RULCOD
	POP P,T
	JRST TEST

STOP:	SKIPE FINISH
	POPJ P,		;S TYPED AT LISTEN LOOP
BARF [STOP> ]		;S TYPED AT THINK LOOP
	PUSHJ P,TYPAR1
	POP P,T
	JRST LISTEN

PLOT:	.OPEN IPLCHN,[5,,(SIXBIT \IPL\)]
	JRST TYIERR
	POP P,T
	.IOT IPLCHN,[PNTRL,,747]
	.CLOSE IPLCHN,
	JRST LISTEN
TYPPAR:	BARF [TYPE> ]	;TYPE PARAMETERS
TYPAR1:	MOVE C,RULCOD
	MOVE D,LENGTH
	MOVE E,NNODES
BARF [#3R L#4. N#5.]
	PUSHJ P,CRLF
	POPJ P,

SETVEC:	ANDI U,77	;SET VECTOR LENGTH
	LSH U,1		;TO ENSURE DX/2*2 = DX
	DPB U,[000700,,VECTAB]
	DPB U,[000700,,VECTAB+3]
	MOVEI T,1(U)
	LSH T,-1
	DPB T,[000700,,VECTAB+1]
	DPB T,[000700,,VECTAB+2]
	DPB T,[000700,,VECTAB+4]
	DPB T,[000700,,VECTAB+5]
	IMULI U,1567
	ADDI U,400
	LSH U,-12
	DPB U,[100700,,VECTAB+1]
	DPB U,[100700,,VECTAB+2]
	DPB U,[100700,,VECTAB+4]
	DPB U,[100700,,VECTAB+5]
	POPJ P,

SETX:	DPB U,[001200,,DIS2]	;SET X OF ORIGIN
	POPJ P,

SETY:	DPB U,[221200,,DIS2]	;SET Y OF ORIGIN
	POPJ P,

SETSLP:	ANDI U,177	;SET AMOUNT TO SLEEP
	MOVEM U,SLEEP
	POPJ P,
SETLEN:	POP P,T		;SET MAXIMUM PATH LENGTH
	CAILE U,100
	MOVEI U,100
	SKIPN U
	MOVEI U,1
	AOS U
	LSH U,-1
	MOVE T,MAXN
	PUSHJ P,GETCOR
	MOVEM U,MAXL
	ADDM U,MAXL
	ADDI U,10
	MOVN T,U
	HRLM T,DISP2
	ADD U,INIT1	;DISBUF
	SOS U
	HRRM U,INIT2
	PUSHJ P,CRLF
	JRST LISTEN

SETNOD:	POP P,T		;SET MAXIMUM NUMBER OF NODES
	ANDI U,77
	MOVSI T,400000	;POWER-OF-2-IFY
	ROT T,1
	CAMGE T,U
	JRST .-2
	MOVE U,T
	MOVE T,MAXL
	LSH T,-1	;2 VECTORS PER WORD
	PUSHJ P,GETCOR
	MOVEM U,MAXN
	MOVEI T,-1(U)
	HRRM T,GSTAT	;UPDATE NODES ADDRESS MASK
	ADDI U,NODES-1
	HRRM U,INIT3
	HRRM U,DISP2
	PUSH U,[100000]
	AOS U		;GETTING DISBUF
	HRRM U,INIT1
	MOVE T,MAXL
	LSH T,-1
	ADDI U,7(T)
	HRRM U,INIT2	;UPDATE INIT2
	PUSHJ P,CRLF
	JRST LISTEN

GETCOR:	LSH T,-12	;MAXL OR MAXN TO BLOCKS
	ADDI T,<NODES+20+1777>/2000(U)
	LSH U,12	;BLOCKS TO LOCATIONS
	.DCLOSE
	.CORE (T)
	SKIPA
	POPJ P,
BARF [.CORE LOST]
	POP P,T
	JRST CRLF
NEXT:	POP P,T		;DO NEXT RULE
NEWRUL:	AOS A,RULCOD
	TRNE A,10000
FINIS:	.VALUE
	JRST TEST

TEST:	SETCM A,RULCOD	;4 PATH CROSSING RULE PAIRS HAVE ONLY 3 CHOICES
	TRNN A,30
	JRST NEWRUL
	TRNN A,140
	JRST NEWRUL
	TRNN A,600
	JRST NEWRUL
	TRNN A,3000
	JRST NEWRUL
	MOVE A,RULCOD
	MOVSI B,4	;SET RULE0
	TRNE A,4000
	MOVSI B,5	;60, NOT 120, DEGREE BEND
	MOVEM B,RULE0
	LDB B,[030200,,A]	;SET RULE2 PAIRS
	MOVE C,R60A(B)
	MOVEM C,RULE2
	MOVE C,R120A(B)
	MOVEM C,RULE2+1
	LDB B,[050200,,A]
	MOVE C,R60B(B)
	MOVEM C,RULE2+2
	MOVE C,R120B(B)
	MOVEM C,RULE2+3
	LDB B,[070200,,A]
	MOVE C,R60C(B)
	MOVEM C,RULE2+4
	MOVE C,R120C(B)
	MOVEM C,RULE2+5
	LDB B,[110200,,A]
	MOVE C,R60D(B)
	MOVEM C,RULE2+6
	MOVE C,R120D(B)
	MOVEM C,RULE2+7
	LDB B,[010200,,A]	;SET 5 RULE1 CASES IN PARALLEL
	IMULI B,5
	MOVSI B,R1TAB(B)
	HRRI B,RULE1
	BLT B,RULE1+4
	LDB B,[000100,,A]	;SET 10. RULE3 CASES IN PARALLEL
	IMULI B,12
	MOVSI B,R3TAB(B)
	HRRI B,RULE3
	BLT B,RULE3+9
	JRST INIT
CRLF:	MOVEI T,15	;CARRIAGE RETURN
	PUSHJ P,TYO
	MOVEI T,12	;LINE FEED
TYO:	.IOT TYOCHN,T
	POPJ P,

TYPMES:	ILDB T,R	;START TYPING A MESSAGE, CLOBBERS T
	CAIN T,"$	;SIGNALS END OF CHARACTER STRING
	POPJ P,
	CAIN T,"#	;SIGNALS PLACE TO OUTPUT A VARIABLE NUMBER
	JRST ACNUM
	PUSHJ P,TYO
	JRST TYPMES

ACNUM:	PUSH P,[TYPMES]	;OUTPUT THE NUMBER IN AN AC
	ILDB U,R	;PICK UP AC LOCATION
	MOVE T,R
	ILDB T,T	;PICK UP NEXT CHARACTER IN STRING
	XORI T,".#12	;. SIGNALS DECIMAL OUTPUT
	CAIE T,12
	MOVEI T,10
	HRRM T,ACN2
	MOVE T,-"0(U)
ACN2:	IDIVI T,.	;12 IF DECIMAL, 10 OTHERWISE
	HRLM U,(P)
	SKIPE T
	PUSHJ P,ACN2
	HLRZ T,(P)
	ADDI T,"0
	JRST TYO
;FOR AUTOMATIC SEQUENCING THROUGH RULE SETS

R60A:	1,,6
	2,,6
	3,,6

R60B:	1,,14
	2,,14
	5,,14

R60C:	1,,30
	4,,30
	5,,30

R60D:	3,,60
	4,,60
	5,,60

R120A:	1,,12
	2,,12
	4,,12

R120B:	3,,24
	1,,24
	5,,24

R120C:	3,,42
	2,,42
	4,,42

R120D:	5,,50
	2,,50
	4,,50

R1TAB:	1,,2
	1,,4
	1,,10
	1,,20
	2,,40

	2,,2
	2,,4
	2,,10
	3,,20
	3,,40

	3,,2
	3,,4
	4,,10
	4,,20
	4,,40

	4,,2
	5,,4
	5,,10
	5,,20
	5,,40
R3TAB:	1,,16
	1,,26
	1,,32
	1,,34
	2,,46
	2,,52
	2,,54
	3,,62
	3,,64
	4,,70

	2,,16
	3,,26
	4,,32
	5,,34
	3,,46
	4,,52
	5,,54
	4,,62
	5,,64
	5,,70
RULES:
;EXIT CODE,,NODE STATE (ARRIVING FROM CODE 0)
;FIRST MATCH FOUND IS USED
;FIVE SPOKES USED (THIRD RETURN TO ORIGIN), FINISH
	7,,76

;FOUR SPOKES USED, ONLY ONE LEFT
	1,,36
	2,,56
	3,,66
	4,,72
	5,,74

;THREE SPOKES USED (SECOND RETURN TO ORIGIN), 2 POSSIBLE EXITS
RULE3:	1,,16	;1,,16 OR 2,,16
	1,,26	;1,,26 OR 3,,26
	1,,32	;1,,32 OR 4,,32
	1,,34	;1,,34 OR 5,,34
	2,,46	;2,,46 OR 3,,46
	2,,52	;2,,52 OR 4,,52
	2,,54	;2,,54 OR 5,,54
	3,,62	;3,,62 OR 4,,62
	3,,64	;3,,64 OR 5,,64
	4,,70	;4,,70 OR 5,,70

;TWO SPOKES USED (CROSSING PATH), 3 POSSIBLE EXITS
;OLD TWO AT 60 DEGREES -- USED IF 5,,0 IN DESERT
RULE2:	1,,6	;1,,6 OR 2,,6 OR 3,,6
	1,,14	;1,,14 OR 2,,14 OR 5,,14
	1,,30	;1,,30 OR 4,,30 OR 5,,30
	3,,60	;3,,60 OR 4,,60 OR 5,,60

;OLD TWO AT 120 DEGREES -- USED IF 4,,0 IN DESERT
	1,,12	;1,,12 OR 2,,12 OR 4,,12
	3,,24	;1,,24 OR 3,,24 OR 5,,24
	3,,42	;2,,42 OR 3,,42 OR 4,,42
	5,,50	;2,,50 OR 4,,50 OR 5,,50

;ONE SPOKE USED (FIRST RETURN TO ORIGIN), 4 POSSIBLE EXITS
RULE1:	1,,2	;1, 2, 3 OR 4,,2
	1,,4	;1, 2, 3 OR 5,,4
	1,,10	;1, 2, 4 OR 5,,10
	1,,20	;1, 3, 4 OR 5,,20
	2,,40	;2, 3, 4 OR 5,,40

;NONE USED (PREFERENCE IN DESERT), 2 POSSIBLE EXITS (AVOIDING SYMMETRY & INFINITE LINE)
RULE0:	4,,0	;4,,0 (120 DEGREE TURN) OR 5,,0 (60 DEGREE TURN)

	-1	;RULES TABLE END
UNIQUE:	JUMPE U,UNIQ2	;DO NEXT UNIQUE
	CAILE U,UNICNT
	JRST TYIERR
	SOS U
UNIQ1:	MOVE U,UNITAB(U)
	MOVEM U,RULCOD
	POP P,T
	JRST TEST

UNIQ2:	MOVSI U,-UNICNT+1
	MOVE T,RULCOD
UNIQ3:	CAMN T,UNITAB(U)
	AOJA U,UNIQ1
	AOBJN U,UNIQ3
	JRST TYIERR

UNITAB:
;120 DEGREE TURN IN VOID
520	;(2) L23. N16.
14	;(63.) L28. N22.
420	;(22.) L29. N18.
22	;(3) L33. N22.
423	;(14.) L33. N18. SYMMETRICAL
447	;(3) L33. N26.
1100	;(6) L34. N26.
1460	;(2) L35. N20.
1400	;(2) L39. N26.
407	;(1) L42. N28.
64	;(1) L43. N29.
1200	;(18.) L44. N34. ALMOST FLOWER
1403	;(1) L44. N29.
1002	;(1) L45. N30.
1506	;(1) L45. N31.
510	;(2) L46. N31.
1044	;(1) L46. N33.
206	;(45.) L48. N36. FLOWER
424	;(18.) L48. N27. TRIANGULAR
23	;(3) L50. N30.
2040	;(2) L50. N35. SYMMETRICAL, FLOWERLIKE
2400	;(2) L50. N31.
1500	;(2) L52. N34.
1064	;(1) L53. N33.
1407	;(1) L53. N34.
2004	;(1) L54. N34.
1213	;(1) L55. N33.
2403	;(1) L55. N34.
67	;(6) L57. N36.
1462	;(1) L58. N32.
1325	;(1) L61. N40.
516	;(1) L62. N40.
1223	;(1) L62. N33.
2064	;(1) L66. N38.
1025	;(1) L67. N39.
1065	;(1) L67. N42.
1224	;(2) L67. N44.
2065	;(1) L68. N41.
2002	;(1) L69. N41.
403	;(1) L71. N43.
1004	;(1) L71. N43. PATTERN DIFFERENT FROM 403R
10	;(2) L73. N46.
1245	;(1) L73. N45.
2410	;(2) L73. N45. PATTERN DIFFERENT FROM 1245R
2406	;(1) L77. N43.
1003	;(1) L78. N46.
2510	;(2) L79. N48.
20	;(2) L81. N44.
24	;(1) L83. N46.
2020	;(2) L83. N46. PATTERN DIFFERENT FROM 24R
2024	;(1) L85. N48.
125	;(3) L86. N58. SYMMETRICAL, PRETTY
1317	;(1) L86. N57.
60	;(6) L90. N50.
2010	;(2) L90. N56.
1066	;(1) L92. N52.
1324	;(1) L93. N66. ALMOST SYMMETRICAL
1020	;(2) L94. N51.
1024	;(1) L96. N53.
405	;(1) L97. N54.
525	;(6) L99. N46. VERY COMPACT
1265	;(1) L99. N66.
1217	;(1) L101. N58.
1005	;(1) L102. N59.
25	;(1) L105. N55.
2506	;(1) L105. N59.
62	;(1) L107. N57.
27	;(3) L110. N57.
1405	;(1) L110. N59.
1412	;(2) L112. N66. SORT OF PRETTY, CITY-ISH
402	;(1) L113. N59.
502	;(1) L113. N64.
462	;(1) L114. N52. COMPACT
1406	;(1) L114. N62.
2504	;(1) L118. N66.
1402	;(1) L119. N69.
1305	;(1) L122. N68.
1303	;(1) L126. N70.
304	;(1) L130. N80.
1203	;(1) L130. N72.
1502	;(1) L132. N71.
1413	;(2) L134. N78. CITY-ISH
1503	;(1) L136. N74.
1242	;(2) L138. N84.
2402	;(1) L140. N70.
2503	;(1) L148. N81.
2407	;(1) L151. N78.
1513	;(2) L152. N84. CITY-ISH
1304	;(1) L153. N82.
2	;(1) L159. N86.
1307	;(1) L162. N91.
412	;(1) L165. N90. CITY-ISH
2247	;(1) L173. N107. ALMOST SYMMETRICAL, PRETTY
513	;(1) L176. N96. CITY-ISH
2066	;(1) L176. N87.
1063	;(1) L180. N95.
1026	;(1) L181. N91.
2063	;(1) L194. N94.
65	;(1) L196. N97.
305	;(1) L196. N121.
2003	;(1) L196. N102.
16	;(1) L201. N111. SEMI-CITY-ISH
224	;(1) L203. N132.
1202	;(1) L206. N112.
1444	;(1) L212. N119.
2500	;(2) L219. N116.
225	;(1) L235. N125.
1207	;(1) L236. N127.
1016	;(1) L237. N126. SEMI-CITY-ISH
1212	;(1) L242. N127.
1263	;(1) L245. N125.
2026	;(1) L247. N119.
2502	;(1) L247. N125.
400	;(2) L248. N124.
1227	;(1) L248. N127.
1313	;(1) L262. N141.
2516	;(1) L281. N141. CITY-ISH
110	;(2) L284. N174. RATHER LOW DENSITY
1000	;(2) L285. N143.
1312	;(1) L286. N153.
1322	;(1) L287. N143.
2405	;(1) L289. N139.
63	;(1) L294. N136.
2025	;(1) L295. N141.
2005	;(1) L296. N150.
2017	;(1) L304. N149.
212	;(1) L306. N186.
26	;(1) L309. N142. FAIRLY DENSE
1262	;(1) L321. N158.
2325	;(1) L327. N187. TRIES TO BE DOILY
2244	;(1) L330. N169. PRETTY DOILY
1302	;(1) L338. N169.
6	;(1) L345. N174.
2245	;(1) L347. N189. PREGNANT DOILY
17	;(1) L354. N178. SLIGHTLY CITY-ISH
2062	;(1) L354. N166.
323	;(1) L356. N168.
503	;(1) L393. N207. ALMOST SYMMETRICAL, PRETTY
1504	;(1) L398. N184.
2047	;(1) L415. N246. HEXAGONAL, NOT DENSE
223	;(1) L419. N197.
1204	;(2) L451. N194.
213	;(1) L454. N281.
325	;(1) L475. N233.
1050	;(2) L496. N262. CITY-ISH
1042	;(1) L512. N273.
1267	;(1) L514. N252.
1323	;(1) L533. N249.
2264	;(3) L534. N283. ALMOST DOUBLE DOILY
2267	;(1) L553. N287. BETTER TRY AT DOUBLE DOILY
2243	;(1) L558. N287. REAL DOUBLE DOILY
1507	;(1) L561. N253.
322	;(1) L606. N273.
406	;(1) L609. N270. 6-DIAMOND STAR INCLUSION
2513	;(1) L623. N284. CITY-ISH
1017	;(1) L631. N281.
2505	;(1) L631. N285. PRETTY, GOES AROUND & AROUND
1222	;(1) L636. N307.
2262	;(1) L663. N325. AUGMENTED DOUBLE DOILY
265	;(1) L684. N348.
1010	;(2) L697. N314.
1062	;(1) L708. N323.
2006	;(1) L711. N338.
2317	;(1) L731. N378. AUGMENTED DOUBLE DOILY
3	;(1) L732. N335.
66	;(1) L735. N327.
1257	;(1) L738. N354. LARGE VOID INCLUSION
505	;(1) L747. N362.
1006	;(1) L839. N377.
312	;(1) L843. N450.
0	;(2) L897. N406.
5	;(1) L970. N411. DENSE
205	;(1) L1020. N536. TRIES FOR SPIRAL
1327	;(1) L1063. N472. SOMEWHAT PRETTY
1247	;(1) L1116. N486. BEARD
1442	;(1) L1457. N669.
1007	;(1) L1515. N640. FAIRLY DENSE
2204	;(1) L1545. N712. ALMOST HYPER DOILY
2304	;(1) L1550. N711. ALMOST HYPER DOILY
222	;(1) L1564. N687.
2312	;(1) L1574. N781. TUMOROUS DOILY
2202	;(2) L1632. N720. HYPER DOILY
4	;(1) L1660. N681.
2413	;(1) L1672. N686. SOMEWHAT CITY-ISH
1040	;(2) L1688. N765.
512	;(1) L1742. N699. CITY-ISH
2265	;(1) L1831. N835. EXPLODED HYPER DOILY
1225	;(1) L1932. N865.
2213	;(1) L1978. N884. LOSING DOUBLE HYPER DOILY
504	;(1) L2056. N900.
2225	;(1) L2155. N955. LOSING DOUBLE HYPER DOILY
2512	;(1) L2377. N940. CITY
2507	;(1) L2526. N1008. SOMEWHAT PRETTY
2307	;(1) L2565. N1144. GOOD TRY AT DOUBLE (OFFSET) HYPER DOILY
1046	;(2) L2578. N1114. NEAT CHANNEL FILL AT END
2303	;(2) L2754. N1208. GOOD TRY AT DOUBLE HYPER DOILY, PRETTY
2263	;(1) L2811. N1214. PRETTY, ALMOST DOUBLE HYPER DOILY
507	;(1) L2857. N1179.
50	;(2) L3566. N1556. CITY
500	;(2) L3793. N1566.
2016	;(1) L3943. N1520. NEAT CHANNEL FILL AT END
324	;(1) L4318. N1871.
203	;(1) L4371. N1989.
2050	;(2) L4419. N1846. CITY
264	;(1) L4432. N1923.
2242	;(1) L4802. N1967. SIX-SIDED, NON-HEXAGONAL HYPER HYPER DOILY
7	;(1) L5132. N1964. NEAT TUNNEL
2212	;(1) L5148. N2108. LOSING TRY AT HYPER HYPER DOILY
2302	;(1) L5708. N2367. COMPLEX OF HYPER DOILYS
410	;(2) L5715. N2145. CITY
1505	;(1) L5865. N2330. TWO LARGE VOID INCLUSIONS
202	;(1) L7143. N3052.
313	;(1) L7275. N3005.
2224	;(1) L7524. N2975. HYPER DOILY WITH DEFECT AREA
1243	;(1) L7565. N4529. OCHERINA
2207	;(1) L7584. N2976. TRUE HYPER HYPER DOILY, SUPER NEAT
506	;(1) L7882. N3126. SORT OF PRETTY
2217	;(1) L9260. N3666. LOSING TRY AT HYPER HYPER DOILY
2412	;(1) L10307. N3736. CITY
2124	;(1) L10460. N5185.
204	;(1) L10795. N4418.
1045	;(1) L17859. N7187.
2416	;(1) L22847. N8066. CITY
1043	;(1) L45477. N17411.
2223	;(1) L52549. N19174. HYPER HYPER DOILIFIES
2313	;(1) L83618. N31529. HYPER HYPER DOILIFIES, PRETTY
2227	;(1) L220142. N77257.
;120 DEGREE UNCERTAIN LENGTHS
;RULCOD	LENGTH		NNODES	<A>X Y	R.H.<NODES>	COMMENTS
100	;143031.	77535.	513000	73 (5 USED)	BEARDY
101	;143025.	77531.	264777	37 (5 USED)	BEARDY
104	;109138.	58460.	375777	23 (3 USED)	VERY BEARDY
105	;109138.	58460.	375777	23 (3 USED)	VERY BEARDY
120	;204894.	127041.	370071	73 (5 USED)	BEHAVIOR UNCLEAR
121	;205017.	127123.	445654	37 (5 USED)	BEHAVIOR UNCLEAR
124	;199490.	127192.	443674	73 (5 USED)	LOOKS MAYBE INFINITE
2104	;113361.	59357.	201777	73 (5 USED)	BEHAVIOR UNCLEAR
2105	;113554.	59551.	201777	67 (5 USED)	BEHAVIOR UNCLEAR
2205	;252410.	87989.	106777	37 (5 USED)	MAYBE HYPER HYPER DOILY SPIRAL
2222	;345046.	119754.	000545	75 (5 USED)	HYPER HYPER DOILIFYING
2322	;225302.	78892.	777253	37 (5 USED)	HYPER HYPER DOILIFYING
2327	;157588.	55279.	000453	67 (5 USED)	HYPER HYPER DOILIFYING
;120 DEGREE INFINITES
;DOUBLE-HEXAGON ZIPPERS
2460	;(4) UNEVEN START
2462	;(2) EVEN START
;HEXAGONAL (ZERO POINTED STAR) SPIRALS
200	;(18.) CHEVRON POINTING AWAY FROM CENTER, HEX CENTER
207	;(9) LIKE 200R, BUT NO HEX IN CENTER
102	;(18.) CHEVRON POINTING OBLIQUE TO PROPAGATION, HEX CENTER
106	;(18.) LIKE 102, BUT TADPOLE CENTER
2110	;(2) CHEVRON OBLIQUE, HEX AND CRUD AT CENTER
2120	;(2) CHEVRON OBLIQUE, WEIRD CENTER
1124	;(1) CHEVRON OBLIQUE, MUCH CRUD AT CENTER
1104	;(2) WEIRD CENTER, KINK ON ONE DIAGONAL
2100	;(2) WEIRD CENTER, KINKS ON TWO 120-DEGREE RADII
2042	;(5) PERIOD 3 EDGE, PERIOD 6 SPIRAL FROM CENTER
;DIAMOND (TWO POINTED STAR) SPIRALS
440	;(15.) ONE MAJOR RADIUS DIAMONDS, OTHER HEXAGONS
2442	;(1) ONE MAJOR RADIUS SINGLE HEX, OTHER DOUBLE IN LINE
2444	;(1) ONE MAJOR RADIUS SINGLE HEX, OTHER DOUBLE OFFSET
;THREE POINTED FROB SPIRAL
444	;(1) SINGLE HEX ROW DOWN EACH POINT
;ARROW (FOUR POINTED STAR) SPIRALS
42	;(4) SINGLE HEX ROW DOWN EACH POINT
43	;(2) TWO HEX ROW DOWN ONE POINT, SINGLE DOWN OTHERS
;SIX-POINTED STAR SPIRALS
442	;(1) ONE HEX ROW DOWN EACH POINT
242	;(1) 1, 1, 1, 1, 1, 2 HEX ROWS, WEIRD CENTER
40	;(3) TRIPLE HEX ROW ON ONE POINT
245	;(1) 1, 1, 1, 2, 2, 2 HEX ROWS, WEIRD CENTER
243	;(1) LIKE 245R, BUT DIFFERENT WEIRD CENTER
;WANKEL SPIRALS
12	;(6)
;BRICK BUILDING OR SHOWER ROOM CORNER SPIRALS
56	;(6) SINGLE HEXAGON AT CENTER
52	;(18.) TWO HEXAGONS AT CENTER
450	;(9) ONE HEXAGON, ONE LOOP AT CENTER
1410	;(4) TWO LOOPS, ONE LINE
413	;(2) SEVERAL LOOPS FROM CENTER
2257	;(1) OH MY GOSH, DIAGONALS TOO
;SHOOT GROWERS
262	;(1) SHOOT ABOUT L12000.
263	;(1) SHOOT ABOUT L2300.
302	;(1) SHOOT ABOUT L1800.
303	;(1) SHOOT ABOUT L250.
2000	;(1) SHOOT ABOUT L480.
2001	;(1) SHOOT ABOUT L450.
2007	;(1) SHOOT ABOUT L440.
;60 DEGREE TURN IN VOID
4000	;(162.) L9. N7.
4022	;(81.) L12. N8.
4021	;(54.) L15. N9.
4002	;(81.) L18. N12. DIFFERENT FORMATION WAYS (EG 5403R VS 5406R)
4027	;(27.) L21. N12.
6001	;(18.) L27. N16.
4001	;(18.) L30. N18.
5041	;(6) L37. N21.
5101	;(6) L45. N25.
6007	;(9) L48. N24.
4007	;(9) L63. N33.
5047	;(3) L68. N36.
5107	;(1) L411. N181.
5307	;(2) L438. N181.
5201	;(2) L609. N246.
5001	;(2) L615. N273.
5007	;(1) L2373. N990.
5207	;(1) L2478. N915.
;60 DEGREE UNCERTAIN LENGTHS
;RULCOD	LENGTH		NNODES	<A>X Y	R.H.<NODES>	COMMENTS
5401	;85614.		29241.	255777	73 (5 USED)	CRYSTAL GROWTH
5405	;183866.	62247.	033777	57 (5 USED)	CRYSTAL GROWTH
5407	;137570.	46794.	000527	67 (5 USED)	CRYSTAL GROWTH
;60 DEGREE INFINITES (TOTAL 162.)
4011	;(108.) ZIPPER WITH UNEVEN START
4016	;(54.) ZIPPER WITH EVEN START

UNICNT==.-UNITAB	;452 = 298.
PDL:	BLOCK 20
PATCH:	BLOCK 40

MAXLEN==4000	;MAX PATH LENGTH, SEGMENTS
MAXNOD==2000	;MAX NODES WORM VISITS (POWER OF 2)

MAXL:	MAXLEN
MAXN:	MAXNOD
LENGTH:	0	;CURRENT PATH LENGTH, SEGMENTS
NNODES:	0	;CURRENT NUMBER OF NODES VISITED
BIGLEN:	0	;RULCOD,,LENGTH OF LONGEST PATH FOUND SO FAR
RULCOD:	0	;RULE CODE (12. BITS)
SLEEP:	0	;AMOUNT TO SLEEP AFTER ADDING EACH PATH SEGMENT
TIMER:	0
CURNUM:	0
FINISH:	0	;-1 ON FINAL RETURN TO ORIGIN

BITS:	1,,0	;SPOKE USAGE BITS FOR STATE OF NODES
	40,,0
	20,,0
	10,,0
	4,,0
	2,,0

JUMPS:	ADDI A,1000	;X+1
	ADDI A,777	;X+1, Y-1
	SUBI A,1	;Y-1
	SUBI A,1000	;X-1
	SUBI A,777	;X-1, Y+1
	ADDI A,1	;Y+1

;SQRT 3 = 1.567 OCTAL
VECTAB:	200032
	313415
	313615
	200232
	213615
	213415
PNTRL:	DISP1,,.+1	;PATH ORIGIN
	DISP2,,.+1	;PATH VECTORS
	DISP3,,0	;PARAMETERS

DISP1:	-10,,DIS1-1
DISP2:	-MAXLEN/2-10,,DISBUF-2
DISP3:	-10,,DIS3-1

DIS1:	20117
DIS2:	221000,,1000
	3000

DIS3:	20157
	220000,,61600
DISNUM:	606060,,603700
	3000

CONSTA
VARIAB

NODES:	BLOCK MAXNOD	;6 BIT USAGE STATE,,X Y (INITIALLY ALL -1)

	100000
DISBUF:	BLOCK MAXLEN/2+10

	1

END BEG
