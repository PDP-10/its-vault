BEGOF("VERTI")

COMMENT

Skipping vertically down the page.  This is short now, but it is
expected to grow with special features for high-resolution devices ;

;

PROCEDURES
PUBLIC SIMPLE PROCEDURE VERTI! ;$"#
BEGIN "VERTI!"
SPREADM _ PREFMODE ;
LEADNM _ LEADFM _ 0 ; TES 11/2/74 ;
MLEADNM _ MLEADFM _ MSPREADM _ MILLVERTIDEFAULT ; TES 11/2/74 ;
END "VERTI!" ;
PUBLIC SIMPLE BOOLEAN PROCEDURE DIMENSIONED(INTEGER DEFAULT; REFERENCE INTEGER K) ;$"#
BEGIN "DIMENSIONED"
K _ CVD(E("0"+DEFAULT, NULL)) ;
IF ITS(MILLS) OR ITS(MILL) THEN BEGIN PASS ; RETURN(TRUE) END ;
IF ITS(INCHES) OR ITS(INCH) THEN BEGIN PASS ; K_1000*K ; RETURN(TRUE) END ;
IF ITS(LINE) OR ITS(LINES) THEN PASS ;
RETURN(FALSE) ;
END "DIMENSIONED" ;
PUBLIC SIMPLE PROCEDURE DPREFACE ;$"#
BEGIN "DPREFACE" TES 11/2/74 ;
INTEGER L, M ;
DBREAK ;
PASS ;
IF NOT ON THEN BEGIN DIMENSIONED(0, L) ; RETURN END ;
M _ ABS(MILLVERTI) ;
IF DIMENSIONED(0, L) THEN BEGIN M _ L ; L _ 0 END ;
IF NOFILL THEN BEGIN MLEADNM _ M ; LEADNM _ L END
ELSE BEGIN MLEADFM _ M ; LEADFM _ L END ;
END "DPREFACE" ;
PUBLIC RECURSIVE PROCEDURE DSKIP(BOOLEAN GRPSKIP) ;$"#
BEGIN
BOOLEAN GM ;
DBREAK ; PASS ;
IF GRPSKIP THEN BEGIN GM_GROUPM ; GROUPM _1 ; END ;
IF ITS(TO) THEN
	BEGIN "SKIP TO"
	DAPART ; PASS ;
	IF ITS(COLUMN) THEN BEGIN PASS; TOCOLUMN(CVD(E(CVS(COL+1),NULL))) END
	ELSE BEGIN IF ITS(LINE) THEN PASS ; TOLINE(CVD(E("1", NULL))) END ;
	END "SKIP TO"
ELSE	BEGIN
	IF THISTYPE>INTERNTYPE OR THISTYPE=-TERQ OR NEXTSCH(_) OR NEXTSCH(:) THEN
		SKIPLINES(1)
	ELSE IF DIMENSIONED(1, K) THEN
		BEGIN
		ASSUREAREA ;
		IF GRPSKIP THEN MILLGSKIP(AREAIXM) _ MILLGSKIP(AREAIXM) + K
		ELSE MILLSKIP(AREAIXM) _ MILLSKIP(AREAIXM) + K
		END
	ELSE SKIPLINES(K) ;
	END ;
IF GRPSKIP AND GM = 0 THEN DAPART ;
END "DSKIP" ;
PUBLIC SIMPLE PROCEDURE DSPACING ;$"#
BEGIN "DSPACING" TES 11/2/74 ;
INTEGER L, M ;
PASS ;
IF NOT ON THEN BEGIN DIMENSIONED(0, L) ; RETURN END ;
M _ ABS(MILLVERTI) ;
IF DIMENSIONED(0, L) THEN BEGIN M _ L ; L _ 0 END ;
MSPREADM _ M ;
SPREADM _ L + 1 ;
END "DSPACING" ;
PUBLIC RECURSIVE PROCEDURE SKIPLINES(INTEGER HMLINES) ;$"#
IF ON THEN
BEGIN "SKIPLINES"
ASSUREAREA ;
IF HMLINES > 0 THEN
	IF GROUPM=0 THEN FIND!ROOM(-HMLINES, 0, COL, LINE, 0)
	ELSE	BEGIN "GROUP SKIP"
		INTEGER I ;
		FIND!ROOM(0, HMLINES, COL, LINE, 0) ;
		IF NOT GLINEM THEN GLINEM _ OLX + 1 ;
		IF COL>COLS AND LINE=0 THEN FTGP2 _ HMLINES  TES 11/2/74;
		ELSE
			BEGIN
			FOR I _ 1 THRU HMLINES DO AA[COL, LINE+I] _
				NEWBLANK(IF GLINEM=0 AND I=1 THEN ABV ELSE ABV!BLW) ;
			LINE _ LINE + HMLINES ;
			END ;
		END "GROUP SKIP" ;
END "SKIPLINES" ;
PUBLIC RECURSIVE PROCEDURE TOLINE(INTEGER LINENO) ;$"#
    IF ON THEN
	BEGIN "TOLINE"
	ASSUREAREA ;
	IF LINENO < LINE THEN
		IF COL = COLS THEN
			BEGIN NEXTPAGE ; IF LINENO>1 THEN OPENAREA(AREAIXM) END
		ELSE BEGIN COL _ COL+1 ; LINE _ 0 ; END ;
	IF LINENO=1 THEN LINE_1 ELSE FIND!ROOM(0, 0, COL, LINENO-1, 0) ;
	END "TOLINE" ;
FINISHED

ENDOF("VERTI")
