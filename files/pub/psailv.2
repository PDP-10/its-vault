TITLE SALV FILE SALVAGER

A_1
B_2
C_3
D_C+1
T_D+1
		;6 UNUSED
DOT_7
PAGES_10
LINES_11
RUBS_12
NULLS_14
		;13 UNUSED
INBPT_15
OUTBPT_16
P_17

;I/O CHANNELS

TTYOCH__1
INCH__2
OUTCH__3

;MISCELLANEOUS PARAMETERS

LPP__=72
BLKLEN__200

DEFINE REPORT(LOC,MESSAGE)
<	SKIPN A,LOC
	JRST .+4
	PUSHJ P,DECPNT
	MOVE A,[440700,,[ASCIZ /MESSAGE/]]
	PUSHJ P,OUTSTR
>
DEFINE RR ! (CHAR,OLD,NEW)
<	REPORT (N!OLD,< !CHAR!'s (old) changed to (new)@>)
>

NOLIT

EXTERNAL INDEV,OUTDEV
INTERNAL TRANS,.VER1,.VER2,DOFN2,DIFN2
TRANS:	MOVEI A,6
	HRLM A,INDEV
	MOVEI A,7
	HRLM A,OUTDEV
	SKIPN A,INDEV+3
	SKIPE A,OUTDEV+3
	.SUSET [.SSNAM,,A]
	.OPEN INCH,INDEV
	JRST INLOS
	SKIPE A,OUTDEV+3
	.SUSET [.SSNAM,,A]
	.OPEN OUTCH,OUTDEV
	JRST OUTLOS
	MOVE A,[INCH,,CHSTS]
	.RCHSTS A,
	SETZB DOT,PAGES
	SETZB NULLS,RUBS
	SETZM TOTALC
	SETZM SUBPAG
	FOR @! I IN (176)
<	SETZM N!I
>
	PUSHJ P,INBUF
	PUSHJ P,OUTB1
	PUSHJ P,PRNPAG
	SKIPA
LOOP0:	PUSHJ P,OUT
LOOP:	PUSHJ P,IN
	JUMPL T,FINISH
	SKIPGE T,CTAB(T)
	JRST (T)
LOOP1:	JUMPG LINES,LOOP0
	PUSHJ P,OUT
	AOS SUBPAG
	PUSHJ P,PRNPAG
	JRST LOOP

FINISH:	PUSHJ P,OUTBUF
	.CLOSE OUTCH,
	REPORT (TOTALC,< CHARACTERS IN FILE@>)
	REPORT (NULLS,< NULLS DELETED@>)
	REPORT (RUBS,< RUBOUTS DELETED@>)
	REPORT (LINES,< LINE NUMBERS DELETED@>)
;	FOR I IN (<under-score,30,137>,<tilde,32,33>,<not-eq,33,32>,<back-arrow,137,30>)
;<	RR (I)
;>
	FOR I IN (<close-brace,176,175>)
<	RR (I)
>
	REPORT (PAGES,< PAGES PROCESSED@>)
	POPJ P,
DEFINE TILL (N)
<	FOR I_.-CTAB,N-1
<	I
>>
DEFINE ZAP ! (OLD,NEW)
<	SETZ [	MOVEI T,NEW
		AOS N!OLD
		JRST LOOP1]
>

CTAB:	SETZ [AOJA NULLS,LOOP]
	TILL 14
	SETZ [	MOVEI T,14
		AOJA PAGES,LOOP1]
	TILL 176
	ZAP (176,175)
	TILL 177
	SETZ [AOJA RUBS,LOOP]
OUTSTR:	ILDB B,A
	JUMPE B,CPOPJ
	CAIN B,"@"
	JRST [	PUSHJ P,CRLF
		JRST OUTSTR]
	PUSHJ P,TYO
	JRST OUTSTR

CRLF:	MOVEI B,15
	PUSHJ P,TYO
	MOVEI B,12
	JRST TYO

DECPNT:	IDIVI A,=10
	PUSH P,B
	SKIPE A
	PUSHJ P,DECPNT
	POP P,B
	ADDI B,"0"
TYO:	.IOT TTYOCH,B
	POPJ P,

OUTLOS:	SKIPA C,[OUTDEV]
INLOS:	MOVEI C,INDEV
	HRLZ A,(C)
	PUSHJ P,SIXO
	MOVEI B,":"
	PUSHJ P,TYO
	SKIPN A,3(C)
	JRST LOSS1
	PUSHJ P,SIXO
	MOVEI B,";"
	PUSHJ P,TYO
LOSS1:	MOVE A,1(C)
	PUSHJ P,SIXO
	MOVEI B," "
	PUSHJ P,TYO
	MOVE A,2(C)
	PUSHJ P,SIXO
	MOVE A,[440700,,[ASCIZ / FILE NOT FOUND@/]]
	JRST OUTSTR

SIXO:	MOVE D,[440600,,A]
	MOVEI T,6
SIX1:	ILDB B,D
	JUMPE B,CPOPJ
	ADDI B,40
	PUSHJ P,TYO
	SOJG T,SIX1
	POPJ P,

DSKIZ:	HRLI D,440700
DSKIZ1:	ILDB T,D
	JUMPE T,CPOPJ
	PUSHJ P,OUT
	JRST DSKIZ1

DSKDEC:	IDIVI D,=10
	HRLM T,(P)
	SKIPE D
	PUSHJ P,DSKDEC
	HLRZ T,(P)
	ADDI T,"0"
	JRST OUT

PRNPAG:	PUSH P,T
	HRLZ D,CHSTS
	PUSHJ P,DSKSIX
	MOVEI T,":"
	PUSHJ P,OUT
	MOVE D,CHSTS+3
	PUSHJ P,DSKSIX
	MOVEI T,";"
	PUSHJ P,OUT
	MOVE D,CHSTS+1
	PUSHJ P,DSKSIX
	MOVEI T," "
	PUSHJ P,OUT
	MOVE D,CHSTS+2
	PUSHJ P,DSKSIX
	MOVEI T,11
	PUSHJ P,OUT
	PUSHJ P,OUT
	PUSHJ P,OUT
	PUSHJ P,OUT
	PUSHJ P,OUT
	PUSHJ P,OUT
	MOVEI D,[ASCIZ /Page /]
	PUSHJ P,DSKIZ
	MOVEI D,1(PAGES)
	PUSHJ P,DSKDEC
	SKIPN D,SUBPAG
	JRST TOP1
	MOVEI T,"-"
	PUSHJ P,OUT
	PUSHJ P,DSKDEC
TOP1:	PUSHJ P,DSKCRL
	PUSHJ P,DSKCRL
	MOVEI LINES,LPP-2
	POP P,T
	POPJ P,

DSKSIX:	JUMPE D,CPOPJ
DSKSX1:	MOVEI C,0
	LSHC C,6
	MOVEI T,40(C)
	PUSHJ P,OUT
	JUMPN D,DSKSX1
	POPJ P,

DSKCRL:	MOVEI T,15
	PUSHJ P,OUT
	MOVEI T,12
	JRST OUT
	PUSHJ P,INBUF
IN:	SOSGE INCC
	JRST .-2
	ILDB T,INBPT
	MOVE A,(INBPT)
	TRZN A,1
CPOPJ:	POPJ P,
LINNUM:	MOVEM A,(INBPT)
	HRROS T
	CAMN A,[BYTE (7) 3,3,3,3,3]
	POPJ P,
	HRRZS T
	CAMN A,[BYTE (7) 40,40,40,40,40]
	JRST PGMARK
	MOVEI B,5
	PUSHJ P,IN
	SOJG B,.-1
	CAIE T,11
	JRST 4,.
	AOJA LINES,IN

PGMARK:	MOVEI B,11
	PUSHJ P,IN
	SOJG B,.-1
	MOVEI T,14
	POPJ P,

INBUF:	MOVE A,[BYTE (7) 3,3,3,3,3 (1) 1]
	MOVEM A,INBLK
	MOVE A,[INBLK,,INBLK+1]
	BLT A,INBLK+BLKLEN-1
	MOVE A,[-BLKLEN,,INBLK]
	.IOT INCH,A
	MOVEI A,BLKLEN*5
	MOVEM A,INCC
	MOVE INBPT,[440700,,INBLK]
	POPJ P,

	PUSHJ P,OUTBUF
OUT:	SOSGE OUTCC
	JRST .-2
	IDPB T,OUTBPT
	AOS TOTALC
	POPJ P,

OUTBUF:	MOVE A,[-BLKLEN,,OUTBLK]
	.IOT OUTCH,A
OUTB1:	MOVE A,[BYTE (7) 3,3,3,3,3]
	MOVEM A,OUTBLK
	MOVE A,[OUTBLK,,OUTBLK+1]
	BLT A,OUTBLK+BLKLEN-1
	MOVEI A,BLKLEN*5
	MOVEM A,OUTCC
	MOVE OUTBPT,[440700,,OUTBLK]
	POPJ P,
.VER1:	.FNAM1
.VER2:	.FNAM2
INCC:	0
OUTCC:	0
TOTALC:	0
INBLK:	BLOCK BLKLEN
OUTBLK:	BLOCK BLKLEN
N176:	0
ZS:	0
SUBPAG:	0
CHSTS:	BLOCK 10
DIFN2:	SIXBIT />/
DOFN2:	SIXBIT /LIST/

XLIST
LIT
VAR
LIST

END
