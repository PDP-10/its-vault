BEGOF("FILES")

COMMENT

      *** Variations at Different Sites for Most Procedures ***

Processing of input file names and opening of input files is
installation-dependent.  Most of the options are handled by a single
routine called OPENTOREAD.

Output file opening is simpler and handled by WRITEON.
;

IFC TENEX THENC
DEFINE CHANGE(A,B)= [IF NULSTR(A) AND FULSTR(B) THEN B ELSE 0];
ELSEC
DEFINE CHANGE(A,B)= [IF A=0 THEN B ELSE 0];
ENDC

PROCEDURES
PUBLIC SIMPLE PROCEDURE FILES! ;$"#
BEGIN "FILES!"
THISFILE _ "(NO FILE)" ;
MAINFILE _ INFILE ;
END "FILES!" ;
PUBLIC SIMPLE PROCEDURE FINIFILES ;$"#
BEGIN "FINIFILES"
IF GENREXT THEN OUTFILE _ OUTFILE &
    IFC CMUVER THENC (IF ABS(DEVICE)=XGP THEN ".XGO" ELSE ".DOC") ENDC
    IFC SAILVER THENC (IF ABS(DEVICE)=XGP THEN ".XGP" ELSE ".DOC") ENDC
    IFCR PARCVER THENC PARCEXT ENDC
    IFC ISIVER THENC (IF ABS(DEVICE)=XGP THEN ".XGO" ELSE ".DOC") ENDC	RT01 10/25/74;
    IFC ITSVER THENC DOCEXT ENDC;	PJ 5/27/74;
END "FINIFILES" ;
IFSITE TENEX THENK
PRIVATE SITE(TENEX) STRING SIMPLE PROCEDURE CVFIL(STRING FILENAME; REFERENCE STRING EXT, PPN) ;$"#
	BEGIN
	STRING NAME ;
	PPN _ IF FILENAME[1 FOR 1] = "<" THEN SCANTO(">", FILENAME, TRUE) ELSE NULL ;
	NAME _ SCANTO(".;", FILENAME, FALSE) ;
	EXT _ IF FILENAME[1 FOR 1] = "." THEN SCANTO(";", FILENAME, FALSE) ELSE NULL ;
	RETURN(NAME) ;
	END ;
ENDC
IFSITE  CMUVER THENK
PRIVATE SITE(CMUVER) STRING SIMPLE PROCEDURE CVPPN(INTEGER VALUE) ;$"#
BEGIN "CVPPN"
OWN SAFE INTEGER ARRAY A[0:1];
INTEGER ERRSW;
STRING S;
DEFINE CALLI="'47000000000",	DECCMU="-3";

IF VALUE = 0 THEN RETURN (NULL);

A[0]_A[1]_0;
START!CODE
    SETZM 0,ERRSW;
    MOVE 1,A;
    HRLI 1,VALUE;
    CALLI 1,DECCMU;
    SETOM 0,ERRSW;
END;

RETURN ("["&
	(  IF ERRSW THEN (CVOS(VALUE LSH -18)&","&CVOS(VALUE LAND '777777))
		    ELSE (CVSTR(A[0])&CVSTR(A[1])[1 FOR 3])  )
	& "]");

END "CVPPN";
ENDC
PUBLIC INTEGER PROCEDURE OPENTOREAD(INTEGER MODE ;
	STRING FILEKIND ; REFERENCE STRING FILENAME;
	IFC TENEX THENC STRING ELSEC INTEGER ENDC EXTDEFAULT, PPNDEFAULT) ;$"#
BEGIN TES 8/24/74 PROCEDURIZED ;
label labeltogetaroundcompilerbug;
INTEGER CHAN, C ; BOOLEAN GOTIT ;
IFC TENEX THENC STRING ELSEC INTEGER ENDC  FEXT, FPPN, EXTD, PPNI, PPND, NAME, EXT, PPN ;
STRING NF ;
STRING INDEVICE ;	RKJ: 5-17-74 ;
SETBREAK(LOCAL!TABLE,":",NULL,"IS");
IF (CHAN_GETCHAN)<0 THEN EARLYWARNING("NO CHANNELS ARE LEFT FOR INPUT!") ;
EOF _ 0 ;
GOTIT _ FALSE ;
DO  BEGIN "NAMELOOP"
	RKJ: 6-FEB-75 MOVED NEX THREE LINES INSIDE LOOP;
	INDEVICE_SCAN(FILENAME,LOCAL!TABLE,DUMMY);
	IF NULSTR(FILENAME) THEN BEGIN FILENAME_INDEVICE; INDEVICE_"DSK" END ;
	OPEN(CHAN,INDEVICE,MODE, 2,0,150,BRC,EOF);
	NAME _ CVFIL(FILENAME, FEXT, FPPN) ;
	FOR C _ 0 THRU 5 DO
	    BEGIN
	    EXT _ FEXT ; PPN _ FPPN ;
	    CASE C OF
	        BEGIN "LKPCASES"
	    	BEGIN END ;
	    	IF (EXTD_EXT_CHANGE(FEXT, EXTDEFAULT)) = 0 THEN CONTINUE ;
	    	IF (PPNI_PPN_CHANGE(FPPN, INPPN)) = 0 THEN CONTINUE ;
	    	IF (EXT_EXTD) = 0 OR (PPN_PPNI) = 0 THEN CONTINUE ;
	    	IF (PPND_PPN_CHANGE(FPPN, PPNDEFAULT)) = 0 THEN CONTINUE ;
	    	IF (EXT_EXTD)=0 OR (PPN_PPND)=0 THEN CONTINUE ;
	        END "LKPCASES" ;
	    GOTIT _ XLOOKUP(CHAN,NAME,EXT,0,PPN);
	    IF GOTIT THEN DONE ;
	    END ;
	IF GOTIT THEN DONE ;
	IFC PARCVER THENC  TES 10/21/74 SO FONT 2 "SERIF" WORKS ;
	IF FILEKIND[1 TO 4] = "Font" AND FULSTR(NF_FONTEQUIV(NAME)) THEN
		FILENAME _ NF ELSE
	ENDC
	BEGIN
	IF NOT SWDBACK THEN OUTSTR(CRLF) ; SWDBACK _ TRUE ;
	OUTSTR(FILEKIND & FILENAME & " not found." & CRLF & "Read file: ");
	IFC TENEX THENC
		RELEASE(CHAN);
		OUTSTR(PPND_PPNDEFAULT) ;
		DO	BEGIN  TES 10/22/74 ;
			CHAN _ GTJFNL(PPND,'162000000000,'100000101,
				NULL,PPNDEFAULT[2 TO -1],
				NAME,EXTDEFAULT[2 TO ],
				NULL, NULL, NULL) ;
			IF CHAN>-1 THEN DONE ;
			OUTSTR("XXX"&CRLF&"Read file: ") ; PPND_NULL ;
			END
		UNTIL FALSE ;
		SETINPUT(CHAN,150,BRC,EOF) ; TES 10/16/74 ;
		OPENF(CHAN, 2) ;
		DONE ;
	ELSEC
		FILENAME _ INCHWL ;
		IFC CMUVER THENC
		  IF NULSTR(FILENAME) THEN FILENAME_"NULL:EMPTY";
		ENDC RKJ: 6-FEB-75;
	ENDC
	END ;
    END "NAMELOOP"
UNTIL GOTIT ;
labeltogetaroundcompilerbug:
FILENAME _ UNCVFIL(CHAN, NAME, EXT, PPN) ;
RETURN(CHAN) ;
END "OPENTOREAD" ;

IFSITE TENEX THENK TES 10/25/73 ;
PRIVATE SITE(TENEX) STRING PROCEDURE SCANTO(STRING BRKS; REFERENCE STRING SCANNEE; BOOLEAN INCLUDE) ;$"#
	BEGIN
	INTEGER DUMMY ;
	SETBREAK(LOCAL!TABLE, BRKS, NULL, IF INCLUDE THEN "IA" ELSE "IR") ;
	RETURN(SCAN(SCANNEE, LOCAL!TABLE, DUMMY)) ;
	END ;
ENDC
IFSITE TENEX THENK
PUBLIC SITE(TENEX) SIMPLE PROCEDURE SFBSZ(INTEGER CHAN, SIZE) ;$"#
	BEGIN "SFBSZ"
	INTEGER K ;
	DEFINE JSYS=['104000000000], SFBSZ=[JSYS '46];
	K _ CVJFN(CHAN) ;
	START!CODE "BYTE16"
	MOVE 1,K; MOVE 2,SIZE; SFBSZ ;
	END "BYTE16" ;
	END "SFBSZ" ;
ENDC
IFSITE TENEX THENK
PUBLIC SITE(TENEX) SIMPLE STRING PROCEDURE UNCVFIL(INTEGER CHAN; STRING NAME, EXT, PPN) ;$"#
	RETURN(JFNS(CHAN, 0)) ;
ENDC

IFSITE NOT TENEX THENK
PUBLIC SITE(NOT TENEX) SIMPLE STRING PROCEDURE UNCVFIL(INTEGER CHAN, NAME, EXT, PPN) ;$"#
BEGIN
IFC ITSVER THENC PJ 11/9/74 I DON'T LIKE TRAILING SPACES OF SYSTEM CVXSTR ;
    SIMPLE STRING PROCEDURE CVXSTR(INTEGER SIXBIT);
	BEGIN
	STRING RETSTR;
	SIMPLE INTEGER PROCEDURE CHAR(REFERENCE INTEGER A);
	    START!CODE
	    MOVEI 1,0;
	    MOVE 2,A;
	    LSHC 1,6;
	    MOVEM 2,A;
	    ADDI 1,'40;
	    END;

	RETSTR_NULL;
	WHILE SIXBIT NEQ 0 DO RETSTR_RETSTR&CHAR(SIXBIT);
	RETURN(RETSTR)
	END ;
ENDC
RETURN(
	IFC SAILVER THENC
		CV6STR(NAME) &
		(IF EXT=0 THEN NULL ELSE "." & CV6STR(EXT)[1 TO 3]) &
		(IF PPN=0 THEN NULL ELSE "[" & CVXSTR(PPN)[1 TO 3] &
			"," & CVXSTR(PPN)[4 TO 6] & "]")
	ENDC
	IFC CMUVER THENC
		CVXSTR(NAME) &
		(IF EXT=0 THEN NULL ELSE "." & CVXSTR(EXT)[1 TO 3]) &
		CVPPN(PPN)
	ENDC
	IFC ITSVER THENC PJ 11/7/74 ;
		(IF PPN=0 THEN NULL ELSE CVXSTR(PPN)&";")&CVXSTR(NAME)&" "&CVXSTR(EXT)
	ENDC
    ) ;
END;
ENDC
PUBLIC INTEGER SIMPLE PROCEDURE WRITEON(BOOLEAN BINARY; STRING FILENAME) ;$"#
BEGIN "WRITEON"
INTEGER CH ;
IF (CH _ GETCHAN) < 0 THEN
	BEGIN
	WARN("=",<"No channel for writing "&FILENAME>);
	RETURN(-1) ;
	END ;
K _ 0 ; OPEN(CH, "DSK", IF BINARY THEN 8 ELSE 0, 0, 2, DUMMY, DUMMY, K) ;
ENTER(CH, FILENAME, DUMMY_0) ;
IF DUMMY THEN WARN("=","ENTER failed for "&FILENAME);
RETURN(CH) ;
END "WRITEON" ;
IFSITE TENEX THENK
PUBLIC SITE(TENEX) BOOLEAN SIMPLE PROCEDURE XLOOKUP(INTEGER CHAN; STRING NAME, EXT; INTEGER JUNK; STRING PPN) ;$"#
	BEGIN COMMENT RETURNS TRUE IF SUCCESSFUL ;
	BOOLEAN FLAG ;
	LOOKUP(CHAN, PPN & NAME & EXT, FLAG) ;
	RETURN(NOT FLAG) ;
	END ;
ENDC

IFSITE NOT TENEX THENK
PUBLIC SITE(NOT TENEX) BOOLEAN SIMPLE PROCEDURE XLOOKUP(INTEGER CHAN, NAME, EXT, JUNK, PPN) ;$"#
START!CODE "XLOOKUP"
    MOVE 2,CHAN;
    LSH 2,23;
    IOR 2,['076017777774]; COMMENT LOOKUP 0,-4(17) ;
    SETO 1,0; COMMENT TRUE ;
    XCT 0,2;
    SETZ 1,0; COMMENT FALSE ;
END "XLOOKUP";
ENDC
FINISHED

ENDOF("FILES")
