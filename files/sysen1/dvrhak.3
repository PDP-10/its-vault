; -*- Midas -*-

title DVRHAK - Hack to launch DVRSPL on MC with translation.

a=:1

chdski=:2
chusro=:3

dir:	sixbit /.DOVR./
fn1:	sixbit /.SPOOL/
fn2:	sixbit /BINARY/

go:	.gensym a,
	.call [	setz ? sixbit /OPEN/
		movsi .bio
		movei chusro
		move [sixbit /USR/]
		movei 0
		setz a ]
	 .lose %lsfil
	.call [	setz ? sixbit /OPEN/
		movsi .uii
		movei chdski
		move [sixbit /DSK/]
		move fn1
		move fn2
		setz dir ]
	 .lose %lsfil
	.call [	setz ? sixbit /LOAD/
		movei chusro
		setzi chdski ]
	 .lose %lssys
	.iot chdski,a
	hrrz a,a
	.uset chusro,[.supc,,a]
	.call [	setz ? sixbit /TRANAD/
		movsi 3
		movei chusro
		move [-1,,[sixbit /PK1/]]
		setz [-1,,[sixbit /DSK/]] ]
	 .lose %lssys
	.call [	setz ? sixbit /DISOWN/
		movsi 5
		setzi chusro ]
	 .lose %lssys
	.logout 1,

end go
