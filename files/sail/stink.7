TITLE TSTINKING ODOR

ZR=0
P=1
A=2
B=3
C=4	;FOR L.OP
D=5
T=6
TT=7
ADR=10
BOT=11
CKS=12
LL=13
RH=14
MEMTOP=15
NBLKS=16
FF=17

TPCHN==1
TYOC==2
TYIC==3
ERCHN==4	;CHANNEL FOR ERROR DEVICE

LOWLOD==0	;LOWEST LOCATION LOADED
LPDL==20
CBUFL==2000	;COMMAND BUFFER LENGTH (MOBY LONG!)
DOLL==44	;REAL DOLLAR SIGN (NOT ALT MODE ETC.)

ICOMM==10000	;INITIAL COMMON

;RIGHT HALF FLAGS
ALTF==1
LOSE==2
ARG==4
UNDEF==10	;COMPLAIN ABOUT UNDEF
INDEF==20	;GLOBAL LOC
GLOSYM==40	;ENTER GLOBAL SYMS INTO DDT TABLE
SEARCH==100	;LIBRARY
CODEF==200	;SPECIAL WORD LOADED
GPARAM==400	;ENTER GPA LOCALS
COND==1000	;LOAD TIME CONDITIONAL
NAME==2000	;SET JOB NAME TO PROGRAM NAME
LOCF=4000	;LOCAL IN SYM PRT
JBN==10000	;JOB NAME SET BY JCOMMAND
GOF==20000	;LEAVING LDR BY G COMMAND
GETTY==40000	;GE CONSOLE
MLAST==100000	;LAST COMMAND WAS AN "M"
NOTNUM==200000	;USED FOR DUMMY SYMBOL LOGIC
SETDEV==400000	;DEVICE SET LAST TIME

STNBLN==200	;STINK INPUT BUFFER SIZE

MFOR==101000	; FOR .CBLK
MBLKS==301000

	LOC 41
	JSR TYPR
	0	;TSINT

IF2,COMLOD=TPOK	;IS YOUR TAPE OK?

DEFINE INFORM A,B
IF1,[PRINTX / A = B
/]
TERMIN

DEFINE CONC69 A,B,C,D,E,F,G,H
A!B!C!D!E!F!G!H!TERMIN
LOC 100
DDPTR:	<INITCR*2000>
REL:	ADDI@ T,FACTOR
ABS:	HRRZ ADR,T
DATABK:	HRRZS ADR
	PUSHJ P,GETBIT
	TRZE TT,4
	JRST DATBK1
	PUSHJ P,RRELOC
COM1:	ADDB T,AWORD
	ADD T,RH
	HLL T,AWORD
	CLEARB RH,AWORD
IFN LOWLOD,[CAIGE ADR,LOWLOD
	AOJA ADR,DATABK
]GCR2:	CAMLE ADR,MEMTOP
	JRST GCR1
	TRNE FF,CODEF
	MOVEM T,(ADR)
	TRNN FF,CODEF
	MOVEM T,@ADRPTR
	AOJA ADR,DATABK
ERR1:
DATBK1:	PUSHJ P,RLKUP
	TRNE TT,2
	JRST DECODE	;LINK OR EXTEND
USE:	ROTC T,3
	HRL ADR,TT
	SKIPE C,TIMES
	CLEARM TIMES
	DPB C,[(261200)ADR]
	JUMPGE D,USE1A
	TLNE B,200000
	JRST USE2	;PREV DEFINED
	TRNE FF,UNDEF
	JRST ERR2
	PUSHJ P,DOWN
	MOVEM ADR,(D)
CDATABK:	JRST DATABK

ADRPTR:	<INITCR*2000>(ADR)
BPTR:	<INITCR*2000>(B)
DPTR:	<INITCR*2000>(D)



GCR1:	TRNE	ADR,400000	; PURE?
	JRST	HIGHSG		; YES, USE HIGH SEG
	PUSHJ P,GETMEM
	JRST GCR2

HIGHSG:	CAMLE	ADR,HIGTOP	; WITHIN HIGH BOUND?
	PUSHJ	P,GETHI		; NO, GROW
	MOVEM	T,(ADR)	; STORE
	AOJA	ADR,DATABK

; ROUTINE TO GROW HIGH SEGMENT

GETHI:	PUSH	P,A
	SKIPE	TT,USINDX	; DO WE KNOW USER INDEX
	JRST	GETHI1		; YES, CONTINUE

	PUSH	P,B		; NO, SAVE B AND FIND IT
	MOVE	A,[SQUOZE 0,USER]
	.EVAL	A,		; FIND OUT WHERE USER IS
	.VALUE	[ASCIZ /NO.USER/]
	MOVSI	A,(A)		; SETUP FOR GETLOC
	HRRI	A,A		; INTO A
	.GETLOC	A,		; A NOW CONTAINS USER INDEX
	MOVE	B,[SQUOZE 0,L]	; GET DIVISION FACTOR
	.EVAL	B,
	.VALUE	[ASCIZ /NOEL.NOEL/]
	IDIVI	A,(B)		; GET REAL INDEX IN A
	POP	P,B		; RESTORE B
	MOVEM	A,USINDX	; SAVE INDEX
	MOVE	TT,A

GETHI1:	MOVEI	A,200001	; FOR SEG #1 FROM CORE JOB
	DPB	TT,[MFOR,,A]	; STORE USER POINTER
	MOVEI	TT,(ADR)	; GET WHERE TO POINTER
	SUBI	TT,400000-2000	; ROUND UP AND REMOVE HIGH BIT
	ASH	TT,-10.		; TO BLOCKS
	DPB	TT,[MBLKS,,A]	; STORE IT ALSO
	.CBLK	A,		; GOT TO SYSTEM
	PUSHJ	P,SCE
	MOVE	A,HIBLK		; GET NO. OF HIGH BLOCKS
	SUBM	TT,A		; GET NEW BLOCKS
	MOVEI	ZR,400000	; GET BITS
	IDPB	ZR,HIGMAP	; AND INTO PAGE MAP
	SOJN	A,.-1
	MOVEM	TT,HIBLK	; AND STORE
	ASH	TT,10.		; NOW COMPUTE NEW HIGTOP
	TRO	TT,400000	; WITH HIGH BIT
	SUBI	TT,1
	MOVEM	TT,HIGTOP
	POP	P,A
	POPJ	P,


USE2:	MOVE T,1(D)	;FILL REQUEST
	PUSHJ P,DECGEN
	ADDM T,AWORD
	ADDM TT,RH
	JRST DATABK

USE1A:	MOVE T,ADR
USE1:	TLO A,400000
	TRNN FF,UNDEF
	JRST DEF1A	;ENTER DEF
ERR2:	(5000+SIXBIT /UGA/)
	JRST DATABK


DEF1:	TLO A,600000
	TRNN FF,INDEF+GPARAM	;DEFINE ALL SYMBOLS
	TLNE A,40000	;OTHERWISE, FLUSH LOCALS
	JRST ENT
	JRST DEF4

RDEF:	TRO TT,10	;SET FLAG FOR REDEFINITION
DEF:	ROTC T,3
	PUSHJ P,RRELOC
DFSYM1:	PUSH P,CDATABK
DEFSYM:	MOVEM T,T1
	MOVEM A,CGLOB	;SAVE SQUOOZE IN CASE WE SATISFY POLISH
DFSYM2:	JUMPGE D,DEF1	;NOT PREV SEEN
	TLNN B,200000	;PREVIOUSLY DEFINED
	JRST PATCH5	;PREVIOUSLY NEEDED

DEF2:	TRNE TT,100	;REDEFINE NOT OK
DEF3:	MOVEM T,1(D)
	CAME T,1(D)
	(5000+SIXBIT /MDG/)
DEF4:	TRZ FF,GPARAM
	POPJ P,

PATCH3:	PUSH P,PATCH6
PATCH:	HRRZ C,D
	SUB C,T2
	HRLS C
	HRRM C,PATCH4
	EXCH BOT,C
	ADD BOT,C
	MOVNI C,(C)
	ADD C,T2
	MOVSS C
	HRR C,T2
	SOS C
PATCH4:	POP C,.(C)
	JUMPGE C,.-1
	SKIPE SATED	;ARE THERE POLISH FIXUPS LYING ARROUND
	PUSHJ P,UNSATE	;DELETE THEM
PATCH6:	POPJ P,.+1

PATCH7:	PUSHJ P,LKUP1A
	JUMPGE D,DEF1
PATCH5:	HRRZM D,T2
	MOVEI TT,(D)	;CHECK FOR INITIAL SYMBOL
	CAIG TT,EISYME	;IS IT GREATER
	JRST DEF3	;DON'T HACK

PATCH1:	MOVE T,T1
	AOBJP D,PATCH3
	SKIPGE B,(D)
	JRST PATCH3	;CLEAN UP TABLE
	HLL ADR,B
	HRRZS B
	TLZE ADR,POLREQ	
	JRST POLSAT	;POLISH REQUEST
	CAIGE B,LOWLOD
	JRST PATCH1
	TLZN ADR,100000
	JRST GEN	;GENERAL REQUEST
	PUSH P,CPTCH1
UNTHR:	TRNN	B,400000	; HIGH SEG?
	MOVEI	B,@BPTR	; NO FUDGE
	HRL T,(B)
	HRRM T,(B)
	HLRZ B,T
	JUMPN B,UNTHR
CPTCH1:	POPJ P,PATCH1


GEN:	PUSHJ P, DECGEN
	TRNN	B,400000	; HIGH SEG
	MOVEI	B,@BPTR		; NO GET REAL LOC
	ADD T,(B)
	ADD TT,T
	HRR T,TT
	MOVEM T,(B)
	JRST PATCH1

DECGEN:	MOVEI TT,0
	TLNE ADR,10
	MOVNS T
	LDB C,[(261200)ADR]
	SKIPE C
	IMUL T,C
	LDB C,[(220200)ADR]
	TLNE ADR,4
	MOVSS T
	XCT WRDTAB(C)

WRDTAB:	POPJ P,		;FW
	EXCH T,TT	;RH
	HLLZS T		;LH
	ROT T,5		;AC


DECODE:	TRNN TT,1
	JRST THRDR	;6 > LINK REQ
	PUSHJ P,GETBIT
	JRST @.+1(TT)
	DEF	;DEFINE SYMBOL (70)
	COMMON	;COMMON RELOCATION (71)
	LOCGLO	;LOCAL TO GLOBAL RECOVERY (72)
	LIBREQ	;LIBRARY REQUEST (73)
	RDEF	;REDEFINITION (74)
	REPT	;GLOBAL MULTIPLIED BY 1024>N>0 (75)
	DEFPT	;DEFINE AS POINT (76)


RLKUP:	PUSHJ P,RPB

LKUP:	MOVE A,T
LKUP1B:	MOVE D,BOT
LKUP3:	MOVEI B,0(ADR)	;CONTAINS GLOBAL OFFSET
	TRNN FF,CODEF
	MOVEM B,CPOINT+1	;$.
	TLZA A,700000
LKUP7:	MOVE D,[(,EISYM-EISYME)EISYM]
LKUP1A:	MOVE B,(D)
LKUP1:	TLZ B,600000
	CAMN B,A
	JRST LKUP5
	AOBJP D,LKUP6
	AOBJP D,LKUP6
	SKIPGE B,(D)
	JRST LKUP1
	JRST .-3

LKUP6:	CAIE D,EISYME
	JRST LKUP7
	TRO FF,LOSE
LKUP5:	MOVE B,(D)
	POPJ P,

RRELOC:	PUSHJ P,RPB
RELOC:	HLRZ C,T
	TRNE TT,1
	ADD T,FACTOR
	TRNE TT,2
	ADD C,FACTOR
	HRL T,C
	POPJ P,

DOWN:	HRL C,BOT
	SUB BOT,[(1)1]
	HRR C,BOT
	BLT C,-1(D)
	SUBI D,(BOT)
	PUSHJ P,ENT1
	ADDI D,(BOT)
	POPJ P,

DEF1A:	PUSH P,CDATABK

ENT:	SUB BOT,[(2)2]
	MOVEM A,(BOT)
	MOVEM T,1(BOT)
ENT1:	PUSHJ P,SYMCHK
	HRRZ C,BOT
	JRST SYMS7
THRDR:	PUSHJ P,RPB
	TLNE T,100000
	ADD T,FACTOR
	HRLI T,100000
	JUMPGE D,USE1
	MOVE B,(D)
	TLNE B,200000
	JRST THRD2	;PREV DEFINED
	PUSHJ P,DOWN	;ENTER LINK REQUEST
	MOVEM T,(D)
	JRST DATABK

THRD2:	HRRZ B,T
	MOVE T,1(D)
	PUSHJ P,UNTHR
	JRST DATABK

LOCGLO:	JUMPGE D,DATABK	;LOCAL-GLOBAL RECOVERY
	MOVE T,D	;D POINTS TO LOCAL
	TLO A,40000	;GLOBAL
	PUSHJ P,LKUP1B	;FIND OCCURANCE OF GLOBAL
	IORM A,(T)	;SMASH OLD LOCAL OCCURENCE
	JUMPGE D,DATABK
	TLNN B,200000
	JRST DATABK
	MOVE B,1(D)	;ALREADY DEFINED
	MOVEM B,T1
	HRRZM D,T2
	ADDI D,2
	PUSHJ P,PATCH	;CLOBBER DEFINITION
	MOVE D,BOT
	PUSH P,CDATABK
	JRST PATCH7	;FILL IN OLD LOCAL REQ

LIBREQ:	JUMPL D,DATABK	;ALREADY THERE
	MOVEI T,0
	JRST USE1

REPT:	MOVEM T,TIMES
	JRST DATABK

COMMON:	ADD RH,COMLOC
	JRST COM1

DEFPT:	MOVEI T,@LKUP3
	TRO FF,GPARAM
	JRST DFSYM1



LDCND:	TRO FF,COND
	JRST LIB

LIB6:	CAIN A,12	;END OF CONDITIONAL
	JRST .OMIT1
	HRRZS T
	CAIN A,1
	CAIE T,5	;LOADER VALUE CONDITIONAL
	CAIN A,11	;COUNT MATCHING CONDITIONALS
	AOS FLSH
	JRST OMIT

LIB2:	TRNE FF,COND
	JRST LIB6
	CAIN A,5
	JRST LIB7
	PUSHJ P,RPB
	CAIN A,4	;PRGM NAME
	TLNN T,40000	;REAL END
	JRST OMIT
	JRST OMIT1	;LEAVE LIB SEARCH MODE

LIB1:	TRO FF,SEARCH
	PUSHJ P,RPB
	JUMPGE T,.-1
	TRZ FF,SEARCH
LIB4:	PUSHJ P,LKUP
	JUMPGE D,LIB3	;NOT ENTERED
	TRNE FF,COND
	JRST LIB5
	TLNE B,200000	;RQST NOT FILLED
LIB3:	TLC T,200000	;"AND NOT" BIT
LIB5:	TLNE T,200000
	JRST LIB1	;THIS ONE LOSES
LIB:	CLEARM FLSH
LIB7:	PUSHJ P,RPB
	JUMPGE T,LIB4
.OMIT1:	SOSGE FLSH
OMIT1:	TRZ FF,SEARCH+COND;END OF SEGMENT,LOAD THIS PROG
OMIT:	PUSH P,.


RPB:	SOSL TC
	JRST GTWD
	PUSHJ P,GTWD	;SOAK UP CKSUM
	AOJN CKS,RCKS

LOAD:	JRST (LL)	;READ SWITCH
LOAD2:	PUSHJ P,GTWD
	LDB A,[(220700)T]
	MOVEM A,TC
	MOVSI A,770000
	ANDCAM A,BITPTR
	LDB A,[(310700)T]
LOAD1:	MOVE P,SAVPDL
	JUMPLE T,OUT
	CAIL A,LOADTE-LOADTB
	JRST TPOK
	TRNE FF,SEARCH
	JRST LIB2
	TRZ FF,COND	;FUDGE FOR IMPROPER USE OF .LIBRA
	JRST @.+1(A)
LOADTB:	TPOK
	LDCMD	;LOADER COMMAND (1)
	ABS	;ABSOLUTE (2)
	REL	;RELOCATABLE (3)
	PRGN	;PROGRAM NAME (4)
	LIB	;LIBRARY (5)
	COMLOD	;COMMON LOADING (6)
	GPA	;GLOBAL PARAMETER ASSIGNMENT (7)
SYMSW:	DDSYMS	;LOCAL SYMBOLS (10)
	LDCND	;LOAD TIME CONDITIONAL (11)
SYMFLG:	SETZ OMIT	;END LDCND (12)
	HLFKIL	;HALF KILL A BLOCK OF SYMBOLS
	OMIT	;OMIT BLOCK GENERATED BY LIBRARY CREATOR
	OMIT	;LATER WILL BE .ENTRY
	AEXTER	;BLOCK OF STUFF FOR SDAT OR USDAT
	OMIT	;FOR .LIFND
	GLOBS	;GLOBAL SYMBOLS BLOCK TYPE 20
	FIXES	;FIXUPS BLOCK TYPE 21
	POLFIX	;POLISH FIXUPS BLOCK TYPE 22
	LINK	;LINK LIST HACK (23)
	OMIT	;LOAD FILE (24)
	OMIT	;LOAD LIBRARY (25)
	OMIT	;LVAR (26) OBSOLETE
	OMIT	;INDEX (27) NEW DEC STUFF
	OMIT	;HIGH SEG(30)
LOADTE:
	
OUT:	MOVE P,SAVPDL
ADRM:	POPJ P,

;HERE TO PROCESS AN .EXTERN

AEXTER:	PUSHJ P,RPB	;READ AND LOOK UP SYMBOL
	TLO T,40000	;TURN ON GLOBAL BIT
	PUSHJ P,LKUP	;NOW LOOK IT UP
	JUMPGE D,.+3	;NEVER APPEARED, MUST ENTER
	TLNE B,200000	;SKIP IF NOT DEFINED
	JRST AEXTER	;THIS ONE EXISTS, GO AGAIN
	MOVE B,USDATP	;GET POINTER TO USDAT
	PUSH P,A	;SAVE SYMBOL
	TLZ A,740000	;KILL ALL FLAGS
	TLO A,400000	;NOT LOADE BIT
	MOVE T,B	;SAVE A COPY OF THIS
	ADD T,[3,,3]	;ENOUGH ROOM?
	JUMPGE T,TMX	;NO, BARF AT THE LOSER
	MOVEM T,USDATP	;NOW SAVE
	TRNN	B,400000	; HIGH SEG?
	MOVEI	B,@BPTR		; NO GET REAL LOC
	MOVEM A,(B)	;STORE INTO CORE IMAGE BEING BUILT
	POP P,A	;RESTORE SYMBOL
	MOVEI T,1(B)	;ALSO COMPUTE 'VALUE' OF SYMBOL
	PUSHJ P,DEFSYM
	JRST AEXTER

	
;USDAT HAS OVERFLOWN

TMX:	(3000+SIXBIT /TMX/)
GPA:	PUSHJ P,RPB
	MOVEM T,T2
	MOVEI T,0

LDCMD:	ADDI T,LDCMD2+1
	HRRM T,LDCMD2
	ROT T,4
	DPB T,[(330300)LDCVAL]
	TRO FF,UNDEF+CODEF
	HRRM ADR,ADRM
	MOVEI B,@LKUP3
	MOVEM B,CPOINT+1
	MOVEI ADR,T1
	JSP LL,DATABK

LDCMD1:	TRZ FF,UNDEF+CODEF
	HRRZ ADR,ADRM
	CLEARB RH,AWORD
	MOVE D,T1
LDCMD2:	JRST @.
	GPA1
	JMP	;JUMP BLOCK (1)
	GLOBAL	;GLOBAL LOCATION ASSIGNMENT (2)
	COMSET	;COMMON ORIGIN (3)
	RESPNT	;RESET GLOBAL RELOCATION (4)
	LDCVAL	;LOADER VALUE CONDITIONAL (5)
	.OFFSET	;GLOBAL OFFSET (6)
	L.OP	;LOADER EXECUTE (7)
	.RESOF	;RESET GLOBAL OFFSET
JMP:	JUMPE D,JMP1
	TRNN FF,JBN
	TLO FF,NAME
	MOVEM D,SA
JMP1:	MOVEI LL,LOAD2
	JRST LOAD2

GLOBAL:	TRO FF,INDEF
	HRRM D,RELADR
	MOVE ADR,D
	MOVEI D,RELADR
GLOB1:	HRRM D,REL
	JRST JMP1

RESPNT:	TRZ FF,INDEF
	MOVEI D,FACTOR
	HRRZ ADR,FACTOR
	JRST GLOB1

LDCVAL:	JUMP D,JMP1
	TRO FF,SEARCH+COND
	CLEARM FLSH
	JRST JMP1

.OFFSET:	HRRM D,LKUP3
	JRST JMP1

L.OP:	MOVE B,T1	;B=3 C=4 D=5
	MOVE 4,T1+1
	MOVE 5,T1+2
	TDNN B,[(757)777777]
IFN 0,[	JRST L.OP2
	HRRM ADR,ADRM
	HRRZ ADR,ADRPTR
	MOVEM 4,4(ADR)
	MOVEM 5,5(ADR)
	MOVEM B,20(ADR)
	HRLZI B,(.RETUUO)
	MOVEM B,21(ADR)
	MOVEM B,22(ADR)
	.XCTUUO NBLKS,
	MOVE 4,4(ADR)
	MOVE 5,5(ADR)
	HRRZ ADR,ADRM
	JRST .+2
L.OP2:]	IOR B,[0 4,5]
	XCT B
	MOVEM 4,.VAL1
	MOVEM 5,.VAL2
	JRST JMP1
.RESOF:	MOVEI	D,0
	JRST	.OFFSET
SETJNM:	MOVEI A,SJNM1
	HRRM A,SPTY
	SETZM A
	MOVE B,[(600)A-1]
	PUSHJ P,SPT
	MOVEM A,JOBNAM
	MOVEI A,TYO
	HRRM A,SPTY
	MOVE A,PRGNAM
	POPJ P,

SJNM1:	TRC T,40
DDT4:	IDPB T,B
	POPJ P,


GPA1:	MOVE T,T2
	PUSHJ P,LKUP
	MOVE T,T1
	MOVEI TT,100	;DON'T GENERATE MDG
	TRO FF,GPARAM
	PUSHJ P,DEFSYM
	JRST JMP1

DDSYMS:	MOVN D,TC
	ADDM D,BOT
	PUSHJ P, SYMCHK
	MOVE C,BOT
	SUB C,D
	HRLS C
	HRR C,BOT
	ADDM D,DDPTR
	HRLZS D
	ADDB D,DDPTR
	BLT C,-1(D)
C8:	JUMP D,8.	;DON'T MUNG D (USED TO BE SOJ D,8.)
	HRL D,TC	;WORD COUNT IN THE LEFT HALF
	ADD D,TC	;ONE ABOVE FREE SYMBOL AREA

DDLUP:		JUMPL D,TPOK	;OOPS!
	SUB D,[2,,2]
	PUSHJ P,RPB
	LDB TT,[(410300)T]
	TLNE T,40000
	JRST DDLUP2
	TLZ T,240000
	TLO T,100000
DDLUP1:	MOVEM T,(D)	;SQUOOZE
	PUSHJ P,RRELOC
	MOVEM T,1(D)	;VALUE
	JRST DDLUP

DDLUP2:	TLZ T,740000	;MARK AS BLOCK NAME
	JRST DDLUP1

.INSRT POLISH >
;HERE TO HALF KILL LOCAL SYMBOLS DEFINED BY LOADER

HLFKIL:	MOVE D,DDPTR	;RESTORE POINTER TO LOCAL TABLE
	ADD D,[2,,2]	;BUMP IT
NXTKIL:	MOVE B,D	;PUT POINTER ALSO IN B
	PUSHJ P,RPB	;GET A WORD
	TLZ T,740000	;MAKE SURE NO FLAGS
NXTSYK:	MOVE A,(B)	;GET A SYMBOL
	TLZN A,740000	;IF PROG NAME HIT, TIME TO QUIT
	JRST NXTKIL
	CAME T,A	;IS THIS ONE
	JRST NOKIL	;NO TRY AGAIN
	TLO A,400000	;TURN ON HALF KILL BIT IN DDT
	IORM A,(B)	;RESTORE SYMBOL TO TABLE
	JRST NXTKIL

NOKIL:	AOBJN B,.+1
	AOBJN B,NXTSYK	;TRY ANOTHER
	JRST NXTKIL	;TRY ANOTHER ONE




SYMCHK:	MOVEM BOT,BOTM
	HRRZS BOT
	CAIG BOT,LOSYM+2
	JRST TMS
	MOVE BOT,BOTM
	POPJ P,

BOTM:	0

TMS:	XCT GETMEM
	JRST TMSERR
	AOS NBLKS
	HRRZ BOT,ADRPTR
	ADDI BOT,2000
	HRRM BOT,ADRPTR
	HRRM BOT,BPTR
	HRRM BOT,DPTR
	HRRZ BOT,GETMEM
	ROT BOT,12
TMS2:	HRRM BOT,TMSBLT
	SOS TMSBLT
	HRLS BOT
	SUBI BOT,2000
	MOVSS BOT
	SUB BOT,[2000,,2000]
	MOVEM BOT,TMS3
TMSBLT:	BLT BOT,.
	MOVE BOT,TMS3
	CAML BOT,TMS1
	JRST TMS2
	MOVE BOT,TMSBLT
	SUBI BOT,2000
	MOVEM BOT,TMSBLT
	MOVE BOT,TMS4
	XCT TMSBLT
	MOVE BOT,DDPTR
	ADDI BOT,2000
	MOVEM BOT,DDPTR
	MOVE BOT,LBOT
	ADDI BOT,2000
	MOVEM BOT,LBOT
	MOVE BOT,BOTM
	ADDI BOT,2000
	AOS GETMEM
	POPJ P,

TMS1:	LOSYM+2000,,LOSYM+4000
TMS3:	0
TMS4:	LOSYM,,LOSYM+2000

TMSERR:	PUSHJ P,COREQ	;QUESTION USER
	JRST TMS
	MOVE BOT,BOTM
	(3000+SIXBIT /TMS/)

	
PRGN:	PUSHJ P,RPB
	MOVE A,T
	MOVEM A,PRGNAM
	TLZE FF,NAME
	PUSHJ P,SETJNM
	MOVE T,FACTOR
	HRL T,ADR
	TLNE A,40000	;REAL PRGM END
	HRRZM ADR,FACTOR
	TLO A,740000
	PUSHJ P,ENT
	HRRI BOT,-2(BOT)
	PUSHJ P,SYMCHK
	MOVE B,DDPTR
	HRLI D,2(BOT)
	HRR D,BOT
	BLT D,-3(B)
	HRRZ D,BOT
	TLZ A,140000
	MOVEM A,-2(B)
	MOVEM T,-1(B)
	PUSHJ P,SYMS
	MOVSI C,740000
	SKIPL SYMSW
	PUSHJ P,SHUFLE	;PUT THE SYMBOLS IN THE RIGHT ORDER
	HLLZS LKUP3
	PUSHJ P,RESET
	JRST OMIT


;WE DO ALL OF THE FOLLOWING HACKING TO INSURE THAT THE
;THE SYMBOLS ARE GIVEN TO DDT IN EXACTLY THE SAME ORDER
;THAT THE TRANSLATOR GAVE THEM TO STINK

SHUFLE:	ANDCAM C,(B)	;CLEAR FLAGS TO MARK PROGRAM NAME
	ADD B,[2,,2]	;IGNORE THIS PROGRAM NAME
	JUMPGE B,CPOPJ	;NO LOCALS IN DDT'S TABLE

SHUF1:	MOVE A,(B)	;SQUOOZE
	TLNN A,740000
	JRST SHUF2	;FOUND A BLOCK NAME
SHUF3:	ADD B,[1,,1]
	AOBJN B,SHUF1

SHUF4:	HRRZ A,DDPTR	;EXTENT OF THE SYMBOLS IS KNOWN
			;A/POINTER TO BOTTOM SYMBOLS
			;B/POINTER TO TOP OF SYMBOLS
SHUF5:	ADDI A,2	;SYMBOL AT BOTTOM
	HRRZI B,-2(B)	;SYMBOL AT TOP
	CAMG B,A
	POPJ P,		;WE HAVE MET THE ENEMY AND THEY IS US!

	MOVE C,(A)	;SWAP THESE TWO ENTRIES
	EXCH C,(B)
	MOVEM C,(A)

	MOVE C,1(A)	;VALUE
	EXCH C,1(B)
	MOVEM C,1(A)
	JRST SHUF5

;HERE WHEN WE FIND A BLOCK NAME

SHUF2:	MOVE A,1(B)	;VALUE
	TLNE A,-1	;PROGRAM NAME?
	JRST SHUF4	;YES
	JRST SHUF3	;IGNORE BLOCK NAME

GTWD:	PUSHJ P,RDWRD	;GOBBLE A WORD FROM THE BUFFER

	JFCL 4,.+1
	ADD CKS,T
	JFCL 4,[AOJA CKS,.+1]
RELADR:	POPJ P,

GETBIT:	ILDB TT,BITPTR
	SKIPL BITPTR
	POPJ P,
	EXCH T,BITS
	SOS BITPTR
	PUSHJ P,RPB
	EXCH T,BITS
	LDB TT,BITPTR
	POPJ P,


HIBLK:	0	; BLOCKS IN HIGH SEG
HIGMAP:	2200,,MAP-1
LOWMAP:	2200,,MAPP-1
MAP:	BLOCK	64.	; CORE MAPS
MAPP:	BLOCK	64.
USINDX:	0	; USER INDEX
HIGTOP:	0		; TOP OF HIGH SEG
PAT:
	BLOCK	100
PATEND:
AWORD:	0
SA:	0
TC:	0
BITS:	0
BITPTR:	(300)BITS
SAVPDL:	0
LBOT:	INITCR*2000
TIMES:	0
COMLOC:	ICOMM
T1:	0
T2:	0
FLSH:	0
PRGNAM:	0

EISYM:	;INITIAL SYMBOLS

CRELPT:	SQUOZE 64,$R.
FACTOR:	100
CPOINT:	SQUOZE 64,$.
	100
	SQUOZE 64,.LVAL1
.VAL1:	0
	SQUOZE 64,.LVAL2
.VAL2:	0
	SQUOZE 64,USDATL
USDATP:	0
EISYME:

RCKS:	(3000+SIXBIT /CKS/)


;SUBROUTINE TO GET A WORD FROM BUFFER (GETS NEW ONE IF NEC.)

RDWRD:	PUSH P,TT	;SAVE TT
	MOVE TT,INPTR	;GOBBLE POINTER
	MOVE T,(TT)	;GOBBLE DATUM
	AOBJN TT,RDRET	;BUFFER EMPTY?
DOREAD:	MOVE TT,[-STNBLN,,STNBUF]	;YES, READ A NEW ONE
	.IOT TPCHN,TT	;GOBBLE IT
	MOVE TT,[-STNBLN,,STNBUF]	;RE GOOBBLE
RDRET:	MOVEM TT,INPTR	;SAVE IT
	POP P,TT
	POPJ P,

;HERE TO START FIRST READ

RDFRST:	PUSH P,TT
	JRST DOREAD	;READ A NEW BUFFER

;BUFFER FOR BLOCK READS

INPTR:	0	;HOLDS CURRENT IO POINTER
STNBUF:	BLOCK STNBLN

;LOADER INTERFACE

TYPR:	0
	PUSH P,C
	PUSH P,T
	PUSH P,TT
	LDB C,[(330300)40]
	MOVEI TT,LI3
	TRON C,4
	HRRM TT,TYPR
	ORCMI C,7
	HRLZ TT,40
TYPR2:	PUSHJ P,SIXTYO
	AOJE C,TYPR1
	PUSHJ P,SPC
	HRRZ T,ADR
	PUSHJ P,OPT
	AOJE C,TYPR1
	PUSHJ P,SPC
	PUSHJ P,ASPT
TYPR1:	PUSHJ P,CRL
	POP P,TT
	POP P,T
	POP P,C
	JRST 2,@TYPR

ASPT:	MOVE T,A
SPT:	TLNN T,40000
	TRO FF,LOCF
SPT2:	TLZ T,740000
SPT1:	IDIVI T,50
	HRLM TT,(P)
	JUMPE T,SPT3
	PUSHJ P,SPT1
SPT3:	TRZE FF,LOCF
	PUSH P,["*-"0+1,,.+1
	HLRE T,(P)
	ADDI T,"0-1
	CAILE T,"9
	ADDI T,"A-"9-1
	CAILE T,"Z
	SUBI T,"Z-"#+1
	CAIN T,"#
	MOVEI T,".
	CAIN T,"/
SPC:	MOVEI T,40
SPTY:	JRST TYO


;0    1-12 13-44 45 46 47
;NULL 0-9   A-Z  .  $  %

LI4:	CAMN A,[(10700)CBUF-1]
	JRST LI3
	LDB T,A
	ADD A,[(70000)]
	SKIPGE A
	SUB A,[(430000)1]
	.IOT TYOC,T
	JRST LI1

TYI:	.IOT TYIC,T
	CAIE T,15
	CAIN T,12
	JRST TYO
	CAIN T,^R
	JRST TYO
	POPJ P,

LIS:	ANDI FF,GETTY
LI3:	MOVE A,[(10700)CBUF-1]
	MOVEM A,CPTR
	MOVE P,[(,-LPDL)PDL-1]
	PUSHJ P,CRLS
	TRZ FF,LOCF
LI1:	TRZ FF,ALTF
LI2:	PUSHJ P,TYI
	CAIN T,33
	MOVEI T,"
	CAIN T,7
	JRST LI3
	CAIN T,177	;RUBOUT
	JRST LI4
	IDPB T,A
	CAMN A,[(10700)CBUF+CBUFL]
	JRST LI4


LIS1:	CAIE T,"
	JRST LI1
	TRON FF,ALTF
	JRST LI2
	PUSHJ P,CRL
CD:	MOVEI D,0
CD3:	TRZ FF,ARG
CD2:	ILDB T,CPTR
	CAIL T,"0
	CAILE T,"9
	JRST CD1
	LSH D,3
	ADDI D,-"0(T)
VALRET:	TRO FF,ARG
	JRST CD2

CD1:	CAIE T,33
	CAIN T,DOLL	;CHECK FOR A REAL DOLLAR SIGN
	JRST LI3
	CAIL T,"<
	CAILE T,"[
	JRST CD
	IDIVI T,4
	LDB T,DTAB(TT)
	MOVEI A,SLIS(T)	;WHERE TO?
	CAIE A,DUMPY	;IS IT A DUMP
	TRZ FF,MLAST+SETDEV	;NO, KILL FUNNY FLAGS
	PUSHJ P,SLIS(T)
	JRST CD
	JRST VALRET


SLIS:	TDZA C,C
MLIS:	MOVEI C,2
	TRNE FF,GETTY
	PUSHJ P,FORMF
	TRNE FF,ARG
	JUMPL D,LIST
	MOVE D,LBOT
	JRST LIST

CCRL:	AOBJP D,CRL	;USED FOR CRL
CCRL1:	AOBJP D,CRL
LIST:	SKIPL A,(D)
	JRST .-2
	LDB TT,[(410300)A]
	ORCMI TT,7
	AOJN TT,LIST2	;NOT PRGM NAME
LIST4:	PUSHJ P,ASPT
LIST5:	PUSHJ P,TAB
	HRRZ T,1(D)
	TRNN FF,ARG
	SKIPN C
	MOVE T,1(D)
	PUSHJ P,OPTCR
	TRNN FF,ARG
	JRST CCRL
	AOBJP D,CRL
	SKIPGE 1(D)
	JRST CCRL1
	JRST LIST5

LIST2:	XOR TT,C		;MISSING OR DEFINED
	AOJN TT,CCRL
	PUSHJ P,SPC
	JRST LIST4



EQLS:	MOVE T,D
OPTCR:	PUSH P,CCRL
OPT:	MOVEI TT,10
	HRRM TT,OPT1
OPT2:	LSHC T,-43
	LSH TT,-1
OPT1:	DIVI T,10
	HRLM TT,(P)
	JUMPE T,.+2
	PUSHJ P,OPT2
	HLRZ T,(P)
	ADDI T,260
	JRST TYO

TAB:	PUSHJ P,SPC
	.IOT TYOC,T
	JRST TYO

CRLS:	TRNE FF,GETTY
	PUSH P,[CRLS1]
CRL:	MOVEI T,15
	.IOT TYOC,T
CRT:	SKIPA T,C.12
FORMF:	MOVEI T,14
TYO:	.IOT TYOC,T
C.12:	POPJ P,12

CRLS1:	MOVEI T,"*
	JRST TYO

TDDT:	SKIPE LINKDB	;TEST FOR LINK HACKAGE
	PUSHJ P,LNKFIN	;CLEAN UP LINKS
	MOVE B,DDPTR
	HRRZ D,LBOT
	MOVE BOT,D
	TRO FF,GLOSYM

SYMS:	CAIN D,(B)
	JRST SYMS5
	SKIPL A,(D)
SYMS6:	AOJA D,SYMS
	TLNN A,200000	;DEFINED
	AOJA D,SYMS6
	TLNE A,40000	;LOCAL
	TRNE FF,GLOSYM	;OK FOR GLOBALS
SYMS2:	TLNE A,100000	;FLUSH PRGM NAME
SYMS1:	AOJA D,SYMS6
	TRNN FF,GLOSYM
	SKIPL SYMSW
	JRST SYMS3	
SYMS8:	HRRZM D,T2	;OMIT SYMBOLS
	ADDI D,2
	PUSHJ P,PATCH	;DELETE FROM TABLE
	JRST SYMS

SYMS3:	TRZ FF,NOTNUM	;ASSUME ALL NUMERIC
	TLZ A,740000
	MOVE T,A	;SEE IF IT IS A FUNNY SYMBOL
	IDIVI T,50	;GET LAST CHAR IN TT
	JUMPE TT,OKSYM
DIVSYM:	CAIG TT,12	;IS THE SYMBOL > 9
	CAIGE TT,1	;AND LESS THAN OR EQUAL TO 0
	TRO FF,NOTNUM	;NO, SAY NOT A NUMBER
	IDIVI T,50	;CHECK NEXT
	JUMPE TT,SYMS8	;NULL IN THE MIDDLE LOSES
	JUMPN T,DIVSYM	;DIVIDE UNTIL T IS 0
	CAIN TT,21	;IS THIS A "G"
	TRNE FF,NOTNUM	;YES, SKIP IF SYMBOL OF FORM "GXXXXX" X IS A DIGGIT
	JRST  OKSYM	;WIN
	JRST SYMS8	;LOSE
OKSYM:	MOVE T,1(D)
	MOVSI C,2(D)
	HRR C,D
	BLT C,-3(B)	;REMOVE DEF IN LOADER TABLE
	SUB B,[(2)2]
	TLO A,40000
	TRNN FF,GLOSYM
	TLC A,140000	;DDT LOCAL
	MOVEM A,(B)
	MOVEM T,1(B)	;STORE VALUE IN NEW SLOT
	JRST SYMS

SYMS5:	MOVEM B,DDPTR
	HRRZ C,BOT
	SUB C,D
	HRLM C,BOT
SYMS7:	MOVEM BOT,LBOT
	POPJ P,

GO:	TRNE FF,ARG
	MOVEM D,SA
	TRO FF,GOF
	JRST DDT

EXAM:	CAMLE D,MEMTOP
	JRST	TRYHI		; COULD BE IN HIGH SEG
	MOVE T,@DPTR
	JRST OPTCR

TRYHI:	TRNE	D,400000	; SKIP IF NOT HIGH
	CAMLE	D,HIGTOP	; SKIP IF OK
	(3000+SIXBIT /NEM/)
	MOVE	T,(D)		; GET CONTENTS
	JRST	OPTCR

C.CD2:	POPJ P,CD2

GETCOM:	MOVE A,[10700,,CBUF-1]
	MOVEM A,CPTR
	MOVE P,[(,-LPDL)PDL-1]
	PUSH P,C.CD2
	MOVEM P,SAVPDL
	MOVEI T,0	;REOPEN CHANNEL IN ASCII MODE
	HLLM T,DEV
	.OPEN TPCHN,DEV	;RE OPEN
	JRST FNF2	;LOSE
GTCM1:	.IOT TPCHN,T
	JUMPL T,FIXOPN	;JUMP IF EOF
	CAIN T,3	;CHECK FOR EOF
	JRST FIXOPN	;IF SO QUIT
	IDPB T,A	;DEPOSIT CHARACTER
	CAME A,[10700,,CBUF+CBUFL]
	JRST GTCM1
TPOK:	SKIPA T,BELL
ERR:	MOVE T,"?
	.IOT TYOC,T
	PUSHJ P,FIXOPN	;FIX UP OPEN CODE
	JRST LI3

;HERE TO RESET OPEN

FIXOPN:	MOVEI T,6
	HRLM T,DEV
	POPJ P,

FNF2:	PUSHJ P,FIXOPN
	JRST FNF

PAPER:	MOVEI A,(SIXBIT /PTR/)
	HRRM A,DEV
	POPJ P,	;REAL OPEN WILL OCCUR LATER

UTAP:	TRZN FF,ARG
	JRST OPNTP
	TRO FF,SETDEV	;SETTING DEVICE
	MOVE A,DEVTBL(D)
	HRRM A,DEV
OPNTP:	TRO FF,MLAST	;SET M LAST COMMAND
	PUSHJ P,FRD
	.SUSET [.SSNAM,,SNAME]
	MOVEM B,NM1
	MOVEM C,NM2
	POPJ P,	;REAL OPEN WILL OCCUR LATER

OPNPTR:	.OPEN TPCHN,DEV
	JRST FNF
	JRST RDFRST	;STAART UP THE READ ING

NTS:	(3000+SIXBIT /NTS/)

DEV:	6,,(SIXBIT /DSK/)
NM1:	SIXBIT /BIN/
NM2:	SIXBIT /BIN/
0
SNAME:	0		;SYSTEM NAME

SIXTYO:	JUMPE TT,CPOPJ
	MOVEI T,0
	LSHC T,6
	ADDI T,40
	PUSHJ P,TYO
	JRST SIXTYO

JOB:	PUSHJ P,FRD
	MOVEM B,JOBNAM
	TRO FF,JBN
	POPJ P,

JOBNAM:	0


DEVTBL:	IRPS DEV,,[DSK UT1 UT2 UT3 UT4 UT5 UT6 UT7 UT8]
	(SIXBIT /DEV/)
	TERMIN

FNF:	PUSHJ P,TYPFIL
	REPEAT 2,PUSHJ P,SPC
	.OPEN ERCHN,ERRBL	;OPEN ERROR DEVICE
	JRST .-1	;DON'T TAKE NO FOR AN ANSWER

ERLP:	.IOT ERCHN,A	;READ A CHAR
	CAIE A,14	;IF FORM FEED
	CAIN A,3	;OR ^C
	JRST ERDON	;STOP

	.IOT TYOC,A	;PRINT
	JRST ERLP

ERDON:	.CLOSE ERCHN,
	JRST LI3


ERRBL:	(SIXBIT /ERR/)	;ERROR DEVICE
	2
	TPCHN


TYPFIL:	MOVSI A,-4
	HRLZ TT,DEV
	JRST .+3
TYPF2:	SKIPN TT,DEV(A)
	AOJA	A,.-1
	PUSHJ P,SIXTYO
	MOVE T,TYPFTB(A)
	PUSHJ P,TYO
	AOBJN A,TYPF2
	POPJ P,

TYPFTB:	":
	40
	40
	0
	";

LOADN:	SKIPA C,SYMFLG
LOADG:	MOVEI C,DDSYMS
	PUSHJ P,OPNPTR	;DO THE REAL OPEN (AND FIRST READ)

	MOVEM C,SYMSW

RESTAR:	MOVE BOT,LBOT
	MOVEM P,SAVPDL
	CLEARB CKS,TC
	CLEARB RH,AWORD
	PUSH P,CJMP1
RESET:	MOVEI A,FACTOR	;LEAVE GLOBAL LOCATION MODE
	HRRM A,REL
	TRZA FF,UNDEF+GPARAM+INDEF+GLOSYM+SEARCH+CODEF+COND
SFACT:	MOVEM D,FACTOR
CJMP1:	POPJ P,JMP1

KILL:	HRRZ BOT,BPTR
	MOVEM BOT,DDPTR
	JRST SYMS7

COMVAL:	SKIPA D,COMLOC
SADR:	HRRZ D,SA
POPJ1:	AOSA (P)
COMSET:	MOVEM D,COMLOC
BELL:	POPJ P,7

LBRAK:	MOVEM D,T1
	TRZ FF,LOSE
	PUSHJ P,ISYM
	MOVE T,T1
	TRO FF,GPARAM
	TRZE FF,ARG
	JRST DFSYM2
	TLNN B,200000
	(3000+SIXBIT /UND/)
	MOVE D,1(D)
	TRZN FF,LOSE
	JRST POPJ1
	(2000+SIXBIT /UND/)

SOFSET:	HRRM D,LKUP3
CPOPJ:	POPJ P,

BEG:	MOVE D,FACTOR
	JRST POPJ1

DDT:	SKIPN JOBNAM
	JRST NJN
	PUSHJ P,TDDT
	MOVE A,JOBNAM
	HRR B,BPTR
	ADDI B,30
	HRRM B,YPTR
	HRLI B,440700
	MOVEI D,^W
	IDPB D,B
	MOVE C,[(000600)A-1]
	MOVEI T,6
DDT2:	ILDB D,C
	JUMPE D,DDT1
	ADDI D,40
	IDPB D,B
	SOJG T,DDT2
DDT1:	MOVEI C,[CONC69 ASCIZ \J,\SA,[/9B!Qî],\DDPTR,[/Q:VP \]]
	HRLI C,440700
DDT6:	ILDB T,C
	IDPB T,B
	JUMPN T,DDT6	;END OF STRING MARKED WITH ZERO BYTE
	MOVE T,SA	;GET STARTING ADDRESS
	TLNN T,777000	;IF INSTRUCTION PART ZERO,
	TLO T,(JRST)	;THEN TURN INTO JRST
	MOVEM T,SA	;USE AS STARTING ADDRESS
	TRNE FF,GOF	;IF G COMMAND,
	MOVEM T,EXIT	;THEN USE AS LOADER EXIT
	HRRZ B,GETMEM	;GET CURRENT CORE ALLOCATION+1
	SUBI B,1(NBLKS)	;REDUCE TO PROGRAM CORE ALLOCATION
	HRRM B,PALLOC	;SAVE IN EXIT ROUTINE
	LSH B,10.	;SHIFT TO MEMORY LOCATION
	SUBI B,1	;REDUCE TO TOP LOCATION IN CORE OF PROGRAM
	HRRM B,PMEMT	;SAVE FOR MAIN PROGRAM BLT (DON'T LET NON-ZERO CORE ABOVE PROGRAM STAY AROUND)
	HRLZ 17,BPTR	;GET LOCATION OF BEGINNING OF PROGRAM IN LH(17)
	ADDM 17,PSV17	;17 BLT POINTER FOR AC'S, TURN SV17 INTO BLT POINTER FOR PROGRAM
	MOVE B,EXBLTP	;GET EXIT ROUTINE BLT POINTER
YPTR:	.VALUE		;ADDRESS POINTS TO VALRET STRING
		;DON'T TRY TO STOP THEN START STINK AFTER HERE (AFTER BREAKPOINT OR WITH $G)
	BLT B,LEXEND	;BLT IN EXIT ROUTINE
	BLT 17,17	;BLT IN PROGRAM AC'S
	EXCH 17,SV17	;SAVE PROGRAM LOCATION 17, SET UP BLT POINTER
	.CLOSE TYOC,
	.CLOSE TYIC,
	.CLOSE TPCHN,
	JRST LEXIT

		;EXIT ROUTINE FROM LOADER
		;BLT'ED INTO 30 - 30+N

EXBLTP:	.+1,,LEXIT	;BLT POINTER
	OFST==30-.	;LEXIT=30
LEXIT=.+OFST
PMEMT:	BLT 17,		;BLT DOWN MAIN PROGRAM
	MOVE 17,SV17	;GIVE USER HIS LOCATION 17
PALLOC:	.CORE		;REDUCE CORE ALLOCATION TO WHAT REQUIRED BY PROGRAM
PSV17:	SV17=.+OFST
	40,,40		;40 FIRST PROGRAM ADDRESS LOADED INTO
EXIT:	.VALUE LEXEND
LEXEND=.+OFST
	0		;END OF EXIT ROUTINE


NJN:	TRZ FF,GOF
	(3000+SIXBIT /NJN/)

ZERO:	.CORE (NBLKS)
	PUSHJ P,SCE	;GO TO ERROR
	SETOM MEMTOP
	MOVEI A,1(NBLKS)
	HRRM A,GETMEM
GETMEM:	.CORE INITCR+1
	PUSHJ P,SCE
	ADDI MEMTOP,2000
	AOS GETMEM
	MOVEI	ZR,600000
	IDPB	ZR,LOWMAP	; UPDATE PAGE MAP
	POPJ P,


SCE:	SOS (P)	;MAKE POPJ BE A "JRST .-1"
	SOS (P)
	PUSHJ P,COREQ	;ASK LOSER
	POPJ P,	;HE SAID YES
	(2000+SIXBIT /SCE/)

COREQ:	PUSH P,A	;SAVE SOME ACS
	MOVEI A,[ASCIZ /NO CORE TYPE "Y" TO RETRY "N" TO LOSE/]
	PUSHJ P,LINOUT
	.IOT TYIC,A	;READ A CHARACTER
	CAIE A,"Y
	AOS -1(P)	;NOT Y, LOSE
	.RESET TYIC,	;KILL ANY EXTRA Y'S
	POP P,A
	POPJ P,

;ROUTINE TO PRINT A LINE

LINOUT:	PUSH P,C
	PUSH P,B
	MOVSI B,440700+A	;BYTE POINTER TO INDEX OF A

LINO1:	ILDB C,B	;GET CHAR
	JUMPE C,LINO2	;ZERO, END
	.IOT TYOC,C
	JRST LINO1

LINO2:	MOVEI A,15	;PUT OUT CR
	.IOT TYOC,A
	POP P,B
	POP P,C
	POPJ P,

DEFINE FOUR A,B,C,D
	(<<A-SLIS>_9>+B-SLIS)<<C-SLIS>_9>+D-SLIS
	TERMIN

DTAB:	(331100+T)DTB-74/4
	(221100+T)DTB-74/4
	(111100+T)DTB-74/4
	(1100+T)DTB-74/4

DTB:	FOUR LBRAK,EQLS,ERR,MLIS,;< = > ?
	FOUR GETCOM,ERR,BEG,COMSET,	;@ A B C
	FOUR DDT,NTS,NTS,GO,	;D E F G
	FOUR ERR,ERR,JOB,KILL,	;H I J K
	FOUR LOADG,UTAP,LOADN,SOFSET,;L M N O
	FOUR PAPER,COMVAL,SFACT,SLIS,;P Q R S
	FOUR CPOPJ,ERR,ERR,ERR,	;T U V W
	FOUR SADR,DUMPY,ZERO,EXAM,;X Y Z [

IFLE 1000-DDT+SLIS,[PRINTX /DISPATCH OVERFLOW
/]
INFORM [DISPATCH ROOM]\<1000-DDT+SLIS>

;THIS CODE DUMPS THE LOADED CORE IMAGE INTO A DISK FILE AND THEN CAUSES
;STINK TO KILL ITSELF.

DUMPY:	TRZN FF,MLAST	;WAS "M" THE LAST COMMAND?
	PUSHJ P,FIXFIL	;FIX UP THE FILE NAME
	MOVEI A,(SIXBIT /DSK/)
	TRZN FF,SETDEV	;WAS DEVICE SET?
	HRRM A,DEV	;NO, SET IT

	.OPEN TPCHN,DEV	;SEE IF IT EXISTS
	JRST OPNOK	;NO, WIN

	.CLOSE TPCHN,	;CLOSE IT
	.FDELE DEV	;DELETE IT
	JFCL	;IGNORE LOSSAGE

OPNOK:	MOVSI A,7	;SET DEVICE SPEC TO BE WRITE/IMAGE/BLOCK
	HLLM A,DEV
	.OPEN TPCHN,DEV	;OPEN THE CHANNEL
	JRST FNF

	PUSHJ P,TDDT	;MOVE ALL SYMBOLS TO DDT TABLE
	SKIPE	HIGTOP	; ANY HIGH CORE?
	PUSHJ	P,MAPOUT
	MOVE B,[JRST 1]	;START FILE WITH "JRST 1"
	PUSHJ P,OUTWRD	;PUT IT OUT

	MOVN ADR,MEMTOP	;GET -<LENGTH OF CORE IMAGE>
	HRLZS ADR  	;AOBJN POINTER

DMP2:	SKIPN B,@ADRPTR	;LOOK FOR THE FIRST NON-ZERO WORD
	AOBJN ADR,.-1	;UNTIL THE WORLD IS EXHAUSTED
	JUMPGE ADR,CHKHI	;DROPPED THROUGH, JUMP IF CORE EMPTY

	MOVEI C,(ADR)	;SAVE POINTER TO NON ZERO WORD
	MOVEI A,(C)	;AND ANOTHER COPY

DMP1:	SKIPE B,@ADRPTR	;NOW LOOK FOR END OF NON ZERO BLOCK
	AOBJN ADR,.-1	;UNTIL WORLD EXHAUSTED
	JUMPGE ADR,DMPLST	;IF WORLD EMPTY, QUIT

	AOBJP ADR,DMPLST	;CHECK NEXT WORD
	SKIPE B,@ADRPTR	;FOR BEING ZERO
	JRST DMP1	;ONE LONE ZERO, DON'T END BLOCK

DMPLST:	MOVEI D,(ADR)	;POINT TO END
	SUB C,D	;C/ -<LENGTH OF BLOCK>
	HRL A,C	;A/ AOBJN TO BLOCK
	MOVE B,A	;COPY TO B FOR OUTWRD
	PUSHJ P,OUTWRD	;PUT IT OUT

	HRRI B,@BPTR	;NOW POINT TO REAL CORE
	.IOT TPCHN,B	;BARF IT OUT

	MOVE B,A	;GET POINTER BACK IN B
	MOVE C,B	;FIRST WORD IN CHECK SUM
	HRRI B,@BPTR	;POINT TO REAL CORE

	ROT C,1	;ROTATE CKS
	ADD C,(B)	;ADD
	AOBJN B,.-2	;AND DO FOR ENTIRE BLOCK

	MOVE B,C	;CKS TO B
	PUSHJ P,OUTWRD	;AND PUT IT OUT


	JUMPL ADR,DMP2	;IF MORE, GO DO IT

CHKHI:	SKIPN	MEMTOP,HIGTOP	; ANY HIGH SEG
	JRST	DMPSYMS		; NO, GO ON TO SYMS
	SETZM	HIGTOP		; RESET IT
	HLLZS	ADRPTR		; FIX UP POINTERS
	HLLZS	BPTR
	LDB	ADR,[2100,,MEMTOP]	; GET NO. OF WORDS
	MOVNS	ADR		; NEGATE
	MOVSI	ADR,(ADR)
	HRRI	ADR,400000	; START OF HIGH SEG
	JRST	DMP2


;HERE TO DO START ADDRESS

DMPSYMS:	HRRZ B,SA	;GET START ADR
	HRLI B,(JUMPA)	;USE "JUMPA" TO MAKE DDT HAPPY
	PUSHJ P,OUTWRD

;HERE TO DO SYMBOLS

	HLLZ B,DDPTR	;GET NUMBER
	PUSHJ P,OUTWRD	;PUT IT OUT

	MOVE C,DDPTR	;FOR CKS
	.IOT TPCHN,DDPTR	;OUT GOES THE WHOLE TABLE


	ROT B,1	;ACCUMULATE IN B
	ADD B,(C)	;ADD IT
	AOBJN C,.-2

	PUSHJ P,OUTWRD	;PUT OUT THE CKS

	MOVSI B,(JRST)	;FINISH WITH "JRST 0"
	PUSHJ P,OUTWRD

	MOVNI B,1	;FINISH WITH NEGATIVE
	PUSHJ P,OUTWRD

	.CLOSE TPCHN,	;CLOSE THE FILE

	.VALUE [ASCIZ /:KILL /]	;KILL

;SUBROUTINE TO PUT OUT ONE WORD

OUTWRD:	HRROI T,B	;AOBJN POINTER TO B
	.IOT TPCHN,T
	POPJ P,


MAPOUT:	MOVEI	B,0	; START MAP WITH 0
	PUSHJ	P,OUTWRD
	MOVE	B,[-128.,,MAP]	; POINT TO MAP
	.IOT	TPCHN,B	; AND OUTPUT IT
	POPJ	P,


;HERE TO BUILD DEFAULT OUTPUT FILE NAME

FIXFIL:	MOVE A,[SIXBIT /_STNK_/]	;DEFAULT NAME 1
	MOVEM A,NM1
	MOVE A,[SIXBIT /DUMP/]	;AND NAME 2
	MOVEM A,NM2
	POPJ P,

ISYM:	MOVSI C,(50*50*50*50*50*50)
	MOVSI T,40000	;GLOBAL BIT

ISYM0:	ILDB A,CPTR
	CAIN A,"*
	TLZ T,40000	;LOCAL
	CAIN A,"*
	JRST ISYM0
	CAIN A,">
	JRST LKUP
	SUBI A,"0-1
	CAIL A,"A-"0+1
	SUBI A,"A-"0+1-13
	JUMPGE A,ISYM2
	ADDI A,61
	CAIN A,60
	MOVEI A,45	;.
ISYM2:	IDIVI C,50
	IMUL A,C
	ADDM A,T
	JRST ISYM0

FRD2:	CAME B,[SIXBIT /@/]
	JRST DEVNAM
	SKIPA B,C
FRD:	MOVSI B,(SIXBIT /@/)
	MOVSI C,(SIXBIT /@/)
	MOVE A,[(600)C-1]
FRD1:	ILDB T,CPTR
	CAIE T,33
	CAIN T,DOLL
	JRST CHBIN	;CHECK IF SHOULD CHANGE NAME 2 TO BIN
	TRC T,40
	JUMPE T,FRD2
	CAIN T,32
	JRST DEVSET
	CAIN T,33
	JRST USRSET
	CAIN T,77
	MOVEI T,0
	CAME A,[(600)C]
	IDPB T,A
	JRST FRD1




USRSET:	MOVEM C,SNAME
	JRST FRD+1

DEVNAM:	PUSH P,CDEVN1
	MOVEM C,NM2
	JRST FRD+1

DEVNM1:	TRO FF,SETDEV	;SAY DEVICE SET
	HLRM C,DEV
	MOVE C,NM2
	JRST CHBIN	;CHECK FOR CHANGE TO BIN

DEVSET:	TRO FF,SETDEV	;DEVICE SET
	HLRM C,DEV
	JRST FRD+1

CHBIN:	CAME B,[SIXBIT /@/]	;WAS NO NAME2 SUPPLIED?
	POPJ P,	;NAME2 SUPPLIED, GO AWAY
	MOVE B,C	;MAKE NAME1 INTO NAME2
	MOVSI C,(SIXBIT /BIN/)	;USE BIN FOR NAME22
	POPJ P,

CDEVN1:	POPJ P,DEVNM1


CPTR:	0

CONSTANTS
INIT:
PDL:	.SUSET [.RSNAM,,SNAME]	;GET INITIAL SYSTEM NAME
	MOVEI A,100
	MOVEM A,FACTOR
	MOVE NBLKS,[20,,INITCR]
	MOVEI A,ICOMM
	MOVEM A,COMLOC
	HLLZS LKUP3
	SETOM MEMTOP
	MOVEI A,FACTOR
	HRRM A,REL
	MOVE P,[(,-1)PDL]
	PUSHJ P,KILL
	PUSHJ P,GETMEM
	.OPEN TYOC,TTYO
	.VALUE 0
	.OPEN TYIC,TTYI
	.VALUE 0
	.STATUS TYIC,T
	ANDI T,77
	CAIN T,2
	TRO FF,GETTY
	MOVE TT,[SIXBIT /STINK./]
	PUSHJ P,SIXTYO
	MOVE TT,[.FNAM2]
	PUSHJ P,SIXTYO
	JRST LIS

TTYO:	1,,(SIXBIT /TTY/)
	SIXBIT /STINK/
	SIXBIT /OUTPUT/

TTYI:	30,,(SIXBIT /TTY/)
	SIXBIT /STINK/
	SIXBIT /INPUT/

CONSTANTS

LOC PDL+LPDL
CBUF:	BLOCK CBUFL

LOSYM:	;LOWEST LOC AVAIL FOR SYM TBL
INITCR==<LOSYM+3000>/2000	;LDR LENGTH IN BLOCKS

INFORM [HIGHEST USED]\LOSYM
INFORM [LOWEST LOCATION LOADED ]\LOWLOD
INFORM [COMMAND BUFFER LENGTH]\<CBUFL*5>
INFORM [INITIAL CORE ALLOCATION]\INITCR

END PDL

ð$œ	/ð$œ	/ð$œ	/ð$œ	/ð$œ	/ð$œ	/ð$œ	/