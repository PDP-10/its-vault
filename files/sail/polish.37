;HERE TO HANDLE GLOBAL BLOCK -- BLOCK TYPE #20

GLOBS:	PUSHJ	P,GETBIT		;CODE BITS
	PUSHJ	P,RPB			;SQOOZE
	MOVEM	T,CGLOB
	PUSHJ	P,GETBIT		;CODE BITS
	PUSHJ	P,RRELOC		;VALUE
	MOVEM	T,CGLOBV
	MOVE	T,CGLOB
	TLO	T,40000			;GLOBAL FLAG
	PUSHJ	P,LKUP			;SYMBOL LKUP
	LDB	C,[400400,,CGLOB]	;FLAGS
	CAIN	C,60_-2
	JRST	GLOBRQ			;GLOBAL REQUEST

;HERE TO HANDLE SYMBOL TABLE FIX UPS OR GLOBAL DEFINITION

	TRNN	C,10_-2		;TEST FOR VALID FLAGS
	TRNN	C,4_-2		;FORMAT IS XX01
	JRST	4,.
	LSH	C,-2		;SHIFT OUT GARBAGE
	CAIN	C,0
	JRST	GLBDEF		;FLAGS 04=> GLOBAL DEFINITION
	TLNE	FF,FIXRT+FIXLT	;IF THESE FLAGS SET WE HAVE LOST EARLIER
	JRST	4,.
	TLO	FF,(C)		;SET FIX UP FLAGS
	MOVE	T,CGLOBV	;SET UP FOR FIXUPS THAT FOLLOW
	MOVEM	T,GLBFV		;VALUE TO FIX
	TLZ	A,740000	;ZERO FLAGS, A SET UP BY LKUP
	MOVEM	A,GLBFS		;SQOOZE TO FIX
	JRST	GLOBS

;HERE TO HANDLE GLOBAL REQUEST -- FLAGS=60

GLOBRQ:	SKIPGE	T,CGLOBV	;SKIP IF THREADED LIST
	JRST	GLOBR1		;SINGLE WORD FIX UP MUST WORK HARDER

;SIMPLE REQUEST

	JUMPE	T,GLOBS		;IGNORE NULL REQUEST
	JUMPGE	D,GLOBNT	;JUMP IF SYMBOL NOT IN TABLE
	TLNE	B,200000	;TEST TO SEE IF DEFINED
	JRST	GLOBPD		;PREVIOUSLY DEFINED
	PUSHJ	P,DOWN		;NOT DEFINED, ENTER REQEST INTO TABLE
	MOVE	C,CGLOBV
	HRLI	C,100000	;THIS IS A LINK LIST
	MOVEM	C,(D)
	JRST	GLOBS

;HERE TO DEFINE GLOBAL SYMBOL, FLAGS=04

GLBDEF:	MOVE	T,CGLOBV	;VALUE
	MOVEI	TT,0		;REDEFINE NOT OKAY, SEE DEF2
	PUSHJ	P,DEFSYM	;SQUOOZE+FLAGS ALREADY IN B BECAUSE OF EARLIER LOOK UP
	JRST	GLOBS
; HERE IF GLOBAL DEFINED, UNTHREAD THE CHAIN

GLOBPD:	MOVE	T,1(D)		;VALUE
	MOVE	B,CGLOBV	;POINTER TO CHAIN
	PUSHJ	P,UNTHR
	JRST	GLOBS

; ENTER NEW SYMBOL WITH LINK REQUEST

GLOBNT:	MOVEI	C,44_-2		;PROPER FLAGS, GLOBAL AND THIS HERE SQUOZ
	DPB	C,[400400,,A]
	HRLI	T,100000	;SET LINK BIT IN REQUEST
	PUSHJ	P,ENT
	JRST	GLOBS

; SINGLE WORD FIX UP -- FLAGS=60

GLOBR1:	TLNE	T,100000	;TEST FOR SYMBOL TABLE FIX
	JRST	GLOBST		;SYMBOL TABLE FIX
	JUMPGE	D,GLOBR2	;JUMP IF NOT IN TABLE
	TLNN	B,200000
	JRST	GLOBR3		;NOT PREVIOUSLY DEFINED
	HRRZ	B,T		;FIX UP LOCATION
	TLNE	T,200000	;LEFT OR RIGHT?
	JRST	HWAL		;LEFT 
HWAR:	HRRE	C,@BPTR		;HALF WORD ADD RIGHT
	ADD	C,1(D)
	HRRM	C,@BPTR
	JRST	GLOBS

HWAL:	HLRE	C,@BPTR		;HALF WORD ADD LEFT
	ADD	C,1(D)
	HRLM	C,@BPTR
	JRST	GLOBS


; HERE FOR SINGLE WORD FIX, SYMBOL UNDEFINED

GLOBR3:	PUSHJ	P,DOWN		;MAKE ROOM IN TABLE
	MOVE	C,T
	HLRI	T,1		;ASSUME RIGHT HALF
	TLNE	C,200000	;RIGHT OR LEFT?
	HLRI	T,2		;LEFT
	MOVEM	T,(D)
	JRST	GLOBS
; HERE FOR SINGLE WORD FIXUP, SYMBOL NOT IN TABLE

GLOBR2:	TLO	A,400000	;SYMBOL FLAG
	MOVE	C,T
	HRLI	T,1		;ASSUME RIGHT HALF FIX
	TLNE	C,200000	;LEFT OR RIGHT?
	HRLI	T,2		;LEFT
	PUSHJ	P,ENT
	JRST	GLOBS

; HERE FOR SYMBOL TABLE FIX

GLOBST:	TLZ	A,740000	;MAKE SURE WE ARE STILL FIXING SAME SYMBOL
	CAME	A,GLBFS
	JRST	4,.		;DON'T AGREE
	HRRE	B,T		;SET UP FOR HALF WORD ADD
	TLNE	T,200000	;LEFT OR RIGHT?
	JRST	GLOBS1		;LEFT
	HRRE	C,GLBFV		;RIGHT
	ADD	C,B
	HRRM	C,GLBFV
	TLZN	FF,FIXRT	;DID WE REALLY WANT TO DO THIS
	JRST	4,.		;NO
	JRST	GLOBS2		;YES

GLOBS1:	HLRE	C,GLBFV		;LEFT HALF FIX
	ADD	C,B
	HRLM	C,GLBFV
	TLZN	FF,FIXLT	;DID WE REALLY WANT TO DO THIS
	JRST	4,.		;NOPE

; HERE TO FINISH UP SYMBOL TABLE FIX

GLOBS2:	TLNE	FF,FIXLT+FIXRT	;DO WE HAVE MORE FIXING
	JRST	GLOBS		;NO
	MOVE	T,GLBFV		;FIXED VALUE
	MOVEI	TT,100		;OKAY TO REDEFINE, TT USED AT DEF2
	PUSHJ	P,DEFSYM
	JRST	GLOBS
; HERE TO HANDLE FIXUPS -- BLOCK TYPE #21

FIXES:	PUSHJ	P,GETBIT	;CODE BITS
	PUSHJ	P,RRELOC	;FIX UP WORD
	CAMN	T,[-1]		;SKIPS ON RIGHT HALF FIX
	JRST	FIXESL		;LEFT HALF FIX
	HLRZ	B,T		;C(T) = POINTER,,VALUE  C(B)=POINTER
	PUSHJ	P,UNTHR
	JRST	FIXES

FIXESL:	PUSHJ	P,GETBIT
	PUSHJ	P,RRELOC
	HLRZ	B,T
	PUSHJ	P,UNTHL
	JRST	FIXES

UNTHL:	HLL	T,@BPTR	;CALL IS POINTER IN B
	HRLM	T,@BPTR	;        VALUE IN T
	HLRZ	B,T
	JUMPN	B,UNTHL
	POPJ	P,

UNTHF:	HRL	B,@BPTR
	MOVEM	T,@BPTR
	HLRZS	B
	JUMPN	B,UNTHF
	POPJ	P,
;POLISH FIXUPS <BLOCK TYPE 22>

PDLOV:	SKIPE POLSW	;PDL OV ARE WE DOING POLISH?
	JRST COMPOL	;YES
	(3000+SIXBIT /POV/)
COMPOL:	(3000+SIXBIT /PTC/)
LOAD4A:	(3000+SIXBIT /IBF/)


;READ A HALF WORD AT A TIME

RDHLF:	TLON FF,HSW	;WHICH HALF
	JRST NORD
	PUSHJ P,RWORD	;GET A NEW ONE
	TLZ FF,HSW	;SET TO READ OTEHR HALF
	MOVEM T,SVHWD	;SAVE IT
	HLRZS T		;GET LEFT HALF
	POPJ P,		;AND RETURN
NORD:	HRRZ T,SVHWD	;GET RIGHT HALF
	POPJ P,		;AND RETURN

RWORD:	PUSH P,C
	PUSHJ P,GETBIT
	PUSHJ P,RRELOC
	POP P,C
	POPJ P,

;HERE TO PUT TWO WORDS INTO SYMBOL TABLE
;	C/ HEADER
;	T/ SECOND WORD

SYM3X2:	SUB BOT,[2,,2]
	MOVEM T,1(BOT)
SYM3X:	MOVEM C,(BOT)
	PUSHJ P,SYMCHK
	MOVEM BOT,LBOT
	POPJ P,

; PASSES WORD IT T INTO SYMBOL TABLE

SYM3X1:	SUB BOT,[1,,1]
	JRST SYM3X
;THIS ROUTINE SEARCHES TO SEE IF GLOBAL DEFINED (SKIPES IF UNDEFINED)
;CALL WITH SQUOOZE IN C AND RETURNS WITH POINTER IN A IF DEFINED

SDEF:	PUSH P,T
	PUSH P,D
	PUSH P,C
	PUSH P,B
	PUSH P,A
	MOVE T,C
	PUSHJ P,LKUP
	SKIPGE D
	TLNN B,200000	;SKIP IF DEFINED
	AOS -5(P)	;INCREMENT ADDRESS
	MOVEM D,(P)	;SET POINTER IN A
	POP P,A
	POP P,B
	POP P,C
	POP P,D
	POP P,T
	POPJ P,

;START READING THE POLISH

POLFIX:	MOVE D,PPDP	;SET UP THE POLISH PUSHDOWN LIST
	MOVEI B,100	;IN CASE OF ON OPERATORS
	MOVEM B,SVSAT
	SETOM POLSW	;WE ARE DOING POLISH
	TLO FF,HSW	;FIX TO READ A WORD THE FIRST TIME
	SETOM GLBCNT	;NUMBER OF GLOBALS IN THIS FIXUP
	PUSH D,[15]	;FAKE OPERATOR SO STORE WILL NOT HACK

RPOL:	PUSHJ P,RDHLF	;GET A HALF WORD
	TRNE T,400000	;IS IT A STORE OP?
	JRST STOROP	;YES, DO IT
	CAIGE T,3	;0,1,2 ARE OPERANDS
	JRST OPND
	CAILE T,14	;14 IS HIGHEST OPERATOR
	JRST LOAD4A	;ILL FORMAT
	PUSH D,T	;SAVE OPERATOR IN STACK
	MOVE B,DESTB-3(T)	;GET NUMBER OF OPERANDS NEEDED
	MOVEM B,SVSAT	;ALSO SAVE IT
	JRST RPOL	;BACK FOR MORE

;HANDLE OPERANDS. THIS GETS COMPLICATED BECAUSE OF THE PRESENCE OF
;GLOBAL REQUESTS

OPND:	MOVE A,T	;GET THE OPERAND TYPE HERE
	PUSHJ P,RDHLF	;THIS IS AT LEAST PART OF THE OPERAND
	MOVE C,T	;GET IT INTO C
	JUMPE A,HLFOP1	;0 IS HALF-WORD OPERAND
	PUSHJ P,RDHLF	;NEED FULL WORD, GET SECOND HALF
	HRL C,T	;GET HALF IN RIGHT PLACE
	MOVSS C		;WELL ALMOST RIGHT
	SOJE A,HLFOP	;1 IS FULL WORD, 2 IS GLOBAL REQUEST

	PUSHJ P,SQZCON	;CONVERT TO STINKING SQUOOZE
	TLO C,40000	;THIS HERE IS A GLOBAL
	PUSHJ P,SDEF	;SEE IF IT IS ALREADY DEFINED
	JRST OPND1	;YES, WE WIN
	AOSN GLBCNT	;NO, INCREMENT NUMBER OF GLOBALS THIS FIXUP
	AOS HEADNM	;INCREMENT FIXUP NUMBER IF FIRST GLOBAL
	PUSH P,C	;SAVE GLOBAL REQUESTS FOR LATER
	MOVE T,C	;SQOOZE
	MOVEI C,0	;MARKS AS SQUOOZE
	PUSHJ P,SYM3X2	;AND PUT INTO UDEFINED AREA ALONG WITH NAME
	SKIPA A,[400000]	;SET UP GLOBAL FLAG
HLFOP:	MOVEI A,0	;VALUE OPERAND FLAG
HLFOP1:	SOJL B,CSAT	;ENOUGH OPERANDS SEEN?
	PUSH D,C	;NO, SAVE VALUE(OR GLOBAL NAME)
	HRLI A,400000	;PUT IN A VALUE MARKER
	PUSH D,A	;TO THE STACK
	JRST RPOL	;GET MORE POLISH

;HERE TO CONVERT TO STINKING SQUOOZE, CAVEAT:  THE FLAG BITS ARE CLEARED

SQZCON:	TLZ C,740000
	JUMPE C,CPOPJ
SQZ1:	CAML C,[50*50*50*50*50]
	POPJ P,
	IMULI C,50
	JRST SQZ1

; HERE IF GLOBAL SYMBOL DEFINED AT POLISH BLOCK READ TIME

OPND1:	MOVE C,1(A)	;SYMBOL VALUE
	JRST HLFOP
;HAVE ENOUGH OPERANDS FOR THE CURRENT OPERATOR

CSAT:	HRRZS A		;KEEP ONLY THE GLOBAL-VALUE HALF
	SKIPN SVSAT	;IS IT UNARY
	JRST UNOP	;YES, NO NEED TO GET 2ND OPERAND
	HRL A,(D)	;GET GLOBAL VALUE MARKER FOR 2ND OP
	POP D,T
	POP D,T		;VALUE OR GLOBAL NAME
UNOP:	POP D,B		;OPERATOR
	JUMPN A,GLOB	;IF EITHER IS A GLOBAL HANDLE SPECIALLY
	XCT OPTAB-3(B)	;IF BOTH VALUES JUST XCT
	MOVE C,T	;GET THE CURRENT VALUE
SETSAT:	SKIPG B,(D)	;IS THERE A VALUE IN THE STACK
	MOVE B,-2(D)	;YES, THIS MUST BE THE OPERATOR
	MOVE B,DESTB-3(B)	;GET NUMBER OF OPERANDS NEEDED
	MOVEM B,SVSAT	;SAVE IT HERE
	SKIPG (D)	;WAS THERE AN OPERAND
	SUBI B,1	;HAVE 1 OPERAND ALREADY
	JRST HLFOP1	;GO SEE WHAT WE SHOULD DO NOW

;HANDLE GLOBALS
GLOB:	TRNE A,-1	;IS IT IN RIGHT HALF
	JRST TLHG	;NO NEED TO SAVE THIS VALUE IF ITS GLOBAL
	PUSH P,T	;SAVE FOR A WHILE
	MOVE T,C	;THE VALUE
	MOVEI C,1	;MARK AS VALUE
	TLZE T,400000	;HACK SIGN BIT
	TLO C,1		;SAVE SIGN IF TURNED IN T
	PUSHJ P,SYM3X2
	POP P,T		;RETRIEVE THE OTHER VALUE
TLHG:	SKIPE SVSAT	;WAS THIS A UNARY OPERATOR
	TLNE A,-1	;WAS THERE A GLOBAL IN LEFT HALF
	JRST GLSET
	PUSH P,C	;SAVE THE FIRST OPERAND
	MOVEI C,1	;SEE ABOVE
	TLZE T,400000
	TLO C,1
	PUSHJ P,SYM3X2

GLSET:	MOVE C,B	;THE OPERATOR
	PUSHJ P,SYM3X1	;INTO THE UNDEF LIST
	MOVEI A,400000	;SET UP AS A GLOBAL VALUE
	JRST SETSAT	;AND SET UP FOR NEXT OPERATOR
;FINALLY WE GET TO STORE THIS MESS

STOROP:	MOVE B,-2(D)	;THIS SHOULD BE THE FAKE OPERATOR
	CAIE B,15	;IS IT
	JRST LOAD4A	;NO, ILL FORMAT
	HRRZ B,(D)	;GET THE VALUE TYPE
	JUMPN B,GLSTR	;AND TREAT GLOBALS SPECIAL
	MOVE A,T	;THE TYPE OF STORE OPERATOR
	CAIGE A,-3
	PUSHJ P,FSYMT	;STORE LOCATION REFERENCED SYMBOLICLY, MUST WORK HARDER
	PUSHJ P,RDHLF	;GET THE ADDRESS
	MOVE B,T	;SET UP FOR FIXUPS
	POP D,T		;GET THE VALUE
	POP D,T		;AFTER IGNORING THE FLAG
	PUSHJ P,@STRTAB+6(A)	;CALL THE CORRECT FIXUP ROUTINE

COMSTR:	SETZM POLSW	;ALL DONE WITH POLISH
	MOVE B,HEADNM
	CAILE B,477777
	JRST COMPOL	;TOO BIG, GIVE ERROR
	PUSHJ P,RWORD	;THIS SHOULD GET US OUT (I.E RUN OUT COUNT)
	JRST LOAD4A	;IF NOT, SOMETHING IS WRONG

GLSTR:	MOVE A,T
	CAIGE A,-3
	PUSHJ P,FSYMT	;STORE LOCATION REFERENCED BY SYMBOL
	PUSHJ P,RDHLF	;GET THE STORE LOCATION
	SUB D,[2,,2]	;VALUE AND MARKER ON STACK MEANINGLESS
	MOVE C,A	;STORE OP
	PUSHJ P,SYM3X2	;STORE LOC ALREADY IN T
	AOS T,GLBCNT	;WE STARTED AT -1 REMEMBER?
	HRRZ C,HEADNM	;GET HEADER #
	TLO C,440000	;MARK FIXUP AS GLOBAL BEASTIE
	PUSHJ P,SYM3X2	;LAST OF POLISH FIXUP
GLSTR1:	SOSGE GLBCNT	;MUST PUT GLOBAL REQUESTS IN TABLE
	JRST COMSTR	;AND FINISH
	POP P,T		;SQUOOZE
	PUSHJ P,LKUP
	MOVE A,HEADNM	;SETUP REQUEST WORD
	TLO A,POLREQ	;MARK AS POLISH REQUEST
	JUMPGE D,GLSTR2	;JUMP IF NOT SEEN
	PUSHJ P,DOWN
	MOVEM A,(D)
	JRST GLSTR1

GLSTR2:	EXCH A,T	;NOT PREVIOUSLY SEEN ENTER FULL REQUEST
	TLO A,400000	;MARK AS NEW TABLE ENTRY
	PUSHJ P,ENT
	JRST GLSTR1
STRTAB:	ALSYM	;-6
	LFSYM	;-5
	RHSYM	;-4
	UNTHF	;-3 FULL WORD FIXUP
	UNTHL	;-2 LEFT HALF WORD FIXUP
	UNTHR	;-1 RIGHT HALF WIRD FIXUP
	CPOPJ	;0

DESTB:	1
	1
	1
	1
	1
	1
	1
	1
	0
	0
	100

OPTAB:	ADD T,C
	SUB T,C
	IMUL T,C
	IDIV T,C
	AND T,C
	IOR T,C
	LSH T,(C)
	XOR T,C
	SETCM T,C
	MOVN T,C

;HERE TO LOOK UP LOCAL IN SYMBOL TABLE

FSYMT:	PUSHJ P,FSYMT1
	MOVE B,C		;SAVE SYMBOL
	PUSHJ P,FSYMT1
	PUSHJ P,SLCL		;SEARCH FOR LOCAL SYMBOL
	POPJ P,

FSYMT1:	PUSHJ P,RDHLF
	HRL C,T
	PUSHJ P,RDHLF
	HRR C,T
	PUSHJ P,SQZCON
	POPJ P,

SLCL:	POPJ P,
;HERE TO SATISFY GLOBAL REQUEST FOR POLISH

POLSAT:	PUSH P,D		;POINTER TO CURRENTLY PROCESSED GLOBAL REQUEST
	HRRZ T,B		;LOOK UP POLISH TO BE FIXED
	TLO T,440000
	PUSHJ P,LKUP
	JUMPGE D,[JRST 4,.]	;CANNOT FIND POLISH
	MOVE T,CGLOB		;SQUOOZE (SET UP AT DEFSYM+1
	MOVEI B,4(D)		;GET FIXPOL STARTED AT FIRST OPERATOR
	PUSHJ P,FIXPOL
	SOSG 1(D)		;UPDATE UNDEFINED GLOBAL COUNT
	JRST PALSAT		;COUNTED OUT FINISH THIS FIXUP
	POP P,D
	JRST PATCH1

;HERE TO FIXUP A SINGLE GLOBAL REQUEST IN POLISH

FIXPOL:	SKIPGE A,(B)
	JRST 4,.	;DID NOT FIND SYMBOL
	SKIPE (B)
	JRST FXP2	;NOT SQUOOZE
	CAME T,1(B)
	JRST FXP1	;SQUOOZE DOES NOT MATCH
	HRRI A,1	;MARK AS VALUE
	MOVE T,T1	;VALUE
	TLZE T,400000	;TEST SIGN BIT
	HRLI A,1	;SAVE IT
	MOVEM A,(B)
	MOVEM T,1(B)
	POPJ P,

FXP1:	ADDI B,2
	JRST FIXPOL

FXP2:	HRRZS A
	CAIN A,1
	ADDI B,1	;VALUE
	AOJA B,FIXPOL	;OPERATOR
;HERE TO FINISH THE POLISH AFTER ALL REQUESTS ARE SATISFIED

PALSAT:	AOS SATED		;NUMBER OF FIXUPS SATISFIED
	PUSH P,(D)		;SAVE THE NAME OF THIS FIXUP FOR LATER DELETION
	MOVEI A,2(D)		;POINTS AT STORE OP
	MOVE D,PPDP
	HRRZ B,(A)		;STORE OP
	HRRZ T,1(A)		;PLACE TO STORE
	PUSH D,T		;STORE ADDRESS
	PUSH D,B		;STORE OP
	PUSH D,[15]		;FAKE OPERATOR
	ADDI A,2		;POINTS TO FIRST TOKEN

PSAT1:	SKIPGE (A)		;TEST FOR END OF FIXUP
	JRST ENDPOL
	HRRZ T,(A)		;PICK UP NEXT TOKEN
	CAIN T,1
	JRST PSOPD		;OPERAND
	PUSH D,T		;SAVE OPERATOR IN STACK
	MOVE B,DESTB-3(T)	;NUMBER OF OPERANDS
	MOVEM B,SVSAT
	AOJA A,PSAT1

;HERE TO PROCESS OPERAND

PSOPD:	HLLZS (A)		;ZERO THE RIGHT HALF
	MOVE C,1(A)		;VALUE
	SKIPE (A)		;SKIP IF SIGN BIT SHOULD BE ZERO
	TLO C,400000		;RESTORE THE SIGN BIT
	ADDI A,2		;POINTER TO NEXT TOKEN
PSOPD1:	SOJL B,COMOP		;ENOUGH OPERANDS SEEN
	PUSH D,C
	PUSH D,[400000,,0]	;MARK AS VALUE
	JRST PSAT1
;HERE IF ENOUGH OPERANDS HAVE BEEN SEEN

COMOP:	SKIPN SVSAT
	JRST COMOP1	;UNARY OP
	POP D,T		;IGNORE MARKER
	POP D,T	;VALUE
COMOP1:	POP D,B		;OPERATOR
	XCT OPTAB-3(B)	;WOW!
	MOVE C,T;CURRENT VALUE
	SKIPG B,(D)	;SKIP IF OPERATOR ON STACK
	MOVE B,-2(D)	;OPERATOR ON TOP OF STACK, THIS IS THE OPERATOR
	MOVE B,DESTB-3(B)	
	MOVEM B,SVSAT
	SKIPG (D)	;TEST FOR OPERATOR AGAIN
	SUBI B,1	;OPERAND AT TOP OF STACK, AND I ALREADY HAVE ONE
	JRST PSOPD1
;HERE TO END PROCESSING OF POLISH IN SYMBOL TABLE

ENDPOL:	MOVE B,-2(D)
	CAIE B,15
	JRST 4,.	;NOPE
	POP D,T		;IGNORE MARKER
	POP D,T		;VALUE
	POP D,A		;IGNORE FAKE OPERATOR
	POP D,A		;STORE OP
	POP D,B		;STORE ADDRESS
	PUSHJ P,@STRTAB+6(A)
	POP P,D		;NAME OF THIS FIXUP
	EXCH P,SATPDP	;SAVE THIS NAME FOR LATER DELETION FROM TABLE
	PUSH P,D
	EXCH P,SATPDP
	POP P,D		;POINTER INTO CURRENT FIXUP
	JRST PATCH1

;UNIMPLEMENTED LOSAGE TRAPS HERE

RHSYM:
LFSYM:
ALSYM:	POPJ P,


;HERE TO REMOVE POLISH FIXUPS FROM SYMBOL TABLE

UNSATE:	PUSH P,A
	PUSH P,B
	PUSH P,D
	PUSH P,T2
	MOVE A,[-SATPDL,,SATPDB-1]
	EXCH A,SATPDP	;SET UP PUSH DOWN POINTER
	MOVE B,SATED	;# FIXUPS TO BE DELETED
	SETZM SATED
UNSAT1:	SOJL B,UNSAT3
	POP A,T		;FIXUP
	PUSH P,A
	PUSH P,B
	PUSHJ P,LKUP	;LOOK IT UP
	HRRZM D,T2
	AOBJP D,[JRST 4,.]	;NOT THERE
	AOBJP D,UNSAT2
	SKIPL (D)
	JRST .-2
UNSAT2:	PUSHJ P,PATCH	;REMOVE IT FROM TABLE
	POP P,B
	POP P,A
	JRST UNSAT1

UNSAT3:	POP P,T2
	POP P,D
	POP P,B
	POP P,A
	POPJ P,
; HERE TO HANDLE LINKS (BLOCK TYPE 23)

LINK:	SETOM LINKDB	;LINKS BEING HACKED
	PUSHJ P,GETBIT	;RELOCATION BITS INTO TT
	PUSHJ P,RRELOC	;LINK #
	MOVE A,T
	JUMPE A,LOAD4A	;ILLEGAL LINK #
	PUSHJ P,GETBIT
	PUSHJ P,RRELOC	;STORE ADDRESS
	HRRZ B,T
	JUMPL A,LNKEND	;JUMP ON LINK END
	CAILE A,MNLNKS
	JRST LOAD4A	;ILLEGAL LINK #

	HRRZ C,LINKDB(A)	;LINK VALUE
	HRRM C,@BPTR		;VALUE INTO STORE ADDRESS
	HRRM B,LINKDB(A)	;NEW VALUE
	JRST LINK

;END LINK

LNKEND:	MOVNS A			;LINK #
	CAILE A,MNLNKS
	JRST LOAD4A		;ILLEGAL LINK #
	HRLM B,LINKDB(A)	;LINK END ADDRESS
	JRST LINK

;HERE AFTER ALL LOADING TO CLEAN UP LINKS

LNKFIN:	PUSH P,A
	PUSH P,B
	MOVEI A,MNLNKS

LNKF1:	MOVS B,LINKDB(A)	;VALUE,,STORE ADDRESS
	TRNE B,-1		;DON'T STORE FOR ZERO STORE ADDRESS
	HLRM B,@BPTR
	SOJG A,LNKF1
	POP P,B
	POP P,A
	POPJ P,
;LEFT HALF FLAGS FOR FF

FIXRT==1
FIXLT==2
HSW==4

;MISCELLANEOUS CONSTANTS

PPDL==60	;POLISH PUSH DOWN LENGTH
SATPDL==5	;SATED PUSH DOWN LENGTH
POLREQ==200000	;MARKS GLOGAL REQUEST AS POLISH REQUEST
MNLNKS==20	;MAXIMUM NUMBER OF LINKS

;IMPURE STORAGE 

POLSW:	0			;-1=>WE ARE DOING POLISH
PPDP:	-PPDL,,PPDB-1		;INITIAL POLISH PUSH DOWN POINTER
PPDB:	BLOCK	PPDL+1		;POLISH PUSH DOWN BLOCK
SATED:	0			;COUNT OF POLISH FIXUPS TO BE DELETED
SATPDP:	-SATPDL,,SATPDB-1	;POINTER TO POLISH FIXUPS TO BE DELETED
SATPDB:	BLOCK	SATPDL+1	;LIST OF POLISH FIXUPS TO BE DELETED
SVSAT:	0			;# OF OPERANDS NEEDED
CGLOB:	0			;CURRENT GLOBAL IN SOME SENSE
CGLOBV:	0			;CURRENT GLOBAL VALUE IN SOME SENSE
GLBFS:	0			;GLOBAL BEING FIXED UP DURINGS DEFERED REQUEST
GLBFV:	0			;VALUE BEING FIXED
SVHWD:	0			;WORD CURRENTLY BEING READ BY POLISH
GLBCNT:	0			;# UNDEFINED FIXUPS DURING READING PHASE OF POLISH
HEADNM:	0			;# POLISH FIXUPS SEEN
LINKDB:	BLOCK	MNLNKS+1	;LINK DATA BLOCK (END LINK,,CURRENT VALUE)
ð$œ	/ð$œ	/ð$œ	/ð$œ	/ð$œ	/ð$œ	/ð$œ	/ð$œ	/ð$œ	/ð$œ	/