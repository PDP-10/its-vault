TITLE CHECKERS
.MLLIT==1

IRP A,,[A,B,C,D,P,V,I,I2] A=.IRPCNT+1 ? TERMIN

SOUT=1_33

LOC 41
	JSR UUOH
LOC 100


;   29  30  31  32		;   32  33  34  35
; 25  26  27  28		; 27  28  29  30
;   21  22  23  24		;   23  24  25  26
; 17  18  19  20		; 18  19  20  21
;   13  14  15  16		;   14  15  16  17
;  9  10  11  12		;  9  10  11  12
;    5   6   7   8		;    5   6   7   8
;  1   2   3   4		;  0   1   2   3


DEFINE CONC A,B
A!B!TERMIN

MASK==1_4+1_13.+1_22.+1_31.
LEGMF==40

MATERIAL:	0

WPCS:	0
BPCS:	0

WKNGS:	0
BKNGS:	0


EVAL:	SETO B,			; EVALUATE POS WITHOUT FURTHER JUMP PENDING
EVALCP:				; EVALUATE POS   WITH  FURTHER JUMP PENDING

	CAIA P,BPRINT ? .IOT TYIC,		; FOR DEBUGGING ONLY

	TLZ I,LEGMF
IRP Q1,,[WPCS,WPCS,WKNGS,WKNGS]Q2,,[5,4,-5,-4]
	MOVE Q1(I)
	CONC JUMPE EV,\<.IRPCNT*2+2>
	AND -1(P)				; -1 OR DEST OF LAST CAPTURE
	LSH @(I)[,Q2 ? ,-Q2]
	AND @(I)[BPCS ? WPCS]
	ANDCM [MASK]
	LSH @(I)[,Q2 ? ,-Q2]
	ANDCM WPCS ? ANDCM BPCS
	ANDCM [MASK]
CONC EV,\<.IRPCNT*2+1>,:
	JFFO .+2
	CONC JRST EV,\<.IRPCNT*2+2>,
	MOVEI D,4+.IRPCNT
	ANDCM BIT(A)
	PUSHJ P,MCEVAL				; MAKE CAPTURE AND RETURN VAL
	CAMLE V,ALPHA ? JSP D,NEWVAR		; RETURNS TO 1(D)
	JSP D,DPV
	ADD V,(P) ? JUMPGE V,EV2A
	CONC JRST EV,\<.IRPCNT*2+1>,
CONC EV,\<.IRPCNT*2+2>,:
TERMIN

	TLNE I,LEGMF ? JRST EV2A
	SETO
	EXCH -1(P)
	AOJE EVMVS
	XORI I,1
	MOVE ALPHA ? EXCH BETA ? MOVEM ALPHA
	JRST EVAL
EVMVS:	CAML P,DEPTH ? JRST EV2C
IRP Q1,,[WPCS,WPCS,WKNGS,WKNGS]Q2,,[5,4,-5,-4]
	MOVE Q1(I)
	CONC JUMPE EV,\<.IRPCNT*2+12>
	LSH @(I)[,Q2 ? ,-Q2]
	ANDCM [MASK]
	ANDCM WPCS ? ANDCM BPCS
CONC EV,\<.IRPCNT*2+11>,:
	JFFO .+2
	CONC JRST EV,\<.IRPCNT*2+12>,
	MOVEI D,.IRPCNT
	ANDCM BIT(A)
	PUSHJ P,MMEVAL			; MAKE MOVE AND RETURN VAL OF RESULTING POS
	CAMLE V,ALPHA ? JSP D,NEWVAR		; RETURNS TO 1(D)
	JSP D,DPV
	ADD V,(P) ? JUMPGE V,EV2A
	CONC JRST EV,\<.IRPCNT*2+11>,
CONC EV,\<.IRPCNT*2+12>,:
TERMIN

	HRROI V,-10000.-PDL(P)
	TLNE I,LEGMF
EV2A:	MOVE V,ALPHA
EV2B:	TLNN P,-1 ? JRST EXIT
IRP A,,[ALPHA,B,CVAR,MATERIAL,I,BKNGS,WKNGS,BPCS,WPCS,BETA,0] POP P,A ? TERMIN
	TRNE B,20 ? MOVN V,V
CPOPJ:	POPJ P,

EV2C:	XCT (I)[MOVE V,MATERIAL ? MOVN V,MATERIAL]
	JRST EV2B

MMEVAL:	JSP C,CCONS
	TLO I,LEGMF
IRP A,,[0,BETA,WPCS,BPCS,WKNGS,BKNGS,I,MATERIAL,CVAR,[[-1]],ALPHA] PUSH P,A ? TERMIN
	MOVE B,ALPHA ? EXCH B,BETA ? MOVEM B,ALPHA
	MOVE B,@(I)[@SWMV(D) ? @SBMV(D)]
	XORM B,WPCS(I)
	TDNE B,WKNGS(I) ? JRST MMEV2			; MOVE BY KING
	AND B,WPCS(I)					; DESTINATION BIT
	TDNN B,LRANK(I) ? JRST MMEV3
	IORM B,WKNGS(I)
	XCT (I)[AOSA MATERIAL ? SOSA MATERIAL]		; PROMOTING MOVE
MMEV2:	XORM B,WKNGS(I)
MMEV3:	XORI I,1
	JRST EVAL

MCEVAL:	JSP C,CCONS
	TLO I,LEGMF
IRP A,,[0,BETA,WPCS,BPCS,WKNGS,BKNGS,I,MATERIAL,B] PUSH P,A ? TERMIN
	MOVE B,@(I)[@SWCP1-4(D) ? @SBCP1-4(D)]
	SETCMI I2,-2(I)
	XORM B,WPCS(I2)					; CLEAR CAPT'D PIECE
	TDNE B,WKNGS(I2)
	SKIPA C,(I)[3 ? -3]
	MOVE C,(I)[2 ? -2]
	ADDM C,MATERIAL
	ANDCAM B,WKNGS(I2)				; CLEAR CAPT'D KING IF ANY
	MOVE B,@(I)[@SWCP2-4(D) ? @SBCP2-4(D)]
	PUSH P,B ? PUSH P,ALPHA
	XORM B,WPCS(I)					; MOVE PIECE
	TDNN B,WKNGS(I) ? JRST .+3
	XORM B,WKNGS(I) ? JRST EVALCP			; MOVE KING IF ANY
	AND B,WPCS(I)					; DESTINATION BIT
	TDNN B,LRANK(I) ? JRST EVALCP			; TRY FURTHER CAPTURES
	XCT (I)[AOS MATERIAL ? SOS MATERIAL]		; PROMOTING CAPTURE
	IORM B,WKNGS(I)
	SETOM -1(P)
	XORI I,1
	JRST EVAL

CCONS:	HRROI B,-1
CONS:	EXCH B,VARL2+1				; SAVE MOVE IN PRINCIPAL VARIATION LIST
	EXCH B,CONS
	HRLI A,(D)
	XOR A,(I)[0 ? SETZ]
	MOVEM A,VARL1-VARL2(B)
	MOVEM B,CVAR
	JRST (C)

DPV:	MOVE B,CVAR
	JRST DPV1

NEWVAR:	MOVEM V,ALPHA
	MOVE B,CVAR
	AOJ D,
	EXCH B,@-2(P)			; NEW VARIATION
	JUMPLE B,(D)
DPV1:	MOVE A,B			; RESTORE OLD PV TO FREE STORAGE
	EXCH A,CONS
DPV2:	SKIPG C,(B) ? JRST DPV3
	SKIPL B,(C) ? JRST DPV2
	MOVEM A,(C) ? JRST (D)
DPV3:	MOVEM A,(B) ? JRST (D)

ALPHA:	0
BETA:	0

BIT:	REPEAT 44,	1_<43-.RPCNT>

SWMV:	SP5(A) ? SP4(A) ? SM5(A) ? SM4(A)
SBMV:	SM5(A) ? SM4(A) ? SP5(A) ? SP4(A)

SP5:	REPEAT 44,	41_<43-.RPCNT-5>
SM4:	REPEAT 44,	21_<43-.RPCNT>
SP4:	REPEAT 44,	21_<43-.RPCNT-4>
SM5:	REPEAT 44,	41_<43-.RPCNT>

LRANK:	17_32. ? 17

SWCP1:	SC1P5(A) ? SC1P4(A)
SBCP1:	SC1M5(A) ? SC1M4(A) ? SC1P5(A) ? SC1P4(A)

SWCP2:	SC2P5(A) ? SC2P4(A)
SBCP2:	SC2M5(A) ? SC2M4(A) ? SC2P5(A) ? SC2P4(A)

SC1P5:	REPEAT 44,	1_<43-.RPCNT-5>
SC1M4:	REPEAT 44,	1_<43-.RPCNT+4>
SC1P4:	REPEAT 44,	1_<43-.RPCNT-4>
SC1M5:	REPEAT 44,	1_<43-.RPCNT+5>

SC2P5:	REPEAT 44,	2001_<43-.RPCNT-12>
SC2M4:	REPEAT 44,	 401_<43-.RPCNT>
SC2P4:	REPEAT 44,	 401_<43-.RPCNT-10>
SC2M5:	REPEAT 44,	2001_<43-.RPCNT>

THINK:	MOVEI VARL2 ? MOVEM CVAR
	SETOM VARL2
	MOVE [EXCH B,VARL2+1] ? MOVSI A,-158. ? MOVEM CONS
	AOJ ? MOVEM VARL2+1(A) ? AOBJN A,.-2
	MOVE [JRST 4,CONS] ? MOVEM VARL2+159.
	MOVNI 700000 ? MOVEM ALPHA ? MOVEM BETA ? MOVEM PDL
	JRST EVAL
EXIT:	MOVE A,VARL2
	JUMPL A,RD
EXIT1:	JUMPL A,EXIT2
	XCT (I)[SKIPGE B,VARL1-VARL2(A) ? SKIPL B,VARL1-VARL2(A)]
	JRST EXIT2
	HLRZ D,B ? ANDI D,7
	PUSH P,A ? MOVEI A,(B)
	XOR D,(I)[0 ? 2]
	ADD A,@MMOVE1
	PUSHJ P,MMOVE
	POP P,A
	MOVE A,(A)
	XORI I,1
	JRST EXIT1
EXIT2:	XORI I,1 ? JRST RD

RESET:	MOVEI GAMEP ? MOVEM GAMEP
	MOVE [IBOARD,,WPCS]
	BLT BKNGS
	SETZB I,MATERIAL
	MOVEI P,PDL
	JRST RD

GO:
TYIC==1
TYOC==2
	.OPEN TYIC,['TTY]
	.VALUE
	.OPEN TYOC,[4021,,'TTY]
	.VALUE
	JRST RESET

UUOH:	0
	HLRZ 40 ? CAIE (SOUT) ? .VALUE
.SOUT:	MOVSI 440700 ? HLLM 40
.SOUT1:	ILDB 40 ? JUMPE @UUOH
	.IOT TYOC, ? JRST .SOUT1

2SP=PUSHJ P,.
	.IOT TYOC,[40]
	POPJ P,

BPRINT:	PUSH P,B
	SOUT [ASCIZ /C/]
	MOVSI A,-8 ? MOVSI B,40000
BP1:	.IOT TYOC,[15]
	TRNN A,1 ? 2SP
	MOVSI C,-4
BP2:	MOVEI "0
	TDNN B,WKNGS ? TDNE B,BKNGS ? MOVEI "2
	TDNE B,WPCS ? ADDI 1
	TDNE B,BPCS ? ADDI 2
	.IOT TYOC, ? 2SP
	ROT B,1
	AOBJN C,BP2
	ROT B,-8
	TRNN A,1 ? ROT B,-1
	SOUT @BDTAB(A)
	AOBJN A,BP1
	.IOT TYOC,[15]
	POP P,B
	POPJ P,

BDTAB:	[ASCIZ /  32-35/]
	[ASCIZ /   27-30/]
	[ASCIZ /  23-26/]
	[ASCIZ /   18-21/]
	[ASCIZ /  14-17/]
	[ASCIZ /    9-12/]
	[ASCIZ /   5-8/]
	[ASCIZ /    0-3/]

PAT:	BLOCK 77

	VARL2
	-1
PDL:	BLOCK 999.
CVAR:	0
VARL1:	BLOCK 160.
VARL2:	BLOCK 160.

IBOARD:	17757 ? 757740,, ? 0 ? 0

DEPTH:	4*9,,

RDNUM:	MOVEI A,
RDNUM1:	.IOT TYIC,B
	CAIL B,"0 ? CAILE B,"9 ? POPJ P,
	IMULI A,12 ? ADDI A,-"0 (B)
	JRST RDNUM1

RD:	PUSHJ P,BPRINT
	MOVSI A,440700 ? MOVEI
RD1:	.IOT TYIC,B
	CAIL B,"A ? CAILE B,"Z ? JRST RD2
	TLNE A,770000 ? IDPB B,A
	JRST RD1
RD2:	MOVSI A,-NSYMS
RD3:	AOJ A, ? CAME SYMTAB-1(A) ? AOBJN A,RD3
	JUMPG A,RD
	SKIPE B,SYMTAB(A) ? JRST (B)
	MOVEI A,SYMTAB-MVS-1(A)
	ASH A,-1
	MOVEI D,(A)
	PUSHJ P,RDNUM
	SETCMI A,-44(A)
	PUSHJ P,MMOVE
	JRST RD

SYMTAB:
	ASCII /R/	?	RESET
	ASCII /M/	?	THINK
	ASCII /U/	?	UNMOVE
	ASCII /S/	?	SETUP
	ASCII /D/	?	SETD
MVS:	ASCII /NE/	?	0
	ASCII /NW/	?	0
	ASCII /SW/	?	0
	ASCII /SE/	?	0
	ASCII /NEJ/	?	0
	ASCII /NWJ/	?	0
	ASCII /SWJ/	?	0
	ASCII /SEJ/	?	0
NSYMS=<.-SYMTAB>/2

SETD:	PUSHJ P,RDNUM
	IMULI A,10.
	HRLZM A,DEPTH
	JRST RD

UNMOVE:	MOVE A,GAMEP ? HRRZ I2,-4(A)
UNMOV2:	TLNN A,-1 ? JRST RD
	CAIE I2,@-4(A) ? JRST UNMOV3
	IRP B,,[WPCS,BPCS,WKNGS,BKNGS,I,MATERIAL] POP A,B ? TERMIN
	JRST UNMOV2
UNMOV3:	MOVEM A,GAMEP
	JRST RD

GAMEP:	.
GAME:	BLOCK 1200.

MMOVE:	MOVE GAMEP
	IRP A,,[MATERIAL,I,BKNGS,WKNGS,BPCS,WPCS] PUSH A ? TERMIN
	MOVEM GAMEP
	MOVE BIT(A)
	TDNE WPCS ? TDZA I,I ? MOVEI I,1
MMOVE1:	SUB A,(D)[5? 4? -5? -4? 12? 10? -12? -10]
	XOR D,(I)[0 ? 2]
	CAIGE D,4 ? JRST MMOVE2
	MOVE B,@(I)[@SWCP1-4(D) ? @SBCP1-4(D)]
	SETCMI I2,-2(I)
	XORM B,WPCS(I2)					; CLEAR CAPT'D PIECE
	TDNE B,WKNGS(I2)
	SKIPA C,(I)[3 ? -3]
	MOVE C,(I)[2 ? -2]
	ADDM C,MATERIAL
	ANDCAM B,WKNGS(I2)				; CLEAR CAPT'D KING IF ANY
	MOVE B,@(I)[@SWCP2-4(D) ? @SBCP2-4(D)]
	XORM B,WPCS(I)					; MOVE PIECE
	TDNN B,WKNGS(I) ? JRST .+3
	XORM B,WKNGS(I) ? JRST MMOVE4			; MOVE KING IF ANY
	AND B,WPCS(I)					; DESTINATION BIT
	TDNN B,LRANK(I) ? JRST MMOVE4			; TRY FURTHER CAPTURES
	XCT (I)[AOS MATERIAL ? SOS MATERIAL]		; PROMOTING CAPTURE
	IORM B,WKNGS(I)
	JRST MMOVE4

MMOVE2:	MOVE B,@(I)[@SWMV(D) ? @SBMV(D)]
	XORM B,WPCS(I)
	TDNE B,WKNGS(I) ? JRST MMOVE3			; MOVE BY KING
	AND B,WPCS(I)					; DESTINATION BIT
	TDNN B,LRANK(I) ? JRST MMOVE4
	IORM B,WKNGS(I)
	XCT (I)[AOSA MATERIAL ? SOSA MATERIAL]		; PROMOTING MOVE
MMOVE3:	XORM B,WKNGS(I)
MMOVE4:	XORI I,1
	POPJ P,

END GO
.PLAUS:	PUSHJ P,PLAUS
	MOVEI Q,(T)
	SUB Q,TSAV
	HRLI Q,(T)
	MOVSM Q,TSAV2
	MOVE [TLNN I,KILLR2]
	MOVEM PLS2
	SOS (P)
IFG 3M,	.VALUE [-2]	; INFORM CONTROLLER TO EMPTY BUFFER
IFG FAST,[MOVE [JRST EVAL2]
	MOVEM WMVGDT+20
	MOVEM BMVGDT+20
]	SKIPE DEPTH
	POPJ P,
	MOVN SALPHA ? MOVEM ALPHA
	MOVE T,TSAV
	SOS LMGN
	POP P,
	JRST EVAL

PLAUS:	MOVEI 16.
	SETZM NACTIV
	PUSH P,
	PUSHJ P,PTABUL
	POP P,
	HRRZ Q,TSAV
	CAIL Q,-50.(T) ? SETOM NACTIV		; DON'T USE ACTIVE MODE IF < 26 MOVES
	CAMG PLYN
	JRST PLAUS1	; OPENING IS OVER AFTER 8 MOVES
	PUSHJ P,PWNFLS
	MOVEI 10.
	HRRM WPADV
OPPLS1:	CAIL Q,(T)
	POPJ P,
IFE FAST+3M,	HRRZ 1(Q)
IFN FAST+3M,	MOVE 1(Q) ? JSP V,MMOVE ? JSP V,UNMOVE ? ANDI -1
	LDB A,[ORIGIN]
	MOVE D,@BOARD(A)
	PUSHJ P,CCNORM
	MOVN S,A
	LDB A,[DESTIN]
	PUSHJ P,CCNORM
	ADD S,A
	IMUL S,OPNPCM(D)
	PUSHJ P,PLCAS
OPPLS2:	PUSHJ P,PHEURS
	LDB A,[ORIGIN]
	SKIPN @BOARD(A)
	JRST OPPLS4
	IDIVI A,12
	CAILE B,4
	ADDI S,2	; PIECE MOVING FROM KING SIDE
OPPLS4:	CAME (I)[116_7+141 ? 60_7+33]
	CAMN (I)[107_7+134 ? 51_7+26]
	SUBI S,15.	; N/N1-R3
	MOVE A,PLYN
	CAIN A,3
	CAIE 56_7+33
	JRST OPPLS3
	HRRZ GAME
	HRRZ A,GAME+1
	CAIN 101_7+125
	CAIE A,67_7+43
	JRST OPPLS3
	HRRZ GAME+2
	CAIN 114_7+141
	SUBI S,50.	; PETROFF'S DEFENSE
OPPLS3:	ADDI S,60.	; CONSTANT
	MOVEM S,2(Q)
	ADDI Q,2
	JRST OPPLS1

OPNPCM:	1 ? 4 ? 3 ? 2 ? 1 ? -1				; OPENING PIECE MULTIPLIER
MDGPCM:	3 ? 4 ? 3 ? 2 ? 1 ? -1				; MIDDLE GAME PIECE MULTIPLIER
EGGPCM:	0 ? 4 ? 3 ? 1 ? 1 ? 4				; GENERAL ENDGAME PIECE MULTIPLIER
CCA:	0 ? 1 ? 2 ? 3 ? 3 ? 4 ? 5 ? 6 ? 7 ? 8		; CENTER CONTROL ARRAY
MDKF:	0 ? 10. ? 8 ? 2 ? 10. ? 8 ? 2 ? 8 ? 2 ? 2	; MIDDLE GAME KING FIELD
EGPNKF:	0 ? 6 ? 5 ? 3 ? 6 ? 4 ? 2 ? 3 ? 1 ? 1		; PAWN ENDGAME KING FIELD
EGGNKF:	0 ? 10. ? 10. ? 6 ? 10. ? 10. ? 5 ? 8 ? 4 ? 4	; GENERAL ENDGAME KING FIELD
EGPCKF:	0 ? 10. ? 9 ? 6 ? 10. ? 10. ? 5 ? 8 ? 4 ? 4	; PIECE ENDGAME KING FIELD
OWNPWN:	 4. ? 12. ? 8 ? 4 ? 15. ? 3 ? 5 ? 15. ? 4 ? 1 ? 2 ? 1 ? 0 ? 0 ? 0
OPPPWN:	24. ? 12. ? 8 ? 8 ? 5 ? 3 ? 7 ? 6 ? 4 ? 5 ? 2 ? 1 ? 1 ? 0 ? 0

CCNORM:	SUBI A,25
	IDIVI A,12
	TRNE A,4 ? ANDCAI A,3
	TRNE B,4 ? ANDCAI B,3
	CAIGE B,(A) ? EXCH A,B
	MOVE A,@(A)[CCA(B)? CCA+3(B)? CCA+5(B)? CCA+6(B)]
	POPJ P,

KFNORM:	MOVE B,A
	PUSH P,0
	MOVE @(I)[PIECEL+20 ? PIECEL]
	SUBI BOARD
	IDIVI 12
	IDIVI B,12
	SUB B,0
	SUB C,A
	MOVMS B
	MOVMS C
	CAIG B,3
	CAILE C,3
	SETZB B,C
	CAILE B,(C)
	EXCH B,C
	MOVEI A,@(B)[0(C)? 4-1(C)? 4+3-2(C)? 4+3+2-3(C)]
	POP P,0
	POPJ P,

MOBIL:	PUSH P,Q
	MOVE 1(Q)
	JSP V,MMOVE
	MOVSI A,177
	PUSH P,FLAGS
	ANDCAM A,FLAGS
	XORI I,1
	PUSH P,TSAV
	MOVEM T,TSAV
	PUSH P,S
	PUSHJ P,LMG
	SUB T,TSAV
	ASH T,-1
	POP P,S
	ADDI S,(T)
	MOVE T,TSAV
	POP P,TSAV
	XORI I,1
	POP P,FLAGS
	JSP V,UNMOVE
	POP P,Q
	POPJ P,

PWNFLS:	SETZM PNFILE
	MOVE A,[PNFILE,,PNFILE+1]
	BLT A,PNFILE+9
	MOVSI A,-8
PWNF1:	SKIPLE B,@(I)[PIECEL+10(A) ? PIECEL+30(A)]
	SKIPE @(I)[PIECE+10(A) ? PIECE+30(A)]
	JRST PWNF2
	SUBI B,BOARD
	IDIVI B,12
	AOS PNFILE(C)
PWNF2:	AOBJN A,PWNF1
	POPJ P,
PNFILE:	BLOCK 10.

PLCAS:	LDB A,[ORIGIN]
	MOVEI B,5
	CAME B,@BOARD(A)
	POPJ P,
	MOVE B,FLAGS
	TDNN B,MMVT3(I)
	POPJ P,
	HRROI B,-20.		; NON-CAS K MV FORFEITING CAS PRIVILEGE
	CAMN MMVT4(I)
	MOVEI B,30.		; O-O
	CAMN MMVT5(I)
	MOVEI B,25.		; O-O-O
	ADD S,B
	POPJ P,

PHEURS:	LDB A,[ORIGIN]
	SKIPE @BOARD(A)
	JRST PHRS1				; NOT A PAWN MOVE
	CAMN (I)[101_7+125 ? 67_7+43]
	ADDI S,30.				; P/K2-K4
	CAMN (I)[100_7+124 ? 66_7+42]
	ADDI S,16.				; P/Q2-Q4
	CAME (I)[101_7+113 ? 67_7+55]
	CAMN (I)[100_7+112 ? 66_7+54]
	ADDI S,2				; P/K3-K4 OR P/Q3-Q4
	CAME (I)[113_7+125 ? 55_7+43]
	CAMN (I)[112_7+124 ? 54_7+42]
	ADDI S,8				; P-K3 OR P-Q3
	CAME (I)[75_7+121 ? 63_7+37]
	CAMN (I)[104_7+130 ? 72_7+46]
	SUBI S,5				; P/R2-R4
	SKIPN @(I)[@BOARD+125 ? @BOARD+43]
	SKIPE @(I)[@BOARD+124 ? @BOARD+42]
	JRST .+4
	CAME A,(I)[125 ? 43]
	CAMN A,(I)[124 ? 42]
	ADDI S,3				; MOVING A CENTER PAWN IF BOTH ARE UNMOVED
	TRNN 740000
	JRST PHRS2
	ADD A,(I)[-12 ? 12]
	PUSHJ P,CCNORM
	MOVE D,A
	LDB A,[DESTIN]
	PUSHJ P,CCNORM
	CAMLE A,D
	ADDI S,5				; CAPTURE TOWARDS CENTER
	CAMGE A,D
	SUBI S,5				; CAPTURE AWAY FROM CENTER
	LDB A,[ORIGIN]
	IDIVI A,12
	MOVE A,PNFILE(B)
	SOJG A,PHRS4				; JUMP IF MORE THAN ONE PAWN ON ORIGIN FILE
	LDB C,[DESTIN]
	IDIVI C,12
	MOVEI A,(D)
	ASH A,1
	SUBI A,(B)
	SKIPN PNFILE(A)
	SUBI S,6				; CAPTURE ISOLATES CAPTURING PAWN
	SKIPE PNFILE(D)
	SUBI S,12.				; DOUBLES CAPTURING PAWN, DOESN'T UNDOUBLE
PHRS4:	LDB A,[DESTIN]
	JRST PHRS3

PHRS2:	LDB A,[ORIGIN]				; NON CAPTURING PAWN MOVE
	IDIVI A,12
	ADDI B,5
	TRNE B,4
WPADV:	SUBI S,10.				; WING PAWN ADVANCE
	CAIE B,3+5
	CAIN B,4+5
	JRST BKWD1
	CAIE B,5+5
	CAIN B,6+5
	JRST BKWD2
	JRST PHRS2A
BKWD3:	HRRZ D,PNFLS1(B) ? HLRZ D,PNFLS2(B)
BKWD1:	SKIPA C,[1]
BKWD2:	MOVEI C,-1
	LDB A,[ORIGIN]
	IDIVI A,12
	XCT BKWD3(I)
	CAME A,D
	JRST PHRS2A				; ADVANCING PAWN NOT FARTHEST BACK ON FILE
	LDB A,[DESTIN]
	IDIVI A,12
	ADDI B,(C)
	XCT BKWD3(I)
	XCT (I)[CAML A,D ? CAMG A,D]
	JRST PHRS2A				; NOT ADV BEYOND NEIGHBOR
	MOVE A,D
	ADDI B,(C)
	XCT BKWD3(I)
	XCT (I)[CAMLE A,D ? CAMGE A,D]
	SUBI S,15.				; MAKES CENTER PAWN BACKWARD
PHRS2A:	POPJ P,
PHRS1A:	MOVE B,@(I)[PIECEL+13 ? PIECEL+33]
	CAMN B,(I)[BOARD+125 ? BOARD+43]
	CAME A,(I)[113 ? 55]
	JRST .+2
	ADD S,C					; PIECE TO/FROM K3 BLOCKING/UNBLOCKING OWN PAWN
	MOVE B,@(I)[PIECEL+14 ? PIECEL+34]
	CAMN B,(I)[BOARD+124 ? BOARD+42]
	CAME A,(I)[112 ? 54]
	JRST .+2
	ADD S,C					; PIECE TO/FROM Q3 BLOCKING/UNBLOCKING OWN PAWN
	POPJ P,
PHRS1:	MOVEI C,50.				; PIECE MOVE
	PUSHJ P,PHRS1A				; ADD 50. FOR UNBLOCKING OWN UNMOVED CENTER PAWN
	MOVNI C,50.
	LDB A,[DESTIN]	
	PUSHJ P,PHRS1A				; SUB 50. FOR BLOCKING OWN UNMOVED CENTER PAWN
	TRNN 740000
	POPJ P,
PHRS3:	SKIPE @BOARD(A)				; MAY REFERENCE AC0
	SKIPN BOARD(A)
	JRST .+2
	POPJ P,					; CAPTURES A PIECE
	IDIVI A,12
	CAIE B,4
	CAIN B,5
	JRST .+2
	POPJ P,
	LDB B,[ORIGIN]
	SKIPN @BOARD(B)
	ADDI S,20.				; BONUS IF TAKING WITH PAWN
	ADDI S,20.				; CAPTURING A CENTER PAWN
	LDB A,[DESTIN]
	SKIPLE B,@(I)[BOARD-13(A) ? BOARD+11(A)]
	XCT (I)[CAIGE B,PIECE+20 ? CAIL B,PIECE+20]
	JRST PHRS5
	SKIPN (B)
	JRST PHRS6
PHRS5:	SKIPLE B,@(I)[BOARD-11(A) ? BOARD+13(A)]
	XCT (I)[CAIGE B,PIECE+20 ? CAIL B,PIECE+20]
	JRST PHRS7
	SKIPN (B)
PHRS6:	SUBI S,35.	; PAWN WAS DEFENDED BY A PAWN
PHRS7:	POPJ P,

PLAUS1:	MOVEI 1950.
	CAMG WPCS
	CAMLE BPCS
	JRST PLAUS2	; ENDGAME
	MOVEI 5
	HRRM WPADV
	PUSHJ P,PWNFLS
MGPLS1:	CAIL Q,(T)
	POPJ P,
IFE FAST,	HRRZ 1(Q)
IFN FAST,	MOVE 1(Q) ? JSP V,MMOVE ? JSP V,UNMOVE ? ANDI -1
	LDB A,[DESTIN]
	PUSHJ P,CCNORM
	MOVE S,A
	LDB A,[ORIGIN]
	PUSHJ P,CCNORM
	SUB S,A
	LDB A,[DESTIN]
	PUSHJ P,KFNORM
	ADD S,MDKF(A)
	LDB A,[ORIGIN]
	MOVE D,@BOARD(A)
	PUSHJ P,KFNORM
	SUB S,MDKF(A)
	IMUL S,MDGPCM(D)
	PUSHJ P,PLCAS
	PUSHJ P,PHEURS
	PUSHJ P,MOBIL	; ADD MOBILITY TERM
	ADDI S,60.	; CONSTANT
	MOVEM S,2(Q)
	ADDI Q,2
	JRST MGPLS1

PNFLS1:	BLOCK 10.		; W PAWNS ON FILES	; LOW RANK IN LH, 11. IF NO PAWN
PNFLS2:	BLOCK 10.		; B PAWNS ON FILES	; HIGH RANK IN RH, 0 IF NO PAWN
							; RANK # = SQ# / 12
PTABUL:	MOVSI A,11.
	MOVEM A,PNFLS1
	MOVE [PNFLS1,,PNFLS1+1]
	BLT PNFLS1+19.
	PUSHJ P,PTBL2			; 0 IN RH OF A
	AOJA A,PTBL2			; 1 IN RH OF A

PTBL2:	MOVE B,(A)[-8,,10 ? -8,,30]
PTBL3:	SKIPL C,PIECEL(B) ? SKIPE PIECE(B) ? JRST PTBL4
	SUBI C,BOARD ? IDIVI C,12
	MOVE E,@PTBL5(A) ? CAILE C,(E) ? HRRM C,@PTBL5(A)
	MOVSS E ? CAIGE C,(E) ? HRLM C,@PTBL5(A)
PTBL4:	AOBJN B,PTBL3
	POPJ P,

PTBL5:	PNFLS1(D) ? PNFLS2(D)

PLAUS2:	MOVSI A,-40
	SKIPL PIECEL(A)
	SKIPE PIECE(A)
	AOBJN A,.-2
	JUMPL A,PLAUS3		; JUMP IF AT LEAST 1 PAWN ON BOARD
	SETOM NACTIV		; DON'T USE ACTIVE SCHEMA IN PAWNLESS ENDGAME
	MOVE [JRST EVEGPC] ? MOVEM EVAL1	; USE SPECIAL EVALUATION FUNCTION
IFG FAST, MOVE [JRST 1(V)] ? MOVEM FC+1		; DON'T FORWARD CUT
	HRRZM I,ISAV'
	MOVEI 675.
	CAME WPCS(I)		; SKIP IF WE HAVE JUST N & B
	POPJ P,
	MOVE A,(I)[-14,,PIECEL+4 ? -14,,PIECEL+24]
	MOVEI 2
	SKIPL B,(A)
	CAME PIECE-PIECEL(A)
	AOBJN A,.-2
	SUBI B,BOARD
	IDIVI B,12
	XOR B,C
	ANDI B,1	; 0 IF B ON BLACK, 1 IF ON WHITE
	MOVE (B)[JFCL ? SETCMI B,-9-1(B)]
	MOVEM TENT1
	MOVE [JRST TENT] ? MOVEM EGPC9+1
	POPJ P,

EVEGPC:	EXCH I,ISAV	; ENDGAME WITH PIECES ONLY
	MOVE A,WPCS(I)
	SUB A,@MMVT9(I)
	MOVEM A,ASAV'	; TECH2'S MATERIAL ADVANTAGE
	MOVE A,@(I)[PIECEL+20 ? PIECEL]
	SUBI A,BOARD
	PUSHJ P,CCNORM
	ASH A,5
	SUBM A,ASAV	; 32*<OPP'S KING'S CENTRALITY>-<TECH2'S MATERIAL ADV>
	MOVE A,@ICHT1(I)
	SUBI A,BOARD
	PUSHJ P,KFNORM
	MOVE A,EGPCKF(A)
	ASH A,4
	SUBM A,ASAV	; <TECH2'S MTRL ADV>-32*<OPP'S K CENT>+16*<K PROXIMITY TO K>
	MOVE A,@ICHT1(I)
	SUBI A,BOARD
	PUSHJ P,CCNORM
	ADDM A,ASAV	; ADD TECH2'S K CENTRALITY
	SETZB E,F
	MOVE Q,(I)[-17,, ? -17,,20]
	XORI I,1
EGPC1:	SKIPG A,PIECEL+1(Q)
	JRST EGPC9
	SUBI A,BOARD
	PUSHJ P,KFNORM
	ADD E,EGPCKF(A)
	AOJ F,
EGPC9:	AOBJN Q,EGPC1
	XORI I,1	; PROGRAM MODIFIED
	IDIVI E,(F)
	ADDM E,ASAV	; ADD <SUM OF PROXIMITIES OF T'S PIECES TO T'S K>/<# OF T'S NON-K PIECES>
	EXCH I,ISAV
	MOVEI B,(I)
	CAMN B,ISAV
	SKIPA A,ASAV
	MOVN A,ASAV	; NEGATE IF SIDE TO MOVE ISN'T TECH II
	JRST EVAL1+1

TENT:	MOVE A,@ICHT1(I)	; USED WHEN TECH HAS N & B
	XORI I,1
	SUBI A,BOARD
	IDIVI A,12
TENT1:	JFCL		; IF B ON WHITE, USE MIRROR IMAGE OF BOARD
	SUBI A,1(B)
	MOVM A,A
	IMULI A,60
	ADDM A,ASAV	; ADD FUNCTION WHOSE VALUE IS 7*48
	JRST EGPC9+2	; FOR CORNERS CONTROLLED BY B,
			; SLOPING DOWN FROM 0 ON DIAGONAL THRU
			; CORNERS NOT CONTROLLED BY B

EGPNH:	LDB A,[ORIGIN]
	SKIPE @BOARD(A)
	POPJ P,	; NOT A PAWN
	IDIVI A,12
	XCT (I)[MOVS C,PNFLS1(B) ? MOVE C,PNFLS2(B)]
	CAIE A,(C)
	JRST [SUBI S,10. ? POPJ P,]	; NOT FIRST PAWN ON FILE
	LDB A,[DESTIN]
	IDIVI A,12
	XCT (I)[SETCMI C,-12.(A) ? MOVEI C,(A)]
	XCT (I)[MOVS D,PNFLS2(B) ? MOVE D,PNFLS1(B)]
	XCT EGPNH2(I)
	JRST EGPNH1
	XCT (I)[MOVS D,PNFLS2-1(B) ? MOVE D,PNFLS1-1(B)]
	XCT EGPNH2(I)
	JRST EGPNH1
	XCT (I)[MOVS D,PNFLS2+1(B) ? MOVE D,PNFLS1+1(B)]
	XCT EGPNH2(I)
	JRST EGPNH1
	ADD S,NOPPSD-4(C)	; ADVANCING A PASSED PAWN, WEIGHT BY DESTIN RANK
	POPJ P,
EGPNH1:	ADD S,OPPSD-4(C)	; ADVANCING A NON-PASSED PAWN
	POPJ P,

EGPNH2:	CAILE A,(D) ? CAIGE A,(D)
OPPSD:	2 ? 1 ? 3 ? 4
NOPPSD:	3 ? 7 ? 10. ? 13. ? 23. ? 80.

PLAUS3:	MOVE A,(I)[-17,,? -17,,20]
	SKIPL PIECEL+1(A)
	SKIPN PIECE+1(A)
	AOBJN A,.-2
	JUMPL A,PLAUS4		; GENERAL ENDGAME
	SETOM NACTIV		; DON'T USE ACTIVE SCHEMA IN PAWN ENDGAME
EGPN1:	CAIL Q,(T)		; PAWN ENDGAME
	POPJ P,
	MOVE 1(Q)
IFN FAST,	JSP V,MMOVE ? JSP V,UNMOVE
	TRNE 740000
	SKIPA S,[65.]		; CAPTURE PLUS CONSTANT
	MOVEI S,50.		; CONSTANT
	PUSHJ P,EGPNH		; ADD BONUS FOR PAWN MOVES
	LDB A,[ORIGIN]
	MOVE B,@BOARD(A)
	CAIE B,5		; SKIP IF KING MOVE
	JRST EGPN2
	PUSHJ P,CCNORM
	SUB S,A
	LDB A,[DESTIN]
	PUSHJ P,CCNORM
	ADD S,A
	LDB A,[ORIGIN]
	LDB B,[DESTIN]
	SUBI A,(B)
	IDIV A,(I)[3 ? -3]
	ADD S,A			; BONUS FOR KING ADVANCE
	LDB A,[ORIGIN]
	PUSHJ P,KFNORM
	SUB S,EGPNKF(A)
	LDB A,[DESTIN]
	PUSHJ P,KFNORM
	ADD S,EGPNKF(A)
	PUSHJ P,PPFH		; PASSED PAWN FIELD HEURISTICS
	 ORIGIN
	SUB S,R
	PUSHJ P,PPFH
	 DESTIN
	ADD S,R
EGPN2:	MOVEM S,2(Q)
	ADDI Q,2
	JRST EGPN1

PPFH:	MOVEI R,
	MOVSI A,-8

IRP Z1,,[1,2]Z2,,[2,1]Z3,,[L,G]Z4,,[G,L]Z5,,[L,R]Z6,,[2,9]	; US,,[WHITE,BLACK]
PPFH!Z1:	H!Z5!RZ B,PNFLS!Z1+1(A)	; ROW OF OUR MOST ADV P ON THIS FILE
	H!Z5!RZ C,PNFLS!Z2+1(A)		; ROW OF HIS LEAST ADV PAWN ON THIS FILE
	H!Z5!RZ D,PNFLS!Z2(A)		; ROW OF HIS LEAST ADV PAWN ON LEFT ADJ FILE
	CAI!Z4!E B,(C)			; SKIP IF BLOCKED OR NO PAWN
	CAI!Z3!E B,(D)			; SKIP IF NOT OPPOSED ON LEFT ADJ FILE
	JRST PPFH!Z6			; PAWN NON-EXISTENT OR NOT PASSED
	H!Z5!RZ C,PNFLS!Z2+2(A)		; ROW OF HIS LEAST ADV PAWN ON RIGHT ADJ FILE
	CAI!Z4 B,(C)			; SKIP IF OPPOSED ON RIGHT ADJ FILE
	PUSHJ P,PPF2H!Z1		; PAWN IS PASSED, ADD FIELD TERM
TERMIN
PPFH9:	AOBJN A,PPFH1î					; LOOP OVER 8 FILES
	JRST CPOPJ1

PPF2H1:	TDZA C,C
PPF2H2:	MOVEI C,1
	LDB E,@-1(P)
	IDIVI E,12
	SUBI E,(B)
	SUBI F,1(A)
	MOVMS F
	SKIPG C
	MOVNS E
	JUMPL E,CPOPJ
	CAIG E,4
	CAILE F,2
	POPJ P,
	IMULI E,3
	ADDI E,(F)
	XOR C,I
	ADD R,@(C)[OWNPWN(E) ? OPPPWN(E)]
	POPJ P,

PLAUS4:	MOVE A,(I)[-20,,20 ? -20,,]
	MOVEI 4
	SKIPL PIECEL(A)
	CAME PIECE(A)
	AOBJN A,.-2
	JUMPG A,.+2
	SETO					; DON'T CENTRALIZE K IF ENEMY HAS Q
	MOVEM EGGPCM+5
PLS4A:	CAIL Q,(T)
	POPJ P,
	MOVE 1(Q)
IFN FAST,	JSP V,MMOVE ? JSP V,UNMOVE
	LDB A,[ORIGIN]
	PUSHJ P,CCNORM
	MOVN S,A
	LDB A,[DESTIN]
	PUSHJ P,CCNORM
	ADD S,A
	LDB A,[ORIGIN]
	MOVE B,@BOARD(A)
	IMUL S,EGGPCM(B)
	MOVEI B,5
	CAME B,@BOARD(A)
	JRST EGGN2
	PUSHJ P,KFNORM
	SUB S,EGGNKF(A)
	LDB A,[DESTIN]
	PUSHJ P,KFNORM
	ADD S,EGGNKF(A)
EGGN2:	PUSHJ P,MOBIL
	PUSHJ P,EGPNH
	LDB A,[ORIGIN]
	MOVEI B,3
	CAME B,@BOARD(A)
	JRST EGGN9
	LDB B,[DESTIN]	; ROOK MOVE
	SUB B,A
	MOVMS B
	CAIL B,12
	JRST EGGN9	; ROOK MOVING ALONG FILE
	LDB A,[DESTIN]
	IDIVI A,12
IRP Z1,,[L,R]Z2,,[L,G]Z3,,[G,L]Z4,,[3,9]Z6,,[1,2]Z7,,[2,1]
	H!Z1!RZ C,PNFLS!Z6(B)
	H!Z1!RZ D,PNFLS!Z7(B)
	CAI!Z2 A,(C)			; SKIP IF NO PAWN OR IN FRONT OF PAWN
	CAI!Z3 D,(C)			; SKIP IF UNBLOCKED IN FILE
	JRST EGGN!Z4
	H!Z1!RZ A,PNFLS!Z7-1(B)
	H!Z1!RZ D,PNFLS!Z7+1(B)
	CAI!Z2 A,(C)			; SKIP IF OPPOSED 1 FILE TO Q-SIDE
	CAI!Z3!E D,(C)			; SKIP IF NOT OFFOSED 1 FILE TO K-SIDE
	JRST EGGN!Z4
	CAI!Z3 C,4+Z7			; SKIP IF FOREMOST PAWN NOT BEYOND 3RD RANK
	ADDI S,15.			; ROOK BEHIND (OR TAKES) FOREMOST PASSED PAWN
EGGN!Z4:
TERMIN
	TRNE 740000
	ADDI S,15.	; CAPTURE
	ADDI S,50.	; CONSTANT
	MOVEM S,2(Q)
	ADDI Q,2
	JRST PLS4A

CONSTA?VARIAB?
PAT:
PATCH:	BLOCK 40. ? EQV

IFN HASHTB,[HASHT:	BLOCK HASHSZ
	HASHV:	BLOCK HASHSZ
]
TOP:

END GO
