 .INSRT AGB;TESTER >

TYIC==TTICHN
UTYIC==17
UTYOC==16
UTICOM==15

TYO:	.IOT TTOCHN,A ? POPJ P,

CHEVAL:	XCT (I)[MOVE ALPHA ? MOVE (T)]
	CAILE 20000 ? MOVEI 20000
	CAMGE [-20000] ? MOVNI 20000
	MOVEM CHALPH
	XCT (I)[MOVN (T) ? MOVN ALPHA]
	CAILE 20000 ? MOVEI 20000
	CAMGE [-20000] ? MOVNI 20000
	MOVEM CHBETA
	MOVEM T,TSAV
	PUSHJ P,STARTC			; START CHEOPS IF STOPPED
	PUSHJ P,ACLPOS			; LOAD POSITION INTO CHEOPS AND RUN
	XCT (I)[MOVEM R,ALPHA ? MOVNM R,ALPHA]	; FINISHED, GET VAL INTO R
	POPJ P,

ZR==0
J=5
W=6
TT=15
T1=16
T2=17

IRP A,,[HXCT,HLOAD,HSTORE,HLOADIR,HSOUT]
A=<4+.IRPCNT>_33 ? TERMIN

STARTC:	MOVEI A,2
	TSNE A,UNIFA ? POPJ P,			; RETURN IF CHEOPS ALREADY RUNNING
	PUSHJ P,HCCOM				; SET PDP-11 FLAGS TO 2
	MOVE HUSADR ? MOVEM HCNUM		; START AT ADDR IN FILE
	PUSHJ P,HG				; SET STARTING ADDRESS
	PUSHJ P,HLEAVE				; RESTORE STATE
	PUSHJ P,XCLK				; CLOCK ONCE FOR LUCK
	JRST XSRUN				; START AND LEAVE RUNNING

ACLPOS:	HRRZM I,CHXSTM
	PUSHJ P,CHXBD		;TRANSFORM POSITION
	MOVE FLAGS ? SETZ B,
	IRP A,,[BOO,BOOO,WOO,WOOO]
	TLNN A ? IORI B,1_.IRPCNT ? TERMIN
	LDB A,[220700,,]
	JUMPE A,.+4
	MOVE CHEOSQ(A)
	IORI 10
	DPB [100400,,B]
	MOVEM HUUOAC
	MOVE [1,,HUUOAC+1]
	BLT HUUOAC+17
	MOVEI A,(I)
LPOS2:	JSP V,INCHK
	JRST [MOVE B,BSAV ? ANDCMI B,100 ? JRST .+2]
	JRST [MOVE B,BSAV ? IORI B,100 ? JRST .+1]
	MOVEM B,CHXFW					; FLAGWORD
	MOVSI 17,HUUOAC ? BLT 17,17
	SETZM HBMEM+1000
	MOVE [-4,,22000_4]		;CHEOPS WORD ADR, = UNIBUS ADR 44000
	MOVEM HBMEM+5		; POINTER TO BUFFER BLOCK
	MOVE A,[442000,,HBMEM+1001]
	MOVE (I)[0 ? 100000]
	IDPB A
	MOVE DEPTH ? SOJGE .+2 ? SETZ ? IDPB A
	IRP B,,[CHALPH,CHBETA,CHXFW]
	MOVE B ? IDPB A ? TERMIN
	MOVE A,[242000,,HBMEM+1017]
	MOVEI B,CHXTB
CHXBD3:	SKIPGE C,(B) ? JRST CHXBD4	; LOAD BOARD INTO BUFFER MEMORY
	LSH C,8 ? IOR C,1(B) ? IDPB C,A
	ADDI B,2
	JRST CHXBD3
CHXBD4:	MOVEI ? IDPB A			; WRITE ZERO AT END OF PIECE LIST
	MOVSI -4 ? MOVEM HBMEM+1000		; INFORM CHEOPS TO BEGIN PROCESSING
CHXBD5:	MOVSI 2 ? TDNN UNIFA ? JRST CHERRH
	SKIPLE HBMEM+1000 ? JRST CHXBD6		; CHEOPS FINISHED
	MOVEI 1 ? .SLEEP ? JRST CHXBD5		; WAIT FOR CHEOPS TO FINISH
CHXBD6:	LDB R,[242000,,HBMEM+1010]
	TRNE R,100000 ? ORCMI R,77777
	POPJ P,

CHERRH:	SOUT [ASCIZ / Cheops halted! /]
	PUSHJ P,DBRD
	JRST PN

CHXBD:	MOVEI C,CHXTB-1		;TRANSLATE BOARD INTO A FORM SUITABLE FOR CHEOPS
	MOVSI B,-40
CHXBD1:	SKIPG A,PIECEL(B) ? JRST CHXBD2
	MOVE D,CHEOPC(B)
	PUSH C,D ? PUSH C,CHEOSQ-BOARD(A)
	HRRZS -1(C)
	JUMPGE D,CHXBD2				; JUMP IF NOT A PAWN
	SKIPG D,PIECE(B) ? JRST CHXBD2
	SETCMI D,-5(D) ? LSH D,6 ? IORM D,(C)
	MOVEI D,40 ? IORM D,-1(C)			; PROMOTED PAWN
CHXBD2:	AOBJN B,CHXBD1
	PUSH C,[-1]
	POPJ P,

CHALPH:	0
CHBETA:	0
CHXFW:	0			;FLAGCELL WORD
CHXSTM:	0			;SIDE TO MOVE (CCHEOP ROUTINES TAKE THIS FROM
CHXTB:	-1			; LOCN BEFORE PIECE LIST
	BLOCK 64.*2

CHEOPC:
	40 ? 41 ? 43 ? 42 ? 45 ? 44 ? 47 ? 46
	SETZ 26 ? SETZ 24 ? SETZ 22 ? SETZ 20 ? SETZ 21 ? SETZ 23 ? SETZ 25 ? SETZ 27 ?
	50 ? 51 ? 53 ? 52 ? 55 ? 54 ? 57 ? 56
	SETZ 36 ? SETZ 34 ? SETZ 32 ? SETZ 30 ? SETZ 31 ? SETZ 33 ? SETZ 35 ? SETZ 37 ?

CHEOSQ==.-24
REPEAT 8,[ ZZQCH==70-.RPCNT_3
	0
   REPEAT 8, ZZQCH+.RPCNT
	0
]
