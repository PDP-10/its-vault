
TITLE EPI

A=1 ? B=2 ? C=3 ? X=4 ? Y=5 ? P=17

TV=756000

C1==<1007777-TV>/22
C2==<C1*22+TV-1007757>*40
URLX==--700.
URLY==-100.

POINT:	SETCMI A,400000+C1-URLY(Y)
	MULI A,1100
	ADDI B,400000+C2-URLX(X)
	CIRC A,5
	PUSH B,BITTAB+40(A)
CPOPJ:	POPJ P,

BITTAB:	REPEAT 40,[.==.RPCNT
		   LOC <.&1>_4+<.&2>_2+.&4+<.&10>_-2+<.&20>_-4+BITTAB
		   SETZ_-.RPCNT ? ]

TVOPEN:	MOVE A,[-9,,TV/2000]
	SETZ B,
	.CALL [	SETZ
		'CORBLK
		1000,,600000
		1000,,-1
		A
		1000,,-2
		SETZ B ]
	.VALUE
	POPJ P,

TVSET:	.SUSET [.RTVCR,,]
	TLZ 36000
	TLO (A)
	.SUSET [.STVCR,,]
	MOVEM TV+20000
	POPJ P,

GO:	MOVEI P,PDL-1
	SETOM SWITCH
	PUSHJ P,TVOPEN
	MOVEI A,34000
	PUSHJ P,TVSET
	MOVSI (-375.0) ? MOVEM CY
	MOVSI (-648.0) ? MOVEM CX
	MOVSI (-1.0) ? MOVEM THETA1
	MOVE [440700,,BUFFER] ? MOVEM BP
	JRST INIT

CX:	0
CY:	0
ITHETA:	0.025
THETA1:	0
TEMPX:	0
TEMPY:	0
PDL:	BLOCK 77

COS:	FADR A,[1.57079633]
SIN:	MOVM B,A
	CAMG B,[0.00017]
	POPJ P,
	FDVRI A,(3.0)
	PUSHJ P,SIN
	FMPR B,B
	FMPRI B,(-4.0)
	FADRI B,(3.0)
	FMPRB A,B
	POPJ P,

LUP:	MOVE X,CX ? MOVE Y,CY
	MOVEI C,
	CAMGE X,[650.0] ? JRST .+3
	 MOVSI X,(650.0) ? MOVEI C,1
	CAMGE Y,[375.0] ? JRST .+3
	 MOVSI Y,(375.0) ? MOVEI C,2
	CAMLE X,[-650.0] ? JRST .+3
	 MOVSI X,(-650.0) ? MOVEI C,3
	CAMGE Y,[-375.0] ? JRST WRAPUP
	MOVEM Y,CY ? MOVEM X,CX
	MOVE XINC(C) ? FADRM CX
	MOVE YINC(C) ? FADRM CY
	MOVE A,ITHETA ? CAML A,[3.14159265] ? SUB A,@.-1 ? MOVEM A,ITHETA
	 FADRB A,THETA1
	PUSHJ P,COS
	FMPRI A,(20.0)
	FADR A,CX
	MOVE X,A ? UFA X,[233400,,] ? MOVE X,X+1
	MOVE A,THETA1 ? PUSHJ P,SIN
	FMPRI A,(20.0)
	FADR A,CY
	MOVE Y,A ? UFA Y,[233400,,] ? MOVE Y,Y+1
;	PUSHJ P,POINT
	PUSHJ P,PLOT
	JRST LUP

PLOT:	EXCH X,OLDX ? EXCH Y,OLDY
	SUB X,OLDX ? SUB Y,OLDY
	AOSN SWITCH ? JRST PLOT2
	MOVM A,X ? MOVM B,Y
	CAIG A,1 ? CAILE B,1 ? .VALUE
PLOT2:	MOVE @PLTAB1+1(X) ? JUMPE CPOPJ
	IDPB BP
	POPJ P,

PLTAB1:	PLTAB2+1(Y) ? PLTAB2+4(Y) ? PLTAB2+7(Y)
PLTAB2:	12 ? 10 ? 11
	2 ? 0 ? 1
	6 ? 4 ? 5

INIT:	MOVEI 20 ? IDPB BP
	MOVSI A,-650.
	MOVE DIR+3		; -X+Y DIRECTION
	IDPB BP ? AOBJN A,.-1
	MOVEI 40 ? IDPB BP
	JRST LUP

INIT2:	MOVEI 40 ? IDPB BP
	MOVSI B,-8
INIT3:	MOVE DIR(B) ? PUSHJ P,ILINE
	MOVE DIR+4(B) ? PUSHJ P,ILINE
	AOBJN B,INIT3
	JRST WRAPUP

ILINE:	MOVSI A,-200.
	IDPB BP
	AOBJN A,.-1
	POPJ P,

DIR:	4 ? 5 ? 1 ? 11 ? 10 ? 12 ? 2 ? 6
	4 ? 5 ? 1 ? 11

WRAPUP:	MOVEI 20 ? IDPB BP
	.OPEN [SIXBIT /  'DSKEPI   PLT/]
	.VALUE
	MOVE A,BP
	MOVSI A,-BUFFER(A) ? EQVI A,-BUFFER-1
	.IOT A
	.CLOSE
	.VALUE

BP:	0
SWITCH:	-1
OLDX:	0
OLDY:	0
XINC:	0.3 ? 0 ? -0.3 ? 0
YINC:	0 ? 0.3 ? 0 ? -0.3
BUFFER:	BLOCK 10000
	-1


END GO
