
TITLE INDEXER

A=1
B=2
C=3
D=4
P=5
LV=6
S=7
T=10
TT=11
J=12
J1=13

INC==1
INC1==2
OUTC==3
LPDL==400

DEFINE Q A
ATC!A!TERMIN

DEFINE CONC A,B
A!B!TERMIN

DEFINE SCH TAG
IRPC X,,/"*,Y,,[\'_]
	CAIE A,Q \"X
	CAIN A,Q \"Y
	JRST TAG
TERMIN
	CAIN A,Q \"^
	JRST TAG
TERMIN

REPEAT 26.,[CONC ATC,\<.RPCNT+101>,==2*.RPCNT+2
CONC CTA,\<2*.RPCNT+2>,==.RPCNT+101
CONC ATC,\<.RPCNT+141>,==2*.RPCNT+3
CONC CTA,\<2*.RPCNT+3>,==.RPCNT+141
]ATC40==0
CTA0==40
ATC54==1
CTA1==54
IRP A,,[0,41,55,60,72,133,173]B,,[100,140,153,66,156,165,173]C,,[40,13,3,12,7,6,5]
REPEAT C,[CONC ATC,\<A+.RPCNT>,==B+.RPCNT
CONC CTA,\<B+.RPCNT>,==A+.RPCNT
]TERMIN

SETUPA:	SKIPA C,COUTF1
SETUP:	MOVEI C,OUTF
	MOVE P,[-LPDL,,PDL-1]
	CLEARM HAKF'
	MOVEI B,INF
	PUSHJ P,FST
	MOVE A,[10700,,BUF-1]
	MOVEM A,BUFP
	MOVE A,[PPTS,,ATP]
	BLT A,OTP
	CLEARM CUPL

STL:	PUSHJ P,RST
	SCH STCL
	CAIN A,Q \"[
	AOS CUPL
	CAIN A,Q \"]
	SOSL CUPL
	JRST STL
	PUSHJ P,RCH
	JRST SETUP1
	CAIG A,15
	JRST .-3
	.VALUE

FST:	.CORE N1KB
	.VALUE
	.OPEN INC,(B)
	.VALUE
	CLEARM IBFP
	CLEARM CWDP
	SKIPN HAKF
	JRST FST1
	.OPEN INC1,INF1
	.VALUE
	CLEARM IBFP1
	CLEARM CWDP1
FST1:	.OPEN OUTC,(C)
	.VALUE
	MOVEI A,OBUF
	MOVEM A,OBFP
COUTF1:	POPJ P,OUTF1

RST:	PUSHJ P,RCHQ
	.VALUE
	IDPB A,BUFP
	POPJ P,

WCH2:	MOVE A,OWD
	PUSHJ P,WWD
	SOSA OWDP
WCHQ:	HLRZ A,CTAB(A)
WCH:	IDPB A,OWDP
	HRLI A,760000
	TDNN A,OWDP
	JRST WCH2
	TLZ A,-1
	POPJ P,

WWD:	MOVEM A,@OBFP
	AOS A,OBFP
	CAIE A,OBUF+200
	POPJ P,
	MOVE A,[-200,,OBUF]
	.IOT OUTC,A
	MOVEI A,OBUF
	MOVEM A,OBFP
	POPJ P,

RCH:	MOVE A,CWDP
	TRNE A,760000
	JRST RCH2
	PUSHJ P,RWD
	MOVEM A,CWD
	MOVE A,[440400,,CWD]
	MOVEM A,CWDP
RCH2:	ILDB A,CWDP
	CAIE A,3
	AOS (P)
	POPJ P,

RCH1:	MOVE A,CWDP1
	TRNE A,760000
	JRST RCH3
	PUSHJ P,RWD1
	MOVEM A,CWD1
	MOVE A,[440700,,CWD1]
	MOVEM A,CWDP1
RCH3:	ILDB A,CWDP1
	CAIE A,3
POPJ1:	AOS (P)
	POPJ P,

RCHQ:	PUSHJ P,RCH
	.VALUE
RQ1:	HRRZ A,CTAB(A)
	POPJ P,
RCH1Q:	PUSHJ P,RCH1
	.VALUE
	JRST RQ1

RWD:	AOSG A,IBFP
	SKIPA A,IBUF+177(A)
	SKIPA A,[-200,,IBUF]
	JRST POPJ1
	.IOT INC,A
	JUMPL A,CPOPJ
	CLEARM CWDP
	MOVNI A,200
	MOVEM A,IBFP
	JRST RWD

RWD1:	AOSG A,IBFP1
	SKIPA A,IBUF1+177(A)
	SKIPA A,[-200,,IBUF1]
	JRST POPJ1
	.IOT INC,A
	JUMPL A,CPOPJ
	CLEARM CWDP
	MOVNI A,200
	MOVEM A,IBFP1
	JRST RWD1

STCL:	PUSHJ P,GETCA
	MOVEI C,0
STCL0:	AOS D,ATP
	CAIL D,STB1
	.VALUE
	MOVEM T,(D)
STCL1:	PUSHJ P,RST
	CAIN A,ATC15
	JRST STCL4
	CAIN A,Q \".
	JRST STCL2
	CAIE A,Q \"#
	CAIN A,ATC33
	JRST STCL2
	CAIN A,Q \"\
	JRST STCL3
	CAIE A,Q \"=
	AOJA C,STCL1
	LDB A,[370500+D,,]
	MOVE T,(D)
	JUMPN A,STCL0
	DPB C,[370500+D,,]
	JRST STCL1

STCL4:	MOVEI A,ATC40
	DPB A,BUFP
	PUSHJ P,RCH
	.VALUE
	JRST STCL1

STCL2:	LDB B,[320500+D,,]
	JUMPN B,STCL5
STCL3:	DPB C,[320500+D,,]
STCL5:	CAIN A,ATC33
	JRST STL
	AOJA C,STCL1

GETCA:	LDB TT,[360600,,BUFP]
	IDIVI TT,7
	HRRZ T,BUFP
	IMULI T,5
	SUBI T,BUF*5-4(TT)
	POPJ P,

GETBP:	IDIVI T,5
	ADDI T,BUF
	IMULI T,70000
	MOVNI TT,-440700(TT)
	HRLM TT,T
	POPJ P,

SGP:	MOVEI C,1(LV)
	IDIVI C,44
	ADD T,C
	ROTC T,6
	SUBB TT,D
	JUMPG TT,.+3
	MOVEI TT,43(D)
	ADDI T,100
	ROTC T,-6
	LDB C,T
	POPJ P,

SORT:	MOVEI D,1-ATAB(A)
	SUBM A,B
	HRL D,B
	JUMPGE D,CPOPJ
S1:	LDB T,[1700+D,,ATAB]
	LDB C,[370500+D,,ATAB]
	ADDI T,1(C)
	PUSHJ P,GETBP
	SUB T,[600,,]
	MOVEM T,STB1(D)
	LDB T,[320500+D,,ATAB]
	SUB T,C
	IMULI T,7
	MOVEM T,STB2(D)
	AOBJN D,S1
	SUBI A,ATAB-2
	SUBM A,B
	CLEARB LV,TT
S2:	CLEARM HAKF
	HRLM B,(P)
	CAIL A,-1(B)
	JRST S7
	PUSH P,A
S3:	CAMLE LV,STB2-1(A)
	JRST S4
	SETOM HAKF
	MOVE T,STB1-1(A)
	PUSHJ P,SGP
	JUMPE C,S4
	SUBI B,1
	CAMLE LV,STB2-1(B)
	JRST STH
	MOVE T,STB1-1(B)
	PUSHJ P,SGP
	JUMPN C,S5
STH:	IRPS UGH,,ATAB STB1 STB2
	EXCH C,UGH-1(A)
	EXCH C,UGH-1(B)
	EXCH C,UGH-1(A)
TERMIN
S4:	ADDI A,1
S5:	CAMGE A,B
	JRST S3
	POP P,A
	SKIPN HAKF
	JRST S6
	AOS LV
	PUSHJ P,S2
	HLRZ B,(P)
	PUSHJ P,S2
S6:	SOS LV
S7:	HLRZ A,(P)
	POPJ P,

CRR:	MOVEI A,15
	PUSHJ P,WCH
	MOVEI A,12
	JRST WCH

SETUP1:	CLEARM CUPL
	MOVEI A,ATAB-1
	MOVE B,ATP
	PUSHJ P,SORT
	MOVEI A,ATAB-1
	SUB A,ATP
	HRLI A,ATAB
	MOVSM A,ATP
	PUSHJ P,ST1
	MOVE B,ATP
	HLRES A,ATP	;!!!
	PUSHJ P,WWD
ST6:	MOVE A,(B)
	PUSHJ P,WWD
	AOBJN B,ST6

EXIT:	SKIPA A,CCC
EXIT2:	MOVEI A,3
	PUSHJ P,WCH
	MOVE TT,OBFP
CCC:	TLNE TT,760014	;HA HA
	JRST EXIT2
	MOVEI TT,OBUF-1
	SUB TT,OBFP
	HRLS TT
	HRRI TT,OBUF
	MOVE 0,[REPEAT 5,[3_<7*.RPCNT>+]]
	.IOT OUTC,TT
	.CLOSE OUTC,
FOO:	.VALUE

ST1:	MOVE J,ATP
ST2:	LDB B,[171300+J,,]
	CAME B,CUPL
ST4:	AOBJN J,ST2
	JUMPGE J,CPOPJ
	LDB T,[1700+J,,]
	PUSHJ P,GETBP
	MOVEI S,0
	LDB D,[370500+J,,]
ST3:	ILDB A,T
	CAIN A,Q \"=
	CAMN S,D
	MOVE C,A
	PUSHJ P,WCH
	CAIE C,ATC33
	AOJA S,ST3
ST5:	ILDB A,T
	SCH ST4
	CAIN A,Q \"]
	JRST ST4
	CAIE A,Q \"[
	JRST ST5
	EXCH B,CUPL
	HRLM B,(P)
	PUSH P,J
	PUSHJ P,ST1
	POP P,J
	HLRZ B,(P)
	EXCH B,CUPL
	JRST ST4

STPT:	ILDB A,B
	JUMPE A,CPOPJ
	PUSHJ P,WCHA
	JRST STPT

PE:	CLEARM HAKF
	MOVEI B,0
	CAIE A,""
	CAIN A,"'
	JRST PE2
	CAIN A,"_
	MOVE B,[440700,,[ASCIZ \  from the \]]
	CAIN A,"/
	MOVE B,[440700,,[ASCIZ \ by \]]
PE2A:	PUSHJ P,STPT
PE1:	PUSHJ P,RCH
	.VALUE
	CAIE A,"=
	CAIN A,"\
	JRST PE1
	CAIE A,33
	CAIN A,"#
	JRST PE3
	PUSHJ P,WCHA
	JRST PE1

PE2:	SETOM HAKF
	MOVE B,[440700,,[ASCIZ \  from "\]]
	JRST PE2A

PE3:	MOVE B,A
	MOVEI A,""
	AOSN HAKF
	PUSHJ P,WCHA
	CAIN B,33
	POPJ P,
	MOVEI A,40
	PUSHJ P,WCHA
	MOVEI A,"(
	PUSHJ P,WCHA
PE3A:	PUSHJ P,RCH
	.VALUE
	CAIN A,33
	JRST PE3B
	PUSHJ P,WCHA
	JRST PE3A
PE3B:	MOVEI A,")
	JRST WCHA

PRINT:	CLEARM HAKF
	MOVE P,[-LPDL,,PDL-1]
	MOVEI B,INF2
	MOVEI C,OUTF2
	PUSHJ P,FST
	MOVEI TT,200.
	MOVEM TT,LINECT
	MOVE B,[440700,,[ASCIZ +COMBINED INDEX
Code: /Author, "Story title, 'Volume title, _Group title, \Other.
+]]
	PUSHJ P,STPT
	MOVEI S,4
PR1:	MOVEI T,0
PR1A:	PUSHJ P,RCH
	.VALUE
	CAIN A,"[
	AOS T
	CAIN A,"]
	SOSL T
	JRST PR1A
	SOJG S,PR1
PR1D:	SETOM HAKF2'
	PUSHJ P,CRR
	MOVEI TT,70.
	MOVEM TT,LINECT'

PR2:	PUSHJ P,RCH
	.VALUE
IRPC X,,/"*,Y,,[\'_]
	CAIE A,"X
	CAIN A,"Y
	JRST PR2A
TERMIN
	CAIN A,33
	JRST PR1D
	CAIN A,"]
	JRST EXIT1
	JRST PR2

PR2A:	AOSN HAKF2
	JRST PR2C
	PUSHJ P,PE
	JRST PR2
PR2C:	MOVE B,A
	PUSHJ P,WCHA
	MOVEI A,40
	PUSHJ P,WCHA
	PUSHJ P,PE1A
	JRST PR2

EXIT1:	PUSHJ P,CRR
	MOVEI A,14
	PUSHJ P,WCH
	PUSHJ P,RCH
	JRST EXIT
	CAIG A,15
	JRST .-3
	.VALUE

PE1A:	CAIE B,""
	CAIN B,"'
	SKIPA
	JRST PE1
	MOVEI A,""
	PUSHJ P,WCHA
	SETOM HAKF
	JRST PE1

WCHA:	SOSGE LINECT
	CAIE A,40
	JRST WCH
	PUSHJ P,CRR
	MOVEI A,11
	PUSHJ P,WCH
	MOVEI TT,62.
	MOVEM TT,LINECT
	POPJ P,

MERGE:	MOVE P,[-LPDL,,PDL-1]
	SETOM HAKF
	MOVEI B,INF2
	MOVEI C,OUTF
	PUSHJ P,FST
	MOVEI S,4
	CLEARM HAKF1'
ME1:	PUSHJ P,RE0
	XORI LV,RCH1Q#RCHQ
ME2:	PUSHJ P,BLE3
	SOJG S,ME1
	SETOM HAKF1
	PUSHJ P,RE0
	XORI LV,RCH1Q#RCHQ
ME3:	PUSHJ P,BLE3
	JRST EXIT

RE0:	MOVEI LV,RCHQ
	SETOM HAKF
RE:	PUSHJ P,(LV)
IRPC X,,/"*,Y,,[\'_]
	CAIE A,Q \"X
	CAIN A,Q \"Y
	JRST REA
TERMIN
	CAIE A,Q \"]
	JRST RE
	POPJ P,

REA:	CAIN LV,RCHQ
	MOVEM A,LCH'
	CAIN LV,RCH1Q
	MOVEM A,LCH1'
	MOVE T,[440700,,BUF]
	CAIN LV,RCH1Q
	ADDI T,4000
	CLEARB C,D
REB0:	MOVE B,D
REB:	PUSHJ P,(LV)
	IDPB A,T
	CAIN A,Q \".
	JRST REB2
	CAIE A,Q \"#
	CAIN A,ATC33
	JRST REB2
	CAIN A,Q \"\
	JRST REB3
	CAIN A,Q \"=
	AOJA D,REB0
	AOJA D,REB

REB2:	JUMPN C,REB1
REB3:	MOVE C,D
REB1:	CAIE A,ATC33
	AOJA D,REB
	SUB C,B
	MOVE T,[B,,FSCH]
	CAIN LV,RCH1Q
	ADDI T,2
	MOVEI TT,1(T)
	BLT T,(TT)
	MOVEI LV,RCH1Q
	AOSN HAKF
	JRST RE

COMP:	CLEARB D,TT
CM1:	CAMLE D,FSCH+1
	JRST 0FST
	CAMLE D,FSCH+3
	JRST 1FST
	MOVE T,[10700,,BUF-1]
	MOVE J,FSCH
	PUSHJ P,MGP
	MOVE B,A
	MOVE T,[10700,,BUF+3777]
	MOVE J,FSCH+2
	PUSHJ P,MGP
	CAMGE A,B
	JRST 0FST1
	CAMGE B,A
	JRST 1FST
	AOJA D,CM1

MGP:	ADDI J,1(D)
	IDIVI J,5
	IMULI J1,7
	ADD T,J
	ROTC T,6
	SUBB TT,J1
	JUMPG TT,.+3
	MOVEI TT,43(J1)
	ADDI T,100
	ROTC T,-6
	LDB A,T
	POPJ P,

0FST:	CAMLE D,FSCH+3
	JRST 0F1
0FST1:	MOVE T,[440700,,BUF]
	MOVEI LV,RCHQ
	PUSHJ P,BLE
	JRST RE

0F1:	MOVE A,LCH
	CAMGE A,LCH1
	JRST 0FST1
	CAMGE A,LCH1
	JRST 1FST
SAME:	SETOM HAKF2
	MOVE T,[440700,,BUF]
	PUSHJ P,BLE
	PUSHJ P,RCHQ
	CAIE A,Q \"[
	JRST SAM1
	PUSHJ P,RCH1Q
	CAIE A,Q \"[
	JRST SAM2
	PUSHJ P,WCHQ
	PUSHJ P,RE
SAM3:	SETOM HAKF
	JRST RE

1FST:	MOVEI LV,RCH1Q
	MOVE T,[440700,,BUF+4000]
	PUSHJ P,BLE
	JRST RE

BLE:	CLEARM CUPL
	MOVE A,LCH
	CAIN LV,RCH1Q
	MOVE A,LCH1
	PUSHJ P,WCHQ
BLE1:	ILDB A,T
BLE1A:	PUSHJ P,WCHQ
	CAIE A,33
	JRST BLE1
	SKIPE HAKF1
	JRST BLE4
BLE2:	AOSN HAKF2
	POPJ P,
	PUSHJ P,(LV)
	CAIE A,Q \"[
	POPJ P,
BLE3A:	PUSHJ P,WCHQ
BLE3:	PUSHJ P,(LV)
	CAIN A,Q \"[
	AOS CUPL
	CAIN A,Q \"]
	SOSL CUPL
	JRST BLE3A
	POPJ P,
BLE4:	ILDB A,T
	CAIN A,ATC33
	POPJ P,
	JRST BLE1A

SAM1:	PUSHJ P,RCH1Q
	CAIE A,Q \"[
	JRST SAM3
	SKIPA LV,SAM1
SAM2:	MOVEI LV,RCHQ
SAM2A:	PUSHJ P,WCHQ
	PUSHJ P,(LV)
	CAIE A,Q \"]
	JRST SAM2A
	JRST RE

CONSTANTS
VARIABLES

CTAB:	REPEAT 200,[CONC CTA,\.RPCNT,,,CONC ATC,\.RPCNT
]PDL:	BLOCK LPDL
CUPL:	0
FSCH:	BLOCK 4
INF:	SIXBIT \  "DD0SCIFI INPUT\
INF1:	SIXBIT \  "DD0SCIFI INDEX1\
INF2:	SIXBIT \  "DD0SCIFI INDEX\
OUTF:	SIXBIT \  #DD0SCIFI INDEX\
OUTF1:	SIXBIT \  #DD0SCIFI INDEX1\
OUTF2:	SIXBIT \  #LPT............\
ATP:	0
TTP:	0
PTPT:	0
OTP:	0
PPTS:	ATAB-1
	TTAB-1
	PTAB-1
	OTAB-1
STB1:	BLOCK 1400
STB2:	BLOCK 1400
ATAB:	BLOCK 100
TTAB:	BLOCK 300
PTAB:	BLOCK 400
OTAB:	BLOCK 400
IBFP:	0
IBFP1:	0
OBFP:	0
IBUF:	BLOCK 200
	3_29.
IBUF1:	BLOCK 200
	3_29.
OBUF:	BLOCK 200
OBFC:	0
BUFP:	0
BUF:	BLOCK 10000

N1KB==<.+1777>/2000

END
,,]
	PUSHJ P,GETBP
	MOVEI S,0
	LDB D,[370500+J,,]
ST3:	ILDB A,T
	CAIN A,Q \"=
	CAMN S,D
	MOVE C,A
	PUSHJ P,WCH
	CAIE C,ATC33
	AOJA S,ST3
ST5:	ILDB A,T
	SCH ST4
	CAIN A,Q \"]
	JRST ST4
	CAIE A,Q \"[
	JRST ST5
	EXCH B,CUPL
	HRLM B,(P)
	PUSH P,J
	PUSHJ P,ST1
	POP P,J
	HLRZ B,(P)
	EXCH B,CUPL
	JRST ST4

STPT:	ILDB A,B
	JUMPE A,CPOPJ
	PUSHJ P,WCHA
	JRST 