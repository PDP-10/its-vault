;COMMENT    VALID 00012 PAGES
	.ASECT
;RECORD PAGE   DESCRIPTION
; 00001 00001	 VALID 00012 PAGES
; 00005 00002	 PDP-11/GT-40 DEFINITIONS	TRAP VECTORS
; 00007 00003		 CLOCKS, TELETYPE
; 00009 00004	 DL11 COMMUNICATIONS INTERFACE
; 00011 00005	 PAPER-TAPE READER/PUNCH
; 00012 00006	 GT40 DISPLAY HARDWARE DEFINITIONS
; 00014 00007		 DISPLAY INSTRUCTION SET OPCODES
; 00016 00008		 INSTRUCTION SET OPCODES AND MACROS
; 00018 00009		 VECTOR DEFINITION MACROS
; 00020 00010	 MORE VECTOR MACROS AND SCREEN SIZE DEFINITIONS
; 00022 00011	 MISCELLANEOUS HANDY PDP-11 MACROS
; 00024 00012		 MORE HANDY MACROS
; 00025 ENDMK
;
;
;
;








;PDP-11/GT-40 DEFINITIONS	TRAP VECTORS

R0	 =%0		;STANDARD REGISTER DEFINITIONS
R1	 =%1
R2	 =%2
R3	 =%3
R4	 =%4
R5	 =%5
SP	 =%6
PC	 =%7

;THESE TRAPS ARE NOT CONNECTED TO THE PRIORITY INTERRUPT SYSTEM

ERR.V	=4	 ;ERROR TRAPS
ILI.V	=10	;ILLEGAL AND RESERVED INSTRUCTIONS
BPT.V	=14	;BPT INSTRUCTION
IOT.V	=20	;IOT INSTRUCTION
PWR.V	=24	;POWER-FAIL TRAP
EMT.V	=30	;EMT INSTRUCTION
TRP.V	=34	;TRAP INSTRUCTION

;PROCESSOR REGISTERS

.PSW	=177776   ;PROGRAM STATUS
.CSW	=177570   ;CONSOLE SWITCHES

.SBOT	=400	;BOTTOM OF STACK FOR ALL BUT 11/45



	;CLOCKS, TELETYPE

;KW11-L LINE-FREQUENCY CLOCK

LFK.P	=6		 ;PRIORITY 6
LFK.V	=100		;INTERRUPT VECTOR
LFK.K	=177546	   ;CONTROL REGISTER
	LFK.FG    =200		;CLOCK FLAG
	LFK.EN    =100		;INTERRUPT ENABLE

;CONSOLE KEYBOARD/PRINTER

TTI.P	=4		 ;PRIORITY 4
TTI.V	=60		;KEYBOARD INTERRUPT
TTI.K	=177560	   ;KEYBOARD CONTROL AND STATUS
	TTI.BF    =4000		;BUSY FLAG
	TTI.FG    =200		;DONE FLAG
	TTI.EN    =100		;INTERRUPT ENABLE
	TTI.GO    =1		 ;START READING
TTI.B	=177562	   ;KEYBOARD BUFFER

TTO.P	=4		 ;PRIORITY 4
TTO.V	=64		;PRINTER INTERRUPT
TTO.K	=177564	   ;PRINTER CONTROL AND STATUS
	TTO.FG    =200		;READY FLAG
	TTO.EN    =100		;INTERRUPT ENABLE
	TTO.MM    =4		 ;MAINTENANCE MODE
TTO.B	=177566	   ;PRINTER BUFFER
;DL11 COMMUNICATIONS INTERFACE

RCV.P	=5		 ;PRIORITY 5
RCV.V	=300		;RECEIVER INTERRUPT
RCV.K	=175610	   ;RECEIVER COMMAND AND STATUS
	RCV.IN    =100000	   ;INTERRUPT
	RCV.RG    =40000	    ;RING
	RCV.CS    =20000	    ;CLEAR TO SEND
	RCV.CD    =10000	    ;CARRIER DETECT
	RCV.RA    =4000		;RECEIVER ACTIVE (BUSY)
	RCV.SR    =2000		;SECONDARY REQUEST
	RCV.FG    =200		;DONE FLAG
	RCV.EN    =100		;RECEIVER INTERRUPT ENABLE
	ECV.DN    =40		;DATA SET INTERRUPT ENABLE
	RCV.ST    =10		;SECONDARY TRANSMIT
	RCV.RQ    =4		 ;REQUEST TO SEND
	RCV.TR    =2		 ;DATA TERMINAL READY
	RCV.GO    =1		 ;ENABLE RECEIVER
RCV.B	=175612	   ;RECEIVER BUFFER
	RCV.ER    =100000	   ;ERROR
	RCV.OR    =40000	    ;OVERRUN
	RCV.FE    =20000	    ;FRAMING ERROR
	RCV.PE    =10000	    ;PARITY ERROR

XMT.P	=5		 ;PRIORITY 5
XMT.V	=304		;TRANSMITTER INTERRUPT
XMT.K	=175614	   ;TRANSMITTER COMMAND AND STATUS
	XMT.FG    =200		;READY FLAG
	XMT.EN    =100		;INTERRUPT ENABLE
	XMT.MM    =4		 ;MAINTENANCE MODE
	XMT.BK    =1		 ;BREAK
XMT.B	=175616	   ;TRANSMITTER BUFFER


;GT;0 DISPLAY HARDWARE DEFINITIONS

DPY.P	=4		 ;PRIORITY 4
DHLT.V    =320		;STOP INTERRUPT
DLTP.V    =324		;LIGHT PEN INTERRUPT
DTSO.V    =330		;TIME OUT/SHIFT OUT INTERRUPT

D.PC	=172000	   ;DISPLAY PROGRAM COUNTER
D.SR	=172002	   ;STATUS REGISTER
	D.STOP    =100000	   ;STOP FLAG
	;BITS 14:11	MODE FROM LAST SET MODE INSTRUCTION
	;BITS 10:8	 INTENSITY FROM LAST SET MODE
	D.LPF	=200		;LIGHT PEN FLAG
	D.SOF	=100		;SHIFT OUT
	D.EDGE    =40		;EDGE INDICATOR
	D.ITAL    =20		;ITALICS
	D.BLIN    =10		;BLINK
	;BITS 1:0  VECTOR TYPE (SOLID, DASH, ETC)

D.XR	=172004	   ;X REGISTER
	;BITS 15:10	GRAPHPLOT INCREMENT
	;BITS 9:0  X POSITION

D.YR	=172006	   ;Y REGISTER
	;BITS 15:10	CHARACTER REGISTER
	;BITS 9:0  Y POSITION



	;DISPLAY INSTRUCTION SET OPCODES

D.JMP	=160000	   ;JUMP OPCODE
D.NOP	=164000	   ;NO-OP OPCODE

.MACR DJMP LOC		;JUMP INSTRUCTION
	  .WORD D.JMP,LOC
.ENDM

.MACR DNOP		;NO-OP INSTRUCTION
	  .WORD D.NOP
.ENDM

;MODE-SETTING OPCODES

D.CHR	=100000	   ;CHAR MODE
D.SVEC    =104000	   ;SHORT RELATIVE VECTOR MODE
D.LVEC    =110000	   ;LONG RELATIVE VECTOR MODE
D.PNT	=114000	   ;ABSOLUTE POINT MODE
D.GX	=120000	   ;GRAPHPLOT X MODE
D.GY	=124000	   ;GRAPHPLOT Y MODE
D.RPNT    =130000	   ;RELATIVE POINT MODE

;MODE BITS FOR THE ABOVE OPCODES

	.INT0	=2000		;INTENSITY 0
	.INT1	=2200
	.INT2	=2400
	.INT3	=2600
	.INT4	=3000
	.INT5	=3200
	.INT6	=3400
	.INT7	=3600		;INTENSITY 7

	.LPEN	=140		;ENABLE LIGHT PEN INTERRUPT
	.LPDIS    =100		;DISABLE LIGHT PEN INTERRUPT

	.BLINK    =30		;TURN BLINK ON
	.NOBLINK=20		  ;TURN BLINK OFF

	.SOLID    =4		 ;SOLID VECTORS
	.DASH	=5		 ;DASHED VECTORS
	.SDASH    =6		 ;SHORT DASHED VECTORS
	.DDASH    =7		 ;DOT-DASH VECTORS

D.GINC    =174100	   ;SET GRAPHPLOT INCREMENT OPCODE

.MACR DGINC VAL		;SET GP INC INSTRUCTION
	  .WORD D.GINC+<VAL&77>
.ENDM

	;INSTRUCTION SET OPCODES AND MACROS

D.SETS    =170000	   ;LOAD STATUS REGISTER OPCODE

	.STOP	=2000		;STOP THE DISPLAY
	.STPEN    =1400		;ENABLE INTERRUPT ON STOP
	.STPDIS   =1000

	.LPBRT    =300		;BRIGHTEN POINT OF LIGHT PEN HIT
	.LPDIM    =200

	.ITAL	=60		;ITALICIZE CHARACTERS
	.NOITAL   =40

	.SYNCH    =4		 ;HALT AND SYNCHRONIZE

.MACR DSETS BITS    ;SET STATUS
	  .WORD D.SETS+<BITS>
.ENDM

.MACR DCHR BITS		;SET CHAR MODE
	  .WORD D.CHR+<BITS>
.ENDM

.MACR DSVEC BITS    ;SHORT RELATIVE VECTORS
	  .WORD D.SVEC+<BITS>
.ENDM

.MACR DLVEC BITS    ;LONG RELATIVE VECTORS
	  .WORD D.LVEC+<BITS>
.ENDM

.MACR DPNT BITS		;ABSOLUTE POINTS
	  .WORD D.PNT+<BITS>
.ENDM

.MACR DGX BITS		;GRAPHPLOT X
	  .WORD D.GX+<BITS>
.ENDM

.MACR DGY BITS		;GRAPHPLOT Y
	  .WORD D.GY+<BITS>
.ENDM

.MACR DRPNT BITS    ;RELATIVE POINTS
	  .WORD D.RPNT+<BITS>
.ENDM


	;VECTOR DEFINITION MACROS

.MACR D.SDIS SYM,VAL		;SHORT DISPLACEMENT
	  SYM=VAL
	  .IFL SYM
	    SYM=-SYM&77!100
	  .IFF
	    SYM=SYM&77
	  .ENDC
.ENDM

.MACR D.LDIS SYM,VAL		;LONG DISPLACEMENT
	  SYM=VAL
	  .IFL SYM
	    SYM=-SYM&1777!20000
	  .IFF
	    SYM=SYM&1777
	  .ENDC
.ENDM

.MACR D.ABS SYM,VAL	 ;ABSOLUTE LOCATION
	  SYM=<VAL>&1777
.ENDM

.VIS	=1
.INVIS    =0

.MACR D.BRT SYM,VAL,DUM	;VISIBLE/INVISIBLE BIT (DEFAULT VISIBLE)
	.IF B,VAL
	  SYM=40000
	.IFF
	  .IF NZ,VAL
	    SYM=40000
	  .IFF
	    SYM=0
	  .ENDC
	.ENDC
	.ENDM

.MACR SVEC DX,DY,BRT,DUM	;MAKE A SHORT VECTOR WORD
	  D.SDIS D.X,DX
	  D.SDIS D.Y,DY
	  D.BRT D.B,BRT,FOO
	  .WORD D.X*200!D.Y!D.B
.ENDM

.MACR RPNT DX,DY,BRT		;RELATIVE POINT
	  SVEC DX,DY,BRT,FOO
.ENDM



;MORE VECTOR MACROS AND SCREEN SIZE DEFINITIONS

.MACR LVEC DX,DY,BRT		;MAKE A LONG RELATIVE VECTOR
	  D.LDIS D.X,DX
	  D.LDIS D.Y,DY
	  D.BRT D.B,BRT,FOO
	  .WORD D.B!D.X,D.Y
.ENDM

.MACR POINT X,Y,BRT	 ;MAKE AN ABSOLUTE POINT
	  D.ABS D.X,X
	  D.ABS D.Y,Y
	  D.BRT D.B,BRT,FOO
	  .WORD D.B!D.X,D.Y
.ENDM

.MACR PLOT Y			 ;MAKE A GRAPHPLOT WORD
	  D.ABS D.Y,Y
	  .WORD D.Y
.ENDM

.MACR DLOC X,Y			;MOVE TO AN ABSOLUTE LOCATION
	  D.PNT
	  POINT X,Y,.INVIS
.ENDM

;SCREEN SIZE, FOUND OUT AT THE COST OF MUCH PAIN BY BO.

;LOWER LIMIT FOR BOTH GRAPHIC AND CHARACTERS IS X=0,Y=0.

.GXUL	=1777		;X UPPER LIMIT FOR GRAPHICS
.GYUL	=1377		;Y UPPER LIMIT FOR GRAPHICS
.CXUL	=1761		;X UPPER LIMIT FOR CHARACTERS
.CYUL	=1377		;Y UPPER LIMIT FOR CHARACTERS
		;(CHARACTERS CAN PRINT ABOVE THE TOP OF THE SCREEN!)
.CXSPC    =16		;X SPACING FOR CHARACTERS
.CYSPC    =30		;Y SPACING FOR CHARACTERS
.CXOFF    =7		 ;X OFFSET TO CENTER OF AN ASTERISK
.CYOFF    =15		;Y OFFSET TO CENTER OF AN ASTERISK
.CXWID    =11		;WIDTH OF A CHARACTER
.CYHGT    =17		;HEIGHT OF A CHARACTER
.CYDSC    =5		 ;DESCENDERS GO DOWN THIS MUCH.

.MACR .BEEP	  ;Yes, that's exactly what it is.
	  CLR D.SR
.ENDM
;MISCELLANEOUS HANDY PDP-11 MACROS

.MACR PUSH V		 ;PUSH ONTO THE STACK
	  MOV V,-(SP)
.ENDM

.MACR POP V		  ;POP OFF THE STACK
	  MOV (SP)+,V
.ENDM

.MACR TVAR		;PUSH A ZERO
	  CLR -(SP)
.ENDM

.MACR ZAP	 ;GET RID OF ONE TEMP CELL
	  TST (SP)+
.ENDM

.MACR ZAPS N		 ;GET RID OF SEVERAL TEMP CELLS
	    .REPT N
		TST (SP)+
	    .ENDR
.ENDM

.MACR PUSHJ SUBR    ;STANDARD SUBROUTINE CALL
	  JSR PC,SUBR
.ENDM

.MACR POPJ		;STANDARD RETURN
	  RTS PC
.ENDM

.MACR PSWD PRI,MODE,REG,DUM   ;DEFINE A PS WORD
	  P.S=PRI*40
	  .IF NB MODE
	    P.S=P.S!<MODE*40000>
	  .ENDC
	  .IF NB REG
	    P.S=P.S!<REG*20000>
	  .ENDC
	  .WORD P.S
.ENDM

.KERNEL   =0		 ;PROCESSOR MODES
.SUPER    =1
.USER	=3

.MACR INTVEC ROUTINE,PRI,MODE,REG	;DEFINE AN INTERRUPT VECTOR
	  .WORD ROUTINE
	  PSWD PRI,MODE,REG,FOO
.ENDM


	;MORE HANDY MACROS
.MACR PUSHACS		;SAVE THE WHOLE WORLD
	  MOV R0,-(SP)
	  MOV R1,-(SP)
	  MOV R2,-(SP)
	  MOV R3,-(SP)
	  MOV R4,-(SP)
	  MOV R5,-(SP)
.ENDM

.MACR POPACS		 ;RESTORE THE SAVED WORLD
	  MOV (SP)+,R5
	  MOV (SP)+,R4
	  MOV (SP)+,R3
	  MOV (SP)+,R2
	  MOV (SP)+,R1
	  MOV (SP)+,R0
.ENDM

.MACR EVEN
	  .IFNZ .&1
	    .BYTE 0
	  .ENDC
.ENDM

                                                                                