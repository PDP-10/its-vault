TITLE FILMER OF HEART PROGRAM DISPLAY

A=1
B=2
C=3
D=4
E=5
F=6
G=7
R=10
S=11
T=12
U=13
X=14	;FOR DISPLAY PI
Y=15	;DITTO
Z=16
P=17

SPECHN==1
DISCHN==2

LOC 40+2*SPECHN
	JSR DISBRK

LOC 40+2*DISCHN
	BLKO DIS,Y

;PDP-6 LOCATION 70
; +M,,+N = PUSHJ P,FRAME, DISPLAY M CYCLES, PUSHJ P,CLOSE, ALL N TIMES
;     -N = PUSHJ P,CLOSE, THEN REWIND N FRAMES
;      0 = DONE
LOC 70
	0
DISPL:	0	;GETS VALUE OF DISPL IN HEART PROGRAM IN PDP-10
LOC 100
BEG:	CLEARM 41
	MOVEI P,PDL-1
	MOVEI A,6DISPL
	MOVEM A,DISPL	;TELL PDP-10 HOW MUCH ROOM THERE IS
	CAMN A,DISPL
	JRST .-1	;WAIT FOR IT TO ANSWER WITH ITS OFFSET
	CONO DIS,0
	CONO PI,12200+1_<7-SPECHN>+1_<7-DISCHN>
MOV1:	CLEARM 70
MOV2:	DATAI A
	JUMPL A,MOV4	;FREE RUN THE 340
	SKIPN A,70
	JRST MOV2	;KEEP WAITING FOR A COMMAND
	JUMPG A,MOV3	;EXPOSE SOME FRAMES
	PUSHJ P,CLOSE	;REWIND
	MOVEI B,40000
	SOJG B,.	;WAIT FOR MOTOR TO STOP
	PUSHJ P,RFRAME
	AOJL A,.-1
	JRST MOV1

MOV3:	HRRZ A,70
MOV3A:	SKIPN A
	JRST MOV1	;FRAMES COMPLETED
	MOVE B,70
	HLRZ B,NDIS
	PUSHJ P,FRAME
	SKIPE NDIS
	PUSHJ P,DODIS
	PUSHJ P,CLOSE
	SOJA A,MOV3A

MOV4:	AOS NDIS	;DO ONE CYCLE
	PUSHJ P,DODIS
	JRST MOV2

DODIS:	MOVEI X,71-6DISPL	;TO START UP LINKED LIST
	CONO PI,4000+1_<7-SPECHN>	;ACTIVATE
	SKIPE NDIS
	JRST .-1
	POPJ P,
DISBRK:	0
	MOVEI Y,-1
	TDNN Y,6DISPL(X)	;WAS THIS LAST LINKED LIST ENTRY?
	JRST DISBR1	;YES
	HRRZ X,6DISPL(X)	;POINTER TO NEXT ENTRY
	SUB X,DISPL	;CORRECT PDP-10 ADDRESS
DISBR3:	MOVS Y,6DISPL(X)	;PICK UP NEXT LIST ENTRY
	SUB Y,DISPL
	MOVE Y,6DISPL(Y)	;PICK UP ITS BLKO POINTER
	SUB Y,DISPL
	ADDI Y,6DISPL
	CONO DIS,100+SPECHN*8+DISCHN
	JRST 12,@DISBRK

DISBR1:	SOSN NDIS
	JRST DISBR2
	MOVEI X,	;RESTART LINKED LIST
	JRST DISBR3

DISBR2:	CONO DIS,0	;FINISHED
	JRST 12,@DISBRK
;CCW = FWD = 0, 100, 300, 200, 0, ...
;WHEN LOADED, THINKS SHUTTER IS CLOSED
;CLOBBERS NO ACCUMULATORS

CLOSE:	SKIPN SHUTTR
	POPJ P,
	CLEARM SHUTTR
	JRST OPEN1

FRAME:	PUSHJ P,CLOSE
	SKIPE STOP
	JRST .-1
OPEN:	SKIPE SHUTTR
	POPJ P,
	SETOM SHUTTR
OPEN1:	PUSH P,A
	PUSH P,B
	MOVEI A,62	;25. COMPLETE CYCLES = 1/2 REVOLUTION
OPEN2:	MOVEI B,100
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,550	;PDP-10 2000 MISSES ABOUT 1 STEP/REVOLUTION
	SOJG B,.
	MOVEI B,200
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,550
	SOJG B,.
	SOJG A,OPEN2
	AOS NHREVS
	POP P,B
	POP P,A
	POPJ P,

SHUTTR:	0	;0 = SHUTTER CLOSED, -1 = OPEN
MOTOR:	0	;LAST DATAO TO MOTOR
NHREVS:	0	;NUMBER OF HALF REVOLUTIONS SINCE START
STOP:	0	;NON-ZERO = PAUSE WITH SHUTTER CLOSED

RFRAME:	PUSH P,A	;1 FRAME REVERSE MOTION
	PUSH P,B
	MOVEI A,144
RFR2:	MOVEI B,200
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,550
	SOJG B,.
	MOVEI B,100
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,550
	SOJG B,.
	SOJG A,RFR2
	SOS NHREVS
	SOS NHREVS
	POP P,B
	POP P,A
	POPJ P,

PDL:	BLOCK 10
NDIS:	0	;NUMBER OF CYCLES TO DISPLAY

CONSTA
VARIAB

6DISPL==400

IFG .-6DISPL,PRINTC \CODE DOES NOT FIT\

END BEG
