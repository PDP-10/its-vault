TITLE 340 TITLER, PDP-10 PART

A=1
B=2
C=3
D=4
E=5
F=6
G=7
R=10
S=11
T=12
U=13
X=14
Y=15
Z=16
P=17

DSKCHN==1
USRI==3
BEG:	MOVEI P,PDL-1
	.OPEN USRI,USRIX	;SET UP PDP-6
NOPDP6:	.VALUE
	MOVE A,[-20,,<6LOC0/2000>]
	MOVEI B,
	.CALL CORBLK
NOCORE:	.VALUE
	.OPEN DSKCHN,6FILE
NOFILE:	.VALUE
	.CALL LOAD
NOLOAD:	.VALUE
	.CLOSE DSKCHN,
	MOVE A,[JRST 100]
	MOVEM A,6LOC0+41
	.OPEN DSKCHN,INFILE
	.VALUE

LOOP1:	MOVEI A,10
	.SLEEP A,
	SKIPN 6LOC0+70
	JRST LOOP1
	MOVE A,[440700,,6IBUF]
LOOP2:	.IOT DSKCHN,B
	SKIPGE B
EOF:	.VALUE
	IDPB B,A
	CAIE B,14	;CTRL L
	JRST LOOP2
	CLEARM 6LOC0+70
	JRST LOOP1
; 1 BIT MEANS IMAGE, BLOCK, OUTPUT
USRIX:	4,,(SIXBIT \USR\)
	0
	SIXBIT \PDP6\

CORBLK:	SETZ
	SIXBIT \CORBLK\
	1000,,300000	;IMMEDIATE,,GET BOTH READ AND WRITE ACCESS
	1000,,-1	;IMMED,,PUT PAGES IN SELF
	A		;AOBJN POINTER FOR DESTINATION OF PAGES
	1000,,USRI	;IMMED,,CHANNEL
	SETZ B		;LAST ARG,,POINTER FOR SOURCE OF PAGES

6FILE:	6,,(SIXBIT \DSK\)
	SIXBIT \340TIT\
	SIXBIT \6BIN\

LOAD:	SETZ
	SIXBIT \LOAD\
	1000,,USRI	;IMMED,,CHANNEL
	401000,,DSKCHN	;LAST ARG AND IMMED,,CHANNEL

INFILE:	0,,(SIXBIT \DSK\)
FN1:	SIXBIT \FN1\
FN2:	SIXBIT \FN2\

PATCH:	BLOCK 40
PDL:	BLOCK 40

CONSTA
VARIAB

LOC <.+1777>/2000*2000
6LOC0=.
6IBUF==6LOC0+4000

END BEG
