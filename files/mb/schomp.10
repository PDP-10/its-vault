TITLE SUPER CHOMP

A=1
B=2
C=3
D=4
E=5
F=6
G=7
R=10
S=11
T=12
U=13
X=14
Y=15
Z=16
P=17

MAXROW==5.
MAXCOL==40.
LISTL==24000

DEFINE BARF A
	MOVE S,[440700,,[ASCII \A!$\]]
	PUSHJ P,TYPMES
TERMIN
BEG:	MOVEI P,PDL-1
	CLEARM CURPOS
	MOVE A,[CURPOS,,CURPOS+1]
	BLT A,CURPOS+MAXROW-1
	CLEARM TRYPOS
	MOVE A,[TRYPOS,,TRYPOS+1]
	BLT A,TRYPOS+MAXROW-1
	CLEARM LIST
	MOVE A,[LIST,,LIST+1]
	BLT A,LIST+LISTL-1
	SETOM LIST
	CLEARM LCOUNT
	MOVE A,ROWS
	MOVE B,COLS
	CAIG A,MAXROW
	CAILE B,MAXCOL
	.VALUE
	CONO 10	;TURN OFF OVF
	MOVE A,ROWS
	MOVE B,COLS
	AOS B
	MOVEI C,1
	IMUL C,B
	SOJG A,.-1
	SOS C
	CONSZ 10	;OVF
	JRST 4,.
NXTPOS:	MOVN A,ROWS	;GET NEXT POSITION
	HRLZS A
NXTP1:	AOS B,CURPOS(A)
	CAMG B,COLS
	JRST NXTP2
	AOBJN A,NXTP1	;GO TO NEXT HIGHER ROW
	JRST POSDUN

NXTP2:	HRRZS A
	MOVEM B,CURPOS(A)
	SOJGE A,.-1

	SETOM MOVNUM
NXTMOV:	AOS A,MOVNUM	;TRY NEXT MOVE
	IDIV A,COLS
	MOVNI A,1(A)
	ADD A,ROWS	;GETTING ROW INDEX
	JUMPL A,MOVDUN
	MOVNI B,1(B)
	ADD B,COLS	;GETTING COUNTERS TO BE REMAINING
	CAML B,CURPOS(A)
	JRST NXTMOV	;MOVE ILLEGAL
	MOVE C,[CURPOS,,TRYPOS]
	BLT C,TRYPOS+MAXROW-1
MAKMOV:	MOVEM B,TRYPOS(A)
	AOS A
	CAMGE B,TRYPOS(A)
	JRST MAKMOV
	MOVE A,ROWS
	SOS A
	MOVE B,COLS
	AOS B
	SKIPE D,[0]
MAK1:	IMUL D,B
	ADD D,TRYPOS(A)
	SOJGE A,MAK1	;GETTING POSITION CODE IN D
	MOVEI R,	;LOW BOUND
	MOVE S,LCOUNT	;HIGH BOUND
SEAR1:	MOVE T,R
	ADD T,S
	LSH T,-1
	CAML D,LIST(T)
	MOVE R,T	;MOVE LOW BOUND UP
	CAMG D,LIST(T)
	MOVE S,T	;MOVE HIGH BOUND DOWN
	CAILE S,1(R)
	JRST SEAR1
	CAME D,LIST(R)
	CAMN D,LIST(S)
	JRST NXTPOS	;FOUND IT
	JRST NXTMOV	;NOT IN LIST
MOVDUN:	MOVE A,ROWS	;APPEND TO LIST
	SOS A
	MOVE B,COLS
	AOS B
	SKIPE D,[0]
MAK2:	IMUL D,B
	ADD D,CURPOS(A)
	SOJGE A,MAK2
	AOS R,LCOUNT	;APPEND TO LIST
	CAIL R,LISTL
LOSE:	.VALUE
	MOVEM D,LIST-1(R)
	DATAI A
	JUMPN A,.-1
	JRST NXTPOS

POSDUN:	JRST 4,.
	.OPEN 1,.+1
	1,,(SIXBIT \TTY\)
	PUSHJ P,CRLF
	MOVE A,ROWS
	MOVE B,COLS
	MOVE C,LCOUNT
BARF [#1. ROWS, #2. COLUMNS, #3. LOSING POSITIONS]
	PUSHJ P,CRLF
	MOVN A,LCOUNT
	HRLZS A
	MOVE B,COLS
	AOS B
PRIN1:	MOVE C,LIST(A)
	IDIV C,B
	MOVE E,D
PRIN2:	IDIV C,B
	CAMN D,E
	JRST PRIN2
	MOVE E,D
PRIN3:	JUMPE C,PRIN5
	IDIV C,B
	CAMN D,E
	JRST PRIN3
PRIN4:	AOBJN A,PRIN1
	PUSHJ P,CRLF
	.CLOSE 1,
	.VALUE
	0

PRIN5:	MOVE C,LIST(A)
PRIN6:	MOVEI T,40
	PUSHJ P,TYO
	IDIV C,B
	PUSHJ P,3DIGIT
	SKIPE C
	JRST PRIN6
	PUSHJ P,CRLF
	JRST PRIN4
	D1==D
D2==E
D3==F

3DIGIT:	IDIVI D1,12	;ARGUMENT IN D1
	MOVE D3,D2
	IDIVI D1,12
	JUMPN D1,3DIG
	MOVNI D1,"0-40
	JUMPN D2,3DIG
	MOVNI D2,"0-40
	JUMPN D3,3DIG
	MOVNI D3,"0-40
3DIG:	MOVEI T,"0(D1)
	PUSHJ P,TYO
	MOVEI T,"0(D2)
	PUSHJ P,TYO
	MOVEI T,"0(D3)
	PUSHJ P,TYO
	POPJ P,
TYPMES:	ILDB T,S
	CAIN T,"$
	POPJ P,
	CAIN T,"#
	JRST ACNUM
	PUSHJ P,TYO
	JRST TYPMES

ACNUM:	PUSH P,[TYPMES]
	ILDB U,S
	MOVE T,S
	ILDB T,T
	XORI T,".#12
	CAIE T,12
	MOVEI T,10
	HRRM T,ACN2
	MOVE T,-"0(U)
ACN2:	IDIVI T,.
	HRLM U,(P)
	SKIPE T
	PUSHJ P,ACN2
	HLRZ T,(P)
	ADDI T,"0
	JRST TYO

CRLF:	MOVEI T,15
	PUSHJ P,TYO
	MOVEI T,12
TYO:	.IOT 1,T
	POPJ P,

PDL:	BLOCK 20
PATCH:	BLOCK 40

ROWS:	0
COLS:	0
MOVNUM:	0
LCOUNT:	0
CURPOS:	BLOCK MAXROW
TRYPOS:	BLOCK MAXROW
	0	;FOR END TEST IN MAKMOV

CONSTA
VARIAB

LIST:	-1	;TEMPORARY FIRST ENTRY TO PREVENT MATCH
	BLOCK LISTL
	-1

END BEG
