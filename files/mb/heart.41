TITLE HEART

A=1
B=2
C=3
D=4
E=5
F=6
G=7
R=10
S=11
T=12
U=13
X=14
Y=15
Z=16
P=17

DSKCHN==1
TABCHN==2
TYOCHN==3
USRI==5
USRO==6

DEFINE FIX X
	MULI X,400
	TSC X,X
	ASH X+1,-243(X)
TERMIN

DEFINE FLOAT X
	TLC X,232000
	FADR X,X
TERMIN

MAXCLS==8000.	;MAXIMUM NUMBER OF CELLS PROGRAM CAN HANDLE
MAXSQS==37.*38.
;MAX NCCOLS IS 2*MAXRAD/NEIGH
;MAXSQS SHOULD BE <MAX NCCOLS + 1>*<MAX NCCOLS + 2>
ELFLG==0	;1 FOR CIRCLE OF ELEVATED THRESHOLD IN CENTER
BEG:	MOVEI P,PDL-1
	MOVE A,NEIGH
	FMPR A,A
	MOVEM A,NEISQ
	MOVE A,MAXSPA
	FMPR A,A
	MOVEM A,MSPASQ
	MOVE A,ELRAD
	FMPR A,A
	MOVEM A,ERADSQ
;PUT THRESH TABLE BUILDER IN HERE
	MOVEI A,2	;FUNC TABLE POINTER
	MOVE B,REFRNO
	SUBI B,2
	MOVEI C,INREF
	MOVEM C,FUNC(A)
	AOS A
	SOJG B,.-2
	MOVEI C,LEAVER
	MOVEM C,FUNC(A)
	AOS A
	MOVE B,SETTIM
	SUB B,REFRNO
	MOVEI C,CHECK
	MOVEM C,FUNC(A)
	AOS A
	SOJG B,.-2
	MOVEI C,RESET
	MOVEM C,FUNC(A)
	AOS A
	MOVEI C,
	EXCH C,FUNC(A)
	SKIPE C
	AOJA A,.-3
	.OPEN USRI,USRIX	;SET UP PDP-6
	SKIPA
	.OPEN USRO,USROX
NOPDP6:	.VALUE
	MOVE A,[DISPL,,TIMES]
	BLT A,TIMES+DPY-DISPL	;SAVE THOSE GOODIES
	MOVE A,[-20,,<6LOC0/2000>]
	MOVEI B,
	.CALL CORBLK
NOCORE:	.VALUE
	.OPEN DSKCHN,FILMER
NOFILM:	.VALUE
	.CALL LOAD
NOLOAD:	.VALUE
	.CLOSE DSKCHN,
	MOVE A,[TIMES,,DISPL]
	BLT A,DPY	;RETRIEVE GOODIES
	CLEARM TIMES
	MOVE A,[TIMES,,TIMES+1]
	BLT A,TIMES+DPY-DISPL
	MOVE A,[JRST 100]
	MOVEM A,6LOC0+41
	SKIPN A,6LOC0+71
	JRST .-1
	CAIE A,6DISPL
NOSYNC:	.VALUE		;DISPLAY LIST MISMATCH
IF2 IFG 6DISPL+DPY-DISPL+2*MAXCLS+10-40000,PRINTC \DISPLAY LIST TOO BIG\
	MOVEI A,DISPL
	MOVEM A,6LOC0+71
BEGOK:	.VALUE

; 1 BIT MEANS IMAGE, BLOCK, OUTPUT
USRIX:	4,,(SIXBIT \USR\)
	0
	SIXBIT \PDP6\

USROX:	5,,(SIXBIT \USR\)
	0
	SIXBIT \PDP6\

CORBLK:	SETZ
	SIXBIT \CORBLK\
	1000,,300000	;IMMEDIATE,,GET BOTH READ AND WRITE ACCESS
	1000,,-1	;IMMED,,PUT PAGES IN SELF
	A		;AOBJN POINTER FOR DESTINATION OF PAGES
	1000,,USRI	;IMMED,,CHANNEL
	SETZ B		;LAST ARG,,POINTER FOR SOURCE OF PAGES

FILMER:	6,,(SIXBIT \DSK\)
	SIXBIT \FILMER\
	SIXBIT \BIN\

LOAD:	SETZ
	SIXBIT \LOAD\
	1000,,USRI	;IMMED,,CHANNEL
	401000,,DSKCHN	;LAST ARG AND IMMED,,CHANNEL
MOVIE:	0	;NONZERO FOR FILMING

;PDP-6 LOCATION 70
; +M,,+N = PUSHJ P,FRAME, DISPLAY M CYCLES, PUSHJ P,CLOSE, ALL N TIMES
;     -N = PUSHJ P,CLOSE, THEN REWIND N FRAMES
;      0 = DONE

;.NDIS ARGUMENTS
; < 0 = WAIT FOR COMPLETION OF LAST .NDIS
;   0 = RETURN COUNT LEFT FROM LAST .NDIS
; > 0 = DISPLAY N TIMES AND THEN STOP

LEADER:	MOVEI A,200.
	PUSHJ P,FRAM1
LEADOK:	.VALUE

;R.H. <A> = NUMBER OF FRAMES TO EXPOSE
FRAMES:	HRLI A,1	;DISPLAY CYCLES PER FILM FRAME
FRAM1:	MOVEM A,6LOC0+70
FRAM2:	MOVEI A,36
	.SLEEP A,
	SKIPE 6LOC0+70
	JRST FRAM2
	POPJ P,

REVINT:	MOVSI A,-MAXSQS	;REVISE INTENSITIES FOR 3-PASS COLOR MOVIES
	MOVE D,REFRNO
	MOVE E,LOWINT
	MOVE F,MEDINT
	MOVE G,HIINT
	MOVE R,LOINEL
REVIN1:	SKIPN B,SQTAB(A)
	JRST REVIN3
REVIN2:	MOVE C,B
	LSH C,1
	DPB E,[300,,DPY(C)]
	JSR B,ELTEST
	DPB R,[300,,DPY(C)]	;ELEVATED THRESHOLD IDLE
	CAMLE D,TIMES(B)
	DPB F,[300,,DPY(C)]	;REFRACTORY (INCLUDING FIRING)
	SKIPN TIMES(B)
	DPB G,[300,,DPY(C)]	;FIRING
	AOBJN B,REVIN2
REVIN3:	AOBJN A,REVIN1
REVIOK:	.VALUE
GENCEL:	MOVEI P,PDL-1
	CLEARM XS
	CLEARM YS
	CLEARM GENPTR
	CLEARM GENFMS
	MOVEI A,	;NUMBER OF CELLS GENERATED SO FAR - 1

;IN GETTING FIRST FEW CELLS STARTED, CURRENT NUMBER OF CELLS
;AS USED TO COMPUTE NEW RADIUS MAY HAVE TO BE SLIGHTLY INCREASED
;INCREASE OF 0 IS GOOD UP TO DENSITY = 0.318
;INCREASE OF 1 IS GOOD UP TO DENSITY = 0.636
;INCREASE OF 2 IS GOOD UP TO DENSITY = 0.955
;INCREASE OF 3 IS GOOD UP TO DENSITY = 1.274 (> MAX THEOR = 2/SQRT 3 = 1.1547)

GEN0:	MOVEI B,
GEN1:	MOVEI T,1(A)
	ADD T,B
	FLOAT T
	FDVR T,[3.14159265]
	FDVR T,DENSIT
	PUSHJ P,SQRT
	CAMG T,[1.0]
	AOJA B,GEN1	;TO GET STARTED WITH LARGE DENSITIES
	CAML T,MAXRAD
	JRST GENDUN
	MOVEM T,CURRAD	;RADIUS FOR NEW CELL
	MOVE D,GENPTR	;UPDATE GENPTR IF POSSIBLE
	MOVE E,CURRAD
	FSBR E,MAXSPA
	JUMPLE E,GEN4
	FMPR E,E
GEN2:	MOVE T,XS(D)
	FMPR T,T
	MOVE U,YS(D)
	FMPR U,U
	FADR T,U
	CAMGE T,E
	AOJA D,GEN2	;ANOTHER OLD CELL IS TOO FAR IN TO AFFECT NEW CELLS
	MOVEM D,GENPTR
GEN4:	MOVE B,CURRAD
	PUSHJ P,RANDOM
	HRRZS T
	FLOAT T
	FSC T,-18.
	FMPR T,[6.2831853]	;TWO PI
	MOVE C,T
	PUSHJ P,SIN
	FMPR T,B	;PROPOSED Y
	EXCH T,C
	PUSHJ P,COSQ	;GET COS OF ANGLE SIN JUST TAKEN OF
	FMPR B,T	;PROPOSED X
	MOVE D,GENPTR	;SCAN FOR TOO CLOSE
GEN5:	MOVE E,XS(D)	;CHECK X
	FSBR E,B
	CAMLE E,[-1.0]
	CAMLE E,[1.0]
	JRST GENOK1
	MOVE F,YS(D)	;CHECK Y
	FSBR F,C
	CAMLE F,[-1.0]
	CAMLE F,[1.0]
	JRST GENOK1
	FMPR E,E	;CHECK DISTANCE
	FMPR F,F
	FADR E,F
	CAMGE E,[1.0]
	JRST GEN4
GENOK1:	CAME D,A
	AOJA D,GEN5
	MOVE D,GENPTR	;SCAN FOR TOO FAR
GEN6:	MOVE E,XS(D)	;CHECK X
	FSBR E,B
	MOVMS E
	CAMLE E,MAXSPA	;MAXIMUM SPACING
	JRST GEN7
	MOVE F,YS(D)	;CHECK Y
	FSBR F,C
	MOVMS F
	CAMLE F,MAXSPA
	JRST GEN7
	FMPR E,E	;CHECK DISTANCE
	FMPR F,F
	FADR E,F
	CAMG E,MSPASQ
	JRST GENOK2
GEN7:	CAME D,A	;CONTINUE LOOKING
	AOJA D,GEN6
	JRST GEN4
GENOK2:	AOS A		;ACCEPTED, SO PUT NEW CELL IN TABLES
	MOVEM B,XS(A)
	MOVEM C,YS(A)
	SKIPE MOVIE
	JRST GEN20
	JRST GEN16	;IF JFCL THIS, DPY MAY BE TOO LONG FOR ONE .DSTAR POINTER
;AND IF USE PDP-6 ROUTINES, FIRST BLT DPY TO 3000,,3000
	MOVEI B,1(A)
	IDIVI B,100.
	JUMPN C,GEN16
	MOVEM A,SAVEA	;TIME TO RENEW DISPLAY
	MOVEM A,TICKS
	AOS TICKS
	PUSHJ P,DOTICK
	MOVE A,TICKS
	PUSHJ P,MAKDPY
	MOVE A,SAVEA
GEN16:	MOVEI B,1(A)
	CAIL B,MAXCLS
	JRST GENDUN	;REACHED MAXCLS
	CAML B,NCELLS
	.VALUE		;REACHED NCELLS
	JRST GEN0

GENDUN:	AOS A
	MOVEM A,NCELLS
	.VALUE		;REACHED MAXRAD

GEN20:	MOVEM A,SAVEA
	ADDI A,2	;CURRENT NUMBER OF CELLS + 1
	FLOAT A
	MOVE B,[57.7078]	;40. * LOG2 E = 40./LN 2
	FDVR B,A	;FRAMES PER NEW CELL SO TOTAL = 40. LOG2 (CELLS+1)
	FADRB B,GENFMS
	FIX B
	MOVN B,C
	FLOAT B
	FADRM B,GENFMS	;SUBTRACT OUT WHOLE FRAMES
	SKIPE A,C	;NUMBER OF FRAMES TO DO
	PUSHJ P,FRAMES
	MOVE A,SAVEA
	JRST GEN16

SAVEA:	0
GENPTR:	0	;POINTER TO FIRST CELL IN RELEVANT OUTER LAYER
GENFMS:	0	;REMEMBERS FRACTIONAL FRAMES OF MOVIE FILM (FLOATING POINT)
;WILL NOT WORK TO WRITE OUT A FILE READ IN

WRITE:	MOVE A,CURRAD
	FSC A,1
	FDVR A,NEIGH
	FIX A
	MOVN A,B
	FLOAT A
	FMPR A,NEIGH
	FADR A,CURRAD
	FADR A,CURRAD
	CAML A,[0.5]	;ALLOW LAST COLUMN TO BE SLIGHTLY WIDER THAN NEIGH
	AOS B
	MOVEM B,NCCOLS
	.OPEN DSKCHN,DSKOUT
	.VALUE
	.IOT DSKCHN,CURRAD
	.IOT DSKCHN,NCELLS
	.IOT DSKCHN,NCCOLS
	CLEARM CWRITC
	MOVEI A,	;CURRENT COLUMN
WRITE1:	MOVEI R,(A)
	FLOAT R
	FMPR R,NEIGH
	FSBR R,CURRAD	;MIN X, INCLUSIVE
	MOVEI S,1(A)
	CAMN S,NCCOLS
	AOS S		;TO GET ANY WIDTH OVERFLOW OF LAST COLUMN
	FLOAT S
	FMPR S,NEIGH
	FSBR S,CURRAD	;MAX X, EXCLUSIVE
	MOVEI B,	;CURRENT ROW
WRITE2:	MOVEI T,(B)
	FLOAT T
	FMPR T,NEIGH
	FSBR T,CURRAD	;MIN Y,INCLUSIVE
	MOVEI U,1(B)
	CAMN U,NCCOLS
	AOS U		;TO GET TOP CELL, IF ANY
	FLOAT U
	FMPR U,NEIGH
	FSBR U,CURRAD	;MAX Y,EXCLUSIVE
	MOVEI C,	;CELL POINTER
WRITE3:	CAMG R,XS(C)
	CAMG S,XS(C)
	JRST WRITE4	;X OUT OF SQUARE
	CAMG T,YS(C)
	CAMG U,YS(C)
	JRST WRITE4	;Y OUT OF SQUARE
	.IOT DSKCHN,XS(C)
	.IOT DSKCHN,YS(C)
	AOS CWRITC	;CELL WRITE COUNT
WRITE4:	AOS C
	CAME C,NCELLS
	JRST WRITE3
	.IOT DSKCHN,[1]	;SIGNAL END OF SQUARE
	AOS B
	CAME B,NCCOLS
	JRST WRITE2
	AOS A
	CAME A,NCCOLS
	JRST WRITE1
	.CLOSE DSKCHN,
	MOVE T,CWRITC
	CAME T,NCELLS
WRTERR:	JRST 4,.	;FENCEPOST ERROR DETECTED
WRITOK:	.VALUE

NCCOLS:	0	;NUMBER OF COLUMNS ACTUALLY OCCUPIED BY CELLS
CWRITC:	0	;CELL WRITE COUNT

DSKOUT:	5,,(SIXBIT \DSK\)	;IMAGE, WORD, OUTPUT
	SIXBIT \CELLS\
FN2:	SIXBIT \>\
WRITEX:	.OPEN DSKCHN,DSKOUT	;TO WRITE OUT A FILE READ IN
	.VALUE
	.IOT DSKCHN,CURRAD
	.IOT DSKCHN,NCELLS
	.IOT DSKCHN,NCCOLS
	MOVN A,NCCOLS
	SOS A
	IMUL A,NCCOLS
	HRLZS A
	HRR A,NCCOLS
	AOS A
	MOVEI B,
WRTX1:	HRRZ C,A
	MOVE D,NCCOLS
	IDIVI C,1(D)
	CAMN D,NCCOLS
	JRST WRTX4	;JUST THE BUFFER SQUARE AT TOP OF COLUMN
	SKIPN C,SQTAB(A)
	JRST WRTX3
WRTX2:	.IOT DSKCHN,XS(C)
	.IOT DSKCHN,YS(C)
	AOS B
	AOBJN C,WRTX2
WRTX3:	.IOT DSKCHN,[1]	;SIGNAL END OF SQUARE
WRTX4:	AOBJN A,WRTX1
	.CLOSE DSKCHN,
	CAME B,NCELLS
	JRST 4,WRTERR
WRTXOK:	.VALUE
READ:	HRLZI A,1
	XORM A,DSKOUT
	.OPEN DSKCHN,DSKOUT
	.VALUE
	.IOT DSKCHN,CURRAD
	.IOT DSKCHN,NCELLS
	.IOT DSKCHN,NCCOLS
	MOVE A,CURRAD
	FMPR A,A
	FMPR A,[3.14159265]
	MOVE B,NCELLS
	FLOAT B
	FDVR B,A
	MOVEM B,READEN	;DENSITY OF CELL CLUSTER READ
	MOVE A,NCELLS
	CAILE A,MAXCLS
	JRST 4,REDERR	;TOO MANY CELLS
	HRRM A,INREF1	;UPDATE NOW BECAUSE SOME MAY GET DELETED WITH SWC
	MOVE A,NCCOLS
	ADDI A,1
	MOVEI B,1(A)
	IMUL A,B
	CAILE A,MAXSQS
	JRST 4,REDERR	;TOO MANY SQUARES (COLUMNS)
	CLEARM SQTAB
	MOVE A,[SQTAB,,SQTAB+1]
	BLT A,SQTAB+MAXSQS-1
	MOVEI A,	;TOTAL CELL READ COUNT
	MOVE B,NCCOLS	;SQUARES READ POINTER
	AOS B
READ1:	MOVE C,A	;CURRENT SQUARE POINTER
	MOVE U,SETTIM
READ2:	.IOT DSKCHN,T
	CAIN T,1
	JRST READ3
	MOVEM T,XS(A)
	.IOT DSKCHN,T
	MOVEM T,YS(A)
	MOVEM U,TIMES(A)
	AOJA A,READ2
READ3:	MOVE D,C
	SUB D,A		;MINUS COUNT
	HRLZS D
	SKIPE D		;BE NEAT IF NO CELLS
	HRR D,C		;INDEX OF FIRST CELL IN SQUARE
	MOVEM D,SQTAB(B)
	AOS C,B
	MOVE D,NCCOLS
	IDIVI C,1(D)
	CAME D,NCCOLS
	JRST READ1	;STILL MORE SQUARES IN THIS COLUMN
	AOS B		;SKIP OVER EMPTY TOP ROW
	CAME C,NCCOLS
	JRST READ1	;STILL MORE COLUMNS
	.CLOSE DSKCHN,	;SHOULD BE ALL DONE
	CAME A,NCELLS
REDERR:	JRST 4,.	;NUMBER OF CELLS DISAGREES
	MOVE A,NCCOLS
	AOS A
	IMUL A,A
	CAME B,A
	JRST 4,REDERR	;SQTAB POINTER LOUSED UP
	HRLZI A,1
	XORM A,DSKOUT
	MOVE A,NCELLS
	MOVEM A,TICKS
	PUSHJ P,DOTICK
	MOVE A,NCELLS
	PUSHJ P,MAKDPY
	MOVE A,SETTIM	;MAKE ALL CELLS IDLE
	MOVEM A,TIMES
	MOVE A,[TIMES,,TIMES+1]
	MOVE B,NCELLS
	ADDI B,TIMES-1
	BLT A,(B)
READOK:	.VALUE

READEN:	0	;DENSITY OF CELL CLUSTER READ
SQTAB:	BLOCK MAXSQS
;EACH COLUMN IS <NCCOLS> OCCUPIED SQUARES + 1 EMPTY AT TOP
;THERE ARE <NCCOLS> OCCUPIED COLUMNS + 2, ONE ON EACH SIDE
;<SQTAB+N> = -COUNT,,INDEX OF CELLS IN THAT SQUARE (OR ZERO, IF NONE)
MULT==8	;SPEED SCALE OF LEFT SWC

SWC:	.OPEN TYOCHN,[5,,(SIXBIT \TTY\)]
	.VALUE
	MOVEI A,DISSWC+1
	HRRM A,DISSWC	;TURN ON FUNCTIONS AND CROSSHAIRS
	MOVSI A,400000
	.IOTLS A,
	CONO 420,40	;SPACE WAR CONSOLES
SWC1:	DATAI 420,A
	SETCM A,A
	TDNN A,[760000,,760000]
	JRST SWC1
	TDNN A,[20000,,20000]
	JRST MOVER
	TRNE A,20000	;EXECUTE BUTTON PUSHED
	JRST NOTCOM
	AOS B,COMIDX	;CYCLE FUNCTION BUTTON PUSHED
	CAIL B,6
	SETZB B,COMIDX
	MOVSI C,-6	;AOBJN POINTER
	MOVEI D,3	;OTHER INTENSITY
	DPB D,TABDPB(C)
	AOBJN C,.-1
	MOVEI D,7	;SELECTED INTENSITY
	DPB D,TABDPB(B)
	MOVEI A,18.-6	;FRAMES WHEN FUNCTION CYCLED, MINUS 6 (SEE BELOW)
	SKIPE MOVIE
	PUSHJ P,FRAMES
SWC2:	MOVEI A,10
	.SLEEP A,
SWC3:	MOVEI A,10
	.SLEEP A,
	MOVEI A,6	;FRAMES WHEN CELL FIRED, REFRACTED, IDLED, OR DELETED
	SKIPE MOVIE
	PUSHJ P,FRAMES
	JRST SWC1

MOVFMS:	0	;COUNTS EIGHTHS OF FRAMES FOR CROSS MOTION
COMIDX:	0	;INDEX OF CURRENT COMMAND
COMTAB:	IDLE
	REFRAC
	FIRE
	DELETE
	EXIT
	STIM
TABDPB:	300,,IINT	;COMMAND INTENSITY BYTE POINTERS
	300,,RINT
	300,,FINT
	300,,DINT
	300,,EINT
	300,,SINT
NOTCOM:	LDB A,[1200,,CROSS]	;X
	LDB B,[221200,,CROSS]	;Y
	FLOAT A		;NOT A COMMAND, SO FIND CELL CLOSEST TO <A>,<B> = X,Y
	FLOAT B
	FSBR A,[512.0]
	FSBR B,[512.0]
	FDVR A,[500.0]
	FDVR B,[500.0]
	FMPR A,CURRAD
	FMPR B,CURRAD	;CROSS X,Y IN CELL CLUSTER COORDINATES
	MOVE T,A
	MOVE U,B
	FMPR T,T
	FMPR U,U
	FADR T,U
	FDVR T,CURRAD
	CAML T,CURRAD
	JRST BELL	;CROSS OUT OF CLUSTER
	MOVE T,A	;X
	FADR T,CURRAD
	FDVR T,NEIGH
	FIX T
	MOVE C,NCCOLS
	AOS C
	IMUL C,U
	MOVE T,B	;Y
	FADR T,CURRAD
	FDVR T,NEIGH
	FIX T
	ADD C,U
	SOS C		;GETTING INDEX OF SQUARE SOUTHWEST TO SQUARE CROSS IS IN
	MOVSI D,(4096.0)	;DISTANCE**2 TO CLOSEST
	CLEARB E,F	;SQTAB INDEX, AOBJN POINTER OF CLOSEST
	PUSHJ P,FIND
	AOS C
	PUSHJ P,FIND
	AOS C
	PUSHJ P,FIND
	ADD C,NCCOLS
	SOS C
	PUSHJ P,FIND
	AOS C
	PUSHJ P,FIND
	AOS C
	PUSHJ P,FIND
	ADD C,NCCOLS
	SOS C
	PUSHJ P,FIND
	AOS C
	PUSHJ P,FIND
	AOS C
	PUSHJ P,FIND
	CAML D,NEISQ
	JRST BELL	;NO CELLS NEAR CROSS
	MOVEI A,	;IDLE
	.IOT TYOCHN,A
	MOVE A,COMIDX
	JRST @COMTAB(A)
FIND:	SKIPN G,SQTAB(C)
	POPJ P,
FIND1:	MOVE T,XS(G)
	MOVE U,YS(G)
	FSBR T,A
	FSBR U,B
	FMPR T,T
	FMPR U,U
	FADR T,U
	CAML T,D
	JRST FIND2
	MOVE D,T	;FOUND A CLOSER CELL
	MOVE E,C
	MOVE F,G
FIND2:	AOBJN G,FIND1
	POPJ P,

EXIT:	.CLOSE TYOCHN,
	MOVEI A,
	.IOTLS A,	;FOR SPACE WAR CONSOLES
	HLLZS DISSWC
	.VALUE

BELL:	MOVEI A,7
	.IOT TYOCHN,A
	JRST SWC2

MOVER:	MOVEI B,	;SWC CROSS MOTION
	TLNE A,400000	;RIGHT FAST
	ADDI B,MULT
	TLNE A,200000	;LEFT
	SUBI B,MULT
	TLNE A,100000	;DOWN
	SUB B,[MULT,,0]
	TLNE A,40000	;UP
	ADD B,[MULT,,0]
	TRNE A,400000	;RIGHT SLOW
	ADDI B,1
	TRNE A,200000	;LEFT
	SUBI B,1
	TRNE A,100000	;DOWN
	SUB B,[1,,0]
	TRNE A,40000	;UP
	ADD B,[1,,0]
	ADD B,CROSS
	XOR B,CROSS
	TDNN B,[776000,,776000]
	XORM B,CROSS
	MOVEI A,2
	.SLEEP A,
	SKIPN MOVIE
	JRST SWC1
	MOVEI A,1	;ONE FRAME...
	AOS B,MOVFMS
	TRNN B,7	;...EVERY 8 MOVER CALLS
	PUSHJ P,FRAMES
	JRST SWC1
STIM:	HRRZS F
	SKIPN A,STMIDX
	JRST STIM2
	CAIN A,STMMAX
	.VALUE		;TRYING TO STIMULATE TOO MANY CELLS
	MOVEI B,
STIM1:	CAMN F,STMLIS(B)
	JRST SWC3	;ALREADY ON STIMULATE LIST
	CAIE A,1(B)
	AOJA B,STIM1
STIM2:	MOVEM F,STMLIS(A)
	AOS STMIDX
FIRE:	MOVE A,HIINT
	MOVEI B,0
	JRST IDLE1

REFRAC:	MOVE A,MEDINT
	MOVEI B,1
	JRST IDLE1

IDLE:	MOVE A,LOWINT
	JSR F,ELTEST
	MOVE A,LOINEL
	MOVE B,SETTIM
IDLE1:	MOVEM B,TIMES(F)
	LSH F,1
	DPB A,[300,,DPY(F)]
	JRST SWC3

ELTEST:	0
IFE ELFLG,JRST ELTSKP	;ALWAYS SKIP IF NO SUCH AREA
	HRRZ T,ELTEST
	SOS T
	LDB T,[270400,,0(T)]	;PICK UP ARGUMENT AC
	MOVE T,(T)
	MOVE U,XS(T)
	FMPR U,U
	MOVE T,YS(T)
	FMPR T,T
	FADR T,U
	CAMLE T,ERADSQ
ELTSKP:	AOS ELTEST	;SKIP IF OUTSIDE ELEVATED AREA
	JRST @ELTEST
DELETE:	MOVE A,SQTAB(E)
	ADD A,[1,,0]
	SKIPL A
	MOVEI A,
	MOVEM A,SQTAB(E)
	MOVEI D,
	MOVEI A,XS
	PUSHJ P,DOBLT
	MOVEI A,YS
	PUSHJ P,DOBLT
	MOVEI A,TIMES
	PUSHJ P,DOBLT
	MOVEI A,DPY
	LSH F,1
	MOVEI D,1	;TURN ON BLT DPY TABLE FLAG
	PUSHJ P,DOBLT
	SOS NCELLS
	JRST SWC3

DOBLT:	HRLI B,(F)
	HRRI B,1(F)
	ADD B,A
	ADD B,D
	MOVSS B
	ADD B,A		;FROM,,TO
	HLRO C,F
	MOVNI C,1(C)
	ADDI C,(F)
	ADD C,A		;DESTINATION
	BLT B,(C)	;BLT ALWAYS TRANSFERS AT LEAST 1 WORD
	CLEARM (C)	;CLEAR OUT LAST WORD
	SKIPE D
	CLEARM -1(C)
	POPJ P,
;UINFAR IS FOR MAKING MICRO-INFARCTIONS
;REPLACE DELETE IN COMTAB WITH UINFAR

UINFAR:	MOVE A,NCELLS
	IDIVI A,8	;CELLS PER INFARCT
	CAIE B,		;PHASE
	JRST DELETE
	PUSHJ P,RANDOM	;RANDOMIZE CROSS POSITION
	HRRZS T
	FLOAT T
	FSC T,-18.
	PUSHJ P,SQRT
	MOVE A,T
	FMPR A,[500.0]	;OR LESS FOR UNINFARCTED OUTER RING
	PUSHJ P,RANDOM
	HRRZS T
	FLOAT T
	FSC T,-18.
	FMPR T,[6.2831853]
	PUSHJ P,SIN
	FMPR T,A
	FADR T,[512.5]
	FIX T
	DPB U,[221200,,CROSS]	;Y
	PUSHJ P,COSQ
	FMPR T,A
	FADR T,[512.5]
	FIX T
	DPB U,[1200,,CROSS]	;X
	JRST DELETE
MAKDPY:	.DSTOP
	MOVNS A		;MAKE UP DISPLAY LIST
	HRLZS A		;A HAD NUMBER OF CELLS
	MOVEI B,
LOOP3:	MOVE C,B
	LSH C,-1
	JSR C,ELTEST
	SKIPA C,LOINEL
	MOVE C,LOWINT
	ADDI C,20110
	MOVEM C,DPY(B)
	AOS B
	MOVE E,XS(A)
	FDVR E,CURRAD
	FMPR E,[500.0]	;NOT QUITE FULL SCREEN
	FADR E,[512.5]
	FIX E
	HRRI D,2000(F)	;X WORD, INTENSIFY, TO PARAMETER MODE
	MOVE E,YS(A)
	FDVR E,CURRAD
	FMPR E,[500.0]
	FADR E,[512.5]
	FIX E
	HRLI D,220000(F)	;Y WORD, KEEP POINT MODE
	MOVEM D,DPY(B)
	AOS B
	AOBJN A,LOOP3
	MOVEI C,20110
	MOVEM C,DPY(B)
	MOVSI C,220000
	MOVEM C,DPY+1(B)	;GET BEAM OUT OF PICTURE
	MOVEI C,3000
	MOVEM C,DPY+2(B)
	ADDI B,10
	MOVNS B
	HRLM B,DISP1
IFN ELFLG,[MOVE A,CURRAD
	CAML A,ELRAD
	PUSHJ P,ELCIRC
]
	POPJ P,
IFN ELFLG,[
ELCIRC:	MOVEI A,2	;SCALE FOR ELEVATED THRESHOLD CIRCLE INCREMENTS
	DPB A,[40200,,ELDPY]
	MOVEI B,DISSWC
	HRRM B,DISPL	;TURN OFF CIRCLE
	MOVSI B,-15
	MOVEI C,1
	LSH C,(A)
ELCIR0:	MOVE A,TRY0(B)
	ANDCMI A,770000	;ISOLATE HALVES
	IMUL A,C
	TRZE A,774000
	ADD A,[-1,,774000]	;UNDO CARRY FROM SOSING RIGHT HALF
	MOVEM A,TRY1(B)
	AOBJN B,ELCIR0
	MOVE A,[500.0]
	FDVR A,CURRAD
	FMPR A,ELRAD	;GETTING ELRAD IN 340 UNITS
	FADRI A,(0.5)
	FIX A
	MOVE A,B
	IMUL A,A
	MOVEM A,ELCTST
	DPB B,[1100,,ELDPY+1]
	MOVSI A,-8	;OCTANT POINTER
	HRLZS B		;X,,Y
	MOVE E,[442200,,ELDPY+2]
	MOVEI F,200000	;ACCUMULATES HALFWORDS
ELCIR1:	ADD B,TRY1(A)
	HLRE T,B
	HRRE U,B
	IMUL T,T
	IMUL U,U
	ADD T,U
	SUB T,ELCTST
	MOVM C,T
	ADD B,TRY1+4(A)
	ADD B,TRY2(A)
	HLRE T,B
	HRRE U,B
	IMUL T,T
	IMUL U,U
	ADD T,U
	SUB T,ELCTST
	MOVM T,T
	CAMGE T,C
	JRST ELCIR2
	ADD B,TRY2+4(A)
	ADD B,TRY1(A)
	SKIPA C,INCR1(A)
ELCIR2:	MOVE C,INCR2(A)
	TRNN F,17	;HALFWORD FILLED?
	JRST ELCIR3	;NO
	IDPB F,E	;YES, SO WRITE IT OUT
	CAML E,[222200,,ELDPYE-1]
	JRST 4,.	;CIRCLE TOO BIG
	MOVEI F,200000	;INTENSIFY
ELCIR3:	SKIPA D,[170000]
	LSHC C,-4
	TDNE D,F	;SEARCH FOR A VACANT BYTE
	JRST .-2
	IOR F,C
	HLRE T,B
	HRRE U,B
	MOVE S,T
	ADD S,U
	XCT ELOCTT(A)
	JRST ELCIR1	;NOT DONE WITH THIS OCTANT
	AOBJN A,ELCIR1
	IORI F,400000	;ESCAPE
	IDPB F,E
	MOVEI F,3000
	IDPB F,E
	MOVEI B,DISPL+1
	HRRM B,DISPL	;TURN ON CIRCLE
	POPJ P,

TRY0:	0,,1	;DELTA X,,DELTA Y FOR OUTSIDE STEP IN EACH OCTANT
	-1,,1
	-1,,0
	-1,,-1
	0,,-1
	1,,-1
	1,,0
	1,,1
	0,,1	;FOR UNDOING STEPS
	-1,,1
	-1,,0
	-1,,-1
	0,,-1	;NEED ONE MORE SINCE INSIDE STEPS LAG BY ONE

TRY1:	BLOCK 15
TRY2=TRY1+1

INCR1:	2_14	;1 = MOVE X, LEFT, MOVE Y, DOWN
INCR2:	16_14
	14_14
	17_14
	3_14
	13_14
	10_14
	12_14
	2_14

ELCTST:	0	;(ELRAD IN 340 UNITS)**2

ELOCTT:	CAMLE T,U	;TEST FOR OCTANT COMPLETED
	SKIPLE T
	SKIPLE S
	SKIPLE U
	CAMGE T,U
	SKIPGE T
	SKIPGE S
	SKIPGE U
]
FIRAND:	MOVE R,REFRNO	;FIRE RANDOM FIREABLE CELLS -- CAN BE CALLED IN MAIN WORK LOOP
	SKIPN A,MAGIC0
	MOVSI A,-MAXSQS
	MOVEI D,20110
	ADD D,HIINT
FIR1:	SKIPN B,SQTAB(A)
	JRST FIR4
FIR2:	CAMLE R,TIMES(B)
	JRST FIR3	;THIS CELL FIRING OR REFRACTORY
	PUSHJ P,RANDOM
	TRNE T,37
	JRST FIR3
	CLEARM TIMES(B)
	MOVE C,B
	LSH C,1
	MOVEM D,DPY(C)
FIR3:	AOBJN B,FIR2
FIR4:	AOBJN A,FIR1
	POPJ P,
WORK:	MOVE A,NCCOLS
	AOS B,A
	SKIPE SQTAB(A)
WRKERR:	.VALUE		;USE OF MAGIC1 LOSES IF FIRST POSSIBLE SQUARE IS OCCUPIED
;TO BE SURE THIS CANNOT HAPPEN, HAVE CURRAD = 2/(2-SQRT 2) * NEIGH = 3.42 * NEIGH
	AOS A
	IMUL A,B
	MOVNS A
	HRLZS A
	CLEARB B,C	;FIRST, LAST OCCUPIED SQUARES
WORK1:	SKIPN SQTAB(A)
	JRST WORK2
	SKIPN B
	HRRZ B,A
	HRRZ C,A
WORK2:	AOBJN A,WORK1
	SUB C,B
	MOVNI C,1(C)	;MINUS COUNT
	HRL B,C
	MOVEM B,MAGIC0
	MOVN A,NCCOLS
	SUBI A,2
	HRRM A,MAGIC1
	AOS A
	HRRM A,MAGIC2
	AOS A
	HRRM A,MAGIC3
	HLLOS MAGIC4
	HLLZS MAGIC5
	HLLZS MAGIC6
	AOS MAGIC6
	MOVE A,NCCOLS
	HRRM A,MAGIC7
	AOS A
	HRRM A,MAGIC8
	AOS A
	HRRM A,MAGIC9

;NOTE... WHEN INTERRUPTING LOOP TO GO TO SWC, ALWAYS USE A BREAKPOINT AT LOOP
;THIS AVOIDS THINGS GETTING OUT OF PHASE LIKE TIMES GETTING TOO LARGE
;MAIN THINK LOOP

LOOP:	MOVE R,REFRNO	;NUMBER OF REFRACTORY TICKS
	MOVE A,MAGIC0
LOOP1A:	SKIPN B,SQTAB(A)	;SCAN FOR OCCUPIED SQUARE
	JRST LOOP1C
	SKIPE TIMES(B)	;SCAN FOR FIRING CELL
LOOP1B:	AOBJN B,.-1
	JUMPL B,LOOP1D	;GOT A FIRING CELL
LOOP1C:	AOBJN A,LOOP1A
	JRST LOOP2

LOOP1D:	MOVE D,XS(B)	;SCAN NEIGHBOR SQUARES OF FIRING CELL
	MOVE E,D
	FSBR D,NEIGH	;MIN X
	FADR E,NEIGH	;MAX X
	MOVE F,YS(B)
	MOVE G,F
	FSBR F,NEIGH	;MIN Y
	FADR G,NEIGH	;MAX Y
MAGIC1:	MOVEI C,.(A)	;. BECOMES -NCCOLS-2 (= SOUTHWEST SQUARE)
	SKIPE C,SQTAB(C)
	JSR LOOP1E
MAGIC2:	MOVEI C,.(A)
	SKIPE C,SQTAB(C)
	JSR LOOP1E
MAGIC3:	MOVEI C,.(A)
	SKIPE C,SQTAB(C)
	JSR LOOP1E
MAGIC4:	MOVEI C,.(A)
	SKIPE C,SQTAB(C)
	JSR LOOP1E
MAGIC5:	MOVEI C,.(A)
	SKIPE C,SQTAB(C)
	JSR LOOP1E
MAGIC6:	MOVEI C,.(A)
	SKIPE C,SQTAB(C)
	JSR LOOP1E
MAGIC7:	MOVEI C,.(A)
	SKIPE C,SQTAB(C)
	JSR LOOP1E
MAGIC8:	MOVEI C,.(A)
	SKIPE C,SQTAB(C)
	JSR LOOP1E
MAGIC9:	MOVEI C,.(A)
	SKIPE C,SQTAB(C)
	JSR LOOP1E
	JRST LOOP1B

MAGIC0:	0	;AOBJN POINTER FOR SQUARES IN SQTAB ACTUALLY OCCUPIED
LOOP1E:	0		;SCAN CELLS IN GIVEN SQUARE NEAR FIRING CELL
LOOP1F:	CAMLE R,TIMES(C)
	JRST LOOP1G	;REFRACTORY
	MOVE T,XS(C)
	CAMG D,T	;RANGE CHECKING SUPPOSEDLY SAVES A FEW MICROSECONDS
	CAMGE E,T
	JRST LOOP1G	;NOT IN NEIGHBORHOOD
	MOVE U,YS(C)
	CAMG F,U
	CAMGE G,U
	JRST LOOP1G	;NOT IN NEIGHBORHOOD
	FSBR T,XS(B)
	FSBR U,YS(B)
	FMPR T,T
	FMPR U,U
	FADR T,U
	CAMLE T,NEISQ
	JRST LOOP1G	;NOT IN NEIGHBORHOOD
	JUMPE T,LOOP1G	;SAME CELL
	PUSHJ P,SQRT
	MOVE S,HEIGHT
	FDVR S,T
	FADRI S,(0.5)	;NOT NECESSARY IN PRACTICE?
	FIX S
	HRLZS T
	ADDM T,TIMES(C)	;GUNCH THIS CELL
LOOP1G:	AOBJN C,LOOP1F
	JRST @LOOP1E
;DETECT FIRINGS, UPDATE DPY INTENSITIES, SLEW THRESHOLDS

LOOP2:	MOVE A,MAGIC0
LOOP2A:	SKIPN B,SQTAB(A)
	JRST INREF
LOOP2B:	AOS C,TIMES(B)
	JRST @FUNC(C)

FUNC:	JRST 4,.	;TIMES AOSED MUST BE > 0
	FIRED		;CELL HAS JUST FIRED
REPEAT 10.-2,INREF	;CELL IN REFRACTORY
	LEAVER		;CELL LEAVING REFRACTORY
REPEAT 17.,CHECK	;CELL FIREABLE
	RESET		;CELL AT MAXIMUM SENSITIVITY
	BLOCK 100	;FOR LARGE FUNC TABLES

FIRED:	MOVE D,MEDINT	;MEDIUM INTENSITY
	JRST CHECK1

LEAVER:	MOVE D,LOWINT	;LOW INTENSITY
IFN ELFLG,[MOVE T,XS(B)
	FMPR T,T
	MOVE U,YS(B)
	FMPR U,U
	FADR T,U
	CAMG T,ERADSQ
	MOVE D,LOINEL
]
	JRST CHECK1

RESET:	SOS C,TIMES(B)	;TIME AT LIMIT OF THRESHOLD TABLE
CHECK:	HLRZ D,C
IFN ELFLG,[MOVE T,XS(B)
	FMPR T,T
	MOVE U,YS(B)
	FMPR U,U
	FADR T,U
	CAMG T,ERADSQ
	ADDI C,THREL-THRESH
]
	CAMGE D,THRESH(C)
	JRST INREF
	CLEARM TIMES(B)
	MOVE D,HIINT	;HIGH INTENSITY
CHECK1:	MOVE C,B
	LSH C,1
	DPB D,[300,,DPY(C)]
INREF:	AOBJN B,LOOP2B
	AOBJN A,LOOP2A
INREF1:	MOVEI A,.	;CLEAR OUT OLD STIMULI, A GETS INITIAL NCELLS READ
	MOVE B,[HRRZS TIMES(A)]
	MOVE C,[SOJGE A,B]
	MOVE D,[JRST LOOP2C]
	JRST B
LOOP2C:	AOS A,TICKS
STMRAT:	IDIVI A,100	;REPEATED STIMULATION RATE
	CAIN B,0	;AND PHASE
	PUSHJ P,STIMUL
	PUSHJ P,DOTICK
FPERT:	MOVEI A,10.	;MOVIE FRAMES PER TICK
	SKIPE MOVIE
	PUSHJ P,FRAMES
	JRST LOOP

STIMUL:	SKIPN A,STMIDX
	POPJ P,
	MOVEI B,
	MOVE C,REFRNO
	MOVEI D,20110
	ADD D,HIINT
ST1:	MOVE E,STMLIS(B)
	CAMLE C,TIMES(E)
	JRST ST2
	CLEARM TIMES(E)
	LSH E,1
	HRRM D,DPY(E)
ST2:	CAIE A,1(B)
	AOJA B,ST1
	POPJ P,

DOTICK:	MOVE A,[374040,,404040]	;DISPLAY TICK COUNT
	MOVE B,TICKS
LOOP6A:	IDIVI B,10.
	ADDI A,20(C)	;TO 340 CHARACTER
LOOP6B:	ROT A,-6
	SKIPE B
	JRST LOOP6A
	TRNE A,40
	JRST LOOP6B	;FILL OUT WORD
	MOVEM A,DISTIK
	POPJ P,
;ANS IN T,U; COSQ GIVES COS OF LAST SIN ARG, -SIN OF LAST COS
SIND:	FMPR T,[.01745329251994]	;PI/180
	JRST SIN

COSD:	FMPR T,[.01745329251994]
COS:	FADR T,[1.5707963267]	;PI/2
SIN:	CAMG T,[164475536723]	;.00015148419(SAVES A CONSTANT. COULD USE 3**<1/3>*2**-13)
	CAMGE T,[-.00017605585]	;-3**<1/3>*2**-13=164561156421=LARGEST X = SIN X
	JRST .+2
	POPJ P,	;SMALLNESS OF ABS T PERMITS SIN T _ T AND THREATENS UNDERFLOW
	FDVR T,[1.5707963267]	;PI/2
	MOVEM T,SINTM'
	MULI T,400
	TSC T,T	;CAML T,...SETZB U,SINTM WOULD PREVENT HUGE ANSWER FOR T > 2**27
	ASH U,-243(T)
	DPB U,[10100,,SINOB]
	MOVNS T,U
	ANDCMI T,1
	TLC T,232000
	FAD T,T
	FADRB T,SINTM
SINQ:	TRNN U,2
SINR:	SKIPA U,SINTM
	MOVNS U,SINTM
	FMPR U,[164475536723]	;.00015148419
	FMP T,T
	FADR T,[574377353254]	;-4.0084431
	FMPR U,T
	FADR T,[574142162777]	;-6.4652405
	FMPR U,T
	FADR T,[202444705313]	;2.2881683
	FMPR T,T
	FADR T,[210547761702]	;179.98621
	FMPRB T,U
	POPJ P,

COSQ:	MOVSI T,(1.0)
	SKIPL SINTM
	MOVNS T
	FADRB T,SINTM
SINOB:	SETCMI U,.
	JRST SINQ
SQRT:	MOVEM U,TSAV
	MOVMS T
	JUMPLE T,SQ2
	ASHC T,-33
	SUBI T,201
	ROT T,-1
	HRRM T,SQ1
	LSH T,-43
	ASH U,-10
	FSC U,177(T)
	MOVEM U,ST
	FMP U,S1(T)
	FAD U,S2(T)
	MOVE T,ST
	FDV T,U
	FAD U,T
	FSC U,-1
	MOVE T,ST
	FDV T,U
	FADR T,U
SQ1:	FSC T,0
SQ2:	MOVE U,TSAV
	POPJ P,

S1:	0.8125
	0.578125
S2:	0.302734
	0.421875

ST: 0
TSAV:	0

RANDOM:	MOVE T,RN1
	MUL T,[11060471625]
	DIV T,[SETZ-37]
	MOVEM U,RN1
	POPJ P,	;RANDOMNESS IN T EXCEPT TOP FEW BITS

RN1:	11060471625
;DISTANCES ARE IN UNITS OF MINIMUM CELL-TO-CELL SPACING
;POSITIONS ARE MEASURED TO FIRST CELL GENERATED AS (0,0)
;UPDATE FUNC TABLE WHEN CHANGING REFRNO OR SETTIM

DENSIT:	.6	;DESIRED DENSITY OF CELLS
NCELLS:	4096.	;TOTAL NUMBER OF CELLS DESIRED
REFRNO:	10.	;NUMBER OF TICKS CELL STAYS REFRACTORY
SETTIM:	10.+17.	;NUMBER OF TICKS CELL TAKES TO SETTLE
NEIGH:	6.0	;NEIGHBORHOOD RADIUS FOR ANY INTERACTION
NEISQ:	36.0	;PROGRAM SETS TO NEIGH**2
MAXSPA:	3.0	;MAXIMUM SEPARATION OF EACH CELL TO CLOSEST NEIGHBOR
MSPASQ:	9.0	;PROGRAM SETS TO MAXSPA**2
MAXRAD:	1000.0	;OPTIONAL CLUSTER RADIUS UPPER BOUND
ELRAD:	20.0	;RADIUS OF ELEVATED THRESHOLD
ERADSQ:	400.0	;PROGRAM SETS TO ELRAD**2
HEIGHT:	512.0	;HEIGHT OF FIRING IMPULSE
THRESH:	BLOCK 10.+1	;THRESHOLDS TO FIRE AS FUNCTION OF TIME
	47000
	30000
	20000
	14000
	11000
	7000
	6000
	5400
	5000
	4400
	4000
	3400
	3000
	2400
	2000
	1400
	1000

THREL:	BLOCK 10.+1	;ELEVATED THRESHOLDS OF CENTER DISK
	47000*3/2
	30000*3/2
	20000*3/2
	14000*3/2
	11000*3/2
	7000*3/2
	6000*3/2
	5400*3/2
	5000*3/2
	4400*3/2
	4000*3/2
	3400*3/2
	3000*3/2
	2400*3/2
	2000*3/2
	1400*3/2
	1000*3/2
LOWINT:	2	;INTENSITY FOR IDLE CELLS
LOINEL:	1	;INTENSITY FOR IDLE CELLS OF ELEVATED THRESHOLD
MEDINT:	4	;INTENSITY FOR REFRACTORY CELLS
HIINT:	7	;INTENSITY FOR FIRING CELLS
CURRAD:	0	;RADIUS OF PROGRAM GENERATED CLUSTER
TICKS:	0	;NUMBER OF TICKS RUN
STMMAX==100
STMIDX:	0	;NUMBER OF ENTRIES IN STMLIS
STMLIS:	BLOCK STMMAX	;INDEX OF CELLS TO REPEATEDLY STIMULATE
PDL:	BLOCK 20
PATCH:	BLOCK 40

TIMES:	BLOCK MAXCLS+10		;CURRENT STIMULUS,,TIME SINCE LAST FIRING
XS:	BLOCK MAXCLS+10		;X POSITIONS (FLOATING POINT)
YS:	BLOCK MAXCLS+10		;Y

CONSTA
VARIAB
LOC <.+1777>/2000*2000
6LOC0=.
6DISPL==400	;MAX ALLOWED FOR FILMER CODE
LOC 6LOC0+6DISPL

DISPL:	DISP1,,.+1+ELFLG	;CELLS
IFN ELFLG,DISP2,,.+1
DISSWC:	DISP3,,0	;TICK COUNT
	DISP4,,0	;COMMANDS AND CROSSHAIRS

DISP1:	.,,DPY-1

DISP2:	-400-10,,ELDPY-1
ELDPY:	20112
	221000,,141000
IFN ELFLG,BLOCK 400
ELDPYE==.

DISP3:	-10,,DISTIC-1
DISTIC:	20153
	220000,,60000+1777-5*4*6
DISTIK:	404040,,406037
	3000

VEC==4		;HALF LENGTH OF CROSS STROKE
DISP4:	-40,,DISL4-1
DISL4:	20320
	220000+5*2*12.,,0
EINT:	60014
	053011,,243433	;EXIT
DINT:	370000,,60014
	040514,,052405	;DELETE
FINT:	343337,,60014
	061122,,053433	;FIRE
RINT:	370000,,60014
	220506,,220103	;REFRAC
	241722,,313433	;TORY
IINT:	370000,,60014
	110414,,053433	;IDLE
SINT:	370000,,60014
	232411,,152514	;STIMUL
	012405,,370000	;ATE
	20113
CROSS:	221000,,101000
	VEC*401,,200000+VEC*1002
	100000+VEC*3000,,200200+VEC*1002
	200+VEC*2,,300200+VEC*1002
	VEC*3000,,700000+VEC*1002
	3000

DPY=.	;DISPLAY LIST

END
