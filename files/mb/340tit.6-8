TITLE 340 TITLER, PDP-6 PART

A=1
B=2
C=3
D=4
E=5
F=6
G=7
R=10
S=11
T=12
U=13
X=14
Y=15
Z=16
P=17
BEG:	MOVEI P,PDL-1
RESUME:	CLEARM REWFLG
	SETOM 70
	SKIPE 70
	JRST .-1
	CLEARM NDISLS
	MOVE R,[442200,,DISL]	;DISPLAY LIST BYTE POINTER
	MOVE B,[440700,,IBUF]
	CLEARM MODE	;PARAMETER MODE
	CLEARM SHIFT	;UPPER CASE
READ:	ILDB T,B
	CAIL T,40
	JRST CHAR	;IT IS A CHARACTER
	CAIN T,12
	JRST LF
	CAIN T,15
	JRST CR
	CAIN T,2	;CTRL B - SET BRIGHTNESS FOR FOLLOWING TEXT
	JRST BRITE
	CAIN T,4	;CTRL D - SET CHARACTER SCALE FOR FOLLOWING TEXT
	JRST CSIZE
	CAIN T,6	;CTRL F - SET NUMBER OF FRAMES FOR TEXT
	JRST FRAMES
	CAIN T,14	;CTRL L - END OF TEXT, START FILMING IT
	JRST PAGEND
	CAIN T,16	;CTRL N - SET LIMIT OF EXPOSURES PER FILM FRAME FOR FOLLOWING TEXT
	JRST NDISS
	CAIN T,22	;CTRL R - SET REWIND ACTION
	JRST SETREW
	CAIN T,31	;CTRL Y - SET X AND Y
	JRST COORDS
	JRST 4,.	;UNUSED CONTROL CHARACTER
LF:	MOVEI T,33
	JRST CHAR1

CR:	MOVEI T,34
	JRST CHAR1

CHAR:	SKIPGE T,CHARTB-40(T)
	JRST 4,.
CHAR1:	SKIPN MODE
	PUSHJ P,GETMD3
	MOVE U,SHIFT
	LSH U,6
	XOR U,T		;100 BIT = SHIFT OUT = LOWER CASE
	TRNE U,100
	CAIN T,40	;DO NOT SHIFT FOR SPACE
	SKIPA
	PUSHJ P,DOSHFT
	IDPB T,R
	JRST READ

DOSHFT:	MOVEI U,1
	XORB U,SHIFT
	ADDI U,35
	IDPB U,R
	POPJ P,
;IN LOCATION CHARTB+ASCII-40 IS 340 CODE (+100 IF SHIFT OUT)
CHARTB:	40	;SPACE
	41	;!
	42	;"
	43	;#
	44	;$
	45	;%
	46	;&
	47	;'
	50	;(
	51	;)
	52	;*
	53	;+
	54	;,
	55	;-
	56	;.
	57	;/
	60	;0
	61 ? 62 ? 63 ? 64 ? 65 ? 66 ? 67 ? 70
	71	;9
	72	;:
	73	;;
	74	;<
	75	;=
	76	;>
	77	;?
	0	;@
	1	;UPPER CASE A
	02 ? 03 ? 04 ? 05 ? 06 ? 07
	10 ? 11 ? 12 ? 13 ? 14 ? 15
	16 ? 17 ? 20 ? 21 ? 22 ? 23
	24 ? 25 ? 26 ? 27 ? 30 ? 31
	32	;UPPER CASE Z
	153	;[
	152	;\
	154	;]
	146	;^
	151	;_
	166	;ACCENT GRAVE
	101	;LOWER CASE A
	102 ? 103 ? 104 ? 105 ? 106 ? 107
	110 ? 111 ? 112 ? 113 ? 114 ? 115
	116 ? 117 ? 120 ? 121 ? 122 ? 123
	124 ? 125 ? 126 ? 127 ? 130 ? 131
	132	;LOWER CASE Z
	REPEAT 5,-1
BRITE:	PUSHJ P,GETARG	;SET BRIGHTNESS (INTENSITY)
	MOVEM T,BRITEN
	SKIPE MODE
	PUSHJ P,GETMD0
	JRST READ

CSIZE:	PUSHJ P,GETARG	;SET CHARACTER SIZE
	MOVEM T,CSIZ
	SKIPE MODE
	PUSHJ P,GETMD0
	JRST READ

FRAMES:	PUSHJ P,GETARG	;SET NUMBER OF FRAMES FOR THIS PAGE
	MOVEM T,FRMLIM
	JRST READ

NDISS:	PUSHJ P,GETARG	;SET NUMBER OF CYCLES PER FRAME FOR FOLLOWING TEXT
	SKIPE MODE
	PUSHJ P,GETMD0
	MOVEI U,
	TLNE R,220000
	IDPB U,R
	MOVE U,NDISLS
	HRRM R,DISLL-1(U)
	HRLZM T,DISLL(U)
	AOS NDISLS
	JRST READ

SETREW:	PUSHJ P,GETARG	;SET REWIND ACTION AT SCENE END
	MOVEM T,REWFLG
	JRST READ

COORDS:	SKIPE MODE
	PUSHJ P,GETMD0
	PUSHJ P,GETMD1
	PUSHJ P,GETARG	;SET Y COORDINATE
	IORI T,220000
	IDPB T,R
	PUSHJ P,GETARG	;AND X ALSO, GIVEN ON NEXT LINE
	IDPB T,R
	JRST READ
GETARG:	MOVEI T,8
	ILDB S,B
	CAIN S,".	;PREFIX DECIMAL POINT NOTATION
	MOVEI T,12
	HRRM T,GETA2
	MOVEI T,
	CAIN S,".
GETA1:	ILDB S,B
	CAIL S,"0
	CAILE S,"9
	JRST GETA3
GETA2:	IMULI T,.
	ADDI T,-"0(S)
	JRST GETA1

GETA3:	CAIN S,12
	POPJ P,
	ILDB S,B	;GOBBLE THROUGH LINEFEED
	JRST GETA3

GETMD0:	MOVEI S,37	;GET TO MODE 0 FROM 3
	IDPB S,R
	TLNE R,140000
	JRST GETMD0	;FINISH OUT HALFWORD
	MOVEI S,22
	DPB S,[300600,,R]	;CHANGE BYTE TO 18 BITS
	CLEARM MODE
	POPJ P,

GETMD1:	MOVEI S,20000	;GET TO MODE 1 FROM 0
	IDPB S,R
;MODE WILL BE RETURNED TO 0 IMMEDIATELY
	POPJ P,

GETMD3:	MOVE S,CSIZ	;GET TO MODE 3 FROM 0
	LSH S,4
	IOR S,BRITEN
	IORI S,60110
	IDPB S,R
	MOVEI S,06
	DPB S,[300600,,R]	;CHANGE BYTE TO 6 BITS
	MOVEI S,3
	MOVEM S,MODE
	POPJ P,
PAGEND:	SKIPE MODE
	PUSHJ P,GETMD0
	MOVEI U,
	TLNE R,220000
	IDPB U,R
	MOVE U,NDISLS
	HRRM R,DISLL-1(U)
	DATAI A
	TRNN A,1
	JRST FILM	;BIT 35 OF DATA SW 0 = FILM
VIEW:	DATAI A
	TRNN A,1
	JRST 4,RESUME	;PUT SW DOWN FOR NEXT PAGE
	CONO DIS,100
	MOVEI T,FADENO
	PUSHJ P,DPY
	JRST VIEW

DPY:	MOVN A,NDISLS
	HRLZS A
	MOVEI D,DISL
DPY1:	HLRZ B,DISLL(A)	;COUNT OF DISPLAY LIST REPEATS
	IMUL B,T	;CURRENT WEIGHTING
	IDIV B,FADENO
	SKIPE C
	AOS B		;ROUND UP
	HRRZ C,D
	SUB C,DISLL(A)
	HRLZI C,-1(C)	;COUNT OF DISL ENTRIES
	HRR C,D		;FIRST DISL ENTRY
DPY2:	MOVE D,C
DPY3:	CONSO DIS,200	;EDGING WILL CAUSE LOOP HERE
	JRST .-1
	DATAO DIS,(D)
	AOBJN D,DPY3
	SOJG B,DPY2
	AOBJN A,DPY1
	CONSO DIS,200
	JRST .-1
	DATAO DIS,[20000]
	CONSO DIS,200
	JRST .-1
	DATAO DIS,[220000,,0]	;GET BEAM OUT OF PICTURE
	CONSO DIS,200
	JRST .-1
	POPJ P,
FADENO==24.	;FRAMES IN FADE

FILM:	MOVE T,FRMLIM
	CAIGE T,2*FADENO
	JRST 4,.	;NOT ENOUGH FRAMES
	CONO DIS,100
	MOVEI T,1	;FADE IN
FILM1:	PUSHJ P,FRAME
	PUSHJ P,DPY
	CAIE T,FADENO
	AOJA T,FILM1
	MOVE U,FRMLIM
	SUBI U,2*FADENO
FILM2:	JUMPE U,FILM3
	PUSHJ P,FRAME
	PUSHJ P,DPY
	SOJA U,FILM2

FILM3:	MOVEI T,FADENO	;FADE OUT
FILM4:	PUSHJ P,FRAME
	PUSHJ P,DPY
	SOJG T,FILM4
	PUSHJ P,CLOSE
	SKIPN T,REWFLG
	JRST 4,RESUME	;NO REWIND ACTION
	MOVEI U,200000
	SOJG U,.
	MOVEI U,FADENO
	CAIE T,1
	MOVE U,FRMLIM
	PUSHJ P,RFRAME
	SOJG U,.-1
	JRST 4,RESUME
;CCW = FWD = 0, 100, 300, 200, 0, ...
;WHEN LOADED, THINKS SHUTTER IS CLOSED
;CLOBBERS NO ACCUMULATORS

CLOSE:	SKIPN SHUTTR
	POPJ P,
	CLEARM SHUTTR
	JRST OPEN1

FRAME:	PUSHJ P,CLOSE
	SKIPE STOP
	JRST .-1
OPEN:	SKIPE SHUTTR
	POPJ P,
	SETOM SHUTTR
OPEN1:	PUSH P,A
	PUSH P,B
	MOVEI A,62	;25. COMPLETE CYCLES = 1/2 REVOLUTION
OPEN2:	MOVEI B,100
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,550	;PDP-10 2000 MISSES ABOUT 1 STEP/REVOLUTION
	SOJG B,.
	MOVEI B,200
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,550
	SOJG B,.
	SOJG A,OPEN2
	AOS NHREVS
	POP P,B
	POP P,A
	POPJ P,

SHUTTR:	0	;0 = SHUTTER CLOSED, -1 = OPEN
MOTOR:	0	;LAST DATAO TO MOTOR
NHREVS:	0	;NUMBER OF HALF REVOLUTIONS SINCE START
STOP:	0	;NON-ZERO = PAUSE WITH SHUTTER CLOSED

RFRAME:	PUSH P,A	;1 FRAME REVERSE MOTION
	PUSH P,B
	MOVEI A,144
RFR2:	MOVEI B,200
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,550
	SOJG B,.
	MOVEI B,100
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,550
	SOJG B,.
	SOJG A,RFR2
	SOS NHREVS
	SOS NHREVS
	POP P,B
	POP P,A
	POPJ P,
PATCH:	BLOCK 40
PDL:	BLOCK 10

BRITEN:	0	;INTENSITY LEVEL
CSIZ:	0	;CHARACTER SIZE
FRMLIM:	0	;FRAMES PER PAGE OF TEXT
REWFLG:	0	;0 = NO ACTION AT END OF SCENE, 1 = REWIND TO
		;START OF FADE-OUT, >1 = REWIND TO START OF SCENE
MODE:	0	;340 MODE, 0 (PARAMETER) OR 3 (CHARACTER), POINT ONLY MOMENTARILY
SHIFT:	0
NDISLS:	0	;NUMBER OF ENTRIES IN DISLL, DISPLAY LIST LIST

	0
DISLL:	BLOCK 100	;MAX NUMBER OF DIFERENT CTRL-F'S EXPECTED
;REPEAT COUNT,,LAST LOCATION

DISL:	BLOCK 2000

CONSTA
VARIAB

6IBUF==4000
IFG .-6IBUF,PRINTC \CODE DOES NOT FIT\
LOC 6IBUF
IBUF:	BLOCK 2000

END BEG
