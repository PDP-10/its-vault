TITLE WEAVING PATTERNER

A=1
B=2
C=3
D=4
E=5
F=6
G=7
R=10
S=11
T=12
U=13
X=14
Y=15
Z=16
P=17

NCOLRS==7	;NUMBER OF THREAD COLORS

DEFINE BARF A
	MOVE S,[440700,,[ASCII \A!$\]]
	PUSHJ P,TYPMES
TERMIN

DEFINE FLOAT X
	TLC X,232000
	FADR X,X
TERMIN

DEFINE FIX X
	MULI X,400
	TSC X,X
	ASH X+1,-243(X)
TERMIN
BEG:	MOVEI P,PDL-1
	.OPEN 1,.+1
	1,,(SIXBIT \LPT\)
	MOVE A,RN
BARF [INITIAL RN = #1]
	PUSHJ P,CRLF
	PUSHJ P,CRLF
	CLEARB A,B
BEG1:	ADD B,SECSIZ(A)
	ADDI A,NCOLRS+1
	SKIPL SECSIZ(A)
	JRST BEG1
	IDIV B,WSSIZ
	IDIVI C,2
	MOVE B,WSSIZ
	SUB B,C
	MOVEM B,WSCNT
	JRST TSTSIZ
NEWSEC:	MOVSI A,-NCOLRS	;BEGIN A NEW SECTION OF THREADS
	CLEARB B,C
SUMLUP:	HLRZ T,PROB(A)
	FLOAT T
	FADR B,T
	HRRZ T,PROB(A)
	FLOAT T
	FADR C,T
	AOBJN A,SUMLUP
	MOVE T,SECSIZ
	SOS T
	FLOAT T
	FMPR B,T
	FMPR C,T
	MOVEM B,BEGSUM
	MOVEM C,ENDSUM
	MOVE T,THDCNT
	MOVEM T,SECBEG
SECLUP:	MOVE T,RN	;RANDOMIZE T
	MUL T,[11060471625]
	DIV T,[SETZ-37]
	MOVEM U,RN
	ANDI T,-1
	SKIPN T
	AOS T
	FLOAT T
	FSC T,-22	;EPSILON TO 1-EPSILON
	MOVSI A,-NCOLRS
	MOVEI B,
COLLUP:	MOVE C,SECBEG
	ADD C,SECSIZ
	SOS C		;LAST THREAD OF SECTION
	SUB C,THDCNT
	FLOAT C
	HLRZ D,PROB(A)
	FLOAT D
	FMPR C,D
	FDVR C,BEGSUM	;GETTING WEIGHTED INITIAL PROBABILITY
	FADR B,C
	MOVE C,THDCNT
	SUB C,SECBEG
	FLOAT C
	HRRZ D,PROB(A)
	FLOAT D
	FMPR C,D
	FDVR C,ENDSUM	;GETTING WEIGHTED FINAL PROBABILITY
	FADR B,C
	CAML B,T
	JRST GOTCOL
	AOBJN A,COLLUP
LOSE:	.VALUE		;SHOULD ALWAYS FIND A COLOR
	0
GOTCOL:	AOS USETAB(A)
	SKIPE QUICKF
	JRST THDDUN
	MOVE B,THDCNT
BARF [#2.   ]
	MOVE S,NAMES(A)
	CAMN A,OLDA
	MOVE S,SAMNAM
	PUSHJ P,TYPMES
	PUSHJ P,CRLF
THDDUN:	MOVEM A,OLDA
	AOS WSUSE(A)
	AOS A,WSCNT
	CAMN A,WSSIZ
	PUSHJ P,DOWSEC
	AOS A,THDCNT
	SUB A,SECSIZ
	CAME A,SECBEG
	JRST SECLUP	;NOT DONE WITH SECTION YET
ZROLUP:	MOVE A,[PROB+NCOLRS,,SECSIZ]
	BLT A,PRBEND-NCOLRS-1
TSTSIZ:	SKIPN SECSIZ
	JRST ZROLUP	;ALLOW, BUT SKIP, SECTIONS OF ZERO THREADS
	SKIPL SECSIZ
	JRST NEWSEC
	PUSHJ P,DOWSEC
	PUSHJ P,CRLF
	MOVSI A,-NCOLRS
TYOUSE:	MOVE S,NAMES(A)
	PUSHJ P,TYPMES
	MOVE B,USETAB(A)
	MOVE C,B
	IMUL C,LENGTH
	IDIVI C,12.
	CAIL D,6
	AOS C
BARF [   #2. THREADS, #3. FEET]
	PUSHJ P,CRLF
	AOBJN A,TYOUSE
	.CLOSE 1,
	.VALUE
	0
DOWSEC:	SKIPN WARPF
	POPJ P,
	PUSHJ P,CRLF
	AOS A,WSNUM
BARF [WARP SECTION #1. SUMMARY]
	PUSHJ P,CRLF
	MOVSI A,-NCOLRS
WSTYO:	MOVE S,NAMES(A)
	PUSHJ P,TYPMES
	MOVE B,WSUSE(A)
BARF [   #2. THREADS]
	PUSHJ P,CRLF
	AOBJN A,WSTYO
	PUSHJ P,CRLF
	CLEARM WSCNT
	CLEARM WSUSE
	MOVE A,[WSUSE,,WSUSE+1]
	BLT A,WSUSE+NCOLRS-1
	POPJ P,
TYPMES:	ILDB T,S
	CAIN T,"$
	POPJ P,
	CAIN T,"#
	JRST ACNUM
	PUSHJ P,TYO
	JRST TYPMES

ACNUM:	PUSH P,[TYPMES]
	ILDB U,S
	MOVE T,S
	ILDB T,T
	XORI T,".#12
	CAIE T,12
	MOVEI T,10
	HRRM T,ACN2
	MOVE T,-"0(U)
ACN2:	IDIVI T,.
	HRLM U,(P)
	SKIPE T
	PUSHJ P,ACN2
	HLRZ T,(P)
	ADDI T,"0
	JRST TYO

CRLF:	MOVEI T,15
	PUSHJ P,TYO
	MOVEI T,12
TYO:	.IOT 1,T
	POPJ P,

PDL:	BLOCK 20
PATCH:	BLOCK 40
LENGTH:	246.	;THREAD LENGTH, INCHES, WARP 246., WEFT 50.

SAMNAM:	440700,,[ASCII \   "$\]
NAMES:	440700,,[ASCII \YELLOW$\]
	440700,,[ASCII \LIGHT GREEN$\]
	440700,,[ASCII \GREEN HEATHER$\]
	440700,,[ASCII \PADDY GREEN$\]
	440700,,[ASCII \MOSS GREEN$\]
	440700,,[ASCII \DARK GREEN$\]
	440700,,[ASCII \BLACK$\]

SECSIZ:	50.	;SIZE OF SECTION, IN THREADS
PROB:		;COLOR PROBABILITIES AT START,,END OF SECTION
	0,,0	;YELLOW
	0,,0	;LIGHT
	0,,0	;HEATHER
	1,,1	;PADDY
	2,,1	;MOSS
	2,,1	;DARK
	2,,0	;BLACK

	100.
	0,,1
	0,,1
	0,,1
	1,,1
	1,,0
	1,,0
	0,,0

	105.	;105. FOR WARP, 348. FOR WEFT
	1,,1
	1,,1
	1,,1
	1,,1
	0,,0
	0,,0
	0,,0

	100.
	1,,0
	1,,0
	1,,0
	1,,1
	0,,1
	0,,1
	0,,0

	50.
	0,,0
	0,,0
	0,,0
	1,,1
	1,,2
	1,,2
	0,,2

PRBEND:	-1	;SIGNALS END OF SECSIZ AND PROB TABLES

WSSIZ:	18.	;SIZE OF WARP SECTIONS
WARPF:	0	;-1 FOR WARP SECTION SUMMARIES
QUICKF:	0	;-1 FOR ONLY FINAL SUMMARY
WSUSE:	BLOCK NCOLRS
WSCNT:	0	;COUNTS THREADS IN WARP SECTION
WSNUM:	0	;COUNTS WARP SECTIONS
THDCNT:	1	;NUMBER OF CURRENT THREAD
SECBEG:	0	;THDCNT AT START OF CURRENT SECTION
RN:	11060471625
BEGSUM:	0
ENDSUM:	0
USETAB:	BLOCK NCOLRS	;COUNTS USES OF EACH COLOR
OLDA:	0

END BEG
