	.MACR SPUSH AA
	MOV AA,-(P)
	.ENDM

	.MACR SPOP AA
	MOV (P)+,AA
	.ENDM
A=%0
B=%1
C=%2
D=%3
E=%4
F=%5
U=%5
P=%6
SP=%6
PC=%7
;DC11 HIGH SPEED LIN INTERFACE
;BREAK LEVEL 5
DCRBRV=300	;RECIEVE BREAK VECTOR
DCRS=174000	;RECIEVE STATUS WORD
DCRB=174002	;RECIEVE BUFFER
DCTBRV=304	;TRANSMIT BREAK VECTOR
DCTS=174004	;TRANSMIT STATUS
DCTB=174006	;TRANSMIT BUFFER
.=DCRBRV
DCRBRK
240
.=DCTBRV
DCTBRK
240
.=1000
START:	RESET
	MOV SPDLP,P	;SET UP SYSTEM PDL
	MOV #121,DCRS
	MOV #121,DCTS
	MOV #'Z-100,DCTB
	WAIT
	BR .-2

;LOGIN STRING
LOGMES:	.BYTE 'Z-100,'S-100,'S-100
	.ASCII /11LOGO/
	.BYTE 33
	.ASCII /U11HERE/
	.BYTE 'K-100,0
	.EVEN

;VARIABLES
RJLSW:	0	;NON-ZERO=> ACTIVATE COMMUNICATE LINK
RFLSAD:	RLOGIN
TFLSAD:	TLOGIN
RBPOIN:	0
TBPOIN:	0

;BUFFERS

;RECIEVE INTERUPT ROUTINES

DCRBRK:	SPUSH A
	MOV DCRB,A
	JMP @RFLSAD
RFLUSH:	SPOP RFLSAD
	SPOP A
	RTI

RLOGIN:	BIS #100,DCTS	;WAKE UP TRANSMIT ROUTINE
	JSR PC,RFLUSH
	BR RLOGIN

;TRANSMIT INTERUPT ROUTINES

DCTBRK:	SPUSH A
	JMP @TFLSAD
TFLUSH:	MOV A,DCTB
T1FLSH:	SPOP TFLSAD
	SPOP A
	RTI

TLOGIN:	MOV #'Z-100,A
	JSR PC,TFLUSH
	MOV #LOGMES,TBPOIN
LOGSEN:	MOVB @TBPOIN,A
	BEQ LOGDNE
	INC TBPOIN
	BIC #100,DCTS	;DON'T CARE IF NO ECHO
	JSR PC,TFLUSH
	BR LOGSEN
LOGDNE:	BIC #100,DCTS	;PUT IT TO SLEEP
	JSR PC,T1FLSH
	BR LOGDNE

	.=.+200
SPDLP:	.
	.END START
