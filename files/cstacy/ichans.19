;-*-Midas-*-

	Title :ICHANS - List status of all I/O channels for a job

A=1	;General porpoise
B=2
C=3
D=4
E=5
T1=6
T2=7
T3=10
P=17

Call=<PUSHJ P,>
Return=<POPJ P,>

TTYo==1
USRi==2

PDLen==10
JCLen==5

 ;;;
;;;;;
 ;;;

Define SYSCAL op,args
	.Call [Setz ? Sixbit /op/ ? args ((Setz))]
Termin

 ;;;
;;;;;
 ;;;

PDList:	-PDLen,,.
	Block PDLen

JCLbuf:	Block JCLen
	-1

Jname:	0
Uname:	0

Device:	0
Sname:	0
FN1:	0
FN2:	0
Mode:	0

 ;;;
;;;;;
 ;;;

Begin:	Move P,PDList
	Syscal OPEN,[%Clbit,,.uao
		     %Climm,,TTYo
		     [Sixbit /TTY/]]
	  .Lose %LSFil
	.Break 12,[..rJCL,,JCLbuf]
	Skipn JCLbuf
	  Jrst dFalt
	Move A,[440700,,JCLbuf]
	Movei D,Jname
	Movei E,2
Parse:	Movei B,6
	Setz T2,
	Move C,[440600,,T2]
GetSix:	Ildb T1,A
	Caie T1,^M
	  Cain T1,^C
	    Jrst GotSix
	Caie T1,40
	  Skipn T1
	    Jrst GotSix
	Jumpe B,GetSix
	Cail T1,140
	  Trz T1,40	;Uppercase
	Subi T1,40
	Idpb T1,C
	Soja B,GetSix
GotSix:	Jumpn T2,Saveit
GS1:	Caie T1,^M
	  Cain T1,^C
	    Jrst dFalt
	Jrst Parse
Saveit:	Movem T2,(D)
	Aoj D,
	Soje E,dFalt
	Jrst GS1
dFalt:	Skipn Uname	;If one thing given, it's jname, if two given,
	  Jrst dF1	;it's jname then uname, so switch them around
	Move T1,Jname	;to right order.
	Exch T1,Uname
	Movem T1,Jname
	Jrst Hehere
dF1:	.Suset [.rUname,,Uname]
	Skipe Jname
	  Jrst Hehere
	.Value [Asciz " J .JNAME/ 1J 1/ 1Qî :VP "]
	Movem A,Jname

Hehere:	Syscal OPEN,[%Clbit,,.bai
		     %Climm,,USRi
		     [Sixbit /USR/]
		     Uname
		     Jname]
	  .Value [Asciz /: No such job? KILL /]
	Setzb A,B
Loop:	Syscal RFNAME,[%Climm,,USRi
		       A
		       %Clout,,Device
		       %Clout,,FN1
		       %Clout,,FN2
		       %Clout,,Sname
		       %Clout,,Mode]
	  .Lose %LsSys
	Skipn Device
	  Jrst NLoop
	Skipe B
	  .Iot TTYo,[^M]
	Seto B,
	Move T1,A
	Cail T1,10
	  Jrst [Idivi T1,10
		Addi T1,60
		.Iot TTYo,T1
		Move T1,T2
		Jrst .+2]
	.Iot TTYo,[" ]
	Addi T1,60
	.Iot TTYo,T1
	.Iot TTYo,[")]
	.Iot TTYo,[" ]
	Move T1,Device
	Call 6Type
	.Iot TTYo,[":]
	.Iot TTYo,[" ]
	Skipn Sname
	  Jrst pFN1
	Move T1,Sname
	Call 6Type
	.Iot TTYo,[";]
	.Iot TTYo,[" ]
pFN1:	Skipn FN1
	  Jrst pFN2
	Move T1,FN1
	Call 6Type
	.Iot TTYo,[" ]
pFN2:	Skipn FN2
	  Jrst pMode
	Move T1,FN2
	Call 6Type
	.Iot TTYo,[" ]
pMode:	.Iot TTYo,[" ]
	Move T2,Mode
	.Iot TTYo,["(]
	.Iot TTYo,[".]
	Movei T1,"u
	Trne T2,2	;1.2	unit/block
	  Movei T1,"b
	.Iot TTYo,T1
	Movei T1,"a
	Trne T2,4	;1.3	ascii/image
	  Movei T1,"i
	.Iot TTYo,T1
	Movei T1,"i
	Trne T2,1	;1.1	input/output
	  Movei T1,"o
	.Iot TTYo,T1
	Trz T2,7
	Jumpe T2,Closep
	.Iot TTYo,["\]
	Hrrz T1,T2
	Setz T3,
SLoop1:	Idivi T1,10
	Push P,T2
	Aoj T3,
	Jumpn T1,SLoop1
SLoop2:	Pop P,T1
	Addi T1,60
	.Iot TTYo,T1
	Sojn T3,SLoop2
Closep:	.Iot TTYo,[")]
NLoop:	Aoj A,
	Caie A,20
	  Jrst Loop
	.Break 16,144000

 ;;;
;;;;;
 ;;;

6Type:	Setz T2,
	Rotc T1,6
	Addi T2,40
	.Iot TTYo,T2
	Jumpn T1,6Type
	Return

	End Begin
