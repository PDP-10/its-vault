;RESTART CODE!
NODSKO,<
NOSPLIT,<
SAVE:	MOVE T,RSTSET		;TRY 100 COMMANDS FOR NOW
	MOVEM T,RSTCNT
	TRNN FLAG,RSTENB	;CAN HE DO THIS?
	POPJ P,
	PUSHJ P,FORCE
	OUTSTR[ASCIZ/SAVING/]
FOR I_0,16
<	PUSH P,I
>
	MOVEM P,SAVEP
	MOVE T,.JBREL
	ADDI T,1777
	LSH T,-12
	HRLM T,SIZE
	JSA 16,WHERE
	JUMP SX
	JUMP SY
	MOVE 1,SY
	HRL 1,SX
	MOVSI 3,PSWAP
CMU,<OPDEF SWAP[0]>;TEMPORARY
	SWAP 3,
MERGE:	JSA 16,PLOTS
	JUMP THING1
	SKIPE THING1
	JRST [	OUTSTR[ASCIZ/FAILURE TO RESTART, PLOTTER NOT AVAILABLE!
/]
		EXIT 1,
		JRST MERGE]
	SKIPE %ROTIT
	SETOM ROTIT		;PRESERVE ORIENTATION
	JSA 16,SETXY		;SET X,Y TO CONTENTS OF 1
	MOVE P,SAVEP
FOR I_16,0,-1
<	POP P,I
>
MXGP,<	SKIPE %XTHICK
	SETOM XTHICK
>;MXGP
	OUTSTR[ASCIZ/
/]
	POPJ P,

PLTRST:
MD,<	MOVE 1,RSTART	>
MPC,<	SETZ 1,		>
	JRST MERGE
>;NOSPLIT
>;NODSKO
;NAMSET
NAMSET:	SETZM SAVNAM			;FORGET SAVED NAME FOR NEW FILE
ASKHIM:	MOVSI T,EXTPLT
	PUSHJ P,SETIT
	JRST ASKHIM
	MOVE T,FILNAM
	MOVEM T,SAVNAM			;SAVE FILENAME FOR PLOT BOX AND FR80 NAME
MD,<	MOVEM T,BOXNAM	>		;SET THIS IN CASE IT ISN'T SET LATER
	TLNE FLAG,NODEL		;SUPPRESS DELETE AT END?
	SETZ T,			;YES
	MOVEM T,DELNAM			;SAVE FOR DELETE
	MOVE T,FILPPN
	MOVEM T,DELPPN
MD,<	MOVEM T,BOXPPN			;SAME HERE
	MOVE T,FILEXT
	MOVEM T,DELEXT
>;MD
MPC,<	MOVEM T,SAVPPN
	HLLZ T,FILEXT
	MOVEM T,SAVEXT
	MOVEM T,DELEXT
>;MPC
	LOOKUP DAT,FILNAM
	JRST [OUTSTR [ASCIZ /LOOKUP FAILED!
/]
		JRST ASKHIM]
	MOVE T,.JBFF
	ADDI T,4000
	CORE T,
	JRST [	EXIT 1,
		JRST .-3 ]
	MOVE TT,.JBFF
	ADDI TT,10
	MOVE T,.JBREL
	SUB T,TT
	MOVEM TT,T3S
	MOVEM TT,T3P
	IDIVI T,3
	MOVE TT,T
	ADD TT,T3S
	MOVEM TT,T2S
	MOVEM TT,T2P
	ADD T,TT
	MOVEM T,T1S
	MOVEM T,T1P
	HRLOI T,377777
	MOVEM T,SMLY
	MOVEM T,SMLX
	MOVSI T,400000
	MOVEM T,LRGY
	MOVEM T,LRGX
MPC,<	SETOM BIGPAD	>
	POPJ P,

MXGP,<
PLTCHK:	TRNN FLAG,XGP
	POPJ P,
	MOVSI T,EXTPLX
	PUSHJ P,FILSET		;SET DEFAULT NAME
	MOVE T,FILNAM
	MOVEM T,XGPNAM
	MOVE T,FILEXT
	HLLZM T,XGPEXT
	MOVE T,FILPPN
	MOVEM T,XGPPPN
	ENTER PCHN,FILNAM
	TRZA FLAG,XGP!%DOPLT	;LOSE, TRY TO IGNORE THIS
	POPJ P,
	OUTSTR[ASCIZ/PLX FILE ENTER FAILED!
/]
	RELEASE PCHN,
	EXIT 1,
	JRST CPOPJ
>;MXGP

PLOCHK:	TRNN FLAG,DODSK
	POPJ P,
NOITS,<	MOVEI T,PCHN
	DEVCHR T,		;DEVCHR
	TLNN T,4		;DIRECTORY DEVICE?
	POPJ P,
>;NOITS
SPL,<	TLNE FLAG,SKPSPL
	JRST DOREGU
	MOVSI T,EXTPLO
	MOVEM T,FILEXT
	TIMER T,
	LSH T,1
	MOVEM T,FILNAM
	DATE T,
	DPB T,[POINT 12,FILNAM,11]
	PJOB T,
	DPB T,[POINT 6,FILNAM,35]
	SETZM FILDAT
	MOVE T,[SPLPPN]
	MOVEM T,FILPPN
	JRST NOREGU

DOREGU:
>;SPL
	MOVSI T,EXTPLO
	PUSHJ P,FILSET
NOREGU:
	ENTER PCHN,FILNAM
	JRST [
NOSPLIT,<	OUTSTR [ASCIZ /PLT ENTER FAILED!
/]
>;NOSPLIT
SPLIT,<		OUTSTR [ASCIZ/PLO ENTER FAILED!
/]
>;SPLIT
		TRZ FLAG,DODSK
		EXIT 1,
		JRST .+1]
	POPJ P,

;ASK FILENAME

;SET DEFAULT FILENAME.EXT
FILSET:	MOVEM T,FILEXT
	MOVE T,SAVNAM
	MOVEM T,FILNAM
	SETZM FILEXT+1
	SETZM FILPPN
	POPJ P,

SETIT:	MOVEM T,EXTSAV
SETNAM:	MOVE TT,EXTSAV
	PUSHJ P,SIXOUT
	TLNE FLAG,QUICK
	OUTCHR["?"]
	TLNN FLAG,QUICK
	OUTSTR [ASCIZ/ FILENAME?/]
SETIT1:	MOVE T,EXTSAV
	MOVEM T,FILEXT
	SETZM FILEXT+1		;CLEAR THIS WORD
	SETZM FILPPN		;HIS OWN AREA
	PUSHJ P,GETWRD		;SCAN FILENAME
	CAIN T,"@"
	JUMPE TT,INDIR
	CAIN T,12
	JUMPE TT,CPOPJ		;LET HIM OUT IF HE TYPES NOTHING
	JUMPN TT,NOLSTG
	CAIE T,""
	JRST NOLSTG
	PUSHJ P,GETWRD
	JUMPN TT,ILLNAM
	SKIPN TT,SAVNAM
	JRST ILLNAM
NOLSTG:
ITS,<
	CAIE T,";"
	JRST NOLST1
	MOVEM TT,FILPPN
	PUSHJ P,GETWRD
	JUMPE TT,CPOPJ
NOLST1:	CAIE T,40
	CAIN T,12
	SKIPA
	JRST [	ILLNAM:	PUSHJ P,IERR	;ILLEGAL BREAK CHARACTER
			OUTSTR[ASCIZ/???
/]
			JRST SETNAM]
	MOVEM TT,FILNAM
	CAIN T,12
	JRST CPOPJ1	;DONE
	PUSHJ P,GETWRD	;GET SECOND FILE NAME
	CAIE T,12
	JRST ILLNAM
	JUMPE TT,ILLNAM
	MOVEM TT,FILEXT
	JRST CPOPJ1
>;ITS
NOITS,<	MOVEM TT,FILNAM		;SAVE FILENAME
	CAIN T,"["		;ANY EXTENSION?
	JRST NOEXT		;NO
	CAIN T,12
	JRST NOEXT		;NO
	CAIE T,"."
	JRST [	ILLNAM:	PUSHJ P,IERR;ILLEGAL BREAK CHARACTER
			OUTSTR[ASCIZ/???
/]
			JRST SETNAM]
	PUSHJ P,GETWRD		;SCAN EXTENSION
	HLLZM TT,FILEXT		;SAVE IT.
NOEXT:	CAIN T,12		;HERE TO SCAN PPN
	JRST CPOPJ1		;LEAVE NOW
	CAIE T,"["
	JRST ILLNAM		;MUST BE [
NOCMU,<
	PUSHJ P,GETWRP		;SCAN P.
	JUMPE TT,ILLNAM
	HRLM TT,FILPPN		;AND SAVE
	CAIE T,","		;BETTER BE COMMA
NOSTAN,<	JRST ILLNAM	>
STAN,<	JRST [	SETZ TT,
		DSKPPN TT,
		HRRM TT,FILPPN
		JRST NOPN]
>;STAN
	PUSHJ P,GETWRP		;SCAN PN.
	JUMPE TT,ILLNAM
	HRRM TT,FILPPN		;AND SAVE
>;NOCMU
CMU,<	SETZM PPNBUF		;CLEAR OUT A BUFFER FOR THE PPN
	SETZM PPNBUF+1
	SETZM PPNBUF+2
	MOVE A,[POINT 7,PPNBUF]
	MOVEI TTT,=13		;13 CHARACTERS AT MOST!
CMUPP1:	PUSHJ P,GETCHL		;GET A CHAR
	CAIL T,"0"		;0-9 ARE LEGAL IN PPN'S
	CAILE T,"9"
	CAIN T,","		;SO IS COMMA
	JRST CMUPP2		;SO GO STORE IT
	CAIL T,"a"		;CONVERT LOWER CASE TO UPPER CASE
	CAILE T,"z"
	JRST .+2
	SUBI T,40		;CONVERT IT
	CAIL T,"A"		;IS IT A LETTER
	CAILE T,"Z"
	JRST CMUPP3		;NO, MUST BE THE END
CMUPP2:	IDPB T,A
	SOJG TTT,CMUPP1		;AND GO GET ANOTHER CHAR UNLES WE HAVE 13
	PUSHJ P,GETCHL		;IN WHICH CASE WE GET THE "]" (WE HOPE!)
CMUPP3:	MOVE A,[XWD FILPPN,PPNBUF]
	CMUDEC A,		;CONVERT THE PPN TO DEC FORMAT
	JRST ILLNAM		;WHOOPS, BAD PPN
>;CMU
NOPN:	CAIN T,12
	JRST CPOPJ1
	CAIE T,"]"		;BETTER END WITH THIS
	JRST ILLNAM
	PUSHJ P,GETWRD		;MAKE SURE HE DIDN'T TYPE TO MUCH
	JUMPN TT,ILLNAM		;LOSE IF HE DID
	CAIE T,12
	JRST ILLNAM		;DIDN'TT END WITH LF
	JRST CPOPJ1
>;NOITS
;CONSOLE IO SUBRS
NOITS,<
NOCMU,<
GETWRP:
IFN DECSW!IIISW,<	SETZ TT,
GETWP1:	PUSHJ P,GETCHL
	CAIN T,40
	JRST GETWP1
GETWP2:	CAIL T,"0"
	CAILE T,"9"
	POPJ P,
	ASH TT,3
	ADDI TT,-60(T)
	PUSHJ P,GETCHL
	JRST GETWP2
>;IFN DECSW!IIISW
NODEC,<NOIII,<	PUSHJ P,GETWRD	>>
RJUST:	HLRZ TT,TT		;THREE LETTERS ONLY
	TRNN TT,7777
	LSH TT,-14
	TRNN TT,77
	LSH TT,-6
	POPJ P,
>;NOCMU
>;NOITS
	
GETWRD:	SETZ TT,		;WORD WILL ACCUMULATE HERE
	MOVE A,[POINT 6,TT]	;BYTE POINTER TO DEPOSIT CHARACTERS
	MOVEI TTT,6		;MAX CHAR COUNT
GETCHR:	PUSHJ P,GETCHL		;READ A CHAR
	CAIN T,40		;AND SPACES
	JRST GETCHR
ITS,<
	SKIPA
GETCH1:	PUSHJ P,GETCHL
	CAIL T,"a"
	CAILE T,"z"
	SKIPA
	SUBI T,40
	CAILE T,40
	CAILE T,"_"
	POPJ P,
	CAIE T,";"
	CAIN T,":"
	POPJ P,
>;ITS
NOITS,<
	CAIL T,"0"		;NUMBERS ARE LEGAL
	CAILE T,"9"
	CAIA
	JRST CHROK
	CAILE T,"z"
	POPJ P,			;BREAK CHAR.
	CAIL T,"a"
	SUBI T,40		;CHANGE LOWER CASE TO UPPER
	CAIL T,"A"		;NOW ONLY UPPER CASE LETTERS ARE LEGAL
	CAILE T,"Z"
	POPJ P,			;BREAK
>;NOITS
	CAIN T,"]"
	POPJ P,
CHROK:	SOJL TTT,GETCHR		;HAVE WE DONE SIX ALREADY
	SUBI T,40		;NO, MAKE IT SIXBIT
	IDPB T,A		;STORE
NOITS,<	JRST GETCHR	>
ITS,<	JRST GETCH1	>

MILIN:	PUSHJ P,GETCHL
	CAIE T,"-"
	JRST MILINT
	PUSHJ P,MILIN1
	MOVN TTT,TTT
	POPJ P,

MILIN1:	PUSHJ P,GETCHL
MILINT:	PUSHJ P,DECINT
	JFCL
	IMULI TTT,=200		;HIGH PART(INCHES) TO PLOTTER STEPS
	CAIN T,"."		;ANY FRACTIONAL PART?
	PUSHJ P,DIGIN
	POPJ P,
	IMULI T,=20
	ADDM T,TTT
	PUSHJ P,DIGIN
	POPJ P,
	IMULI T,=2
	ADDM T,TTT
	PUSHJ P,DIGIN
	POPJ P,
	JRST .-2

DIGIN:	PUSHJ P,GETCHL
	CAIL T,"0"
	CAILE T,"9"
	POPJ P,
	SUBI T,60
	JRST CPOPJ1

DECINS:	PUSHJ P,GETCHL
	CAIE T,"-"
	JRST DECINT
	PUSHJ P,DECIN
	CAIA
	AOS (P)
	MOVN TTT,TTT
	POPJ P,

DECIN:	TDZA TTT,TTT
DECINT:	TDZA TTT,TTT
READN:	PUSHJ P,GETCHL
	CAIL T,"0"
	CAILE T,"9"
	JRST ENDNUM
	IMULI TTT,=10
	ADDI TTT,-60(T)
	JRST READN
ENDNUM:	CAIN T,12
	JRST CPOPJ1
	POPJ P,

MILOUT:	SKIPGE T
	OUTCHR["-"]
	MOVMS T
	IDIVI T,=1000
	HRLM TT,(P)
	PUSHJ P,DECOUT
	OUTCHR["."]
	HLRZ T,(P)
	CAIGE T,=100
	OUTCHR["0"]
	CAIGE T,=10
	OUTCHR["0"]
DECOUT:	IDIVI T,=10
	HRLM TT,(P)
	JUMPE T,.+2
	PUSHJ P,DECOUT
	HLRZ T,(P)
	ADDI T,60
	OUTCHR T
	POPJ P,

LSIXOUT:
IFN DECSW!IIISW,<	HLRZ T,TT
OCTOUT:	IDIVI T,10
	HRLM TT,(P)
	JUMPE T,.+2
	PUSHJ P,OCTOUT
	HLRZ T,(P)
	ADDI T,60
	OUTCHR T
	POPJ P,
>;IFN DECSW!IIISW
NODEC,<
NOIII,<	TLNN TT,777700
	LSH TT,14
	TLNN TT,770000
	LSH TT,6
>;NOIII
>;NODEC
SIXOUT:	JUMPE TT,CPOPJ
	SETZ T,
	LSHC T,6
	ADDI T,40
	OUTCHR T
	JRST SIXOUT

^GETYES:PUSHJ P,GETCHL
	CAIE T,"Y"
	CAIN T,"y"
	AOS (P)
	PUSH P,T
	CAIA
GETCR:	PUSHJ P,GETCHL
	CAIE T,12
	JRST GETCR
	POP P,T
	POPJ P,
;INDIRECT
INDIR:	TROE FLAG,IFLAG
	JRST IERR
	PUSH P,EXTSAV
	MOVSI T,EXTTXT
	MOVEM T,EXTSAV
	PUSHJ P,SETIT1
	JRST [	TRZ FLAG,IFLAG
		POP P,EXTSAV
		POPJ P,]
	POP P,EXTSAV
	TRZ FLAG,IFLAG
	INIT ICHN,0
	'DSK   '
	IBUFR
	JRST [	OUTSTR[ASCIZ/CAN'T GET DSK FOR @ FILE!
/]
		EXIT 1,
		JRST .-3]
	LOOKUP ICHN,FILNAM
	JRST [	OUTSTR[ASCIZ/INDIRECT FILE NOT FOUND!
/]
		TRZ FLAG,IREAD
		JRST SETNAM]
	MOVEI T,IBUFRD
	EXCH T,.JBFF
	INBUF ICHN,1
	MOVEM T,.JBFF
	TRO FLAG,IREAD
	JRST SETNAM

IERR:	TRZE FLAG,IREAD
	JRST IERR1
	OUTSTR [ASCIZ /???
/]
	CLRBFI
	POPJ P,

IERR1:	OUTSTR[ASCIZ/ERROR IN INDIRECT FILE!
/]
IERR2:	OUTSTR[ASCIZ/END OF INDIRECT FILE!
/]
	RELEASE ICHN,
	POPJ P,

GETCH:	TRNE FLAG,IREAD
	JRST GETCHF
	INCHRW T
	CAIE T,14
	CAIN T,15
	JRST .-3
	POPJ P,

GETCHL:	TRNE FLAG,IREAD
	JRST GETCHF
	INCHWL T
	CAIE T,14
	CAIN T,15
	JRST .-3
ULCONV:	CAIL T,"a"
	CAILE T,"z"
	POPJ P,
	SUBI T,40
	POPJ P,

GETCHF:	SOSG IBUFR+2
	IN ICHN,
	CAIA
	JRST [	STATO ICHN,1B22
		OUTSTR[ASCIZ/INPUT ERROR ON I-FILE!
/]
		TRZ FLAG,IREAD
		PUSHJ P,IERR2
		SETO T,
		GETLIN T
		AOJN T,GETCHL
		EXIT]			;EXIT IF DONE AND DETACHED!
	ILDB T,IBUFR+1
	JUMPE T,GETCHF
	MOVE T,@IBUFR+1
	TRNN T,1
	JRST NOSKIP
	MOVNI T,5
	ADDM T,IBUFR+2
	AOS IBUFR+1
	JRST GETCHF
NOSKIP:	LDB T,IBUFR+1
	CAIN T,15
	JRST GETCHF
	CAIN T,12
	OUTCHR [15]
ECHO:	OUTCHR T
	JRST ULCONV

XLIST
LIT
VAR
LIST


END STRT
