
;3 DEC 71, 1254:
;D. MURPHY

	TITLE	DUMPER
	SUBTTL	R.S.TOMLINSON 13 APRIL 71

; AC'S

NUSL__30
NBUF__30		; NUMBER OF DUMP BUFFERS
HSHLEN__400

?MAXUSR_1000		;MAX USER NUMBER

?A_1
?B_2
?C_3
?D_4
?E_6
?F_7
?XX_10
?YY_11
?P_17

DEFINE	ERROR(S,X)<
JRST [	TMSG <S>
	X]>

DEFINE	TMSG(S)<
	HRROI B,[ASCIZ S]
	PUSHJ P,TMSGQ>

DEFINE	LPMSG(S)<
	HRROI B,[ASCIZ S]
	PUSHJ P,LPMSGQ>

DEFINE	BTTMSG(S)<
	HRROI B,[ASCIZ S]
	PUSHJ P,BTMSGQ>
LALL

; Parameters and bits pertinent to directories

SET TPC,0
USE TPC			; Symbols relative to fdb origin
	BLOCK 1		; The header, not referenced symbolically
FDBCTL_:BLOCK 1		; Lh ==> control bits (see below)
			; Rh ==> location of file name block
FDBEXT_:BLOCK 1		; Lh ==> location of extension block
			; Rh ==> pointer to other extensions
FDBADR_:BLOCK 1		; The file address & class field
FDBPRT_:BLOCK 1		; File protection word
FDBCRE_:BLOCK 1		; Creation date & time of version 1
FDBUSE_:BLOCK 1		; Lh ==> last writer directory number
			; Rh ==> use count (+1 for each indirect pointer
			;  and saved environment)
FDBVER_:BLOCK 1		; Lh ==> version number
			;  this is job number for temp files
			; Rh ==> pointer to other versions
FDBACT_:BLOCK 1		; Account infor for charging
			;  + for location of string block
			;  - for number
FDBBYV_:BLOCK 1		; 0-5 ==> number of version to retain
			; 6-11 ==> last byte size
			; Rh ==> count of actual pages in file
FDBSIZ_:BLOCK 1		; Length of file in bytes
FDBCRV_:BLOCK 1		; Creation date and time of this version
FDBWRT_:BLOCK 1		; Date & time of last write
FDBREF_:BLOCK 1		; Date & time of last reference
FDBCNT_:BLOCK 1		; Lh ==> count of writes
			; Rh ==> count of references
FDBBCK_:BLOCK 5		; Words for backup system
FDBUSW_:BLOCK 1		; User settable word
FDBLEN:			; Length of fdb

USE

; Bits in lh of fdbctl

FDBTMP__400000		; File is temporary
FDBPRM__200000		; File is permanent
FDBNEX__100000		; No extension for this fdb yet
			;  the file does not really exist
FDBDEL__040000		; File is deleted
FDBNXF__020000		; File does not exist (first write not complete)
FDBLNG__010000		; Long file
FDBSHT__004000		; Compressed page table
FDBENV__002000		; Environment file
FDBSUB__001000		; Subroutine file

; CONTROL LOOP

START:	MOVE P,[IOWD 100,PDL]
	PUSHJ P,RESET
	TMSG </
MINI-DUMPER  13 APRIL 71
/>
	SETOM WHEEL
	MOVEI A,400000
	RPCAP
	TRNN C,600000
	SETZM WHEEL
	MOVEI A,400000
	MOVEI 2,303
	SKIPE WHEEL
	JSYS 243		; ENABLE HIGH PRIORITY
RESTRT:	TMSG </
DUMP, LOAD, CHECK, OR SINGLE? />
	MOVEI 1,100
	BIN
	MOVEI A,[POP P,A
		HRROI A,[ASCIZ /  ?  D, L, C, OR S/]
		PSOUT
		JRST RESTRT]
	CAIN B,"D"
	MOVEI A,DUMP
	CAIN B,"S"
	MOVEI A,SING
	CAIN B,"C"
	MOVEI A,CHCK
	CAIN B,"L"
	MOVEI A,LOAD
	PUSHJ P,(A)
	PUSHJ P,RESET
	TMSG </
DONE.
/>
	HALTF

RESET:	MOVNI 1,1
	CLOSF
	JFCL
	RLJFN
	JFCL
	MOVNI A,1
	MOVE B,[XWD 400000,BUFPAG]
	PMAP
	MOVE A,[ASCIZ /MTA0:/]
	MOVEM A,MTDEV
	MOVEI A,BUFFER
RESET1:	MOVEI B,NHEAD+1002(A)
	MOVEM B,(A)
	MOVEI A,-1(B)
	CAIE A,BUFFER+(NBUF-1)*(NHEAD+1001)
	JRST RESET1
	MOVEI B,BUFFER+1
	MOVEM B,(A)
	MOVEI 1,101
	RFMOD
	TRO 2,770000
	SFMOD
	MOVEI A,-4
	KFORK
CPOPJ:	POPJ P,

BEGIN	SINGLE FILE TRANSFER

^SING:	SETZM CHECK
	PUSHJ P,MTOPEN
	MOVE A,MTJFN
	MOVEI B,1
	MTOPR
	SETZM RSEQ
	AOS RSEQ
SINGLP:	TMSG </
COPY />
	MOVE C,[POINT 7,NAMBUF]
	MOVEI A,100
SINGL1:	BIN
	CAIE B,40
	CAIN B,37
	JRST SINGLZ
	CAIN B,33
	JRST SINGLZ
	CAIN B,1
	JRST DELCH
	CAIN B,177
	JRST SINGLP
	CAIN B,"X"-100
	JRST START
	IDPB B,C
	JRST SINGL1

DELCH:	CAME C,[POINT 7,NAMBUF]
	CAMN C,[POINT 7,NAMBUF-1,34]
	JRST DING
	LDB B,C
	MOVEI A,101
	BOUT
	MOVE A,C
	BKJFN
	JFCL
	MOVE C,A
	MOVEI A,100
	JRST SINGL1

DING:	MOVEI B,7
	MOVEI A,101
	BOUT
	MOVEI A,100
	JRST SINGL1

SINGLZ:	MOVEI B,";"
	IDPB B,C
	MOVEI B,"@"
	IDPB B,C
	MOVEI B,0
	IDPB B,C
	TMSG </ TO />
	MOVSI 1,460003
	MOVE 2,[XWD 100,101]
	GTJFN
	ERROR(</?/>,<JRST SING>)
	MOVEM 1,JFN
SINGLA:	PUSHJ P,MTRED
	JFCL

SINGLB:	MOVE B,TYP
	CAMN B,[-FLHDX]
	JRST SINGLC
	CAMN B,[-TPTRX]
	JRST SINGLE
	CAME B,[-TPHDX]
	CAMN B,[-FLTRX]
	JRST SINGLF
	JRST SINGLA

SINGLF:	PUSHJ P,LODEOF
	JRST SINGLA

SINGLE:	TMSG </
END OF TAPE, FILE NOT FOUND/>
	MOVE A,MTJFN
	MOVEI B,1
	MTOPR
	MOVE A,JFN
	RLJFN
	JFCL
	JRST SINGLP

SINGLC:	MOVEI A,BUFF
	MOVEI B,NAMBUF
	PUSHJ P,STRCMP
	SKIPA A,JFN
	JRST [	PUSHJ P,LODSKP
		JRST SINGLA]
	PUSHJ P,LODSBA
	JRST SINGLP

BEND	SINGLE

BEGIN	DUMP ROUTINES

^DUMP:	MOVEI 1,400000
	MOVE 2,[XWD LEVTAB,CHNTAB]
	SIR
	MOVSI 2,(1B11)
	AIC
	EIR
	SETOM FIRST
	SETZM SNGUSR
	SKIPN WHEEL
	JRST DUMPOM
	TMSG </
DUMP WHOLE DISC? />
	MOVEI B,100
	BIN
	CAIN B,"Y"
	JRST DUMPON
	CAIE B,"N"
	JRST DUMP
	TMSG </
SPECIFIC USERS? />
	PBIN
	CAIN A,"Y"
	JRST [	TMSG </ USERS: />
		PUSHJ P,REDUSS
		MOVEM C,SNGUSR
		JRST DUMPON]
	CAIE A,"N"
	JRST DUMP
	TMSG </
START WITH USER: />
	PUSHJ P,REDUSR
	HRRZM A,FIRST
	SETZM SNGUSR
DUMPON:	TMSG </
INCREMENTAL DUMP? />
	PBIN
	CAIN A,"Y"
	JRST [	SETOM INCRSW
		JRST DUMPOM]
	CAIE A,"N"
	JRST DUMP
	SETZM INCRSW

DUMPOM:	TMSG </
TYPE IDENTIFICATION INFO: />
INDAT0:	MOVE B,[POINT 7,DATBUF]
INDAT:	PBIN
	CAIN A,"X"-100
	JRST [	TMSG </___
/>
		JRST INDAT0]
	CAIN A,1
	JRST [	CAME B,[POINT 7,DATBUF]
		CAMN B,[POINT 7,DATBUF-1,35]
		JRST [	MOVEI A,7
			PBOUT
			JRST INDAT]
		LDB A,B
		PBOUT
		MOVE A,B
		BKJFN
		JFCL
		MOVE B,A
		JRST INDAT]
	CAIN A,4
	JRST INDAT2
	CAIE A,32
	CAIN A,37
	JRST INDAT2
	IDPB A,B
	JRST INDAT

INDAT2:	MOVEI A,11
	IDPB A,B
	MOVE A,B
	MOVNI B,1
	MOVSI 3,(1B10!1B12!3B2)
	ODTIM
	MOVEI B,0
	IDPB B,A
	SETZM USRCNT
	SETZM TOTCNT
	SETZM HSHTAB
	MOVE A,[XWD HSHTAB,HSHTAB+1]
	BLT A,HSHUSR+HSHLEN-1
	MOVE A,[POINT 7,[ASCIZ /OTHERS/]]
	MOVEM A,HSHTAB
	MOVE A,[POINT 7,HSHBUF]
	MOVEM A,HSHSTR
	HRROI 2,[ASCIZ /MINI-DUMP.LISTING/]
	MOVSI 1,400001
	GTJFN
	JRST LPTNAV
	MOVE B,[XWD 70000,100000+1B28]
	MOVEM 1,LPTJFN
	OPENF
	JRST [	MOVE A,LPTJFN
		RLJFN
		JFCL
		JRST LPTNAV]

DUMP0:	MOVEM A,LPTJFN
	PUSHJ P,INIFRK
	PUSHJ P,MTOPEN		; OPEN A MAG TAPE
	LPMSG </

FILE			ACCOUNT			SIZE/>
	SETZM TAPNO		; START AFTER TAPE 0
	GJINF
	SKIPN WHEEL
	HRRZM 2,SNGUSR
	MOVEM 2,CONDIR
	MOVEI F,DMPFIL		; ROUTINE TO CALL FOR EACH FILE
	PUSHJ P,DMPFI1
	SKIPE WHEEL
	PUSHJ P,SCANDI		; START SCAN WITH DI
	MOVE A,SNGUSR
	SKIPN WHEEL
	PUSHJ P,SCANFD
	PUSHJ P,ENDTAP		; TERMINATE TAPE
	MOVEI F,FIXBCK
	SKIPN INCRSW
	JRST DMPABC
	SKIPE WHEEL
	PUSHJ P,SCANDI
	MOVE A,SNGUSR
	SKIPN WHEEL
	PUSHJ P,SCANFD

DMPABC:	MOVE 1,CONDIR
	MOVEI 2,0
	CNDIR
	JFCL
	BTTMSG </

TOTAL FILES DUMPED = />
	MOVE B,TOTFIL
	MOVEI C,12
	PUSHJ P,LPNOUT
	PUSHJ P,TTNOUT
	BTTMSG </
TOTAL PAGES DUMPED = />
	MOVE B,TOTCNT
	MOVEI C,12
	PUSHJ P,LPNOUT
	PUSHJ P,TTNOUT
	MOVE A,LOWFRK
	WFORK
	LPMSG </

SUMMARY OF FILE CHARGES FOR ALL USERS

/>
	MOVEI XX,HSHCUM
	PUSHJ P,ACTPRT
	MOVE A,LPTJFN
	HRLI A,400000
	CLOSF
	0
	HRRZS A
	MOVE B,[XWD 70000,200000]
	OPENF
	0
	MOVEM A,LSTJFN
	MOVSI A,1
	HRROI B,[ASCIZ /LPT:/]
	GTJFN
	0
	MOVEM A,LPTJFN
	MOVE B,[XWD 70000,100000+1B26]
	OPENF
	JRST [	TMSG </
LPT NOT AVAILABLE, LISTING IS ON FILE MINI-DUMP.LISTING
/>
		MOVE A,LSTJFN
		CLOSF
		JFCL
		POPJ P,]
LSTLUP:	MOVE A,LSTJFN
	BIN
	JUMPE B,LSTLST
	MOVE 1,LPTJFN
	BOUT
	JRST LSTLUP

LSTLST:	MOVE A,LSTJFN
	HRLI A,400000
	CLOSF
	0
	HRRZS A
	DELF
	JFCL
	RLJFN
	JFCL
	MOVE A,LPTJFN
	CLOSF
	0
	POPJ P,

LPTNAV:	TMSG </
MINI-DUMP.LISTING NOT AVAILABLE, LIST TO FILE: />
LPTNA1:	MOVSI A,2
	MOVE 2,[XWD 100,101]
	GTJFN
	JRST LSTNAV
	MOVEM A,LPTJFN
	MOVE 2,[XWD 70000,100000]
	OPENF
	JRST [	MOVE A,LPTJFN
		RLJFN
		JFCL
		JRST LSTNAV]
	JRST DUMP0

LSTNAV:	TMSG </
NOT AVAILABLE, LIST TO FILE: />
	JRST LPTNA1

FIXBCK:	MOVE A,JFN
	MOVE B,[XWD 1,FDBBCK]
	MOVEI C,C
	GTFDB
	JUMPGE C,CPOPJ
	HLLZS C
	LSH C,1
	HRLZI B,-1
	HRLI A,FDBBCK
	CHFDB
	POPJ P,

^REDUSS: MOVSI C,-NUSL
	MOVEM C,SNGUSR
	PUSHJ P,REDUSR
	MOVE C,SNGUSR
	HRRZM A,SUSL(C)
	AOBJN C,.+1
	CAIN B,","
	JUMPL C,.-6
	MOVNI C,(C)
	HRLZM C,SNGUSR
	MOVE C,SNGUSR
	POPJ P,

^REDUSR:MOVEI 1,100
	RFCOC
	TRZ 3,600000
	SFCOC
	MOVE B,[POINT 7,NAMBUF]
REDUSL:	PBIN
	CAIN A,1
	JRST [	CAME B,[POINT 7,NAMBUF]
		CAMN B,[POINT 7,NAMBUF-1,35]
		JRST [	MOVEI A,7
			PBOUT
			JRST REDUSL]
		LDB A,B
		PBOUT
		MOVE A,B
		BKJFN
		0
		MOVE B,A
		JRST REDUSL]
	CAIE A,"X"-100
	CAIN A,177
	JRST [	TMSG </___
/>
		JRST REDUSR]
	CAIE A,","
	CAIN A,37
	JRST REDUS1
	CAIN A,33
	JRST REDUS2
	IDPB A,B
	JRST REDUSL

REDUS1:	MOVEI A,100
	BKJFN			; BACK UP TO REREAD TERMINATOR
	0
	TDZA A,A
REDUS2:	MOVSI A,400000
	PUSH P,B
	IDPB A,B
	MOVE B,[POINT 7,NAMBUF]
	STDIR
	JRST [	TMSG </
NO SUCH USER, TRY AGAIN: />
		POP P,A
		JUMPL A,REDUSR	;TRY AGAIN IF TERM WAS ESC
		PBIN		;OTHERWISE, FLUSH BKJFN'D TERM
		JRST REDUSR]
	JRST [	MOVEI A,7
		PBOUT
		POP P,B
		JRST REDUSL]
	MOVEI C,0
	IDPB C,B
	EXCH A,(P)
	PSOUT
	PBIN			; GET TERMINATOR
	MOVE B,A
	POP P,A
	POPJ P,

; SCAN DIRECTORIES

^SCANDI:MOVEI B,1
SCNDIL:	MOVE A,[POINT 7,DIRNAM]
	DIRST
	JRST SCNDI1
	PUSH P,B
	HRRZ A,B
	PUSHJ P,SCANFD
	POP P,B
SCNDI1:	CAIGE B,MAXUSR-1
	AOJA B,SCNDIL
	POPJ P,

DMPUSR:	SETZM BUFF
	MOVE B,[XWD BUFF,BUFF+1]
	BLT B,BUFF+777
	MOVE A,DIRNUM
	MOVEI B,BUFF
	MOVE C,[POINT 7,BUFF+100]
	GTDIR
	JFCL
	MOVE B,[XWD DIRNAM,BUFF+40]
	BLT B,BUFF+77
	MOVNI A,USRX
	MOVEM A,TYP
	PUSHJ P,MTOUT
	POPJ P,

^SCANFD:HRRZS A
	SKIPL FIRST
	CAMN A,FIRST
	JRST .+2
	POPJ P,
	SETOM FIRST
	SKIPGE B,SNGUSR		; LIST OF USERS?
	 JRST [	CAMN A,SUSL(B)	; YES CHECK IT
		JRST .+1
		AOBJN B,.
		POPJ P,]
	MOVEM A,DIRNUM
	MOVE B,A
	MOVE A,[POINT 7,DIRNAM]
	MOVEM A,DIRPTR
	DIRST
	JFCL
	MOVEI B,0
	IDPB B,A
	CAIE F,DMPFIL
	JRST SCANF1
	SKIPN SNGUSR
	SKIPN WHEEL
	JRST SCANF1
	PUSHJ P,DMPUSR		;OUTPUT USER BLOCK

SCANF1:	MOVE A,DIRNUM
	MOVEI B,0
	CNDIR			; CONNECT TO DIRECTORY FOR SPEED
	 JFCL
	CAIN F,DMPFIL
	PUSHJ P,BEGUSR
	MOVE A,[POINT 7,NAMBUF]
	MOVEI B,"<"
	IDPB B,A
	MOVE B,DIRPTR
	MOVEI C,0
	SOUT
	HRROI B,[ASCIZ />*.*;*/]
	SOUT
	HRROI B,NAMBUF
	MOVSI A,100101
	GTJFN
	 JRST ENDU
	MOVEM A,SCNJFN
SCNLUP:	HRRZ A,SCNJFN
	MOVEM A,JFN
	PUSHJ P,(F)
	MOVE A,SCNJFN
	GNJFN
	 JRST ENDU
	JRST SCNLUP

ENDU:	CAIN F,DMPFIL
	JRST ENDUSR
	POPJ P,

DMPFI0:	PUSHJ P,ENDTAP
	MOVE A,LOWFRK
	WFORK
	MOVE 1,MTJFN
	CLOSF
	JFCL
	TMSG </
MOUNT NEXT TAPE/>
	PUSHJ P,MTOPEN
DMPFI1:	PUSHJ P,REWIND
	RADIX =10
	MOVE A,[2100*12000]		; MILLI-INCHES PER REEL
	SUB A,[514*((512+NHEAD)*60/8+750)]	; MILLI-INCHES IN LONGEST FILE
	RADIX =8
	MOVEM A,TAPLFT
	MOVE 1,[POINT 7,BUFF]
	HRROI 2,[ASCIZ /

MINI-DUMP TAPE # /]
	MOVEI 3,0
	SOUT
	AOS 2,TAPNO
	MOVEI 3,=10
	NOUT
	JFCL
	MOVEI B,11
	IDPB B,A
	MOVE B,[POINT 7,DATBUF]
	MOVEI C,0
	SOUT
	MOVEI B,0
	IDPB B,A
	MOVNI A,TPHDX
	MOVEM A,TYP
	PUSHJ P,MTOUT
	PUSHJ P,ENDFIL
	LPMSG </

/>
	HRROI 2,BUFF
	PUSHJ P,BTMSGQ
	BTTMSG </

/>
	POPJ P,

DMPFIL:	MOVE 2,[XWD 1,1]
	MOVEI 3,B
	GTFDB
	TLNE B,FDBNXF!FDBNEX!FDBDEL!FDBTMP
	JRST DMPFIZ
	LPMSG </
/>
	MOVE A,[POINT 7,NAMBUF]
	MOVEI C,0
	MOVEI B,"<"
	IDPB B,A
	MOVE B,DIRPTR
	SOUT
	MOVEI B,">"
	IDPB B,A
	MOVEM A,NAMPTR
	PUSH P,A
	PUSHJ P,CLENG
	EXCH A,(P)
	MOVE B,JFN
	MOVE C,[BYTE (3)0,0,1,1,1,0,0(5)0,0,1]
	JFNS
	PUSHJ P,CLENG
	POP P,B
	SUB A,B
	PUSH P,A
	MOVE B,NAMPTR
	PUSHJ P,LPMSGQ
	POP P,A
	MOVEI B,30
	PUSHJ P,TAB
	MOVEI A,NAMBUF
	MOVEI B,[ASCIZ /<SYSTEM>DSKBTTBL.;@/]
	PUSHJ P,STRCMP
	JRST NODUMP
	MOVEI A,NAMBUF
	MOVEI B,[ASCIZ /<SYSTEM>DIRECTORY.;@/]
	PUSHJ P,STRCMP
	JRST NODUMP
	MOVEI A,NAMBUF
	MOVEI B,[ASCIZ /<SYSTEM>INDEX.;@/]
	PUSHJ P,STRCMP
	JRST NODUMP

REGET:	MOVE A,JFN
	SKIPE INCRSW
	JRST [	MOVE B,[XWD 25,0]
		MOVEI 3,BUFF
		GTFDB
		HLRZ B,BUFF+FDBCNT
		CAIN B,0
		AOS B
		SKIPLE C,BUFF+FDBBCK
		CAME B,C
		JRST .+1
		JRST NDM]
	MOVE 2,[XWD 440000,1B19+1B25+1B27]
	SKIPN WHEEL
	TRZ 2,1B27
	OPENF
	JRST [	MOVE A,JFN
		MOVE B,[XWD 440000,1B19+1B27]
		SKIPN WHEEL
		TRZ 2,1B27
		OPENF
		JRST NDM1
		JRST .+1]
	MOVE 2,1
	MOVE 1,[POINT 7,BUFF]
	MOVE 3,[BYTE (3)0,1,1,1,1,1,1(1)1(4)0(5)0(5)1]
	JFNS
	MOVEI B,0
	IDPB B,A
	MOVNI A,FLHDX
	MOVEM A,TYP
	PUSHJ P,MTOUT
	MOVE A,JFN
	MOVE B,[XWD 1,FDBCTL]
	MOVEI C,A
	GTFDB
	TLNN A,FDBLNG
	TDZA A,A
	SETO A,
	MOVEM A,LONGSW
	MOVE A,JFN
	MOVE B,[XWD 1,FDBBYV]
	MOVEI C,A
	GTFDB
	HRRZM A,PAGCNT
	HRRZM A,CNTR
	HRLZ A,JFN
	AOS NOFILS

DMPLUP:	MOVEM A,PAGNO		; SAVE IN CASE NO PAGES ARE FOUND
	FFUFP
	JRST DMPLUX
	RPACS
	MOVEM B,ACCESS
	MOVEM A,PAGNO
	TLNN B,(1B5)
	JRST DMPLUX
	MOVE 2,[XWD 400000,BUFPAG]
	MOVSI C,140000
	PMAP
	MOVEM 1,LASTID
	SETZM TYP
	PUSHJ P,MTOUT
	SOS CNTR
	AOS A
	TRNE A,777777
	JRST DMPLUP
DMPLUX:	SKIPG CNTR
	JRST DMPLX1
	BTTMSG </
CANNOT FIND ALL THE PAGES OF FILE />
	MOVE 2,JFN
	MOVEI 3,0
	MOVEI 1,101
	JFNS
	MOVE 1,LPTJFN
	JFNS
	BTTMSG </
/>

DMPLX1:	MOVNI 1,1
	MOVE 2,[XWD 400000,BUFPAG]
	PMAP
	MOVE 1,JFN
	MOVE 2,[XWD 25,0]
	MOVEI 3,BUFF
	GTFDB
	MOVNI 1,FLTRX
	MOVEM 1,TYP
	HLRZ C,BUFF+FDBCNT
	CAIN C,0
	AOS C
	HRRZ B,BUFF+FDBBCK
	CAME B,C
	TLOA C,400000
	MOVE C,BUFF+FDBBCK
	MOVNI B,1
	MOVE A,JFN
	HRLI A,FDBBCK
	SKIPE INCRSW
	JRST [	CHFDB
		HRRM C,BUFF+FDBBCK
		HLLZS C
		LSH C,1
		HLLM C,BUFF+FDBBCK
		JRST .+1]
	SETZM NODMSW

DMPDNN:	MOVE A,[POINT 7,NAMBUF]
	MOVE B,JFN
	MOVEI C,100000
	JFNS
	PUSHJ P,CLENG
	PUSH P,A
	HRROI B,NAMBUF
	PUSHJ P,LPMSGQ
	SKIPE NODMSW
	JRST DMPDNA
	PUSHJ P,MTOUT
	PUSHJ P,ENDFIL
	MOVE A,JFN
	HRLI A,400000
	CLOSF
	0
DMPDNA:	POP P,A
	MOVEI B,20
	PUSHJ P,TAB
	MOVE A,[POINT 7,NAMBUF]
	PUSHJ P,ACTLUK
	MOVE B,PAGCNT
	ADDM B,HSHUSR(XX)
	ADDM B,HSHCUM(XX)
	SKIPN NODMSW
	ADDM B,USRCNT
	MOVE C,[XWD 100003,12]
	PUSHJ P,LPNOUT
	HRROI B,[ASCIZ  / PAGES/]
	MOVE C,PAGCNT
	CAIN C,1
	HRROI B,[ASCIZ / PAGE /]
	PUSHJ P,LPMSGQ
	SKIPE B,NODMSW
	PUSHJ P,LPMSGQ

DMPFID:DMPFIZ:	SKIPL TAPLFT
	POPJ P,
	JRST DMPFI0

NODUMP:	HRROI B,[ASCIZ /NOT DUMPED/]
	PUSHJ P,LPMSGQ
	JRST DMPFID

NDM1:	SKIPA B,[POINT 7,[ASCIZ /	CANNOT OPEN/]]
NDM:	MOVE B,[POINT 7,[ASCIZ /	NOT DUMPED/]]
	MOVEM B,NODMSW
	SIZEF
	JFCL
	MOVEM C,PAGCNT
	JRST DMPDNN

LEVTAB:	RET
	RET
	RET

CHNTAB:	REPEAT =11,<0>
	XWD 1,MEMERR
	REPEAT =35-=11,<0>

MEMERR:	MOVEM 17,INTAC+17
	MOVEI 17,INTAC
	BLT 17,INTAC+16
	MOVE P,[XWD -30,INTSTK-1]
	PUSH P,40
	MOVE 1,LASTID
	CAMN 1,LSTERO
	JRST MEMXIT
	MOVEM 1,LSTERO
	BTTMSG </
DISC ERROR IN FILE />
	HLRZ 1,LASTID
	GTSTS
	TLNN 2,(1B10)
	JRST [	BTTMSG </(UNKNOWN FILE NAME)/>
		JRST MEMERZ]
	HLRZ 2,LASTID
	MOVEI 1,101
	MOVEI 3,0
	JFNS
	JFCL
	MOVE 1,LPTJFN
	JFNS
	JFCL
	BTTMSG </, PAGE />
	HRRZ 2,LASTID
	MOVEI 1,101
	MOVEI 3,=10
	NOUT
	0
	MOVE 1,LPTJFN
	NOUT
	0

MEMERZ:	TMSG </
DISC ADDRESS =	/>
	MOVE 1,[SIXBIT /DSKERR/]
	SYSGT
	HRRZM 2,DSKERN
	MOVE 1,DSKERN
	HRLI 1,1
	GETAB
	0
	MOVE 2,1
	TLZ 2,700000
	MOVEI 3,10
	MOVEI 1,101
	NOUT
	0
	TMSG </
CLASS & COUNT =	/>
	MOVE 1,DSKERN
	HRLI 1,2
	GETAB
	0
	MOVE 2,1
	MOVEI 1,101
	MOVEI 3,10
	NOUT
	0
	TMSG </
CORE ADDRESS =	/>
	MOVE 1,DSKERN
	HRLI 1,3
	GETAB
	0
	MOVE 2,1
	MOVEI 1,101
	MOVEI 3,10
	NOUT
	0

	TMSG </
ERROR STATUS =	/>
	MOVE 1,DSKERN
	HRLI 1,4
	GETAB
	0
	MOVE 2,1
	MOVEI 1,101
	MOVEI 3,10
	NOUT
	0
	TMSG </
/>
MEMXIT:	POP P,40
	MOVSI 17,INTAC
	BLT 17,17
	DEBRK

BADERR:	TMSG </
UNEXPECTED IO ERROR INTERRUPT...IGNORED/>
	JRST MEMXIT

^STRCMP:HRLI A,(<POINT 7,0>)
	HRLI B,(<POINT 7,0>)
STRCML:	ILDB C,B
	ILDB XX,A
	CAIE C,"@"
	CAMN C,XX
	JRST STRCM1
	AOS (P)
	POPJ P,

STRCM1:	CAIN C,"@"
	JUMPN XX,STRCML+1
	JUMPN XX,STRCML
	POPJ P,

ENDUSR:	MOVEI A,100
	RFPOS
	HRRZ C,B
	CAIGE C,=18
	JRST ENDUS1
	MOVEI B,37
	MOVEI A,101
	BOUT
	SETZ C,
ENDUS1:	MOVEI B,40
	MOVEI A,101
	BOUT
	CAIGE C,=18
	AOJA C,ENDUS1
	SKIPN NOFILS
	JRST [	LPMSG  </
/>
		BTTMSG </NO FILES/>
		LPMSG </
/>
		POPJ P,]
	MOVE B,USRCNT
	MOVE C,[XWD 100005,12]
	PUSHJ P,TTNOUT
	TMSG </ PAGES/>
	LPMSG </

TOTAL/>
	MOVE C,[XWD 100026,12]
	MOVE B,NOFILS
	ADDM B,TOTFIL
	PUSHJ P,LPNOUT
	LPMSG </ FILES/>
	MOVE C,[XWD 100016,12]
	MOVE B,USRCNT
	ADDM B,TOTCNT
	PUSHJ P,LPNOUT
	LPMSG </ PAGES


	SUMMARY OF FILE CHARGES

/>
	MOVEI XX,HSHUSR
	JRST ACTPRT

BEGUSR:	BTTMSG </
/>
	SETZM USRCNT
	SETZM NOFILS
	MOVE B,DIRPTR
	PUSHJ P,BTMSGQ
	LPMSG </
/>
	SETZM HSHUSR
	MOVE A,[XWD HSHUSR,HSHUSR+1]
	BLT A,HSHUSR+HSHLEN-1
	POPJ P,

CLENG:	MOVEI B,0
	IDPB B,A
	MOVEI B,-NAMBUF(A)
	IMULI B,5
	HRLI A,440700
	ILDB C,A
	JUMPE C,.+2
	AOJA B,.-2
	MOVE A,B
	POPJ P,

TAB:	PUSH P,B
	MOVE C,A
	CAMG C,B
	JRST TAB1
	MOVEI C,0
	MOVEI B,37
	MOVE A,LPTJFN
	BOUT
TAB1:	MOVEI B,40
	MOVE A,LPTJFN
	BOUT
	CAMG C,(P)
	AOJA C,TAB1
	POP P,B
	POPJ P,

ACTPRT:	HRLI XX,YY
	MOVEI YY,HSHLEN-1
ACTPR1:	SKIPE B,HSHTAB(YY)
	PUSHJ P,ACTPRR
	SOJGE YY,ACTPR1
	POPJ P,

ACTPRR:	SKIPG @XX
	POPJ P,
	PUSHJ P,LPMSGQ
	LPMSG </		/>
	MOVE B,@XX
	MOVE C,[XWD 100006,12]
	PUSHJ P,LPNOUT
	LPMSG </ PAGES
/>
	POPJ P,

ACTLUK:	PUSH P,A
	MOVEI XX,1
	ILDB B,A
	JUMPE B,HSHD
	IMUL XX,B
	JRST .-3

HSHD:	TSC XX,XX
	LSH XX,-1
	MULI XX,HSHLEN
	MOVEI YY,HSHLEN
LUKLUP:	MOVE A,(P)
	SKIPN B,HSHTAB(XX)
	JRST HSHNEW
LUKLU1:	ILDB C,A
	ILDB D,B
	CAME C,D
	JRST HSHNXT
	JUMPN C,LUKLU1
ACTXIT:	POP P,A
	POPJ P,

HSHNXT:	SOSGE XX
	MOVEI XX,HSHLEN-1
	SOJG YY,LUKLUP
	MOVEI XX,0
	JRST ACTXIT

HSHNEW:	MOVE A,(P)
	MOVE B,HSHSTR
	MOVEM B,HSHTAB(XX)
LUKLU2:	ILDB C,A
	IDPB C,B
	JUMPN C,LUKLU2
	MOVEM B,HSHSTR
	JRST ACTXIT

BEND	DUMP

BEGIN	LOAD AND CHECK

^CHCK:	SETOM CHECK
	SKIPA
^LOAD:	SETZM CHECK
	GJINF
	SKIPN 1
	TMSG </
THIS WOULD BE SOMEWHAT FASTER IF YOU WERE LOGGED IN./>
	SETZM RTAPNO
LOA0:	SETOM LOADSW
	SETOM SAMDIR		;NORMAL IS LOAD TO SAME DIRECTORY
	SETOM CDIRN		;NO CONNECTED DIR NOW
	SETZM SUSER
	SKIPE CHECK
	JRST LODFI1
	TMSG </
SPECIFIC USERS? />
	PBIN
	CAIN A,"N"
	JRST [	SETZM SUSER
		JRST LOA1]
	CAIE A,"Y"
	JRST LOA0
	TMSG </ES, USERS: />
	PUSHJ P,REDUSS
	MOVEM C,SUSER
LOA1:	TMSG </
INTO SAME DIRECTORY(S)? />
	PBIN
	CAIN A,"Y"
	JRST LODFI1		;YES MEANS SAME DIR AS ON TAPE
	CAIE A,"N"
	JRST LOA1
	SETZM SAMDIR		;NO MEANS ONLY TO PRESENTLY CONNECTED D
LODFI1:	PUSHJ P,MTOPEN
	MOVEI A,101
	MOVEI B,37
	BOUT
	MOVE A,MTJFN
	MOVEI B,1
	MTOPR
	SETZM RSEQ
	AOS RSEQ
	PUSHJ P,MTRED
	JRST LODFI2
	TMSG </  IN TAPE HEADER
/>
LODFI2:	MOVNI A,TPHDX
	CAME A,TYP
	ERROR(</
TAPE DOESN'T START WITH HEADER/>,<JRST LODEND>)
	MOVE 2,[POINT 7,BUFF]
	MOVEI 1,101
	MOVEI 3,0
	SOUT
	TMSG </
/>
	AOS A,RTAPNO
	CAME A,TAPNO
	ERROR(</
TAPES NOT IN ORDER...CONTINUING/>,<JRST .+1>)
	MOVE A,MTJFN
	MOVEI B,6
	MTOPR
	AOS RSEQ
LODFIL:	PUSHJ P,MTRED
	JRST LODFI3
	TMSG </  IN TAPE TRAILER OR FILE HEADER, />
LODFI5:	PUSHJ P,MTRED
	JRST LODFI4
	MOVE A,MTJFN
	GTSTS
	TLNN A,1000
	JRST LODFI5
	TMSG </FOLLOWED BY EOF, END OF TAPE ASSUMED/>
	JRST LODEND

LODFI4:	PUSHJ P,LODSKP
	JRST LODFIL

LODFI3:	MOVNI A,TPTRX
	CAMN A,TYP
	JRST LODEND
	MOVNI A,FLHDX
	CAME A,TYP
	JRST LODUSR
	SKIPN LOADSW
	JRST [	PUSHJ P,LODSKP
		JRST .+2]
	PUSHJ P,LODSBR
	JRST LODFIL

LODEND:	MOVE A,MTJFN
	MOVEI B,1
	MTOPR
	MOVE A,MTJFN
	CLOSF
	0
	SETOM LOADSW
	SETOM CDIRN
LODEN1:	TMSG </
MOUNT NEXT TAPE IF ANY, TYPE C WHEN READY. TYPE N IF NO MORE: />
	MOVEI 1,100
	BIN
	CAIN B,"C"
	JRST LODFI1
	CAIE B,"N"
	JRST LODEN1
	MOVEI A,101
	MOVEI B,37
	BOUT
	SKIPE WHEEL
	SKIPE CHECK
	POPJ P,
	SKIPE SUSER
	POPJ P,
	GTAD
	SKIPL A
	STAD		; SET TIME AND DATE AFTER LOADING
	 JFCL
	POPJ P,

LODUSR:	MOVNI A,USRX
	CAME A,TYP
	ERROR(</
BAD RECORD TYPE ON TAPE...IGNORED/>,<JRST LODFIL>)
	HRROI B,BUFF+40
	PUSHJ P,TMSGQ		;TYPE NAME
	TMSG </
/>
	SETOM LOADSW
	SKIPE A,SUSER
	 JRST [	SETZ A,
		HRROI B,BUFF+40
		STDIR		;CONVERT NAME ON TAPE TO NUMBER
		JFCL
		JRST LODUS1	;NO SUCH
		MOVEI 1,0(1)	;FLUSH LH BITS
		MOVE B,SUSER
	..A:	CAMN A,SUSL(B)
		JRST LODUS1	;LOAD THIS ONE
		AOBJN B,..A
		SETZM LOADSW	;NOT FOUND, DON'T LOAD
		JRST LODUS1]
	SKIPN SAMDIR		;LOADING ONLY TO CONNECTED DIR?
	JRST LODUS1		;YES, DON'T CREATE NEW DIR
	SKIPE WHEEL
	SKIPE CHECK
	JRST LODUS1
	MOVE A,[POINT 7,BUFF+40]
	MOVE B,[XWD 367740,BUFF]
	CRDIR
LODUS1:	JRST LODFIL

^LODSBR:SKIPGE CDIRN		;GET A CONNECTED DIR?
	JRST CDIR		;NO
	MOVE A,[POINT 7,NAMBUF]
	MOVE B,[POINT 7,BUFF,6]
LODSB1:	ILDB C,B
	ILDB D,A
	CAMN C,D
	JRST LODSB1
	CAIN C,">"
	CAIE D,0
	JRST CDIR		;NAME DIFFERENT, GO CHANGE DIR
CDIR2:	SKIPE CHECK
	PUSHJ P,DELACT		; GET RID OF ACCOUNT AND PROTECTION 1ST
	MOVSI 1,401001
	SKIPE CHECK
	MOVSI 1,100001
	GTJFN
	JRST CANNOT
	MOVEM 1,JFN
^LODSBA:MOVE B,[XWD 440000,342000]
	SKIPE CHECK
	MOVE B,[XWD 440000,200000+1B27]
	SKIPN WHEEL
	TRZ B,1B27
	OPENF
	JRST CANTOP
LODLUP:	PUSHJ P,MTRED
	JRST LODLU1
	TMSG </  IN DATA OR TRAILER OF FILE: />
	MOVEI A,101
	MOVE B,JFN
	MOVEI C,0
	JFNS
	TMSG </,
  DATA ASSUMED VALID/>
LODLU1:	SKIPE TYP
	JRST LODEFL
	HRLZ B,JFN
	HRR B,PAGNO
	SKIPE CHECK
	JRST CHKLUP
	MOVE 1,[XWD 400000,BUFPAG]
	MOVSI 3,160000
	PMAP
	PUSH P,B
	MOVE B,A
	MOVNI A,1
	PMAP
	POP P,1
	MOVE 2,ACCESS
	SPACS
	JRST LODLUP

CDIR:	SETOM CDIRN		;FORGET CURRENT DIR
	MOVE A,[POINT 7,NAMBUF]
	MOVE B,[POINT 7,BUFF,6]	;START AFTER <
	ILDB C,B
	CAIN C,">"
	SETZ C,
	IDPB C,A		;MOVE USER NAME TO BUFFER
	JUMPN C,.-4
	SETZ A,
	HRROI B,NAMBUF
	STDIR
	JFCL
	JRST [	SKIPN SUSER	;NO SUCH USER, HOWEVER IF NOT
		SKIPE SAMDIR	;SINGLE USER AND NOT SAME DIR
		JRST LODSKP
		SETZM CDIRN	;THEN LOAD IT ANYHOW
		JRST LODSBR]
	MOVEI 1,0(1)
	SKIPN C,SUSER		;SPECIFIED USERS?
	JRST CDIR1		;NO
	CAMN A,SUSL(C)		;YES, THIS ONE?
	JRST CDIR1		;YES
	AOBJN C,.-2
	JRST LODSKP		;DON'T WANT THIS USER

CDIR1:	MOVEM 1,CDIRN		;CAN LOAD FOR THIS USER
	SKIPN SAMDIR		;SAME DIRECTORY AS ON TAPE?
	JRST LODSBR		;NO, DON'T RE-CONNECT
	SETZ 2,
	CNDIR
	JRST .+2
	JRST LODSBR
	SETOM CDIRN
	MOVE B,[POINT 7,BUFF]
	JRST CDIR2		;TRY WHOLE STRING

DELACT:	PUSH P,B
	ILDB A,B
	CAIE A,";"
	JRST .-2
	ILDB A,B
	CAIE A,";"
	JRST .-2
	MOVEI A,0
	DPB A,B
	POP P,B
	POPJ P,

CHKLUP:	MOVE A,B
	MOVE B,[XWD 400000,BF2PAG]
	MOVSI 3,100000
	PMAP
	MOVSI XX,-1000
CMPLUP:	MOVE A,BUFF(XX)
	CAME A,BUFF2(XX)
	JRST CMPERR
	AOBJN XX,CMPLUP
	HRLZ A,JFN
	HRR A,PAGNO
	RPACS
	XOR B,ACCESS
	AND B,[XWD 170000,0]
	JUMPE B,LODLUP
	TMSG </
ACCESS ERROR/>
	JRST CMPER1

CMPERR:	TMSG </
COMPARE ERROR/>
CMPER1:	TMSG </, PAGE />
	HRRZ B,PAGNO
	MOVEI C,12
	PUSHJ P,TTNOUT
	TMSG </, FILE />
	MOVE B,JFN
	MOVEI C,0
	JFNS
	JRST LODLUP

LODEFL:	MOVNI A,FLTRX
	CAME A,TYP
	ERROR(</
BAD RECORD TYPE ON TAPE...IGNORED/>,<JRST LODLUP>)
	SKIPE CHECK
	JRST CHKFDB
	MOVSI XX,-25
FIXFDB:	MOVE A,JFN
	MOVE 3,BUFF(XX)
	SKIPN WHEEL
	SKIPA 2,NWMASK(XX)
	MOVE 2,MASK(XX)
	HRL A,XX
	CHFDB
	AOBJN XX,FIXFDB
LODCLZ:	MOVE 1,JFN
	CLOSF
	0
^LODEOF:MOVE A,MTJFN
	MOVEI B,6
	MTOPR
	AOS RSEQ
	POPJ P,

CHKFDB:	MOVNI A,1
	MOVE B,[XWD 400000,BF2PAG]
	PMAP
	MOVE A,JFN
	MOVE B,[XWD 25,0]
	MOVEI 3,BUFF2
	GTFDB
	MOVSI XX,-25
CHKFDL:	MOVE A,BUFF(XX)
	CAME A,BUFF2(XX)
	JRST FDBERR
CHKFD0:	AOBJN XX,CHKFDL
	JRST LODCLZ

FDBERR:	TMSG </
DIFFERENCE IN />
	MOVE B,FDBNAM(XX)
	MOVEI A,0
	ROTC A,6
	JUMPE A,FDBER9
	ADDI A,40
	PBOUT
	JRST .-5

FDBER9:	TMSG </ OF FILE />
	MOVE B,JFN
	MOVEI 1,101
	MOVEI C,0
	JFNS
	JRST CHKFD0

CANNOT:	TMSG </
CANNOT GET JFN FOR FILE />
	JRST CANT1

CANTOP:	MOVE A,JFN
	RLJFN
	0
	TMSG </
CANNOT OPEN FILE />
CANT1:	HRROI 2,BUFF
	MOVEI 3,0
	MOVEI 1,101
	SOUT
^LODSKP:SETOM RSEQ
	MOVE A,MTJFN
	MOVEI B,16
	MTOPR
	POPJ P,

BEND	LOAD

COMCHK:	MOVSI XX,-1000-NHEAD
	MOVEI A,0
	JCRY0 COMCH1
COMCH1:	ADD A,BUFF-NHEAD(XX)
	JCRY0 [AOJA A,.+1]
	AOBJN XX,COMCH1
	CAMN A,[-1]
	AOS A
	POPJ P,

INIFRK:	MOVSI A,400000
	CFORK
	ERROR(</
CANNOT CREATE LOWER FORK/>,<HALTF>)
	MOVEM A,LOWFRK
	HRLZ B,A
	MOVSI A,400000
	MOVSI 3,160000
	PMAP
	AOS A
	TRNN A,777600
	AOJA B,.-3
	SETOM BUFCNT
	MOVEI A,BUFFER+1
	MOVEM A,PRGBUF
	MOVEM A,FRKBUF
	POPJ P,

MTOUT:	PUSH P,A
	PUSH P,B
	RADIX =10
	MOVN A,[(512+NHEAD)*60/8+750]
	RADIX =8
	ADDM A,TAPLFT
	AOS SEQ
	SETZM CHKSUM
	PUSHJ P,COMCHK
	SETCAM A,CHKSUM
	PUSHJ P,WATFRK
	MOVEI B,777+NHEAD(A)
	BLT A,(B)
	PUSHJ P,STFRK
	POP P,B
	POP P,A
	POPJ P,

MTRED:	PUSH P,A
	PUSH P,B
	MOVEI XX,10
	MOVEI B,0
MTRED0:	MOVE A,MTJFN
	MTOPR
	MOVEI 2,MTREDC
	DUMPI
	JRST [	MOVE A,MTJFN
		GTSTS
		TLNE B,1000
		JRST MTEOF
		MOVEI B,7
		SOJG XX,MTRED0
		MOVSI A,400000
		IORM A,RSEQ
		AOS RSEQ
		TMSG </
READ ERROR!!!/>
		AOS -2(P)
		JRST MTRED1]
	SKIPG A,RSEQ
	JRST [	MOVE B,SEQ
		TLNE A,(1B1)
		JRST [	MOVEM B,RSEQ
			JRST MTRED2]
		TLZ A,(1B0)
		EXCH A,SEQ
		MOVEM A,RSEQ
		CAME A,SEQ
		JRST [	TMSG </ NOT RECOVERED/>
			JRST MTRED2]
		TMSG </ RECOVERED/>
		JRST MTRED2]
	AOS A,RSEQ
	CAME A,SEQ
	JRST [	TMSG </
SEQUENCE ERROR
/>
		AOS -2(P)
		MOVE A,SEQ
		MOVEM A,RSEQ
		JRST MTRED1]
MTRED2:	PUSHJ P,COMCHK
	JUMPN A,[TMSG </
CHECKSUM ERROR/>
		MOVSI A,400000
		IORM A,RSEQ
		AOS RSEQ
		AOS -2(P)
		JRST MTRED1]

MTRED1:	POP P,B
	POP P,A
	POPJ P,

MTEOF:	TMSG </
UNEXPECTED EOF/>
	AOS -2(P)
	JRST MTRED1

MTREDC:	XWD -1000-NHEAD,BUFF-NHEAD-1
	0

WATFRK:	MOVE A,BUFCNT
	CAIL A,NBUF-1
	JRST [	MOVEI 1,=200
		DISMS
		JRST .-2]
	MOVE A,PRGBUF
	MOVE B,-1(A)
	MOVEM B,PRGBUF
	HRLI A,BUFF-NHEAD
	POPJ P,

STFRK:	AOSE BUFCNT
	POPJ P,
	MOVE A,LOWFRK
	WFORK
	MOVE A,LOWFRK
	MOVEI B,FRK1
	SFORK
	POPJ P,

FRK1:	MOVE P,[XWD -10,FRKPDL-1]
	MOVE A,FRKBUF
	MOVE B,-1(A)
	MOVEM B,FRKBUF
	SKIPG B,XTYP(A)
	JRST FRKWRT
	CAIN B,1
	SETZM FRKSEQ
	AOS C,FRKSEQ
	CAME C,XSEQ(A)
	JRST FRKSQE
	MOVE A,MTJFN
	MTOPR
FRKXT:	SOSL BUFCNT
	JRST FRK1
	HALTF

FRKSQE:	TMSG </
SEQUENCE ERROR IN FORK, MUST BE PROGRAM BUG./>
	HALTF

FRKWRT:	AOS B,FRKSEQ
	CAME B,XSEQ(A)
	JRST FRKSQE
FRKWR1:	SOS A
	HRRM A,MTCOMS
	MOVEI B,0
	MOVE A,MTJFN
	MTOPR		; CLEAR ANY ERRORS ETC
	MOVEI B,MTCOMS
	DUMPO
	SKIPA
	JRST FRKCLZ
	MOVEI A,101
	DOBE
	HRROI A,[ASCIZ /
WRITE ERROR ON TAPE. PROBABLY NOT MOUNTED OR NO WRITE RING.
PLEASE CHECK AND TYPE ANY CHARACTER WHEN READY. /]
	PSOUT
	PBIN
	MOVEI A,37
	PBOUT
	AOS A,MTCOMS
	JRST FRKWR1

FRKCLZ:	AOS B,MTCOMS
	MOVNI A,FLTRX
	JRST FRKXT

MTCOMS:	XWD -1000-NHEAD,BUFF-NHEAD-1
	0

REWIND:	PUSH P,A
	PUSH P,B
	MOVEI 2,1
	MOVEM 2,TYP
	SETZM SEQ
	SETZM RSEQ
	PUSHJ P,MTOUT
	POP P,B
	POP P,A
	POPJ P,

ENDTAP:	MOVNI A,TPTRX
	MOVEM A,TYP
	PUSHJ P,MTOUT
	PUSHJ P,ENDFIL
	JRST REWIND

ENDFIL:	PUSH P,A
	PUSH P,B
	MOVNI A,=3000
	ADDM A,TAPLFT
	MOVEI B,3
	MOVEM B,TYP
	PUSHJ P,MTOUT
	POP P,B
	POP P,A
	POPJ P,

MTOPEN:	TMSG </
TYPE MAG TAPE UNIT NUMBER: />
	MOVEI A,100
	BIN
	DPB B,[POINT 7,MTDEV,27]
	MOVEI B,37
	MOVEI A,101
	BOUT
	MOVE B,MTDEV
	ANDCMI B,377		;FLUSH COLON
	HRROI A,B		;STRING PTR TO B
	STDEV
	JRST [	TMSG </NO SUCH DEVICE
/>
		JRST MTOPEN]
	MOVE 1,2		;DEV DSG'R
	TLO 1,(1B3)		;SAY NO DIRECTORY
	MOUNT
	JRST [	TMSG </FAILED TO MOUNT
/>
		JRST MTOPEN]
	HRROI B,MTDEV
	MOVSI A,1
	GTJFN
	ERROR(</
NO SUCH UNIT AVAILABLE./>,<JRST MTOPEN>)
	MOVE 2,[XWD 7400,300000]
	OPENF
	ERROR(</
TAPE NOT AVAILABLE/>,<JRST MTOPEN>)
	MOVEM 1,MTJFN
	POPJ P,

TTNOUT:	MOVEI A,101
	JRST LPNOUT+1

LPNOUT:	MOVE A,LPTJFN
	NOUT
	JFCL
	POPJ P,

TMSGQ:	MOVEI 1,101
	MOVEI C,0
	SOUT
	POPJ P,

BTMSGQ:	PUSH P,B
	PUSHJ P,TMSGQ
	POP P,B
LPMSGQ:	MOVE 1,LPTJFN
	MOVEI C,0
	SOUT
	POPJ P,

BUFF2_601000
BF2PAG_601
BUFF_600000
BUFPAG_600
SEQ_BUFF-1
TYP_BUFF-2
PAGNO_BUFF-3
TAPNO_BUFF-4
ACCESS_BUFF-5
CHKSUM_BUFF-6
NHEAD__6

TPHDX__1
FLHDX__2
FLTRX__3
TPTRX__4
USRX__5
XBUFF__NHEAD
XPAGNO__PAGNO-BUFF+XBUFF
XTYP__TYP-BUFF+XBUFF
XSEQ__SEQ-BUFF+XBUFF

MASK:	0
	603000000000
	0
	0
	0
	777777777777
	777777000000
	0
	0
	777700000000
	777777777777
	777777777777
	777777777777
	777777777777
	777777777777
	777777777777
	0
	0
	0
	0
	777777777777

NWMASK:	0
	0
	0
	0
	0
	0
	0
	0
	0
	777700000000
	777777777777
	0
	0
	0
	0
	0
	0
	0
	0
	0
	777777777777

FDBNAM:	SIXBIT /HEADER/
	SIXBIT /FDBCTL/
	SIXBIT /FDBEXT/
	SIXBIT /FDBADR/
	SIXBIT /FDBPRT/
	SIXBIT /FDBCRE/
	SIXBIT /FDBUSE/
	SIXBIT /FDBVER/
	SIXBIT /FDBACT/
	SIXBIT /FDBBYV/
	SIXBIT /FDBSIZ/
	SIXBIT /FDBCRV/
	SIXBIT /FDBWRT/
	SIXBIT /FDBREF/
	SIXBIT /FDBCNT/
	SIXBIT /FDBCK1/
	SIXBIT /FDBCK2/
	SIXBIT /FDBCK3/
	SIXBIT /FDBCK4/
	SIXBIT /FDBCK5/
	SIXBIT /FDBUSW/

LIT

ENDCOD:

LOC 10000

BUFFER:	BLOCK NBUF*(NHEAD+1001)
HSHTAB:	BLOCK HSHLEN
HSHCUM:	BLOCK HSHLEN
HSHUSR:	BLOCK HSHLEN
HSHBUF:	BLOCK HSHLEN*2
HSHSTR:	BLOCK 1
DATBUF:	BLOCK 30
NAMBUF:	BLOCK 100
FRKPDL:	BLOCK 10
PDL:	BLOCK 100
NAMPTR:	BLOCK 1
DIRPTR:	BLOCK 1
NOFILS:	BLOCK 1
TOTFIL:	BLOCK 1
WHEEL:	BLOCK 1
DIRNUM:	BLOCK 1
SCNJFN:	BLOCK 1
MTDEV:	BLOCK 1
LASTID:	BLOCK 1
LSTERO:	BLOCK 1
INTAC:	BLOCK 20
INTSTK:	BLOCK 30
RET:	BLOCK 1
DSKERN:	BLOCK 1
RTAPNO:	BLOCK 1
RSEQ:	BLOCK 1
FRKSEQ:	BLOCK 1
TAPLFT:	BLOCK 1
CNTR:	BLOCK 1
PAGCNT:	BLOCK 1
USRCNT:	BLOCK 1
TOTCNT:	BLOCK 1
LOADSW:	BLOCK 1
SAMDIR:	BLOCK 1
CDIRN:	BLOCK 1
SUSER:	BLOCK 1
CHECK:	BLOCK 1
CONDIR:	BLOCK 1
LSTJFN:	BLOCK 1
LPTJFN:	BLOCK 1
BUFCNT:	BLOCK 1
PRGBUF:	BLOCK 1
FRKBUF:	BLOCK 1
SNGUSR:	BLOCK 1
SUSL:	BLOCK NUSL
NODMSW:	BLOCK 1
FIRST:	BLOCK 1
LONGSW:	BLOCK 1
INCRSW:	BLOCK 1
LOWFRK:	BLOCK 1
DIRNAM:	BLOCK 12
JFN:	BLOCK 1
MTJFN:	BLOCK 1

	END START
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                      