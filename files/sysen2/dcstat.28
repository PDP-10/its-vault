; -*-MIDAS-*-

TITLE DCSTAT

DCHOST==37
DCSOKT==141

A==1
B==2
C==3
D==4
E==5
T==6
TT==7
P==17

TTO==1

DEBUG:	0

$$ICP==1
$$ERRHAN==1
$$ARPA==1

;.INSRT SYSENG;NETWRK
.INSRT MOON;XNETWK

DCSTAT:	.OPEN TTO,[.UAO,,'TTY]
	 .LOGOUT 1,
	MOVE P,[-STSIZ,,STBEG-1]
	SETZ A,			; CONNECT ON CHANNEL 0,2,3
	MOVEI B,DCHOST
	MOVEI C,DCSOKT
	MOVEI D,.UAO
	PUSHJ P,NETWRK"ARPICP
	 .LOGOUT 1,
	SKIPE FLAGDD
	 JRST QUIT
HEADER:	PUSHJ P,RENPRE
	CAMN 15,[ASCII/.J900/]
	 .LOGOUT 1,
	CAME 15,[ASCII/.I280/]
	 JRST HEADER
STALIN:	.IOT TTO,["]] ? .IOT TTO,[" ]
STATUS:	.IOT 2,
	.IOT TTO,
	CAIE ^J
	 JRST STATUS
	.IOT 2,
	CAIN <" >
	 JRST STALIN
FINISH:	PUSHJ P,RENLIN
QUIT:	.IOT 2,
	JUMPL [.LOGOUT 1,]
	SKIPE FLAGDD
	 .IOT TTO,
	JRST QUIT

; PUTCHR -- WRITE OUT A CHARACTER

PUTCHR:	.IOT TTO,T
	POPJ P,

; RETURNERS

POPJ1:	AOS (P)
CPOPJ:	POPJ P,

; RENLIN -- REQUEST END: PROCEED TO NEW LINE

RENLIN:	.IOT 2,
	CAIE ^J
	 JRST RENLIN
	POPJ P,

; RENPRE -- REQUEST END: RETURN PREFIX

RENPRE:	SETZ 15,
	MOVE 16,[440700,,15]
RENPRL:	.IOT 2,
	CAIE ^M
	 CAIN ^J
	  JRST RENPRE
	IDPB 16
	TRNN 15,376
	 JRST RENPRL
	LDB [350700,,1]
	CAIE "?
	 CAIN "!
	  CAIA
	   JRST RENLIN
	.IOT TTO,[" ]
	.IOT TTO,["(]
RENLIP:	.IOT 2, 
	CAIE ^I
	 JRST RENLIP
	.IOT 2,
RENLPP:	.IOT TTO,
	.IOT 2,
	CAIE ^M
	 JRST RENLPP
	.IOT 2,
	.IOT TTO,[")]
	.IOT TTO,[^M]
	.IOT TTO,[^J]
	POPJ P,

FLAGDD:	0

	STSIZ==100
STBEG:	BLOCK STSIZ

END DCSTAT
