(LAP WHEN-WRITTEN SUBR)
	(*CALL 0 CALL-CRAP)
	(JFCL)
	(JSP T FXCONS)
	(POPJ P)
CALL-CRAP
	(SETZ)
	(SIXBIT RFDATE)
	(0 0 3 1000)
	(0 0 TT 402000)
NIL 

(LAP INTER-READ SUBR)
	(MOVEI TT INTER-TYI)
	(MOVEM TT TYIMAN)
	(SETZM 0 PERCT)
	(SETZM 0 UNREAD)
	(SETZM 0 SPACE-FLG)
	(SETZM 0 BRAK-CT)
	(SETOM 0 PAREN-CT)
	(CALL 1 (FUNCTION READ))
	(SETZM 0 TYIMAN)
	(POPJ P)
BRAK-CT (BLOCK 1)
PAREN-CT (BLOCK 1)
SPACE-FLG (BLOCK 1)
PERCT (BLOCK 1)

INTER-TYI
	(SKIPE A UNREAD)
	(JRST 0 0 A)
	(PUSHJ P (TYI 2))
	(AOSN 0 PERCT)
	(POPJ P)
CHECK
	(CAIN A 56)	;.
	(JRST 0 DOT)
	(SETZM 0 SPACE-FLG)
	(CAIN A 133)	;[
	(JRST 0 OPENB)
	(CAIN A 135)	;]
	(JRST 0 CLOSEB)
	(CAIN A 40)
	(SETOM 0 SPACE-FLG)
	(CAIN A 45)	;%
	(SETOM 0 PERCT)
	(CAIN A 50)	;OPEN-PAREN
	(AOS 0 PAREN-CT)
	(CAIN A 51)	;CLOSE-PAREN
	(SOS 0 PAREN-CT)
	(POPJ P)
DOT
	(AOSE 0 SPACE-FLG)
	(JRST 0 NEED-PCT)	;%.
	(PUSHJ P (TYI 2))
	(CAIN A 40)
	(JRST 0 GOOD-DOT)	;<SPACE> . <SPACE>
	(MOVSS 0 A)		;% . <CHAR>
	(HRRI A 45)		;%
	(JSR 0 UNREAD)
	(HRRI A 56)		;.
	(JSR 0 UNREAD)
	(HLRZS 0 A)
	(SETZM 0 UNREAD)
	(JRST 0 CHECK)
GOOD-DOT
	(MOVEI A 56)		;.
	(JSR 0 UNREAD)
	(SETZM 0 UNREAD)
	(MOVEI A 40)
	(SETOM 0 SPACE-FLG)
	(POPJ P)
NEED-PCT
	(MOVEI A 45)		;%
	(JSR 0 UNREAD)
	(SETZM 0 UNREAD)
	(MOVEI A 56)		;.
	(POPJ P)

UNREAD	(BLOCK 1)
	(HLLM A UNREAD)
	(HRRZS 0 A)
	(POPJ P)

OPENB	(PUSH FXP PAREN-CT)
	(SETZM 0 PAREN-CT)
	(AOS 0 BRAK-CT)
	(POPJ P)

CLOSEB1 (MOVEI A 51)	;CLOSE PAREN
	(JSR 0 UNREAD)
CLOSEB	(SOSL 0 PAREN-CT)
	(JRST 0 CLOSEB1)
	(SETZM 0 UNREAD)
	(SETOM 0 PAREN-CT)
	(MOVEI A 135)	;CLOSE BRAK
	(SKIPG 0 BRAK-CT)
	(POPJ P)
	(SOS 0 BRAK-CT)
	(POP FXP PAREN-CT)
	(POPJ P)
NIL
