
TITLE INFO DOCUMENTATION PROGRAM

TYIC==1
TYOC==2
DSKC==3
ADDC==4
LPTC==5
DISC==6

F=0
A=1
B=2
C=3
D=4
NAME=5
IC=6
P=7
NL=10
T=11
NLSA=12

TEXT=1000,,0
TERR=2000,,0
PRINT=4000,,0
.TEXT=11000,,0	;TO 340
.TERR=12000,,0	;TO 340

CM==0	; =1 => SYS:TS XXX ONLY

ADDFL==1	;SET DURING $A
GETTY==2	;TTY = DISPLAY CONSOLE
LPTF==4		;LPT OUTPUT ON
STOPF==10	;TTY OUTPUT STOPPED, LPT ON
DISF==20	;OUTPUT TO 340
ARDS==40	;TTY = ARDS CONSOLE
RLPT==100	;LPT ON BUT STOPPED

	LOC 41

	JSR UUOH
	JSR TSINT

	LOC 100

NODIS:	TRZ DISF
	TEXT DISOFL
	JRST NDS
GET:	TRO GETTY
	MOVEI NL,23.
	MOVEI NLSA,23.
	JRST NAR

GO:	MOVEI P,PDL
	.OPEN TYIC,TYI
	.VALUE [ASCIZ /./]
	.OPEN TYOC,TYO
	.VALUE [ASCIZ /./]
	.STATUS TYIC,A
	TRZ DISF+ARDS
	TRNN A,200000	;SKIP IF AT 340 OR 340 SLAVE
	JRST NDS
YESDIS:	TRO DISF
	.OPEN DISC,DISO
	JRST NODIS
	MOVEI NL,39.
	MOVEI NLSA,39.
NDS:	TRZ GETTY
	.STATUS TYOC,A
	TRNE A,2
	JRST GET
NAR:	.SUSET [.SMSK2,,[377,,1_<TYIC>]]
	.SUSET [.SSNAM,,[SIXBIT /.INFO./]]
	.OPEN DSKC,SYSF
	JRST NOSYS
	PUSHJ P,PRINTF	;PRINT SYSTEM MODS
NOSYS:	TEXT INIT	;SAY HELLO

LOOP:	MOVEI P,PDL
	TRZ STOPF
	TRZE RLPT	;IF LPT WAS RESET
	TRO LPTF	;ALLOW OUTPUT
	PUSHJ P,GETCH	;GET COMMAND
DISP:	CAIE D,"T
	CAIN D,^T	;LIST COMMANDS
	JRST LISTCM
	CAIN D,"?	;EXPLAIN INFO
	JRST QM
	CAIN D,^B	;TURN ON LPT OUTPUT
	JRST LPT
	CAIN D,^D	;FLIP 340 USAGE
	JRST DISPLY
	CAIE D,^E	;TURN OFF LPT OUTPUT
	CAIN D,^L	;CLEAR SCREEN
	JRST LOOP
	CAIN D,^F	;LIST INFO FILES
	JRST CTLF
	CAIE D,"Q
	CAIN D,^Q	;QUIT
	.VALUE [ASCIZ /./]
IFN CM,[	MOVEM NAME,CMDNM	;END OF NO-ARG DISPATCH
	.OPEN DSKC,CMDF
	TERR NOCMD
]
DDT:	CAIE D,^P	;LEFTOVER FROM OLD DAYS
	CAIN D,"D
	PRINT [SIXBIT /INFO/]
	CAIN D,"E
	PRINT [SIXBIT /ERROR/]
	CAIN D,"I
	PRINT [SIXBIT /ORDER/]
	CAIN D,"S
	JRST SIMUL
	CAIN D,33	;ALTMODE
	JRST OTHER	;STRANGE FN2
	MOVEM NAME,RECNM	;MUST BE LAST
	CAIN D,"A
	JRST ADD
	CAIE D,15	;END OF DISPATCH
	TERR HUH
	.OPEN DSKC,RECF	;RECENT MODS
	TERR NOREC	;NONE FOUND
	PUSHJ P,FORMF
	.TEXT RECINF
	PUSHJ P,PRLP	;PRINT MODS
	JRST DISLOC

DOPRI:	MOVEM NAME,INFNM
	MOVE A,(B)	;GET 2ND FILE NAME
	MOVEM A,INFN2
	.OPEN DSKC,INFF	;GET BLURB
	TERR NOINF	;SIGN OF LAZY SYSTEM HACKER
	PUSHJ P,PRINTF
	JRST DISLOC	;ASK FOR COMMAND

QM:	MOVE NAME,[SIXBIT /INFO/]
	PRINT [SIXBIT /INFO/]

LPT:	.OPEN LPTC,LPTO
	TERR NOLPT
	TRO LPTF
	JRST DISLOC

DISPLY:	TRZE DISF
	JRST DCLS	;TURN OFF 340
	.OPEN DISC,DISO	;TURN ON 340
	TERR DISOFL
	TROA DISF
DCLS:	.CLOSE DISC,
	JRST DISLOC

ADD:	.SUSET [.RUNAME,,UFNM]
	.OPEN ADDC,ADDF
	TERR OOF
	TRO ADDFL	;SO ^G WILL WIN
	MOVE A,UFNM	;IDENTIFY LOSER, DATE, TIME
	PUSHJ P,6TO7
	.IOT ADDC,C40
	PUSHJ P,BUGDAT
	PUSHJ P,BUGTIM
	MOVE D,[10700,,CBUF-1]	;GOBBLE TEXT
ILOOP:	.IOT TYIC,C
ILF:	CAIN C,177
	JRST ARO
	CAIE C,^L
	CAIN C,^C
	JRST ENDIN
	IDPB C,D
	CAIE C,15
	JRST ILOOP
	PUSHJ P,CRL
	MOVEI C,12
	JRST ILF
ENDIN:	MOVEM D,BPT'
	MOVE D,[10700,,CBUF-1]	;OUTPUT TEXT
CLOOP:	CAMN D,BPT
	JRST ENDC
	ILDB C,D
	.IOT ADDC,C
	JRST CLOOP
ENDC:	.IOT ADDC,C15
	.IOT ADDC,C12
	.OPEN DSKC,RECF	;COPY OLD STUFF
	JRST CLOSE
OLOOP:	.IOT DSKC,C
	JUMPL C,CLOSE
	.IOT ADDC,C
	JRST OLOOP
CLOSE:	MOVEM NAME,RENM
	.FDELE RENAME	;WHILE OPEN FOR WRITING
	TERR RENMF
QUIT:	.CLOSE ADDC,
	TRZ ADDFL
	JRST DISLOC

ARO:	CAMN D,[10700,,CBUF-1]
	TERR [ASCIZ /?
/]
	LDB C,D
	.IOT TYOC,C
	ADD D,[70000,,]
	SKIPGE D
	SUB D,[430000,,1]
	JRST ILOOP

CTLF:	MOVE NAME,FD1	;.FILE.
	PRINT FD2	;(DIR)

BUGDAT:	.RDATE B,
	ROT B,12.
	MOVEI C,"/
	JRST BUGTL2
BUGTIM:	.RTIME B,
	MOVEI C,":
BUGTL2:	MOVEI D,0
BUGTL1:	MOVEI A,0
	ROTC A,6
	ADDI A,40
	.IOT ADDC,A
	JUMPE B,BUGSPC
	MOVE A,C
	TRNE D,1
	.IOT ADDC,A
	AOJA D,BUGTL1
BUGSPC:	.IOT ADDC,C40
	POPJ P,
6TO7:	MOVEI B,0
	ROTC A,6
	ADDI B,40
	.IOT ADDC,B
	JUMPN A,6TO7
C40:	POPJ P,40

FORMF:	TRNE LPTF
	.IOT LPTC,CFF
NLFF:	TRNE DISF
	JRST DPFF
	TRNN GETTY
CC:	POPJ P,"C
	.IOT TYOC,CCTLP
	.IOT TYOC,CC
	MOVEI NL,(NLSA)
CCTLP:	POPJ P,^P
DPFF:	.STATUS DISC,T
	TRNE T,20	;IS THIS NUL DEV?
	JRST NULX
	.IOT DISC,CCT
	MOVEI NL,(NLSA)
CFF:	POPJ P,^L
NULX:	.CLOSE DISC,
	TRZ DISF
	TEXT DISOFL	;DIS HAS BEEN STOLEN
	JRST NLFF

TYOH:	TRNE LPTF
	.IOT LPTC,C
NLTYO:	TRNE STOPF
	POPJ P,
	TRNE DISF
	.IOT DISC,C
	TRNE DISF
CCT:	POPJ P,^T
	CAIN C,^P
	JRST CPTYP
	.IOT TYOC,C
CUPAR:	POPJ P,"^
CPTYP:	.IOT TYOC,CUPAR
	.IOT TYOC,CP
CP:	POPJ P,"P

UUOH:	0
	MOVE B,40
	TLNE B,4000	;IS THIS PRINT?
	JRST DOPRI	;OTHERWISE TEXT OR TERR
	TRZE ADDFL
	.CLOSE ADDC,	;ERROR IN $A
	PUSH P,C
	HRRZ A,40
	JUMPE A,DONE
	HRLI A,440700
UOLP:	ILDB C,A	;PRINT CHARS
	JUMPE C,DONE
	TLNE B,10000
	PUSHJ P,NLTYO	;.TEXT, .TERR
	TLNN B,10000
	.IOT TYOC,C	;TEXT, TERR
	JRST UOLP
DONE:	POP P,C
	TLNE B,1000	;WHICH UUO?
	JRST 2,@UUOH	;TEXT, ALL DONE
DISLOC:	TEXT ERRM	;TERR, GET COMMAND
	JRST LOOP

TSINT:	0
	0
	MOVE IC,TSINT
	TRNN IC,1_<TYIC>	;IS IT TYI
	JRST UCLS	;NO, INFERIOR
	MOVEI IC,TYIC	;GET CHAR
	.ITYIC IC,
	.DISMISS TSINT+1	;SHOULDN'T HAPPEN
	CAIN IC,^G	;FULL QUIT
	JRST CTLG
	TRNE ADDFL	;FOLLOWING NOT COMMANDS AFTER $A
	.DISMISS TSINT+1
	CAIN IC,^E	;FLUSH LPT
	JRST CTLE
	CAIN IC,^R
	JRST CTLR
	CAIE IC,^S	;STOP TYPEOUT
	.DISMISS TSINT+1	;END OF DISPATCH
	TRNE LPTF
	TRO STOPF
CTLS:	.RESET TYOC,	;STOP TYPING
	.RESET TYIC,
	PUSHJ P,CRL
	TRNN STOPF	;IF NOT STILL PRINTING
	.DISMISS [DISLOC]	;GET COMMAND
	.DISMISS TSINT+1

CTLG:	TRZ STOPF
	TRZE ADDFL
	.CLOSE ADDC,
	JRST CTLS

CTLR:	TRNN LPTF+RLPT	;IF LPT WAS OFF
	JRST CTLE2	;DO NOTHING
	.RESET LPTC,	;STOP OLD PRINTOUT
	TROA RLPT	;TURN LPTF BACK ON AT NEXT COMMAND

CTLE:	.CLOSE LPTC,
	TRZ LPTF
CTLE2:	.IOT TYIC,IC	;MASK  FROM COMMAND LOOP
	TRNE STOPF
	.DISMISS [DISLOC]
	HLRZ IC,@TSINT+1
	CAIN IC,(.IOT TYIC,)
	.DISMISS [DISLOC]	;GET NEW COMMAND
	CAIN IC,(.IOT LPTC,)
	AOS TSINT+1
	.DISMISS TSINT+1

UCLS:	.UCLOSE DSKC,
	TEXT PROC
	.DISMISS [DISLOC]

GETCH:	MOVE A,[440600,,NAME]
	MOVEM NAME,ONAME'	;FOR DEFAULT
	MOVEI NAME,0
CHLP:	.IOT TYIC,D	;GET CHAR
	CAIN D,177
	JRST RO
	CAIN D,":	;FOR MONMOD LOSERS
	JRST CHLP
	CAIN D,33	;ALTMODE
	JRST ALTM
	CAIE D,"?
	CAIG D,40
	JRST FINI	;CR OR COMMAND CHAR
	SUBI D,40
	TLNE A,770000
	IDPB D,A
	JRST CHLP
ALTM:	.IOT TYIC,D
FINI:	SKIPN NAME	;IF NO NAME
	MOVE NAME,ONAME	;GET DEFAULT
	CAIN D,33	;2 ALTMODES
C15:	POPJ P,15	;MORE TEXT COMING
CRL:	.IOT TYOC,C15
	.IOT TYOC,C12
C12:	POPJ P,12

RO:	TLNE A,400000
	JRST DISLOC	;NO CHARS TO RUB OUT
	LDB D,A
	ADDI D,40
	.IOT TYOC,D
	ADD A,[060000,,0]
	JRST CHLP

SIMUL:	MOVEM NAME,PRONM	;CHECK IF SIM EXISTS
	.OPEN DSKC,PROF
	TERR NOSIM
	.SUSET [.RUNAME,,USRNM]	;OPEN INFERIOR
	.GENSYM A,
	MOVEM A,USNM2
	.OPEN DSKC,USRF
	TERR CORE
	MOVEM NAME,LD3-OFST	;PUT COMMAND NAME INTO LOADER
	.ACCESS DSKC,43	;PUT LOADER IN INFERIOR LOC. 43FF
	MOVE A,[LOADER-LDEND,,LOADER]
	.IOT DSKC,A
	.USET DSKC,[.SUPC,,[43]]	;SET INFERIOR PC
	.USET DSKC,[.SSNAM,,[SIXBIT /.INFO./]]	;SET INFERIOR SNAME
	.ATTY DSKC,
	TERR ATYFLD
	.USET DSKC,[.SUSTP,,[0]]	;LET 'ER RIP
	MOVSI A,(SETZ)	;WAIT FOR CONTROL Z
	.SLEEP A,
	CONSTANTS

OFST==43-.
LOADER:	OFFSET OFST
	.OPEN 1,OPN
	.VALUE
	MOVEI 3,2000
	MOVE 1,[-1,,2]
	.IOT 1,1
	CAME 2,[JRST 1]
	JRST .-3
LD1:	MOVE 1,[-1,,2]
	.IOT 1,1
	JUMPG 2,2
	HLRE 1,2
	MOVN 1,1
	ADDI 1,(2)
	CAMGE 1,3
	JRST LD2
	AOS .+1
	.CORE 1
	.VALUE
	ADDI 3,2000
LD2:	.IOT 1,2
	MOVE 1,[-1,,2]
	.IOT 1,1
	JRST LD1
OPN:	6,,(SIXBIT /DSK/)
LD3:	0
	SIXBIT /PROG/
	CONSTANTS
	IFGE .-100,[PRINTC /LDR TOO BIG
/]
	OFFSET 0
LDEND:
PRINTF:	PUSHJ P,FORMF
PRLP:	.IOT DSKC,C	;GET CHAR FROM FILE
	CAIE C,^L
	SKIPGE C
	POPJ P,	;DONE
	TRNE STOPF
	JRST PRCH	;NOT TYPING
	CAIE C,15
	JRST PRCH
	SOJG NL,PRCH
SAYM:	.TEXT [ASCIZ /
--MORE--/]
	.IOT TYIC,D	;GET CHAR FROM TTY
	CAIE D,40
	JRST FLS	;NOT SPACE, FLUSH
	PUSHJ P,NLFF	;CLEAR SCREEN
PRCH:	PUSHJ P,TYOH	;PRINT CHAR
	JRST PRLP

FLS:	TRNN LPTF	;FINISHED IF NO LPT
	.TERR [ASCIZ /FLUSHED
/]
	.TEXT [ASCIZ /LPT CONTINUES./]
	TRO STOPF
	JRST PRCH	;AND CONTINUE LPT

OTHER:	PUSHJ P,GETCH	;GET 2ND FILE NAME
	EXCH NAME,ONAME	;SCREWS DEFAULT
	PRINT ONAME	;FROM FN1$$FN2

LISTCM:	.OPEN DSKC,FDIR
	TERR PANIC
	PUSHJ P,FORMF
	HRLOI D,-1	;TO SKIP TWO LINES
L3:	MOVEI B,5	;NAMES PER TTY LINE
L2:	.IOT DSKC,C
	CAIE C,3	;EOF
	JRST .+3
	PUSHJ P,CRL
	JRST DISLOC
	CAIE C,12	;END OF LINE
	JRST L2
	AOJE D,L2	;SKIP 2ND--FIRST TIME AROUND ONLY
CMLP:	MOVEI A,5	;5 CHARS RANDOMNESS
	.IOT DSKC,C
	SOJGE A,.-1
	CAIN C,40	;MIGHT BE ANOTHER SPACE
	.IOT DSKC,C
	CAIE C,"T	;MUST BE TS XXX
	JRST L2	;NO, SKIP LINE
	.IOT DSKC,C
	CAIE C,"S
	JRST L2	;NO
	MOVEI A,5	;MUST BE 5 SPACES
SPLP:	.IOT DSKC,C
	CAIE C,40
	JRST L2	;NO
	SOJG A,SPLP
	MOVEI A,7	;OK, TYPE 7 CHARS, NAME + SPACE
TYLP:	.IOT DSKC,C
	PUSHJ P,TYOH
	SOJG A,TYLP
	SOJG B,L2	;GET NEXT LINE UNLESS END OF TTY LINE
	MOVEI C,15
	PUSHJ P,TYOH
	MOVEI C,12
	PUSHJ P,TYOH
	JRST L3	;GET 5 MORE NAMES

PROF:	(SIXBIT /DSK/)
PRONM:	0
	SIXBIT /PROG/

USRF:	7,,(SIXBIT /USR/)
USRNM:	0
USNM2:	0

ADDF:	1,,(SIXBIT /DSK/)
UFNM:	0
	SIXBIT /OUTPUT/

SYSF:	(SIXBIT /DSK/)
	SIXBIT /SYS/
	SIXBIT /RECENT/

CMDF:	(SIXBIT /SYS/)
	SIXBIT /TS/
CMDNM:	0

RECF:	(SIXBIT /DSK/)
RECNM:	0
	SIXBIT /RECENT/

INFF:	(SIXBIT /DSK/)
INFNM:	0
INFN2:	SIXBIT /INFO/

FDIR:	(SIXBIT /SYS/)
FD1:	SIXBIT /.FILE./
FD2:	SIXBIT /(DIR)/

TYI:	20,,(SIXBIT /TTY/)
TYO:	21,,(SIXBIT /TTY/)
LPTO:	1,,(SIXBIT /LPT/)
DISO:	1,,(SIXBIT /DIS/)

RENAME:	0
	0
	ADDC
RENM:	0
	SIXBIT /RECENT/

INIT:	ASCIZ /TYPE ? FOR HELP.
*/
NOCMD:	ASCIZ /NO SUCH COMMAND./
RECINF:	ASCIZ /RECENT INFORMATION:

/
NOREC:	ASCIZ /NO RECENT INFO./
NOINF:	ASCIZ /NO INFORMATION AVAILABLE./
ERRM:	ASCIZ /
ENTER COMMAND.
*/
PANIC:	ASCIZ /SYS:.FILE. (DIR) OPEN FAILED!!/
OOF:	ASCIZ /OUTPUT OPEN FAILED./
RENMF:	ASCIZ /RENAME FAILED./
HUH:	ASCIZ /BAD FORMAT--TYPE ? FOR HELP./
CORE:	ASCIZ /NO CORE!/
ATYFLD:	ASCIZ /WHAT HAVE YOU DONE TO MY TRANSLATION TABLE?/	;JSF'S IDEA
PROC:	ASCIZ /END OF SIMULATION./
NOSIM:	ASCIZ /NO SIMULATION AVAILABLE./
NOLPT:	ASCIZ /LPT BUSY./
DISOFL:	ASCIZ /DISPLAY NOT AVAILABLE./
	CONSTANTS
	VARIABLES

PDL:	BLOCK 20
CBUF:

	END GO
