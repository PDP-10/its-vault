
TITLE	USER TELNET

A=1
B=2
C=3
D=4
E=5
TT=6
I=7
Q=10
J=11
R=12
W=13
H=14
P=15
T=16
U=17

LSOC==1
TYIC==2
TYOC==3
NTIC==4
NTOC==5
DKOC==6

PAT:
LOC	42
	JSR TSINT
LOC	PAT
PATCH:	BLOCK 100

GO:	.OPEN TYIC,[4,,(SIXBIT /TTY/)]
	.VALUE
	.OPEN TYOC,[5,,(SIXBIT /TTY/)]
	.VALUE
	MOVE P,PDL
	MOVE A,[SQUOZE 0,IMPUP]
	.EVAL A,
	.VALUE
	MOVEI B,C
	HRL B,A
	.GETLOC B,
	JUMPLE C,GOOK
	MOVEI A,[ASCIZ /
OUR NCP IS DOWN.
/]
	PUSHJ P,TYPE
	.VALUE [ASCIZ /:KILL /]
GOOK:	.CALL TTYGT
	.VALUE
	MOVSI A,001000
	ANDCAM A,TTGT+1
	.CALL TTYST
	.VALUE
	.CALL RTERMT
	.VALUE
	SKIPN A
	SETOM NLINES	;PRINTING TERMINAL
	MOVEI A,[ASCIZ /
TELNET./]
	PUSHJ P,TYPE
	MOVE A,[.FNAM2]
	PUSHJ P,SIXTYP
	.SUSET [.SMASK,,[200000,,]]	;REALT
GOGO:	MOVSI A,(SETZ)
	.REALT A,
	JFCL
	.RESET TYIC,
	SETOM CH1ST
	SETOM CASE	;UPPER CASE
	SETOM ECHO	;LOCAL ECHO
	SETOM QUOTE
	SETOM CBC
	SETZM RETOIN
	MOVEI A,3
	MOVEM A,ICPDNE
GO3:	.SUSET [.SMSK2,,[0]]
	MOVEI A,[ASCIZ /
HOST = /]
	PUSHJ P,TYPE
	MOVEI B,0	;B GETS HOST NUM (OCTAL)
	MOVEI C,0	;(DECIMAL)
	MOVEI H,0
	MOVEI I,HSTBL	;MAX POSSIBLE INDEX
	MOVEI TT,0	;-1 => MNEMONIC MODE
	MOVEI Q,0	;-1 => HAVE GOTTEN DEC PNT
	MOVE J,[360600,,HOSTTB(B)]

GO1:	.IOT TYIC,A
	CAIN A,177
	JRST GO2
	CAIE A,12
	CAIN A,15
	JRST GOTRM
	CAIN A,40
	JRST GOTSP
	CAIE A,"?
	CAIN A,33
	JRST GOTALT
	JUMPL TT,GOTC
	CAIL A,"0
	CAILE A,"9
	JRST NOTN
	LSH B,3
	IORI B,-"0(A)
	IMULI C,10.
	ADDI C,-"0(A)
GOECH:	.IOT TYOC,A
	JRST GO1
GO2:	.IOT TYOC,["?]
	JRST GO3

NOTN:	JUMPE B,GOTC
	CAIE A,".
	JRST BAD
	JUMPL Q,BAD
	MOVNI Q,1
	MOVE B,C
	JRST GOECH

BAD1:	SUB P,[1,,1]
BAD:	MOVEI A,7
	JRST GOECH

GOTC:	MOVNI TT,1
	CAIL A,141
	CAILE A,172
	JRST .+2
	SUBI A,40
	SUBI A,40
	MOVE B,H
GOTC1:	CAML B,I
	JRST BAD
	LDB C,J
	CAMGE A,C
	JRST BAD
	CAMN A,C
	JRST GOTC2
	ADDI B,NHSTE
	JRST GOTC1

GOTC2:	MOVEI C,40(A)
	.IOT TYOC,C
	MOVE H,B
GOTC3:	ADDI B,NHSTE
	CAML B,I
	JRST GOTC4
	LDB C,J
	CAMN A,C
	JRST GOTC3
GOTC4:	MOVE I,B
	IBP J
	JRST GO1

GOTX:	CAIE H,-NHSTE(I)
	JRST GOTX2
	MOVE B,H
GOTX1:	LDB C,J
	JUMPE C,CPOPJ
	ADDI C,40
	.IOT TYOC,C
	IBP J
	JRST GOTX1

GOTX2:	MOVE B,H
	LDB C,J
	JUMPN C,BAD1
	MOVEI I,NHSTE(H)
	POPJ P,

GOTALT:	SKIPL TT
	JUMPN B,GO1
	PUSH P,J
	.CALL RCPOS
	.VALUE
	HLRZS VPOS
	MOVE B,H
GOTAL2:	PUSHJ P,GOTX1
	AOS J,VPOS	;CURRENT LINE # (COUNTING FROM 1)
	CAMN J,NLINES
	JRST GOTALM
GOTAL1:	PUSHJ P,CRLF
	MOVE J,[360600,,HOSTTB(B)]
	ADDI B,NHSTE
	CAMGE B,I
	JRST GOTAL2
GOTAL5:	POP P,J
	JUMPGE TT,GO3
	PUSHJ P,CRLF
	MOVE B,H
	MOVE Q,[440600,,HOSTTB(B)]
GOTAL3:	ILDB C,Q
	CAMN Q,J
	JRST GO1
	ADDI C,40
	.IOT TYOC,C
	JRST GOTAL3

	GOTALM:	SETZM VPOS
	PUSH P,A
	PUSH P,B
	MOVEI A,[ASCIZ /   MORE/]
	PUSHJ P,TYPE
	.IOT TYIC,J
	CAIN J,40
	JRST GOTAL4
	MOVEI A,[ASCIZ /  FLUSHED/]
	PUSHJ P,TYPE
	POP P,B
	POP P,A
	JRST GOTAL5

GOTAL4:	POP P,B
	POP P,A
	JRST GOTAL1

GOTSP:	JUMPGE TT,GO1
	PUSHJ P,GOTX
	JRST GO1

GOTRM:	JUMPGE TT,GO4
	PUSHJ P,GOTX
	MOVE B,HOSTTB+NHSTE-1(H)
	JRST GO4

CRLF:	.IOT TYOC,[15]
	.IOT TYOC,[12]
	POPJ P,

GO4:	JUMPE B,GO3
	PUSHJ P,CRLF
	.IOT TYOC,["*]
	PUSHJ P,CRLF
	MOVEM B,NET1+3
	MOVEM B,NET4+3
	MOVEM B,NET5+3
GO5:	.SUSET [.SMSK2,,[76]]
	MOVE J,[600000,,[60.*60.]]
	.REALT J,
	JFCL
	.OPEN LSOC,NET1
	JRST ANAL1
	SKIPE CH1ST
	.HANG
	MOVE J,LOCSOC
	ADDI J,2	;RECEIVE
	MOVEM J,NET4+1
	ADDI J,1
	MOVEM J,NET5+1
	.IOT LSOC,J	;FOREIGN RECEIVE SOCKET
	MOVEM J,NET5+2
	ADDI J,1
	MOVEM J,NET4+2
	.OPEN NTIC,NET4	;RECEIVE
	JRST ANAL4
	.OPEN NTOC,NET5	;SEND
	JRST ANAL5
	.CLOSE LSOC,
	HRLOI J,377777
	.SLEEP J,
	JRST .-2

ANAL1:	.STATUS LSOC,J
	JRST ANALY
ANAL4:	.STATUS NTIC,J
	JRST ANALY
ANAL5:	.STATUS NTOC,J
ANALY:	HLRZS J
	ANDI J,77
	CAIE J,20
	JRST ANALY1
	MOVEI A,[ASCIZ /
DESTINATION HOST DEAD
/]
	JRST ANALY2

ANALY1:	CAIE J,6
	.VALUE
	MOVEI A,[ASCIZ /
ALL SOCKETS IN USE
/]
ANALY2:	PUSHJ P,TYPE
	MOVSI J,(SETZ)
	.REALT J,
	JFCL
	MOVE J,XRESET
	.SUSET J
	.CLOSE LSOC,
	.CLOSE NTIC,
	.CLOSE NTOC,
	JRST GOGO

TSINT:	0
	0
	SKIPL U,TSINT
	JRST TSFW
	TLZ U,400000
TSINT1:	TRZE U,11
	.VALUE	;CH 0 OR 3
	TRZE U,2
	JRST CH1
	TRZE U,4
	JRST CH2
	TRZE U,20
	JRST CH4
	TRZE U,40
	JRST CH5
	.VALUE

TSRET:	SKIPL RETOIN
	JRST TSRET1
	SETZM RETOIN
	JRST CH4S

TSRET1:	JUMPN U,TSINT1
	.DISMISS TSINT+1

TSFW:	MOVEI A,[ASCIZ /TIMED OUT
/]
	PUSHJ P,TYPE
	JRST RETRY

TYPE:	HRLI A,440700
TYPE1:	ILDB B,A
	JUMPE B,CPOPJ
	.IOT TYOC,B
	JRST TYPE1

NTYPE:	IDIVI A,10
	HRLM B,(P)
	SKIPE A
	PUSHJ P,NTYPE
	HLRZ B,(P)
	.IOT TYOC,"0(B)
CPOPJ:	POPJ P,

SIXTYP:	MOVE B,[440600,,A]
SIXTY1:	ILDB C,B
	JUMPE C,CPOPJ
	ADDI C,40
	.IOT TYOC,C
	TLNE B,770000
	JRST SIXTY1
	POPJ P,

CH1:	SKIPL CH1ST
	JRST CH1B
	MOVE A,[LSOC,,RCH]
	.RCHST A,
	MOVE A,RCH+1
	MOVEM A,LOCSOC
	MOVE A,RCH+4	;STATE
	CAIN A,10
	JRST CH1A
	CAIE A,11	;INPUT AVAILALBE
	CAIN A,5	;CONNECTION OPEN
	JRST CH1A
	JUMPN A,CH1V1
	.CLOSE LSOC,
	MOVE A,RCH+5
	CAIE A,2	;CLSED BY FOREIGN HOST
	JRST CH1C
	MOVEI A,[ASCIZ /
LOGGER CONNECTION REFUSED
/]
	PUSHJ P,TYPE
RETRY:	MOVSI A,(SETZ)
	.REALT A,
	JFCL
	MOVE A,XRESET
	.SUSET A
	.CLOSE LSOC,
	.CLOSE NTIC,
	.CLOSE NTOC,
	.DISMISS [GOGO]

CH1C:	CAIE A,5
	CAIN A,4
	JRST .+2	;DESTINATION HOST DEAD OR INCOMPLETE TRANSMISSION
	.VALUE
CH1CA:	MOVE A,MESSTB-4(A)
	PUSHJ P,TYPE
	JRST RETRY

MESSTB:	[ASCIZ /
HOST DIED
/]
	[ASCIZ /
INCOMPLETE TRANSMISSION
/]

CH1A:	SETZM CH1ST
	JRST TSRET1

CH1V1:	.VALUE

CH1B:	MOVE A,[LSOC,,RCH]
	.RCHST A,
	MOVE A,RCH+4
	JUMPL A,CH1V2
	JUMPN A,TSRET1
	MOVE B,RCH+5
	CAIE B,2
	.VALUE
	.CLOSE LSOC,
	JRST TSRET1

CH1V2:	.VALUE

CH2:	MOVEI A,TYIC
	.ITYIC A,
	JRST TSRET
CH2Z:	.IOT TYIC,A
	SKIPL CBC
	JRST CH2G
	CAIN A,^^
	JRST CH2F
	SKIPL QUOTE
	JRST CH2C
	CAIE A,"\
	JRST CH2D
	SETCMM CASE
CH2E:	.IOT TYOC,A	;ALWAYS ECHO
	JRST TSRET
CH2D:	CAIE A,^Q
	JRST CH2H
	SETZM QUOTE
	PUSHJ P,CH2EC
	JRST TSRET

CH2H:	CAIE A,^S
	JRST CH2A
	.RESET TYOC,
	SKIPE ICPDNE
	JRST TSRET
CH2K:	MOVE A,[NTIC,,RCH]
	.RCHST A,
	MOVE A,RCH+4
	CAIN A,5
	JRST TSRET
	CAIE A,10
	CAIN A,11
	JRST CH2J
	JRST TSRET

CH2J:	.IOT NTIC,A
	JRST CH2K

CH2C:	SETOM QUOTE
CH2A:	CAIL A,"A
	CAILE A,"Z
	JRST CH2B
	SKIPL CASE	;SKIP IF UPPER CASE
	TRO A,40	;MAKE LOWER CASE
CH2B:	SKIPE ICPDNE
	JRST TSRET
	.IOT NTOC,A
	CAIN A,15
	.IOT NTOC,[12]
	.NETS NTOC,
	SKIPGE ECHO	;SKIP IF NOT LOCAL ECHO
	PUSHJ P,CH2EC
	JRST TSRET

CH2F:	SETZM CBC
	PUSHJ P,CH2EC
	JRST TSRET

CH2G:	SETOM CBC
	MOVSI B,-NCBCCH
	CAIL A,140
	CAILE A,172
	JRST .+2
	SUBI A,40
	CAME A,CBCTBL(B)
	AOBJN B,.-1
	JUMPGE B,CH2C
	PUSHJ P,CH2EC
	JRST @CBCDTB(B)

CH2CL:	.CLOSE NTIC,
	.CLOSE NTOC,
	.SUSET [.SIFPIR,,[0]]
	.SUSET [.SPIRQC,,[0]]
	.SUSET [.SMSK2,,[0]]
	.DISMISS [GOGO]

CBCTBL:	"U	;YOU ECHO
	"I	;I ECHO
	"G	;RESTART
	"D	;DDT
	"C	;CLOSE CONNECTION (DISCONNECT)
	"Q	;QUIT
	"B	;OPEN DISK FILE AND BEGIN OUTPUT
	"E	;CLOSE DISK FILE
	"W	;SILENCE OUTPUT
	"V	;MAKE OUTPUT VERBOSE AGAIN
	^P	;PROCEED GIVING DDT TTY
	177	;SEND NOTHING
NCBCCH==.-CBCTBL

CBCDTB:	CH2U
	CH2I
	GO
	[.VALUE [ASCIZ /:DDT/]]
	CH2CL
	[.VALUE [ASCIZ /:KILL /]]
	CH2BB
	CH2EE
	CH2W
	CH2V
	CH2PRO
	CH2RO

CH2U:	SKIPE ICPDNE
	JRST TSRET
	.IOT NTOC,[204]
	.NETS NTOC,
	SETZM ECHO
	JRST TSRET

CH2I:	SKIPE ICPDNE
	JRST TSRET
	.IOT NTOC,[203]
	.NETS NTOC,
	SETOM ECHO
	JRST TSRET

CH2BB:	.SUSET [.RUNAME,,A]
	.SUSET [.SSNAME,,A]
	.OPEN DKOC,DKOP
	.VALUE
	SETOM DKOSW
	JRST TSRET

CH2EE:	.CLOSE DKOC,
	SETZM DKOSW
	JRST TSRET

CH2W:	SETOM SILNCE
	.RESET TYOC,
	JRST TSRET

CH2V:	SETZM SILNCE
	JRST TSRET

CH2PRO:	.VALUE [ASCIZ /:PROCED /]
	JRST TSRET

CH2RO:	.IOT TYOC,["?]
	JRST TSRET

CH2EC:	CAIN A,33
	JRST CH2EC1
	CAIE A,7
	CAIN A,11
	JRST CH2ECS
	CAIN A,12
	JRST CH2ECS
	CAIN A,15
	JRST CH2EC2
	CAIL A,40
	JRST CH2ECS
	.IOT TYOC,["^]
	ADDI A,100
CH2ECS:	.IOT TYOC,A
	POPJ P,

CH2EC1:	MOVEI A,"$
	JRST CH2ECS

CH2EC2:	.IOT TYOC,A
	MOVEI A,12
	JRST CH2ECS

CH4:	SKIPN ICPDNE
	JRST CH4IC1
	MOVEI A,2
	ANDCAB A,ICPDNE
	JUMPE A,CH4ICP
CH4IC1:	SETOM RETOIN
	MOVEI A,TYIC
	.ITYIC A,
	JRST CH4SA
	JRST CH2Z
CH4SA:	SETZM RETOIN
CH4S:	MOVE A,[NTIC,,RCH]
	.RCHST A,
	MOVE A,RCH+4	;STATE
	JUMPL A,CH4V1
	JUMPE A,CH4CL
	CAIN A,5
	JRST TSRET
CH4A:	CAIE A,10
	CAIN A,11
	JRST CH4B
	JRST TSRET
CH4B:	.IOT NTIC,A
	JUMPE A,CH4
	TRZE A,200
	JRST CH4C
	SKIPL SILNCE
	.IOT TYOC,A
	SKIPGE DKOSW
	.IOT DKOC,A
	JRST CH4

CH4C:	CAIE A,2
	CAIN A,40
	JRST CH4	;IGNORE DATA TYPE OR NOP
	CAIE A,3	;DON'T ECHO
	JRST CH4D
	SETZM ECHO
	JRST CH4
CH4D:	CAIE A,4	;ECHO
	JRST CH4E
	SETOM ECHO
	JRST CH4
CH4E:	CAIE A,5	;HIDE INPUT
	JRST CH4	;IGNORE IF NOT ANYTHING
	SETZM ECHO
	JRST CH4

CH4V1:	.VALUE

CH4CL:	MOVEI A,[ASCIZ /
CONNECTION CLOSED
/]
	PUSHJ P,TYPE
	MOVE A,RCH+5
	CAIN A,2
	JRST RETRY
	CAIE A,5
	CAIN A,4
	JRST CH1CA
	MOVEI A,[ASCIZ /REASON = /]
	PUSHJ P,TYPE
	MOVE A,RCH+5
	ADDI A,"0
	.IOT TYOC,A
	.IOT TYOC,[15]
	.IOT TYOC,[12]
	JRST RETRY

CH4ICP:	MOVSI A,(SETZ)
	.REALT A,
	JFCL
	JRST CH4IC1

CH5:	SKIPN ICPDNE
	JRST CH5IC1
	MOVEI A,1
	ANDCAB A,ICPDNE
	JUMPE A,CH5ICP
CH5IC1:	MOVE A,[NTOC,,RCH]
	.RCHST A,
	MOVE A,RCH+4
	JUMPL A,CH5V1
	CAIE A,5
	CAIN A,6
	JRST TSRET
	SKIPE A
	.VALUE
	JRST CH4CL

CH5ICP:	MOVSI A,(SETZ)
	.REALT A,
	JFCL
	JRST CH5IC1

CH5V1:	.VALUE

NHSTE==4	;NUMBER OF WORDS PER HOST TABLE ENTRY

DEFINE 	HST NAME,NUMD,NUMO
FOO==.
	SIXBIT /NAME/
BAR==3-.+FOO
IF2,[IFL <NUMD-NUMO>\<NUMO-NUMD>\BAR,[PRINTX \LOSSAGE AT HOSTTB - NAME
\]]
	BLOCK BAR
	NUMO
TERMIN

;ALPHABETICAL LIST OF ALL LEGAL HOST ID'S
;THREE WORDS OF SIXBIT IDENTIFIER FOLLOWED BY HOST NUMBER

HOSTTB:	HST AI,134.,206
	HST AMES,15.,17
	HST AMES-67,16.,20
	HST AMES-ILLIAC,15.,17
	HST AMES-TIP,144.,220
	HST ARPA-TIP,156.,234
	HST BBN,69.,105
	HST BBN-1D,197.,305
	HST BBN-NCC,5,5
	HST BBN-TENEXA,69.,105
	HST BBN-TENEXB,133.,205
	HST BBN-TESTIP,158.,236
	HST BURR,15.,17
	HST CASE-10,13.,15
	HST CCA,31.,37
	HST CCN,65.,101
	HST CMU,78.,116
	HST CMU-10A,78.,116
	HST CMU-10B,14.,16
	HST CMU-CC,78.,116
	HST DMCG,70.,106
	HST DOCB,153.,231
	HST ETAC-TIP,148.,224
	HST GWC-TIP,152.,230
	HST HARV,9,11
	HST HARV-1,73.,111
	HST HARV-10,9.,11
	HST HARV-11,137.,211
	HST HARVARD,9,11
	HST ILL-ANTS,12.,14
	HST ILLIAC,15.,17
	HST ISI,86.,126
	HST LL-67,10.,12
	HST LL-TSP,138.,212
	HST LL-TX-2,74.,112
	HST MATHLAB,198.,306
	HST MCCL-418,22.,26
	HST MIT-AI,134.,206
	HST MIT-DMCG,70.,106
	HST MIT-ML,198.,306
	HST MIT-MULTICS,6,6
	HST MITRE-TIP,145.,221
	HST ML,198.,306
	HST MULTICS,6,6
	HST NBS-COST,19.,23
	HST NBS-TIP,147.,223
	HST NCAR-TIP,153.,231
	HST NIC,2,2
	HST NMC,1,1
	HST RADC-TIP,146.,222
	HST RAND-RCC,7,7
	HST SAAC-TIP,154.,232
	HST SAIL,11.,13
	HST SDC-ADEPT,8,10
	HST SRI-AI,66.,102
	HST SRI-ARC,2,2
	HST SU-AI,11.,13
	HST TENEX,69.,105
	HST TINK-418,21.,25
	HST UCLA-NMC,1,1
	HST UCLA-CCN,65.,101
	HST UCSB-MOD75,3,3
	HST UCSD-CC,129.,201
	HST USC-44,23.,27
	HST USC-ISI,86.,126
	HST USC-TIP,151.,227
	HST UTAH-10,4,4

HSTBL==.-HOSTTB
NHSTS==HSTBL/NHSTE

NET1:	40054,,(SIXBIT /NET/)	;IMAGE IN, GENSOC, 32 BITS
	-1
	1
	-1

NET4:	40,,(SIXBIT /NET/)	;RECEIVE 8 BIT ASCII
	-1
	-1
	-1

NET5:	41,,(SIXBIT /NET/)
	-1
	-1
	-1

TTYST:	SETZ
	SIXBIT /TTYSET/
	[TYOC]
REPEAT 3,TTGT+.RPCNT
	SETZ TTGT+3

TTYGT:	SETZ
	SIXBIT /TTYGET/
	[TYOC]
REPEAT 3,2000,,TTGT+.RPCNT
	402000,,TTGT+3

TTGT:	BLOCK 4

RCPOS:	SETZ
	SIXBIT /RCPOS/
	1000,,TYOC
	402000,,VPOS

RTERMT:	SETZ
	SIXBIT /RSSIZE/
	1000,,TYOC
	2000,,NLINES
	2000,,A
	402000,,A	;CONSOLE TYPE

XRESET:	-4,,.+1
	.SMASK,,[0]
	.SMSK2,,[0]
	.SPIRQ,,[0]
	.SIFPI,,[0]

DKOP:	1,,(SIXBIT /DSK/)
	SIXBIT /NETOUT/
	SIXBIT />/

NLINES:	0	;NUMBER OF VERTICAL LINES ON SCREEN
		;-1 => PRINTING TERMINAL
VPOS:	0
QUOTE:	-1	;0 => NEXT CHAR IS QUOTED
CH1ST:	-1	;-1 => JUST OPENED
LOCSOC:	0
CASE:	-1	;-1 => UPPER CASE
ECHO:	-1	;-1 => LOCAL ECHO
CBC:	-1	;0 => NEXT CHAR IS ^_ COMMAND
RETOIN:	0	;-1 => RETURN TO CH4 INPUT ROUTINE
ICPDNE:	3	;0 => ICP DONE
DKOSW:	0	;-1 => OUTPUT TO DISK
SILNCE:	0	;-1 => SILENCE OUTPUT
RCH:	BLOCK 10
LPDL==20
PDL:	-LPDL,,APDL-1
APDL:	BLOCK LPDL

END	GO
