.MLLIT=1
IF1,EXPUNG TTY

TITLE LOCK

A=1
B=2
C=3
D=4
E=6
F=7
U=10
J=11
P=17

;CHANNELS 0 UPWARD USED FOR LOCK AND CTEST
MXCH==13	;MAX FREE CH
RTYO==14
RTYI==15
TYIC==16
TYOC==17

SNGEC==2
PAT=44
PATCH=44

LOC 42
	JSR TSINT

LOC 100

BEG:	MOVEI P,PDL
	PUSHJ P,REGIO
	MOVEI C,INMESG
	PUSHJ P,TITYOT
	.SUSET [.RUNAME,,RUNAME]
	.SUSET [.SMASK,,[1]]
	.SUSET [.SPICL,,[-1]]
	JRST BLUP

SYMSU:	IRPS X,,LUBLK UNAME JNAME USRHI
	SQUOZE 0,X
X:	0
TERMIN
0
SYMST:	IRPS X,,NFDPT NNTYS NCT TTYTYP
	SQUOZE 0,X
X:	0
TERMIN
0


TSINT:	0
	0
	MOVE A,TSINT
	CAIE A,1
	.DISMIS TSINT+1	;??
	MOVEI A,TYIC
	.ITYIC A,
	.DISMIS TSINT+1	;??
	CAIN A,7	;^G
	JRST QUIT
	.DISMIS TSINT+1


QUIT:	SKIPE CHARF	;INSIDE "O" LOOP
	PUSHJ P,OCTPNT
	.RESET TYOC,
	.RESET TYIC,
	SETZM CHARF
	.DISMI [BLUPR]


REVIVE:	MOVEI B,[ASCIZ /
REVIVE SYSTEM?/]
	PUSHJ P,QHACK
	.REVIVE
	JRST BLUP

PVAL:	.VALUE [0]
BEGLUP:	MOVEI P,PDL
BLUPR:	PUSHJ P,REGIO
BLUP:	PUSHJ P,TCRR
	.IOT TYOC,["_]
	SETZB D,TNUM
	MOVE E,[440600,,D]
	PUSHJ P,GNUM
	JRST NOTNUM
GNUM:	SETZM TNUM
GNUM1:	.IOT TYIC,A
	CAIL A,"0
	CAILE A,"9
	POPJ P,
	MOVEI B,10
	IMUL B,TNUM
	ADDI B,-"0(A)
	MOVEM B,TNUM
	JRST GNUM1

INITST:	MOVEI A,SYMST
INITS1:	SKIPN B,(A)
	POPJ P,
	SKIPE 1(A)
	AOJA A,[AOJA A,INITS1]
	.EVAL B,
	.VALUE
	MOVEM B,1(A)
	AOJA A,[AOJA A,INITS1]

REGIO:	MOVEI A,21	;REGULAR I/O
	HRLM A,TTY
	.OPEN TYOC,TTY
	.VALUE
	MOVEI A,30
	HRLM A,TTY
	.OPEN TYIC,TTY
	.VALUE
	POPJ P,

PLS:	MOVE B,TNUM
	MOVE A,CHARAY
	SKIPE CHARAY+1(A)
	AOBJN A,.-1
	JUMPGE A,QLUP
	MOVEM B,CHARAY+1(A)
	DPB A,[270400,,PLS.]
	DPB B,[300,,TY.]
	LDB B,[30300,,B]
	DPB B,[060300,,TY.]
PLS.:	.OPEN .,TY.
	JRST LUP9
	.IOT TYOC,["W]
	MOVEI C,[SIXBIT \THIS CONSOLE LOCKED BY !\]
	PUSHJ P,DITYOT
	MOVEI C,RUNAME
	PUSHJ P,ITYOUT
	JRST BLUP

INMESG:	SIXBIT / LOCK./
	.FNAM2+'!

FLUSH:	MOVE B,TNUM
	MOVE A,CHARAY
	CAME B,CHARAY+1(A)
	AOBJN A,.-1
	JUMPGE A,QLUP
	SETZM CHARAY+1(A)
	PUSHJ P,TTYSTA
	SKIPN B
	JRST QLUP
	.IOT TYOC,["*]
	DPB A,[270400,,.+1]
	.CLOSE .,
	JRST BLUP

LFTAR:	PUSHJ P,IMOUT
	.IOT TYOC,TNUM
	JRST BEGLUP

ATTACH:	MOVEI A,1
	MOVEM A,GUNDET	;1 FOR ATTACH.
	JRST DETAC0

DETACH:	SETOM GUNDET	;-1 FOR DETACH.
	JRST DETAC0

GUN:	SETZM GUNDET	;0 FOR GUN.
DETAC0:	MOVEI A,SYMSU
	PUSHJ P,INITS1
	SKIPGE B,TNUM
	JRST QLUP
	JUMPE B,GUN1
	PUSHJ P,USER
	JRST QLUP
	JUMPE U,QLUP
	MOVEM U,USTORE
	MOVEM J,JSTORE
	PUSHJ P,TCRR
	MOVE C,GUNDET
	MOVE C,[[SIXBIT \DETACH !\]
		[SIXBIT \GUN DOWN !\]
		[SIXBIT \ATTACH !\]]+1(C)
	PUSHJ P,ITYOUT
	MOVEI C,USTORE
	PUSHJ P,ITYOUT
	MOVEI C,JSTORE
	PUSHJ P,ITYOUT
	.IOT TYOC,["?]
	PUSHJ P,QHACK1
	PUSHJ P,USER
	JRST QLUP
	CAMN U,USTORE
	CAME J,JSTORE
	JRST DETAC0
GUNACT:	SKIPE GUNDET
	 JRST DETAC1	;DETACH OR ATTACH.
	.GUN B,
	JRST QLUP
	JRST BLUP

DETAC1:	.CALL [SETZ? 'DETACH ? 1000,,400000(B)]
	JFCL
	SKIPG GUNDET
	 JRST BLUP	;DETACH.
	.CALL [SETZ? 'ATTACH ? 1000,,4^5(B)]
	 JRST QLUP
	.VALUE [ASCIZ/:LOGOUT /]	;ATTACH.

GUNDET:	0

USER:	MOVE C,B
	IMUL C,LUBLK
	HRLZ D,USRHI
	HRRI D,D
	.GETLOC D,
	AOS (P)
	CAMG D,C
	SOS (P)
	MOVE D,C
	ADD D,UNAME
	HRLI D,U
	MOVSS D
	.GETLOC D,
	ADD C,JNAME
	HRLI C,J
	MOVSS C
	.GETLOC C,
	POPJ P,

GUN1:	MOVEI B,[ASCIZ \
UNAME=\]
	PUSHJ P,QEXPL2
	PUSHJ P,6NAMEG
	MOVE F,[SIXBIT \HACTRN\]
	JUMPN C,GUN3
	MOVE F,E
	MOVEI B,[ASCIZ \
JNAME=\]
	PUSHJ P,QEXPL2
	PUSHJ P,6NAMEG
	EXCH E,F
GUN3:	MOVEI B,0
GUN2:	PUSHJ P,USER
	JRST QLUP
	CAME U,E
	AOJA B,GUN2
	CAME J,F
	AOJA B,GUN2
	JRST GUNACT

USTORE:	0
	SIXBIT \ !\
JSTORE:	0
	SIXBIT \!\

6NAMEG:	MOVE D,[440600,,E]
	MOVEI E,0
6NAME1:	.IOT TYIC,C
	SUBI C,40
	CAIL C,100
	JRST QLUP
	JUMPLE C,CPOPJ
	TLNE D,770000
	IDPB C,D
	JRST 6NAME1

NOTNUM:	CAIE A,177
	CAIN A,15
	JRST BLUP
NOTNM2:	SUBI A,40
	JUMPLE A,QLUP
	TLNE E,770000
	IDPB A,E
	MOVSI F,-CTABL
	CAME D,COMTAB(F)
	AOBJN F,.-1
	JUMPG F,CLUP
	HRRZ F,COMT2(F)
	JRST (F)

CLUP:	.IOT TYIC,A
	CAIN A,177
	JRST QLUP
	JRST NOTNM2

QEXPL:	PUSHJ P,TFORMF
	MOVSI F,-CTABL
QEXPLL:	HLRZ B,COMT2(F)
	JUMPE B,QEXPLE
	MOVE A,COMTAB(F)
	MOVEM A,QMESST
	MOVE A,[440600,,QMESST]
	PUSHJ P,TCRR
QEXP2L:	ILDB C,A
	ADDI C,40
	.IOT TYOC,C
	CAIE C,40
	JRST QEXP2L
	.IOT TYOC,C
	PUSHJ P,QEXPL2
QEXPLE:	AOBJN F,QEXPLL
	JRST BEGLUP

QMESST:	0
	0	;TERM

COMTAB:	'"_36
	'+_36
	'-_36
	'__36
	SIXBIT /ATTACH/
	SIXBIT /DETACH/
	SIXBIT /DOWN/
	SIXBIT /FLASH/
	SIXBIT /GUN/
	SIXBIT /I37/
	SIXBIT /KILL/
	'O_36
	'P_36
	'Q_36
	SIXBIT /REVIVE/
	SIXBIT /SPEED/
	SIXBIT /SPY/
	SIXBIT /SYS/
	SIXBIT /TEST/
	SIXBIT /TPL/
	SIXBIT /UCLEAR/
	'?_36
CTABL==.-COMTAB
COMT2:	[ASCIZ /OUTPUT NEXT CHARACTER IN OCTAL/],,1CHAR
	[ASCIZ /LOCK TTY (PRECEDE BY #)/],,PLS
	[ASCIZ /UNLOCK TTY (")/],,FLUSH		;-
	[ASCIZ /TYPE CH WITH ASCII VALUE # (")/],,LFTAR
	[ASCIZ /ATTACH JOB # TO THIS TTY (")/],,ATTACH
	[ASCIZ /DETACH JOB # FROM ITS TTY (")/],,DETACH
	[ASCIZ /SYSTEM DOWN IN # MINUTES (")/],,KILL
	FLASH
	[ASCIZ /KILL USER WITH # INDEX (")/],,GUN
	[ASCIZ /INITIALIZE MODEL 37 TTY/],,I37
	KILL
	[ASCIZ /OUTPUT CHARACTERS IN OCTAL/],,CHAR		;O
	PVAL
	[ASCIZ /RETURN TO DDT KILLING LOCK/],,[.LOGOUT?.VALUE [ASCIZ /:KILL /]]	;Q
	REVIVE
	[ASCIZ /CHANGE DATAPOINT LINE SPEED (PRECEDE BY LINE #)/],,SPEED
	[ASCIZ /LOOK AT TYI FROM TTY (PRECEDE BY TTY #)/],,SPY
	[ASCIZ /CHANGE SYS CHECKER BLOCKED STATUS/],,SYSJ
	[ASCIZ /TEST CORE ALLOCATOR/],,CTEST
	[ASCIZ /CHANGE TPL BLOCK STATUS/],,SYSL		;TPL
	[ASCIZ /INITIALIZE UTAPE # DIRECTORY/],,UCLR
	[ASCIZ /LIST COMMANDS/],,QEXPL
IFN .-COMT2-CTABL,[PRINT /LOSS AT COMTAB
/]

LUP9:	.IOT TYOC,["L]
	JRST BLUP

QLUP:	.IOT TYOC,["?]
	JRST BEGLUP

KILL:	MOVE B,TNUM
	CAIGE B,5.
	JRST QLUP
	MOVEI B,[ASCIZ /
DO YOU REALLY WANT THE SYSTEM TO GO DOWN?
/]
	PUSHJ P,QHACK
	IMULI B,60.*30.
	.SHUTDN B,
	JRST QLUP
	JRST BLUP

QHACK:	PUSHJ P,QEXPL2
	.LISTEN
	.RESET TYIC,
QHACK1:	.IOT TYIC,C
	CAIN C,"N
	JRST BEGLUP
	CAIE C,"Y
	JRST QLUP
	MOVE B,TNUM	;BARF^BARF
	POPJ P,

SYSL:	MOVEI F,2
	.SUPSET F,
	JRST BLUP

SYSJ:	MOVEI F,1
	.SUPSET F,
	MOVEI C,[SIXBIT /SYS CHECKER !/]
	PUSHJ P,TITYOT
	MOVEI C,[SIXBIT /UN!/]
	TRNE F,1
	PUSHJ P,ITYOUT
	MOVEI C,[SIXBIT /BLOCKED?!/]
	PUSHJ P,ITYOUT
	JRST BLUP

QEXPL2:	HRLI B,440700
QEXPL3:	ILDB A,B
	JUMPE A,CPOPJ
	.IOT TYOC,A
	JRST QEXPL3
SPEED:	PUSHJ P,INITST
	SKIPN B,TNUM
	.SUSET [.RTTY,,B]
	MOVE D,TTYTYP
	ADD D,B
	MOVSS D
	HRRI D,E
	.GETLOC D,
	SUB B,NFDPT
	SKIPL B
	CAMLE B,NNTYS
	JRST QLUP
	DPB B,[140400,,SSC1]
	DPB B,[140400,,SSC2]
	MOVEI B,[ASCIZ / INPUT SPEED=/]
	PUSHJ P,GSPD
	JRST QLUP
	MOVE J,B
	DPB B,[110300,,SSC1]
	DPB B,[250300,,E]
	MOVEI B,[ASCIZ / OUTPUT SPEED=/]
	PUSHJ P,GSPD
	JRST QLUP
	MOVE U,B
	DPB B,[110300,,SSC2]
	DPB B,[220300,,E]
	MOVNI B,1000
	.IOTLSR B,
SPELUP:	SKIPL J
SSC1:	CONO 604,703	;+.&77000
	SKIPL U
SSC2:	CONO 604,503	;+.&77000
	AOJL B,SPELUP
	.IOTLSR B,
	MOVSS D
	.SETLOC D,	;UPDATE TTYTYP TABLE
	JRST BLUP

GSPD:	PUSHJ P,QEXPL2
	PUSHJ P,GNUM
	JUMPE B,GSPD1
	MOVE C,[-10,,0]
	CAME B,SPTBL(C)
	AOBJN C,.-1
	HRRZ B,C
	JUMPL C,GSPD2
	POPJ P,
GSPD1:	SETOM B
GSPD2:	AOS (P)
	POPJ P,

SPTBL:	135
	600
	110
	150
	300
	1200
	2400
	4800

SPY:	PUSHJ P,INITST
	PUSHJ P,IMOUT
	SKIPL B,TNUM
	CAMLE B,NCT
	JRST QLUP
	MOVEM B,SPYS+1
	.OPEN RTYI,SPYS
	JRST QLUP
	.IOT RTYI,A
	.IOT TYOC,A
	JRST .-2
SPYS:	SIXBIT /   SPY/
	0

;SET TO IMAGE MODE OUTPUT
IMOUT:	MOVEI B,5
	HRLM B,TTY
	.OPEN TYOC,TTY
	.VALUE
	POPJ P,

IMIN:	MOVEI B,4
	HRLM B,TTY
	.OPEN TYIC,TTY
	.VALUE
	POPJ P,

I37:	PUSHJ P,IMOUT
	.RESET TYOC,
	.IOT TYOC,["]
	.IOT TYOC,["F]
	.IOT TYOC,["]
	.IOT TYOC,[":]
	MOVEI A,15.	;CHOMP
	.SLEEP A,
	JRST BEGLUP

1CHAR:	PUSHJ P,IMIN
	.IOT TYIC,A
	PUSHJ P,TCRR
	PUSHJ P,OCTPNT
	JRST BEGLUP

CHAR:	PUSHJ P,IMIN
	SETOM CHARF'	;FLAG FOR QUIT TO KNOW
CHARL:	PUSHJ P,TCRR
	.IOT TYIC,A
	PUSHJ P,OCTPNT
	JRST CHARL

OCTPNT:	IDIVI A,8
	HRLM B,(P)
	SKIPE A
	PUSHJ P,OCTPNT
	HLRZ A,(P)
	ADDI A,"0
	.IOT TYOC,A
CPOPJ:	POPJ P,

UCLR:	MOVE B,TNUM	;SIGH
	DPB B,[260300,,TAPNO]
	MOVEI B,UCLMES
	PUSHJ P,QHACK
	.ASSIGN B,
	JRST QLUP
	.UINIT B,
	JRST QLUP
	JRST BLUP
UCLMES:	ASCII /
ZAP TAPE/
TAPNO:	ASCIZ / 0?
/

FLASH:	.OPEN RTYI,SPYF	;MORE THAN ONE AT A TIME
	JRST QLUP	;FAILED, BOMB OUT
	MOVEI D,100	;INITIALIZE TO EXPECT UNSHIFTED CHARS
FLLUP:	.IOT RTYI,A	;GET A CHAR FROM THE AP LINE
	ANDI A,77	;MASK IT TO 6 BITS
	CAIN A,33	;IS IT "SHIFT"
	MOVEI D,0	;SET SHIFT FLAG OFFSET
	CAIN A,37	;IS IT "UNSHIFT"
	MOVEI D,100	;SET UNSHIFT FLAG OFFSET
	ADD A,D		;ADD THE FLAG OFFSET TO THE CHAR
	IDIVI A,5	;SELECT THE WORD AND BYTE
	LDB A,BYTAB(B)	;GET THE ASCII CHAR FROM THERE
	JUMPE A,FLLUP	;IF IT'S ZERO, IGNORE AND GET NEXT
	.IOT TYOC,A	;IF IT'S NOT, TYPE IT
	JRST FLLUP	;GET THE NEXT ONE

SPYF:	SIXBIT /   SPY/
	10	;TTY NUMBER


BYTAB:	350700,,SHIFT(A)	;LEFTMOST CHAR OF 5
	260700,,SHIFT(A)
	170700,,SHIFT(A)
	100700,,SHIFT(A)
	010700,,SHIFT(A)	;RIGHTMOST

SHIFT:	ASCII \ E A SIUîDRJNFCKTZLWHYPQOBG MXV   !  - '+ ,    (    ? & : .    \
NOSHFT:	ASCII \e a siuîdrjnfcktzlwhypqobg mxv  3$  87'-4,   5) 2 60 9 ; .1    \


CTEST:	.SUSET [.RUNAME,,XUNAME]
	PUSHJ P,RAN7
	IRPC X,,012345
	DPB A,[270400,,CDPB!X]
	TERMIN
CDPB0:	.STATUS .,B
	ANDI B,77
	SKIPN B
	JRST CTEST2	;CH NOT OPEN
	CAIE B,61
	JRST CTEST	;CH PROBABLY BEING USED TO LOCK TTY
CDPB1:	.UCLOSE .,
CTEST2:	.GENSYM B,
	MOVEM B,XJNAME
CDPB2:	.OPEN .,XDEV
	JRST CTEST
	MOVE B,[-2000,,0]
CDPB3:	.IOT .,B
	PUSHJ P,RAN7
	MOVEI B,CCORE
	SKIPN A
	MOVEI B,CTEST
CDPB4:	.USET .,[.SUPC,,B]
CDPB5:	.USET .,[.SUSTP,,[0]]
	PUSHJ P,RAN17
	.SLEEP 1,
	JRST CTEST

XDEV:	3,,(SIXBIT /USR/)
XUNAME:	0
XJNAME:	0

CCORE:	PUSHJ P,RAN17
	.CORE 1(A)
	SOJA A,.-1
	PUSHJ P,RAN17
	.SLEEP 1,
	JRST CCORE

RAN17:	SKIPA C,[17]
RAN7:	MOVEI C,7
RAND:	.RDTIME A,
	XOR A,RANDN
	FMPB A,RANDN
	AND A,C
	POPJ P,

RANDN:	3.14159

TITYOT:	MOVEI A,TYOC
DITYOT:	DPB A,[270400,,IOINST]
	PUSHJ P,FORMF
ITYOUT:	HRLI C,440600
ITYOT2:	ILDB D,C
	CAIN D,'!
	POPJ P,
	CAIN D,'?
	JRST ITYCRR
	ADDI D,40
IOINST:	.IOT .,D
	JRST ITYOT2

ITYCRR:	PUSHJ P,CRRF
	JRST ITYOT2

TTYSTA:	DPB A,[270400,,.+1]
	.STATUS .,B
	ANDI B,77
	POPJ P,

TFORMF:	MOVEI A,TYOC
	DPB A,[270400,,IOINST]
FORMF:	PUSHJ P,TTYSTA
	MOVEI D,14
	CAIN B,SNGEC
	XCT IOINST
CRRF:	MOVEI D,15
	XCT IOINST
	MOVEI D,12
	XCT IOINST
	POPJ P,

TCRR:	.IOT TYOC,[15]
	.IOT TYOC,[12]
	POPJ P,

TNUM:	0

TTY:	SIXBIT /   TTY.LOCK..LOCK./

TY.:	5,,(SIXBIT /T00/)
	SIXBIT /.LOCK./
RUNAME:	BLOCK 1
	SIXBIT /?!/

CHARAY:	-MXCH-1,,0
	BLOCK MXCH+1

PDL:	BLOCK 20

NPATCH:	BLOCK 20

END BEG
