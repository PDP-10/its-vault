TITLE STRING HANDLER
;KNOWN BUGS:
; .0-1
; -.02@*.03
; 69@@/7
; .001/9@
; "A*-1 +1
; 1.0*1.0

TS==1

FF==0
A"=1
B=2
C=3
TAPAC"==C
D=4
E=5
F=6
G=7
H=10
I=11
J=12
K=13

V=14
VP=15
PP=16
P"=17

;FF LH
EVLF==1	;LETTER
EVCF==2	;COLON
EVSF==4	;SEMICOLON
EVMF==10	;COMMA
EVNF==20	; # SIGN
SERSKF==40	;(SERCH)
SERCNF==100	;(SERCH)
SERRLF==200	;(SERCH)
REPLF==400	;(RLESS)
REPRF==1000	;(RGRET)
REPCF==2000	;(REQAL)
EVDF==4000	; $ SIGN
TINGF==10000	;(TIMES)
SLSHSS==20000	;(SLASH)
SLSHSC==40000	;(SLASH)
SYPF==100000	;(SYH)
TYDF==200000	;(PERCT)

;FF RH: I-O SWITCHES
IOV==1
IOB==2
IOR==4
ION==10
IO%==20
IOE1==40
IOE2==100
IOE3==200
IOX==400
IOQ==1000

PDLL==1000+1400*<2-TS>
PPDLL==20000*<2-TS>
PPBL==20
VSL==100000-74000*TS
ERR=(1000)

IFE TS,[
HI==,-1
NDIR"==3
NURBUF"==4
NUWBUF"==4
BLKSPC"==3
LPTBSZ"==200

TPCHN"==2
TPCHNA"==200_<-TPCHN>
UTCCHN"==3
LPTCHN"==4
APRCHN"==6
TTYCHN==7

.LIBRQ UINIT
]
IFN TS,[
TYIC==1
TYOC==2
]

LOC 41
	JSR ERROR

IFE TS,[
LOC 40+2*APRCHN
	JSR APRBK

LOC 40+2*TTYCHN
	JSR TYBK
]

IFN TS,[
LOC 42
	JSR TSINT
]

DEFINE DBP7 X
	ADD X,[7_30.]
	SKIPGE X
	ADD X,[-<43_30.>-1]
TERMIN

DEFINE PVAK
	IDPB A,VP
	MOVEI K,@SP
	CAIG K,1(VP)
	ERR [ASCII /SCE/]
TERMIN

DEFINE RANDY V
	MOVE J,RANDMM
	IMULB J,RANDMS
	HLRZS J
	IDIVI J,V
TERMIN
LOC 100

BEG:	MOVEI FF,IOV
IFE TS,[
	CONO TTY,TTYCHN
	MOVE A,[(350700)TYIB]
	MOVEM A,TYIPI
	MOVEM A,TYIPO
	HRRI A,TYOB
	MOVEM A,TYOTO
	MOVEM A,TYOTI
]
	SETZM TYCIP'
	SETZM UTOOK'
	SETZM UTIOK'
IFE TS,[
	CONO 603440
	CONO PI,12200+1_<7-APRCHN>+1_<7-UTCCHN>+1_<7-LPTCHN>+1_<7-TTYCHN>
	CONO APRCHN
]
IFN TS,[
	.CORE @CORSIZ
	JRST 4,.
	MOVEI A,200001
	.SETMSK A,
	.OPEN TYIC,[SIXBIT /  $TTY/]
	JRST 4,.
	.OPEN TYOC,[SIXBIT /  %TTY/]
	JRST 4,.
	.STATU TYOC,A
	ANDI A,77
	CAIE A,2
	JRST BEG1
	MOVEI A,14
	.IOT TYOC,A
BEG1:]

GO:	TRZ FF,IOE1
	MOVEI C,"\
	TRZE FF,IOX
	PUSHJ P,TYOT
	SETZB V,TYITC'
	MOVE P,[(-PDLL)PDL]
	MOVE PP,[(-PPDLL)PPDL]
	MOVE VP,[(10700)VS-1]
GO0:	TRNE FF,ION
	TRNN FF,IOV
	TRNN FF,IOQ
	TRO FF,IOE2
	PUSHJ P,CRLF
	TRZ FF,IOE2

GO1:	PUSHJ P,TYI
	CAIN C,177
	JRST GORUB
	JUMPE C,GO2
	CAIE C,33
	CAIL C,175
	JRST GOALT
	SKIPA A,C
GO2:	MOVEI A,33
	PVAK
	JRST GO1

GORUB:	CAMN VP,[(10700)VS-1]
	JRST GO0
	LDB C,VP
	TRNN FF,IOQ
	TRO FF,IOE3
	PUSHJ P,TYO
	DBP7 VP
	JRST GO1

GOALT:	SETZM VUT'
	TRNE FF,IOQ
	SETOM VUT
	MOVEI A,GOALT1
	HRRM A,CLOSX
	JRST CLOSE
GOALT1:	MOVEM VP,PPDLV
	TRNE FF,ION
	TRNN FF,IOV
	SKIPN VUT
	TRO FF,IOE2
	PUSHJ P,CRLF
	TRZ FF,IOE2
	HRLOI C,377777
	MOVE B,[(440700)VS]
	HRLI FF,0
	MOVE E,[(1)1]
	MOVE A,SGNF0
	PUSHJ PP,EV
GOALT2:	TRNE FF,ION
	TRNN FF,IOV
	SKIPN VUT
	TRO FF,IOE2
	PUSHJ P,TYA
	TRZ FF,IOE2
	JRST GO

;A POINTS TO 0 AFTER BLOCK, B TO 0 BEFORE; USES A, B, C, D

SYCL:	PUSH P,E
	MOVEM B,SYCLD'
	SUBM A,SYCLD
	MOVE C,[<43_30.>+1]
	SKIPL SYCLD
	MOVNS C
	MOVEM C,SYCLC'
	MOVEI C,PPDL+PPBL-1

SYCL3:	MOVE D,-14(C)
	MOVE E,B
	ROTC D,6
	CAMLE D,E
	JRST SYCL2
	MOVE D,-14(C)
	MOVE E,SP
	ROTC D,6
	CAMGE D,E
	JRST SYCL2

SYCL4:	MOVE D,-14(C)
	ADD D,SYCLD
	SKIPGE D
	ADD D,SYCLC
	MOVEM D,-14(C)
	CAIN C,DSYNP'+14
	JRST SYCL1
	MOVE D,-1(C)
	ADD D,SYCLD
	SKIPGE D
	ADD D,SYCLC
	MOVEM D,-1(C)
	MOVE D,(C)
	ADD D,SYCLD
	SKIPGE D
	ADD D,SYCLC
	MOVEM D,(C)

SYCL2:	CAIN C,DSYNP+14
	JRST SYCL1
	ADDI C,PPBL
	CAIG C,(PP)
	JRST SYCL3
	MOVEI C,DSYNP+14
	JRST SYCL3

SYCL0:	DBP7 A
	DBP7 B
	LDB C,B
	DPB C,A
SYCL1:	CAME B,SP
	JRST SYCL0
	MOVEM A,SP
	POP P,E
	POPJ P,

;NAME IN B, ENDED BY NON-CONST.
;RETURNS BEG. OF BLOCK IN B, OR 0 IF NOT IN S.T.
;RETURNS BEG. OF VALUE IN A, LEVEL OF DEF. IN C
;USES A, B, C, D

SYCH:	MOVE A,SP
	MOVEM B,SYCHB'
SYCH1:	MOVE B,SYCHB'
	CAMN A,SSEND
	JRST SYCH6
	MOVEM A,SYCHA'
SYCH2:	ILDB C,B
	ILDB D,A
	CAME C,D
	JRST SYCH3
	MOVE C,TYPE(C)
	JUMPGE C,SYCH2
	CAIN D,"\
	IBP A

SYCH0:	LDB D,A	;FOUND
	ILDB C,A
	DPB D,[(70700)C
	CAMLE C,V
	JRST SYCH7
	MOVE B,SYCHA'
	POPJ P,

SYCH3:	MOVE C,TYPE(C)
	JUMPGE C,SYCH4
	CAIGE D,40
	JRST SYCH0
SYCH5:	ILDB D,A
SYCH4:	CAIL D,40
	JRST SYCH5
	IBP A
SYCH7:	ILDB D,A
	JUMPN D,.-1
	JRST SYCH1

SYCH6:	MOVE A,SYCHB'	;NOT IN S.T.
	SETZB B,C
	POPJ P,

;VAL. IN A, NAME IN B; USES A ... H

LSY:	MOVSI F,400000
	IORM F,-4(PP)
	SKIPA F,V
DSY:	MOVEI F,0
	PUSH P,[0
	MOVSI D,400000
	SETZM DSYATF'
	PUSH P,B
	MOVEM B,DSYNP'
DSY0:	ILDB C,B
	CAIN C,100
	AOS DSYATF
	MOVE C,TYPE(C)
	SKIPG DSYATF
	JUMPL C,[ERR [ASCII /NSD/]]
	JUMPL C,DSYI0
	CAME C,TYPE+100
	MOVEM D,DSYATF
	MOVEI C,-LETER(C)
	JUMPN C,DSY0
DSYI0:	MOVE B,(P)
DSYIN:	PUSH P,A	;REMAINDER ENTRY
	MOVEM A,LSYLSY'	;TO RECOVER IF SCE
	PUSHJ P,DSY9

DSY1:	MOVE B,SP
	MOVEI G,0
DSY1A:	MOVE A,(P)
	SETOM DSY20C'
DSY20:	TLNN FF,SYPF
	JRST DSY21
	CAMN A,-6(PP)	;L
	MOVEM G,DSYLC'
	CAMN A,-7(PP)	;R
	MOVEM G,DSYRC'
	ADDI G,1
DSY21:	ILDB D,A
	AOS DSY20C
	SKIPN -1(P)
	MOVE D,TYPE(D)
	JUMPG D,DSY20
	SKIPE DSY20C
	JRST DSY3
	CAMN D,TYPE+"\
	IBP A

DSY3:	PUSHJ P,DSYB
	DBP7 A
	CAMN A,(P)
	JRST DSY2
	LDB C,A
	DPB C,B
	JRST DSY3

DSY2:	SKIPN -1(P)
	JRST DSY4
	TLNN FF,SYPF
	JRST DSY22
	MOVE G,DSYRC
DSY23:	REPEAT 3,[
	IDIVI G,177
	ADDI H,1
	DPB H,B
	PUSHJ P,DSYB]
	MOVE G,DSYLC
	TLZE FF,SYPF
	JRST DSY23
	MOVEI H,177
	DPB H,B
	PUSHJ P,DSYB
DSY22:	DPB F,B
	PUSHJ P,DSYB
	ROT F,-7
	DPB F,B
	MOVEI F,0
	POP P,A
	MOVE A,DSYNP
	MOVEM A,(P)
	JRST DSY20

DSY4:	POP P,A
	POP P,A
	DPB A,B
	MOVEM B,SP
	POPJ P,

DSYB:	DBP7 B
	MOVEI C,(B)
	CAIG C,1(VP)
	ERR [ASCII /SCE/]
	POPJ P,
DSY9:	MOVEM B,DSY9B'
DSY91:	PUSHJ P,SYCH
	JUMPE B,CPOPJ
	ILDB D,A
	JUMPN D,.-1
DSY8:	JUMPLE F,DSY8A
	CAME C,F
	POPJ P,
DSY8A:	PUSH P,C	;LEVEL OF DEF.
	SKIPN C
	MOVEI C,1
	IMULI C,PPBL
	ADDI C,PPDL-1
DSY11:	CAILE C,(PP)
	JRST DSY5
	MOVE D,B
	MOVE E,-1(C)	;P2
	ROTC D,6
	CAMGE E,D
	JRST DSY6
	MOVE D,A
	MOVE E,-1(C)
	ROTC D,6
	CAML E,D
	JRST DSY6
	MOVSI E,400000
	IORM E,-4(C)	;SET FLAG ON THAT LEVEL
	MOVE D,"/
	IDPB D,B	;DEFACE NAME
DSY7:	ILDB D,B	;MARK SYMBOL WITH LEVEL
	SKIPL TYPE(D)
	JRST DSY7
	SUBI C,PPDL
	IDIVI C,PPBL
	IDPB C,B
	DBP7 B
	ROT C,-7
	DPB C,B
DSY10:	POP P,C
	JUMPE C,CPOPJ	;ONLY DEF.
DSY12:	JUMPL F,CPOPJ
	MOVE B,DSY9B
	JRST DSY91

DSY6:	ADDI C,PPBL
	JRST DSY11

DSY5:	JUMPLE F,DSY13
	POP P,C
	CAMN F,C
	PUSHJ P,SYCL
	POPJ P,

DSY13:	PUSHJ P,SYCL
	POP P,C
	JUMPN C,DSY12
	POPJ P,

USY:	PUSH P,B
	MOVNI F,1
	PUSHJ P,DSY9
	POP P,B
	PUSHJ P,SYCH
	SKIPE B
	TDCA B,B
	MOVEI B,1	;# OF WDS SKIPPED
	POPJ P,

PSY:	TLZN FF,400000	;FF HAS BEEN POPPED
	POPJ P,
	TDCA E,E
FSY:	MOVEI E,1
	MOVE A,SP
PSY0:	CAMN A,SSEND
	POPJ P,
	MOVE B,A
PSY1:	ILDB C,A
	MOVE D,TYPE(C)
	JUMPGE D,PSY1
	ILDB D,A
	DPB C,[(70700)D
	ILDB C,A
	JUMPN C,.-1
	JUMPE E,PSY2
	JUMPE D,PSY0
	JRST PSY3

PSY2:	CAME D,V
	JRST PSY0
PSY3:	PUSH P,A
	PUSHJ P,SYCL
	POP P,A
	JRST PSY0
;C: LEVEL LIMIT  B: WHERE TO START READING
;RETURNS VAL. IN BOTH A AND B; PTR TO END OF READING IN C

EV:	PUSH PP,A	;SIGNIF
	MOVEI A,SPACE1
	HRRM A,SPACE
	PUSH PP,E	;LEVUP,,LEVDOWN
	PUSH PP,B	;TAG SEARCH BASE
	PUSH PP,[0]	;BEG. OF SEARCH STRING
	PUSH PP,VP	;VALUE BASE
	PUSH PP,CLOSX
	PUSH PP,C	;LIM
	PUSH PP,[0	;R
	PUSH PP,[0	;L
	PUSH PP,VP
	TLZ FF,400000
	PUSH PP,FF	;FLAGS
	PUSH PP,SPACE
	PUSH PP,[0	;VAL
	PUSH PP,B	;P2
	PUSH PP,B	;P1
	AOJA V,EV0


SPACE0:	MOVE VP,-12(PP)
SPACE2:	SETZM -2(PP)

EV0:	MOVEI A,CLOS1
EV0B:	HRRM A,CLOSX
	CLEARM DIGITT'
	SETOM EVPF'	;POINT COUNTER
	SETOM EVCC'	;COMMA COUNTER
	TLZ FF,EVLF+EVCF+EVSF+EVMF+EVNF+EVDF
	CLEARM SECOND'

EV0A:	MOVE A,(PP)
	MOVEM A,-1(PP)
	MOVEM VP,-5(PP)
	MOVEI E,0

IGNOR:
EV1:	ILDB A,(PP)
	JRST @TYPE(A)
POINT:	AOSG EVPF
	AOS EVPF
	JRST EV0A

LETER:	ILDB A,(PP)
	SKIPL TYPE(A)
	JRST LETER
LETER1:	MOVE A,(PP)
	DBP7 A
	MOVEM A,(PP)

SYMBOL:	MOVE B,-1(PP)
	ILDB A,B
	ILDB C,B
	CAIN A,"@
	SKIPL TYPE(C)
	JRST SYMBL1
	MOVEI A,40
	PVAK
	JRST CLOSE
SYMBL1:	MOVE B,-1(PP)
	PUSHJ P,SYCH
	JUMPE B,QSYMBL
SYMBL3:	SKIPL C,EVPF
	CAMLE C,-10(PP)
	MOVE C,-10(PP)
	JUMPE C,QSYMB2
	MOVE B,A
	ILDB D,A
	CAIE D,177
	JRST SYMBL4
	REPEAT 2,IBP B
	AOS B
SYMBL4:	SOJE C,SYMNE
	HRRZ A,CLOSX
	MOVEM A,-11(PP)
	HRRZ A,SPACE
	MOVEM A,-3(PP)
	MOVE A,-16(PP)
	HRRZ D,-15(PP)
	HRLZ E,D
	SUBI D,1
	HRRI E,1
	IMUL D,[(PPBL)PPBL]
	ADD PP,D
	PUSHJ PP,EV
SYMBL2:	MOVE D,-3(PP)
	HRRM D,SPACE
	MOVE D,-11(PP)
	HRRM D,CLOSX

CLOSA:	MOVEM A,-5(PP)
	JRST CLOS

QSYMBL:	SOSN EVPF
	JRST SPACE1
QSYMB2:	ILDB A,-1(PP)
	CAIE A,"\
	JRST QSYMB1
	PUSH P,PVA
	JRST CLOSE

QSYMB0:	PVAK
	ILDB A,-1(PP)
QSYMB1:	MOVE B,TYPE(A)
	JUMPGE B,QSYMB0
	JRST CLOSE

;

ZERO:	MOVE B,(PP)
	DBP7 B
	MOVEM B,(PP)
RETRN:	HRRZ A,SPACE	;)
	CAIE A,SPACE1
	JRST (A)
ZERO1:	HLL FF,-4(PP)
	PUSHJ P,PSY
	MOVE D,1-PPBL(PP)
	MOVE C,(PP)	;USED BY (
	MOVE A,-2(PP)
	HLRZ E,-15(PP)
	IMUL E,[(PPBL)PPBL
	SUB PP,E
ZERO1A:	MOVE B,A
	SOJA V,(D)

DOLAR:	TLO FF,EVDF
	MOVE A,(PP)
	ILDB B,A
	CAIN B,".
	JRST EV0A
	SKIPGE TYPE(B)
	JRST EV0A
	MOVE C,(PP)
	MOVEM C,OLDP1'
	PUSHJ P,IGNSYM
	JRST ACLOS2

;

ATSGNN:	TDCA B,B
ATSGN:	MOVEI B,1
ATSGNA:	ILDB A,(PP)
	HRRZ A,TYPE(A)
	CAIN A,LETER
	JRST LETER
	CAIE A,POINT
	CAIN A,DIGIT
	JRST ATSGNN
	CAIN A,ATSGN
	JRST ATSGNA
	JUMPE B,DIGIT1
	JRST LETER1

NSIGN:	SKIPE SECOND'
	JRST NSIGN1
NSIGN0:	MOVE B,(PP)
	ILDB C,B
	MOVE B,(PP)
	JUMPE C,NSIGN3
	CAIN C,40
	JRST NSIG3A
	SKIPL TYPE(C)
	JRST NSIGN2
	TLO FF,EVNF
	AOS SECOND
	MOVE F,A
	JRST EV0A

NSIGN1:	TLO FF,EVNF
	MOVE B,(PP)
	ILDB C,B
	CAIN C,"/
	JRST EV0A
	CAIN F,":
	JRST NSIGN0
	CAME F,A
	ERR [ASCII /IDO/]
	PUSHJ P,INFIX

NSIGN2:	PUSHJ P,SYCH
	PUSHJ P,IGNSYM
	SKIPE B
NSW:	TLC FF,EVCF
NSL:	TLNE FF,EVCF
WIN:	PUSHJ P,IGNWR0
	JRST EV0

NSIG3A:	IBP (PP)
NSIGN3:	MOVE B,-2(PP)
	JUMPE B,NSL
	ILDB C,B
	JUMPE C,NSL
	CAIE C,"+
	CAIN C,"-
	JRST NSIGN4
	MOVNI D,1
NSIGN8:	CAIN C,".
	AOJA D,NSIGN4
NSIGN7:	CAIN C,"@
	JRST NSIGN5
	CAIL C,"0
	CAILE C,"9
	JRST NSL
NSIG5A:	MOVEI A,0
NSIGN5:	ILDB C,B
	JUMPE C,NSIGN6
	CAIN C,".
	AOJA D,NSIG5A
	JRST NSIGN7

NSIGN6:	JUMPG D,NSL
	JUMPN A,NSL
	JRST NSW

NSIGN4:	ILDB C,B
	JUMPE C,EV0
	MOVEI A,0
	MOVNI D,1
	JRST NSIGN8

CPARN:	SKIPGE EVPF
	JRST RETRN
IFN TS,[
	.LOGOUT
	.VALUE @-2(PP)
]
IFE TS,[
	MOVE A,-2(PP)
	MOVEM A,HI
	JRST HI-3777
]
	JRST EV0

DIGIT:	SKIPGE EVPF
	JRST DIGIT0
DIGQ1:	ILDB A,(PP)
	SKIPGE A,TYPE(A)
	JRST DIGQP
	CAIE A,LETER
	JRST DIGQ1
	JRST LETER

DIGQP:	MOVEI A,".
	PVAK
	JRST DIGIT1

DIGITP:	TLNE FF,EVLF
	ERR [ASCII /DIS/]
	MOVEM E,EVPF
	MOVE A,(PP)
	MOVEM A,DIGITT'
	JRST DIGIT2

DIGITA:	MOVEI A,60
DIGIT0:	IMULI E,10.
	ADDI E,-60(A)
DIGIT2:	ILDB A,(PP)
	HRRZ B,TYPE(A)
	CAIN B,PAREN
	JRST PARENN
	CAIN B,UPARR
	JRST UPARRN
	CAIN B,DIGIT
	JRST DIGIT0
	CAIN B,ATSGN
	JRST DIGITA
	CAIN B,POINT
	JRST DIGITP
	CAIN B,COMMA
	JRST DIGITC
	CAIN B,GREAT
	JRST GREAT
	CAIN B,LESS
	JRST LESS
	CAIN B,AMPER
	JRST AMPER
	CAIE B,LETER
	JRST DIGIT1
	TLO FF,EVLF
	MOVE A,DIGITT'
	JUMPE A,LETER
	MOVEM A,-1(PP)
	JRST LETER

DIGITC:	TLO FF,EVMF
	MOVEM E,EVCC
	AOS SECOND'
	MOVE F,A
	MOVE A,(PP)
	ILDB A,A
	CAIE A,"<
	CAIN A,">
	JRST EV0A
DIGIT1:	MOVE A,(PP)
	DBP7 A
	MOVEM A,(PP)
QNUMBR:	ILDB A,-1(PP)
	HRRZ B,TYPE(A)
	CAIE B,ATSGN
	CAIN B,DIGIT
	JRST QNUMB1
	CAIE B,POINT
	JRST CLOSE
QNUMB1:	PVAK
	JRST QNUMBR

PVA:	IDPB A,VP
	HRRM A,PVAA
	MOVEI A,@SP
	CAIG A,1(VP)
	ERR [ASCII /SCE/]
PVAA:	MOVEI A,
CPOPJ:	POPJ P,

QUOTE0:	PVAK
QUOTE:	ILDB A,(PP)
	JUMPE A,[ERR [ASCII /UCQ/]]
	CAIE A,40
	JRST QUOTE0
	JRST CLOSE
SYMNE0:	PVAK
SYMNE:	ILDB A,B
	JUMPN A,SYMNE0
	JRST CLOSE


SPACEA:	MOVEI A,CLOS1
	HRRM A,CLOSX
SPACE:	JRST .	;SPACE1 OR ARGRET

RSPACE:	MOVE VP,-5(PP)
RCLOSE:	MOVEI A,CLOS1
	HRRM A,CLOSX
	JRST CLOSE

SPACE1:	MOVE VP,-5(PP)

CLOSE:	MOVEI A,0
	PVAK

CLOS:	HRLI VP,10700
CLOSX:	JRST .	;CLOS1 OR INFIX1

CLOS1:	SKIPE A,-5(PP)	;CLOSE 1ST VAL OR RESULT OF OP
	ILDB A,A
	JUMPE A,SPACE0
	MOVE A,-12(PP)
	CAMN A,-5(PP)
	JRST CLOSE1
	HRL A,-5(PP)
	AOBJN A,.+1
	MOVE B,-12(PP)
	SUB B,-5(PP)
	ADDI B,(VP)
	BLT A,(B)
	MOVE A,-12(PP)
	HRLI B,10700
	MOVEM B,VP
CLOSE1:	MOVEM A,-2(PP)
	JRST EV0

PARENN:	MOVEM E,EVPF
PAREN:	TLNE FF,EVLF
	ERR [ASCII /PIS/]
	SKIPL C,EVPF
	CAMLE C,-10(PP)
	MOVE C,-10(PP)
	JUMPE C,QPAREN
	SUBI C,1
	MOVE B,(PP)
	HRRZ A,CLOSX
	MOVEM A,-11(PP)
	HRRZ A,SPACE
	MOVEM A,-3(PP)
	MOVE A,-16(PP)
	HRRZ D,-15(PP)
	HRLZ E,D
	SUBI D,1
	HRRI E,1
	IMUL D,[(PPBL)PPBL
	ADD PP,D
	PUSHJ PP,EV
	MOVE D,-3(PP)
	HRRM D,SPACE
	MOVE D,-11(PP)
	HRRM D,CLOSX
	MOVEM C,(PP)
	JRST CLOSA

QPARN1:	PVAK
QPAREN:	ILDB A,(PP)
	JUMPE A,[ERR [ASCII /UCP/]]
	CAIE A,")
	JRST QPARN1
	JRST CLOSE

AMPER:	TLNE FF,EVDF
	JRST AMPER4
	PUSHJ P,TYIAZ
	CAIN C,40
	JRST AMPER2
AMPER1:	PVAK
	SKIPL EVPF
	JRST AMPER3
	CAIN C,""
	JRST AMPERQ
	CAIE C,"[
	JRST AMPER
AMPERS:	PUSHJ P,TYIAZ
	CAIE C,"[
	JRST AMPER1
	PVAK
	PUSHJ P,SUPER2
	  PVAK
	JRST AMPER

AMPERQ:	PUSHJ P,TYIAZ
	PVAK
	CAIE C,40
	JRST AMPERQ
	JRST AMPER

AMPER2:	SKIPGE EVPF
	JRST CLOSE
	PVAK
AMPER3:	SOSLE EVPF
	JRST AMPER
	JRST CLOSE

AMPER4:	TLO FF,EVNF
	TLZ FF,EVDF
	JRST PCT1

;CALLED TO GET SECOND ARGUMENT OF INFIX OPERATORS

INFIX:	PUSH P,(PP)
	PUSHJ P,IGNSYM
	MOVE A,(P)
	CAMN A,(PP)
	JRST INFIX2
	MOVEM A,(PP)
INFIX0:	PUSH P,FF
	JSP A,EV0B
INFIX1:	POP P,FFTEM'	;AT JSP+1
	HLL FF,FFTEM
	POP P,OLDP1'
	MOVE B,-5(PP)	;RIGHT OPERAND
	ILDB A,-5(PP)
	SKIPN A
	MOVEI B,0
INFIX3:	MOVEM VP,-5(PP)
	MOVE A,-2(PP)	;LEFT OPERAND
	POPJ P,

INFIX2:	MOVEI B,0
	POP P,(PP)
	JRST INFIX3

;

PRIME:	PUSHJ P,INFIX
	TLNE FF,EVCF+EVMF
	EXCH A,B
	MOVEM A,-2(PP)
	JUMPE A,PRIME1
PRIME0:	ILDB A,-2(PP)
	JUMPE A,PRIME1
	PVAK
	JRST PRIME0

PRIME1:	SKIPE A,B
PRIME2:	ILDB A,B
	JUMPE A,PCLOSE
	PVAK
	JRST PRIME2

PCLOSE:	TLNN FF,EVCF+EVDF
	JRST RCLOSE
ACLOSE:	MOVEI A,0
	PVAK
	HRLI VP,10700
ACLOS2:	MOVE C,OLDP1
	MOVEM C,OLDP1X'
	MOVEI D,0
ACLOS9:	ILDB A,C
	CAIE A,".
	JRST ACLOS0
	MOVEM C,OLDP1
	AOJA D,ACLOS9
ACLOS0:	MOVE B,OLDP1
	SOJLE D,ATCH2
	MOVE B,OLDP1X
	IBP B
	MOVEM B,(PP)
	PUSH P,-5(PP)
	MOVEM VP,-5(PP)
	PUSH P,FF
	JSP A,EV0B
ATCH1:	POP P,FFTEM
	HLL FF,FFTEM
	MOVE B,-5(PP)
	POP P,-5(PP)
	MOVEM B,OLDP1
ATCH2:	PUSHJ P,SYCH
	SKIPN B
	MOVE C,V
	JUMPE C,ACLOS1
	PUSH P,V
	MOVE V,C
	MOVE B,OLDP1
	MOVE A,-5(PP)
	PUSHJ P,LSY
	POP P,V
	JRST CLOS1

ACLOS1:	MOVE B,OLDP1
	MOVE A,-5(PP)
	PUSHJ P,DSY
	JRST CLOS1

ILLEG:	ERR [ASCII /IOP/]

QMARK:	SKIPL EVPF
	JRST QMARK1
	TLNE FF,EVMF
	JRST QMARK4
	TLNE FF,EVCF
	ERR [ASCII /IDO/]
	TLNN FF,EVNF+EVSF+EVDF
	JRST QMARK2
	MOVE A,-2(PP)
	PUSHJ P,NATVAL
	TLNE FF,EVNF
	MOVEM B,SGNF0
	TLNE FF,EVSF
	MOVEM B,-16(PP)
	SKIPG B
	JRST EV0
	TLNE FF,EVDF
	MOVEM B,LINEL1
	JRST EV0

QMARK1:	MOVE B,-16(PP)
	JUMPL B,SPACE1
	PUSHJ P,DPV
	JRST CLOSE

DPV:	IDIVI B,12
	JUMPE B,DPV1
	PUSH P,C
	PUSHJ P,DPV
	POP P,C
DPV1:	MOVEI A,60(C)
	PVAK
	POPJ P,

QMARK2:	MOVE B,(PP)
	ILDB E,B
	CAIE E,"<
	CAIN E,">
	JRST QMARK3
	ERR [ASCII /IOP/]

QMARK3:	IBP (PP)
	MOVE A,-2(PP)
	PUSHJ P,NATVAL
	MOVE A,B
	MOVE C,-16(PP)
	CAIE E,"<
	EXCH B,C
	CAMGE B,C
	MOVEM A,-16(PP)
	JRST EV0

QMARK4:	MOVE B,-16(PP)
	JUMPL B,QMARK5
	JUMPE B,QMARK6
	MOVEI A,".
	PVAK
	MOVEI A,"0
QMARK7:	SOJLE B,QMARK6
	PVAK
	JRST QMARK7

QMARK5:	SKIPA A,["0]
QMARK6:	MOVEI A,"1
	PVAK
	JRST CLOSE

;

PERCT:	MOVE A,-2(PP)
	TLNE FF,EVCF+EVSF+EVDF+EVMF
	JRST PCT1
	TRO FF,IO%
	TLNE FF,EVNF
	TLO FF,TYDF
	PUSHJ P,TYA
	TRZ FF,IO%
	TLZ FF,TYDF
	JRST EV0

PCT1:	TLNN FF,EVDF+EVMF
	PUSHJ P,INFIX
	MOVNI C,1
PCT1A:	ILDB D,A
	JUMPE D,PCT2
	CAIL D,"0
	CAILE D,"9
	JRST PCT1A
	MOVEI C,-60(D)
	JRST PCT1A

PCT2:	SKIPGE C
	MOVE C,TAPENM
	TLNN FF,EVSF
	MOVEM C,TAPENM
	TLNE FF,EVMF
	JRST PCT5
	PUSHJ P,FILEST"
	JRST PCT2ET+2(C)
	TLNE FF,EVDF
	JRST PCT3
	MOVSI D,'@_12.
PCT20:	MOVE C,D
	MOVSI D,'@_12.
	MOVE E,[(440600)D]
PCT21:	ILDB F,B
	JUMPE F,PCT6
	SUBI F,40
	JUMPE F,PCT20
	SKIPGE F
	AOSN F
	CAIL F,100
	MOVEI E,0
PCT22:	TLNE E,770000
	IDPB F,E
	JRST PCT21

PCT3:	PUSHJ P,OPNWR"
	ERR [ASCII /NFF/]
	SETOM UTOOK'
	TROA FF,IOR
PCT5:	PUSHJ P,TAPKIL"
	JRST EV0

PCT6:	TLNN FF,EVSF+EVNF
	PUSHJ P,UDELE"
	TLNN FF,EVCF+EVNF
	JRST PCT7
	TLNE FF,EVCF+EVSF
	JRST EV0
	PUSHJ P,OPNRD"
	ERR [ASCII /FNF/]
	SETOM UTIOK'
	JRST EV0

PCT7:	PUSHJ P,FILE"
	ERR [ASCII /NFF/]
	JRST EV0

TAPENM:	0

PCT2ET:	ERR [ASCII /UNA/]
	ERR [ASCII /BDD/]
	ERR [ASCII /TMD/]
IFN TS,[
UTIC==3
UTOC==4
FILEST:	CAIE C,0
	CAIN C,11
	JRST FILES1
	MOVEI D,(SIXBIT /UT0/)
	DPB C,[400,,D]
	HRRM D,UTF
	JRST PPOPJ

FILES1:	MOVEI D,(SIXBIT /DSK/)
	HRRM D,UTF
	JRST PPOPJ

OPNRD:	HRRZS UTF
	MOVEM C,UTF+1
	MOVEM D,UTF+2
	SKIPGE UTF
	JRST OPNRDD
	.OPEN UTIC,UTF
	POPJ P,
	JRST PPOPJ

OPNRDD:	.FDELE UTF
	POPJ P,
	JRST PPOPJ

TAPKIL:	.UDISMT C,
	ERR [ASCII /UNF/]
	POPJ P,

OPNWR:	MOVSI C,1
	HLLM C,UTF
	MOVE C,[SIXBIT /SH*T/]
	MOVEM C,UTF+1
	.GENSY C,
	MOVEM C,UTF+2
	MOVEM C,FNAM2'
	.OPEN UTOC,UTF
	POPJ P,
	JRST PPOPJ

UDELE:	SETZM UTF+3
	MOVSI E,400000
	HLLM E,UTF
	JRST OPNRD+1

FILE:	MOVEM C,UTF+3
	MOVEM D,UTF+4
	SETZM UTF+1
	MOVEI C,UTOC
	MOVEM C,UTF+2
	.FDELE UTF
	POPJ P,
	.CLOSE UTOC,
	SETZM UTOOK'
	JRST PPOPJ

UTF:	(SIXBIT /UT0/)
	BLOCK 4
]

COLON:	TLO FF,EVCF
	MOVEI D,USY
	SKIPL EVPF
	MOVEI D,RSS
	JRST SYH

SEMCL:	TLO FF,EVSF
	MOVEI D,LSY
	JRST SYH

COMMA:	TLO FF,EVMF
	AOSG EVCC
	AOS EVCC
COMMA1:	MOVEI D,DSY
SYH:	TLZ FF,SYPF
	MOVE B,(PP)
	SKIPE SECOND'
	JRST SYHA
	ILDB B,B
	SKIPGE TYPE(B)
	JRST SYHD
SYH2:	MOVE B,(PP)
	TLZ FF,TINGF
SYH0:	MOVE C,B
	ILDB A,C
	SKIPGE EVPF
	CAIE A,"@
	JRST SYH1
	ILDB A,C
	SKIPGE TYPE(A)
	ERR [ASCII /ASD/]
SYH1:	MOVE A,-2(PP)
	TLNE FF,SYPF
	MOVE A,-13(PP)
	SKIPN A
	MOVE A,[(10700)[0]-1]
	PUSH P,V
	TLNE FF,EVDF
	JRST SYH3
SYH5:	PUSHJ P,(D)
	POP P,V
	TLZN FF,TINGF
	PUSHJ P,IGNSYM
	JUMPGE F,EV0
	SKIPE B
	PUSHJ P,IGNWRD
	JRST EV0

SYH3:	PUSH P,A
	PUSH P,B
	PUSH P,D
	PUSHJ P,SYCH
	POP P,D
	JUMPE B,SYH4
	MOVEI D,DSY
	JUMPE C,SYH4
	MOVEI D,LSY
	MOVE V,C
SYH4:	POP P,B
	POP P,A
	JRST SYH5

SYHD:	AOS SECOND'
	MOVE F,A
	JRST EV0A

SYHA:	CAMN F,A
	JRST SYHA1
	CAIN A,":
	CAIE F,"#
	JRST SYHA2
	ILDB B,B
	SKIPGE TYPE(B)
	JRST SYHD
	ERR [ASCII /IDO/]
SYHA2:	CAIE F,":
	ERR [ASCII /IDO/]
	TLO FF,SYPF
	MOVE C,SECOND
	CAIG C,1
	JRST SYH2
SYHA1:	PUSH P,D
	PUSHJ P,INFIX
	POP P,D
	TLO FF,TINGF
	JRST SYH0

OPBRK:	ILDB C,(PP)
	CAIE C,15
	CAIN C,12
	JRST OPBRK2
	CAIE C,"[
	JRST OPBRK1
	PUSHJ P,SUPER
	  PUSHJ P,PVA
	DBP7 VP
	DBP7 VP
	JRST CLOSE

OPBRK1:	PUSHJ P,IGNAM
	JRST EV0

OPBRK2:	ILDB C,(PP)
	CAIE C,15
	CAIN C,12
	JRST OPBRK2
	MOVE C,(PP)
	DBP7 C
	MOVEM C,(PP)
	JRST EV0
;USES A, C.  IGNORES ANY ITEM
;  IN FACT, THIS ROUTINE IS THE DEFINITION OF AN ITEM

IGNSYM:	SETZM IGNAL'
	SETOM IGNSYC'
	MOVE C,(PP)
	MOVEM C,IGNSYB'

IGNS0:	AOS IGNSYC
IGNS1:	ILDB C,(PP)
	CAIN C,""
	JRST IGNQ
	CAIN C,"[
	JRST IGNS
	CAIN C,"(
	JRST IGNS0
	CAIN C,")
	JRST IGNP
	SKIPE IGNSYC
	JRST IGNS1
	CAIE C,"<
	CAIN C,">
	JRST IGNL
	CAIE C,"&
	CAIN C,"^
	POPJ P,
	CAIE C,40
	CAIN C,"\
	POPJ P,
	CAIN C,".
	JRST IGND
	CAIN C,",
	JRST IGNM
	SKIPL TYPE(C)
	JRST IGNA

IGNS2:	MOVE C,(PP)
	DBP7 C
	MOVEM C,(PP)
	POPJ P,

IGNQ:	ILDB C,(PP)
	CAIN C,40
	POPJ P,
	JUMPN C,IGNQ
	ERR [ASCII /UCQ/]

IGNS:	ILDB C,(PP)
	CAIE C,"[
	JRST IGNB
	PUSHJ P,SUPER
	  JFCL
	POPJ P,

IGNP:	SOSLE C,IGNSYC
	JRST IGNS1
	JUMPL C,IGNS2
	POPJ P,

IGNL:	ILDB C,(PP)
	CAIE C,">
	CAIN C,"<
	POPJ P,
	JRST IGNS2

IGND:	ILDB C,(PP)
	CAIE C,"&
	CAIN C,"^
	POPJ P,
	CAIE C,"\
	CAIN C,"=
	POPJ P,
	CAIN C,"?
	POPJ P,
	CAIN C,"(
	JRST IGNS3
	CAIE C,"<
	CAIN C,">
	JRST IGNL
	CAIN C,".
	JRST IGND
	SKIPA
IGND1:	ILDB C,(PP)
	SKIPL TYPE(C)
	JRST IGND1
	JRST IGNS2

IGNA:	IORM C,IGNAL
	HRRZ C,TYPE(C)
	CAIN C,LETER
	SETOM IGNAL
	ILDB C,(PP)
	SKIPL TYPE(C)
	JRST IGNA
	SKIPGE IGNAL
	JRST IGNS2
	CAIN C,".
	JRST IGND
	CAIN C,"(
	JRST IGNS0
	CAIN C,"^
	POPJ P,
	CAIE C,",
	JRST IGNS2
	MOVE C,(PP)
	DBP7 C
	MOVEM C,IGNSYB

IGNM:	ILDB C,(PP)
	CAIE C,"<
	CAIN C,">
	JRST IGNL
IGNS3:	MOVEI C,"0
	CAME C,IGNAL
	JRST IGNS0
	ILDB C,(PP)
	CAIN C,")
	POPJ P,
	JUMPN C,.-3
	ERR [ASCII /UCP/]

IGNB:	MOVE C,IGNSYB
	MOVEM C,(PP)
	POPJ P,

IGNWRS:	ILDB A,(PP)
	CAIE A,"[
	JRST IGNWR2
	PUSHJ P,SUPER
	  JFCL
	JRST IGNWR2

SUPER2:	SKIPA A,[PUSHJ P,TYIA
SUPER:	MOVE A,[ILDB A,(PP)
SUPER0:	MOVEM A,SUPER1
	SETZM SUPERC'
SUPER1:	ILDB A,(PP)
SUPERE:	JUMPE A,[ERR [ASCII /UCS/]]
	XCT @(P)
SUPERF:	CAIN A,"]
	JRST SUPERR
	CAIE A,"[
	JRST SUPER1
SUPERL:	XCT SUPER1
	XCT SUPERE
	XCT @(P)
	CAIE A,"[
	JRST SUPERF
	AOS SUPERC
	JRST SUPER1

SUPERR:	XCT SUPER1
	XCT SUPERE
	XCT @(P)
	CAIE A,"]
	JRST SUPERF
	SOSL SUPERC
	JRST SUPER1
PPOPJ:	AOS (P)
	POPJ P,

IGNAM:	SKIPGE TYPE(C)
	JRST IGNS2
	ILDB C,(PP)
	JRST IGNAM

EXCLM:	TLZ FF,SERSKF+SERRLF
	TLOA FF,SERCNF
SERCH:	TLZ FF,SERSKF+SERCNF+SERRLF
	TLNE FF,EVCF+EVSF
	TLO FF,SERSKF
	TLNE FF,EVMF+EVSF
	TLO FF,SERRLF
	HRRZ A,SPACE
	CAIE A,SPACE1
	ERR [ASCII /EAS/]
	TLNE FF,SERRLF
	JRST SERRL
	TLNE FF,SERCNF
	MOVE B,-7(PP)
	TLNN FF,SERCNF
	PUSHJ P,INFIX
	JUMPE B,SSPACE
	MOVEM B,SERCHB'
	MOVEM B,SERCHC'
	ILDB B,B
	TLNN FF,SERCNF
	JUMPE B,SSPACE
SERCH9:	MOVE B,SERCHC
	MOVE A,-2(PP)
	JUMPE A,SERCH1
SERCH0:	ILDB C,A
	JUMPE C,SERCH1
	ILDB D,B
	CAMN C,D
	JRST SERCH0
	ILDB A,SERCHC
	JUMPN A,SERCH9

SERCH8:	MOVE B,SERCHC	;NOT FOUND
	DBP7 B
SEV2:	MOVEM B,-7(PP)
	TLC FF,SERSKF
	JRST SEV1

SSPACE:	TLNE FF,SERSKF
	PUSHJ P,IGNWR0
	MOVE VP,-5(PP)
	JRST EV0

SERCH1:	MOVEM B,-7(PP)	;FOUND
	MOVE B,SERCHC
SEV1:	MOVEM B,-6(PP)	;L
SERRL9:	PUSHJ P,SERCHV
	TLNN FF,SERSKF
	PUSHJ P,IGNWR0
	JRST EV0

SERRL:	TLNE FF,SERCNF
	MOVE B,-13(PP)
	TLNN FF,SERCNF
	PUSHJ P,INFIX
	JUMPE B,SSPACE
	MOVEM B,SERCHB'
	MOVE C,-6(PP)
	TLNE FF,SERCNF
	JRST SERRL0
	ILDB A,B
	JUMPE A,SSPACE
SERRL1:	MOVE C,B
	ILDB A,B
	JUMPN A,SERRL1
SERRL0:	MOVEM C,SERCHC'
	MOVE B,-2(PP)
SERRL2:	MOVE A,B
	ILDB C,B
	JUMPN C,SERRL2
	MOVEM A,SERCHA'
	MOVE B,SERCHC
SERRL5:	CAMN B,SERCHB
	JRST SERRL6	;NOT FOUND
	MOVE A,SERCHA
SERRL3:	CAMN A,-2(PP)
	JRST SERRL4
	LDB C,A
	DBP7 A
	LDB D,B
	DBP7 B
	CAMN C,D
	JRST SERRL3
	MOVE B,SERCHC
	DBP7 B
	MOVEM B,SERCHC
	JRST SERRL5

SERRL6:	MOVE A,SERCHA
	CAME A,-2(PP)
	JRST SEV2
SERRL4:	MOVEM B,-6(PP)	;FOUND
	MOVE B,SERCHC
	MOVEM B,-7(PP)	;R
	JRST SERRL9

RSS:	MOVEM VP,SERCHB'
	TLO FF,SERSKF
	TLZ FF,SERCNF
	SETZB G,RSSLC'
	SETZM RSSRC'
	PUSHJ P,SYCH
	JUMPE B,RSS2
	MOVE C,A
	ILDB A,C
	CAIE A,177
	JRST RSS1
RSS9:	ILDB D,C
	SUBI D,1
	IMULI D,177
	ILDB A,C
	ADDI D,-1(A)
	IMULI D,177
	ILDB A,C
	ADDI D,-1(A)
	MOVEM D,RSSLC
RSS9A:	ILDB D,C
	SUBI D,1
	IMULI D,177
	ILDB A,C
	ADDI D,-1(A)
	IMULI D,177
	ILDB A,C
	ADDI D,-1(A)
	MOVEM D,RSSRC

RSS0:	ILDB A,C
RSS1:	CAMN G,RSSLC
	MOVEM VP,-6(PP)
	CAMN G,RSSRC
	MOVEM VP,-7(PP)
	JUMPE A,RSS3
	PVAK
	AOJA G,RSS0

RSS2:	MOVEI A,0
	JRST RSS1

RSS3:	MOVEI A,0
	PVAK
	HRLI VP,10700
	JRST SERCHV
IGNWR0:	MOVEI B,1
IGNWRD:	SETZM IGNWRC'
	ILDB A,(PP)
	CAIN A,40
IGNWR1:	ILDB A,(PP)
IGNWR2:	JUMPE A,IGNWRZ
	CAIN A,""
	JRST IGNWRQ
	CAIN A,"[
	JRST IGNWRS
	CAIN A,"(
	AOS IGNWRC'
	CAIN A,")
	SKIPG IGNWRC
	SKIPA
	SOS IGNWRC
	SKIPN IGNWRC
	CAIE A,40
	JRST IGNWR1
	SOJG B,IGNWR1
	POPJ P,

IGNWRQ:	ILDB A,(PP)
	JUMPE A,[ERR [ASCII /UCQ/]]
	CAIE A,40
	JRST IGNWRQ
	JRST IGNWR1

IGNWRZ:	SKIPE IGNWRC
	ERR [ASCII /UCP/]
	MOVE A,(PP)
	DBP7 A
	MOVEM A,(PP)
	POPJ P,

SERCHV:	MOVE A,-13(PP)
	TLNE FF,SERCNF
	JRST SERCV2
	SKIPN D,-13(PP)
	MOVE D,-12(PP)
	MOVE A,D
	MOVEM A,-13(PP)
	CAMN A,SERCHB'
	JRST SERCV1
	HRL A,SERCHB
	AOBJN A,.+1
	MOVE B,D
	SUB B,SERCHB
	ADDM B,-6(PP)
	ADDM B,-7(PP)
	ADDI B,(VP)
	BLT A,(B)
	HRLI B,10700
	MOVE A,D
	MOVE VP,B
SERCV1:	MOVEM VP,-12(PP)
SERCV2:	MOVEM A,-2(PP)
	POPJ P,

LESS:	TLNE FF,EVCF+EVNF
	JRST LESSC
	TLNE FF,EVSF
	JRST RLESS
	SKIPN B,-13(PP)
	JRST SPACE1
	MOVEI F,-6(PP)
	MOVE A,(PP)
	ILDB A,A
	CAIN A,"<
	JRST LESSN
	CAIE A,">
	JRST LESS0
	IBP (PP)
	MOVEI F,-7(PP)
LESS0:	TLNN FF,EVDF
	JRST LESS00
	MOVE A,-2(PP)
	PUSHJ P,NATVAL
	MOVEM B,EVPF
	JUMPGE B,LES10
	MOVE B,-13(PP)
LESS00:	MOVE C,(F)
	SKIPL EVPF'
	JRST LESS2
	TLNE FF,EVMF
	JRST LESS3
LESS1:	CAMN B,C
	JRST LESS1A
	ILDB A,B
	PVAK
	JRST LESS1

LESS1A:	TRNE FF,EVDF
	JRST LESS2A
	JRST CLOSE

LESS2:	CAMN C,-13(PP)
	JRST LESS2A
	LDB A,C
	DBP7 C
	PVAK
	SOSLE EVPF
	JRST LESS2
LESS2A:	CAIN F,-6(PP)
	MOVEM C,(F)
	JRST CLOSE

LESS3:	MOVEM C,D
LESS5:	CAMN C,-13(PP)
	JRST LESS7A
	LDB A,C
	CAIN A,40
	JRST LESS7
LESS4:	DBP7 C
	JRST LESS5

LESS7:	CAMN C,D
	JRST LESS4
LESS7A:	MOVEM C,E
LESS6:	CAMN C,(F)
	JRST LESS8
	ILDB A,C
	CAIN A,40
	JRST LESS8
	PVAK
	JRST LESS6

LESS8:	SOSG EVCC
	JRST LESS9
	CAMN E,-13(PP)
	JRST LESS9
	MOVEI A,40
	PVAK
	MOVE C,E
	JRST LESS3

LESS9:	CAIN F,-6(PP)
	MOVEM E,-6(PP)
	JRST CLOSE

LES10:	MOVE D,(F)
LES101:	CAME D,-13(PP)
	SOSGE EVPF
	JRST LES100
	DBP7 D
	JRST LES101

LES100:	MOVE C,D
LES102:	CAMN D,(F)
	JRST LESS2A
	ILDB A,D
	PVAK
	JRST LES102

GREAT:	TLNE FF,EVCF+EVNF
	JRST GREAC
	TLNE FF,EVSF
	JRST RGRET
	SKIPN B,-7(PP)
	JRST SPACE1
	MOVE A,EVPF
	MOVEM A,GREPF'
	TLNN FF,EVDF
	JRST GREA00
	MOVE A,-2(PP)
	PUSHJ P,NATVAL
	MOVEM B,EVPF
	MOVE B,-7(PP)
	SETZM GREPF'
GREA00:	MOVEI F,-7(PP)
	MOVE A,(PP)
	ILDB A,A
	CAIN A,">
	JRST GREAN
	CAIE A,"<
	JRST GREAT0
	IBP (PP)
	MOVEI F,-6(PP)
	MOVE B,-6(PP)
GREAT0:	TLNE FF,EVMF
	JRST GREAT2
GREAT1:	SKIPL EVPF
	JRST GREA1F
	HRLOI A,377777
	MOVEM A,EVPF
GREA1F:	SOSGE EVPF
	JRST GREA1B
	ILDB A,B
	JUMPE A,GREA1A
	PVAK
	JRST GREA1F

GREA1A:	DBP7 B
GREA1B:	CAIN F,-7(PP)
	SKIPGE GREPF
	JRST CLOSE
	MOVEM B,-7(PP)
	JRST CLOSE

GREAT2:	ILDB A,B
	JUMPE A,SPACE1
	CAIE A,40
GREAT3:	PVAK
	ILDB A,B
	JUMPE A,GREAT4
	CAIE A,40
	JRST GREAT3
	SOSLE EVCC
	JRST GREAT3
GREAT4:	DBP7 B
	CAIN F,-7(PP)
	MOVEM B,-7(PP)
	JRST CLOSE

LESSC:	PUSHJ P,INFIX
	MOVEM B,-5(PP)
	EXCH A,B
	JRST GREAC1

LESSN:	SKIPGE EVPF
	TLNE FF,EVDF+EVMF
	JRST LESS0
	IBP (PP)
	MOVE F,(F)
LESSN2:	MOVEI A,0
LESSN0:	CAMN B,F
	JRST LESSN1
	ILDB C,B
	AOJA A,LESSN0

GREAN:	SKIPGE EVPF
	TLNE FF,EVMF+EVDF
	JRST GREAT0
	IBP (PP)
	MOVEI A,0
GREAN0:	ILDB C,B
	JUMPE C,LESSN1
	AOJA A,GREAN0

LESSN1:	MOVE B,A
	PUSHJ P,DPV
	JRST CLOSE
GREAC:	PUSHJ P,INFIX
	MOVEM B,-5(PP)
GREAC1:	SETZM GREQF'
GREQ1:	SKIPN A
	MOVE A,[(10700)[0]-1]
	SKIPN B
	MOVE B,[(10700)[0]-1]
	SETZM GRBAAF'
	MOVEM A,GRSA
	MOVEM B,GRSA+1
	MOVSI I,-2
GRG9:	SETZB E,GRNZ(I)
	SETZM GRNY(I)
	SETOB G,F
	MOVE D,GRSA(I)
	MOVEI C,"+
GRG0:	MOVEM C,GRSG(I)
GRG1:	SKIPN GRNY(I)
	MOVEM D,GRSP(I)
	ILDB C,D
	JUMPE C,GRG3
	CAIE C,"+
	CAIN C,"-
	AOJE F,GRG0
	CAIE C,"@
	JRST GRG4
	RANDY 10.
	MOVEI C,60(K)
	DPB C,D
GRG4:	CAIE C,"0
	SETOM GRNY(I)
	CAIN C,"0
	JUMPE E,GRG1
	CAIN C,".
	AOJE G,GRG1
	CAIE C,"0
	SETOM GRNZ(I)
	CAIL C,"0
	CAILE C,"9
	SETOM GRBAAF	;NON NUM ARG
	JUMPE G,GRG1
GRG2:	AOJA E,GRG1

GRG3:	MOVEM E,GRSE(I)
	AOBJN I,GRG9
	SKIPE GRBAAF
	JRST CLOS1
	SKIPN GRNZ
	SKIPE GRNZ+1
	JRST GRNBZ
GREQ:	SKIPE GREQF'
	JRST WINT
LOSE:	TLNE FF,EVCF
	PUSHJ P,IGNWR0
	JRST CLOS1

GRNBZ:	MOVE A,GRSG
	CAMN A,GRSG+1
	JRST GRBSS
	CAIE A,"-
	SKIPE GREQF
	JRST LOSE
WINT:	TLNN FF,EVCF
	PUSHJ P,IGNWR0
	JRST CLOS1

GRBSS:	MOVE B,GRSE
	CAMN B,GRSE+1
	JRST GRBSL
	CAIN A,"-
	EXCH B,GRSE+1
	CAML B,GRSE+1
	SKIPE GREQF
	JRST LOSE
	JRST WINT

GRBSL:	SKIPN GRSA
	JRST GRBSL1
	ILDB A,GRSP
	JUMPN A,GRBSL2
	SETZM GRSA
GRBSL1:	MOVEI A,"0
GRBSL2:	CAIL A,"0
	CAILE A,"9
	JRST GRBSL
GRBSL3:	SKIPN GRSA+1
	JRST GRBSL4
	ILDB B,GRSP+1
	JUMPN B,GRBSL5
	SETZM GRSA+1
GRBSL4:	SKIPN GRSA
	JRST GREQ
	MOVEI B,"0
GRBSL5:	CAIL B,"0
	CAILE B,"9
	JRST GRBSL3
	CAMN A,B
	JRST GRBSL
	SKIPE GREQF
	JRST LOSE
	MOVE C,GRSG
	CAIN C,"-
	EXCH A,B
	CAML A,B
	JRST WINT
	JRST LOSE

GRSA:	BLOCK 2
GRSG:	BLOCK 2
GRNZ:	BLOCK 2
GRNY:	BLOCK 2
GRSP:	BLOCK 2
GRSE:	BLOCK 2

;

EQUAL:	TLNE FF,EVSF
	JRST REQAL
	SKIPL A,EVPF
	JRST EQUALB
	PUSHJ P,INFIX
	MOVEM B,-5(PP)
	TLNE FF,EVNF
	JRST EQALN
	SKIPN A
	MOVE A,[(10700)[0]-1]
	SKIPN B
	MOVE B,[(10700)[0]-1]

EQUAL1:	ILDB C,A
	ILDB D,B
	CAMN C,D
	JUMPN C,EQUAL1
	JUMPN D,EQUAL2
	JUMPN C,EQUAL2
	TLNN FF,EVCF	;SAME
	PUSHJ P,IGNWR0
	JRST CLOS1

EQUAL2:	TLNE FF,EVCF	;DIFFERENT
	PUSHJ P,IGNWR0
	JRST CLOS1

EQALN:	SETOM GREQF'
	JRST GREQ1

EQUALB:	SKIPN B,-6(PP)
	JRST SPACE1
	MOVE F,-7(PP)
	CAIL A,2
	JRST LESSN2
EQULB1:	CAMN B,-7(PP)
	JRST CLOSE
	ILDB A,B
	PVAK
	JRST EQULB1
RLESS:	TLZ FF,SERCNF+REPRF
	TLO FF,REPLF+SERSKF+REPCF
	MOVE A,(PP)
	ILDB A,A
	CAIE A,">
	TLZA FF,REPCF
	IBP (PP)
	JRST RANY

RGRET:	TLZ FF,SERCNF+REPLF
	TLO FF,REPRF+SERSKF+REPCF
	MOVE A,(PP)
	ILDB A,A
	CAIE A,"<
	TLZA FF,REPCF
	IBP (PP)
	JRST RANY

REQAL:	TLZ FF,SERCNF+REPLF+REPRF
	TLO FF,REPCF+SERSKF
RANY:	MOVEM VP,SERCHB'
	TLNE FF,REPLF
	JRST RLEFT
	MOVE B,-13(PP)
	JUMPE B,RGLEF2
RGLEF1:	CAMN B,-6(PP)
	JRST RGLEF2
	ILDB A,B
	PVAK
	JRST RGLEF1

RLEFT:	MOVE B,-2(PP)
	JUMPE B,RGLEF7
RLEFT1:	ILDB A,B
	JUMPE A,RGLEF7
	PVAK
	JRST RLEFT1

RGLEF7:	TLNE FF,REPCF
	JRST RGLEF8
RGLEF2:	TLNE FF,REPCF
	JRST RCNTR
	MOVE B,-6(PP)
	MOVEM VP,-6(PP)
	JUMPE B,RGLEF3
RGLEF4:	CAMN B,-7(PP)
	JRST RGLEF3
	ILDB A,B
	PVAK
	JRST RGLEF4

RCNTR:	MOVEM VP,-6(PP)
	MOVE B,-2(PP)
	JUMPE B,RGLEF9
RCNTR1:	ILDB A,B
	JUMPE A,RGLEF9
	PVAK
	JRST RCNTR1

RGLEF9:	TLNE FF,REPRF
	JRST RGLE10
	JRST RGLE11

RGLEF8:	MOVEM VP,-6(PP)
RGLEF3:	TLNE FF,REPRF
	SKIPA B,-2(PP)
RGLE11:	MOVE B,-7(PP)
	MOVEM VP,-7(PP)
	JUMPE B,RGLEF6
RGLEF5:	ILDB A,B
	JUMPE A,RGLEF6
	PVAK
	JRST RGLEF5

RGLE10:	MOVEM VP,-7(PP)
RGLEF6:	MOVEI A,0
	PVAK
	HRLI VP,10700
	JRST SERRL9

;

NATVAL:	SETOB D,B
	ILDB C,A
	CAIN C,"-
	TLOA FF,TINGF
	TLZA FF,TINGF
NATVL1:	ILDB C,A
	JUMPE C,CPOPJ
	CAIN C,".
	AOJA D,NATVL1
	CAIL C,"0
	CAILE C,"9
	JRST NATVL9
	JUMPGE D,NATVL1
	SKIPGE B
	MOVEI B,0
	IMULI B,10.
	ADDI B,-60(C)
	JRST NATVL1

NATVL9:	MOVNI B,1
	POPJ P,

UPARRN:	MOVEM E,EVPF

UPARR:	CAIG V,1
	ERR [ASCII /ATL/]
	SKIPL C,EVPF
	CAMLE C,-10(PP)
	MOVE C,-10(PP)
	JUMPE C,QUPARR
	SOJE C,UPARR0

UPARE:	MOVE A,CLOSX	;EVAL THAT ARG
	MOVEM A,-11(PP)
	MOVE A,SPACE
	MOVEM A,-3(PP)
	HLRZ D,-15(PP)
	MOVE E,-15(PP)
	ADD E,D
	IMUL D,[(PPBL)PPBL
	SUB PP,D
	PUSH P,-10(PP)
	CAMGE C,-10(PP)
	MOVEM C,-10(PP)
	PUSH P,-15(PP)
	PUSH P,1-PPBL(PP)
	HRRM E,-15(PP)
	HLLM E,1-PPBL(PP)
	MOVE A,(PP)
	ILDB A,A
	CAIN A,40
	IBP (PP)
	PUSH P,-3(PP)
	MOVEI A,ARGRET
	HRRM A,SPACE
	PUSH P,-12(PP)
	MOVEM VP,-12(PP)
	PUSH P,-2(PP)
	PUSH P,-11(PP)
	SOJA V,SPACE2

ARGRET:	MOVE A,-2(PP)
	POP P,-11(PP)
	POP P,-2(PP)
	POP P,-12(PP)
	POP P,-3(PP)
	HLRZ D,1-PPBL(PP)
	POP P,1-PPBL(PP)
	POP P,-15(PP)
	POP P,-10(PP)
	IMUL D,[(PPBL)PPBL
	ADD PP,D
	MOVEM A,-5(PP)
	MOVE D,-3(PP)
	HRRM D,SPACE
	MOVE D,-11(PP)
	HRRM D,CLOSX
	AOJA V,(D)

UPARR0:	MOVEI C,PVA
UPARR4:	HLRZ D,-15(PP)
	IMULI D,PPBL
	SUBM PP,D
	ILDB A,(D)
	CAIN A,40
UPARR1:	ILDB A,(D)
UPARR3:	JUMPE A,CLOSEU
	CAIN A,""
	JRST UPARRQ
	CAIN A,"[
	JRST UPARRS
	CAIN A,40
	JRST CLOSE
UPARR2:	PUSHJ P,(C)
	JRST UPARR1

CLOSEU:	MOVE A,(D)
	DBP7 A
	MOVEM A,(D)
	JRST CLOSE

UPARRQ:	PUSHJ P,(C)
	ILDB A,(D)
	JUMPE A,[ERR [ASCII /UCQ/]]
	CAIE A,40
	JRST UPARRQ
	JRST UPARR2

UPARRS:	PUSHJ P,(C)
	ILDB A,(D)
	CAIE A,"[
	JRST UPARR3
	PUSHJ P,(C)
	EXCH B,(D)
	EXCH B,(PP)
	PUSHJ P,SUPER
	  PUSHJ P,(C)
	EXCH B,(PP)
	EXCH B,(D)
	JRST UPARR1

QUPARR:	MOVEI A,"^
	PVAK
	MOVEI C,CPOPJ
	JRST UPARR4

MINUS:	PUSHJ P,INFIX
	TLNE FF,EVCF+EVMF
	EXCH A,B
	MOVEM A,-2(PP)
	JUMPE B,PRIME0
	PUSH P,A
	PUSHJ P,NEGAT
	POP P,A
	JUMPE A,PRIME1
	MOVEM VP,-5(PP)
	JRST PLUSM

PLUS:	PUSHJ P,INFIX
	JUMPE A,PRIME1
	JUMPE B,PRIME0
PLUSM:	MOVEM A,PLUSA'
	MOVEM B,PLUSB'
	MOVEM VP,PLUSVP'
	MOVE D,PLUSA
PLUS1:	MOVE B,D
	SETZM PLUSMA'
PLUS2:	ILDB E,D
	JUMPE E,PLUS17
	CAIE E,"+
	CAIN E,"-
	JRST PLUS1
	SKIPL PLUSMA
	AOS PLUSMA
	CAIN E,"@
	JRST PLUS2
	SETOM PLUSMA
	CAIN E,".
	JRST PLUS2
	CAIL E,"0
	CAILE E,"9
	JRST PLUS3
	JRST PLUS2

PLUS3:	EXCH D,(PP)
	PUSHJ P,IGNSYM
	EXCH D,(PP)
	JRST PLUS1

PLUS17:	DBP7 D
	SKIPLE PLUSMA
	MOVE B,D
	CAMN B,PLUSA
	JRST PLUS10
	MOVE D,B
	ILDB E,D
	JUMPE E,PLUS10
	DBP7 B
	JRST PLUS10

PLUS11:	ILDB A,PLUSA
	PVAK
PLUS10:	CAME B,PLUSA
	JRST PLUS11
	ILDB A,PLUSA	;GET OP. AT LEFT OF NUMBER
	JUMPE A,PLUS18
	CAIN A,"@
	JRST PLS18A
	CAIE A,".
	CAIL A,"0
	CAILE A,"9
	JRST PLUS18
PLS18A:	MOVEI A,"+
PLUS18:	MOVEM B,PLUSA
PLUS19:	MOVEM A,AADDS1'
	SETZM PLUSPF'
	MOVE D,PLUSB

PLUS12:	MOVE B,D
	SETZM PLUSMA
PLUS13:	ILDB E,D
	JUMPE E,PLUS20
	CAIE E,"+
	CAIN E,"-
	JRST PLUS12
	SKIPL PLUSMA
	AOS PLUSMA
	CAIN E,"@
	JRST PLUS13
	SETOM PLUSMA
	CAIN E,".
	JRST PLUS13
	CAIL E,"0
	CAILE E,"9
	JRST PLUS14
	JRST PLUS13

PLUS14:	DBP7 D
	MOVE B,D
	EXCH D,(PP)
	PUSHJ P,IGNSYM
	CAME B,(PP)	; = IF OPERATOR
	JRST PLUS15
	IBP (PP)
	PUSHJ P,IGNSYM
	SETOM PLUSPF
PLUS15:	EXCH D,(PP)
	JRST PLUS12

PLUS20:	DBP7 D
	SKIPLE PLUSMA
	MOVE B,D
	CAMN B,PLUSB
	JRST PLUS25
	MOVE D,B
	ILDB E,D
	JUMPE E,PLUS25
	DBP7 B
PLUS25:	CAMN VP,PLUSVP
	SETZM PLUSPF
	SKIPE D,PLUSPF
	JRST PLUS30
	CAMN B,PLUSB
	JRST PLUS28
	ILDB A,PLUSB
	JUMPE A,EV0
	CAIN A,"+
	JRST PLUS26
	CAIN A,"-
	JRST PLUS21
	MOVE A,PLUSB
	DBP7 A
	MOVEM A,PLUSB
	MOVEI A,"+
PLUS26:	CAMN VP,PLUSVP
	JRST PLUS23
	JRST PLUS21

PLUS22:	ILDB A,PLUSB
PLUS21:	PVAK
PLUS23:	CAME B,PLUSB
	JRST PLUS22
PLUS28:	ILDB A,B
	JUMPE A,PLUS29
	CAIE A,"+
	CAIN A,"-
	JRST PLUS29
	MOVEI A,"+
PLUS29:	MOVEM A,AADDS2'
	JUMPE D,PLUS24
	MOVEI A,")
	PVAK

PLUS24:	MOVE A,AADDS1
	MOVE B,PLUSB
	JUMPE A,PLUS31
	MOVE A,AADDS2
	MOVE B,PLUSA
	JUMPE A,PLUS31
	MOVEI C,"+
	MOVEM C,BADS'
	MOVE C,[ADDI D,-60(E)]
	CAME A,AADDS1
	MOVE C,[SUBI D,-60(E)]
	MOVEM C,BADD5
	MOVE C,PLUSB
	CAIE A,"-
	JRST PLUS32
	CAMN A,AADDS1
	MOVEM A,BADS
	JRST BADD
PLUS32:	EXCH B,C

BADD:	MOVEM B,BADDB'
	MOVEM C,BADDC'
	SETZM BADDBE'
	ILDB D,B
	CAIE D,"+
	CAIN D,"-
	IBP BADDB
	ILDB E,C
	CAIE E,"+
	CAIN E,"-
	IBP BADDC
	SETZM BABR'
	MOVE B,BADDB
	MOVE C,BADDC
	MOVNI I,1
	SETZM B6RZ'
	SETZM B6RA'
BADD0:	MOVEM J,BADDL'
	MOVEM K,BADDM'
	MOVEM F,G
	SETZB J,B6RZ'
	SETZB F,H
	SETZM K
BADD1:	ILDB D,B(I)+1
	JUMPE D,BADD2
	SKIPG H
	AOSA J
	AOS K
	CAIN D,".
	AOJA H,BADD1
	JUMPLE H,BADD1
	AOJA F,BADD1

BADD2:	AOJE I,BADD0
	SUBM G,F	; +F = FILL ARG 2; -F = FILL ARG 1
	CAMGE J,BADDL
	MOVE J,BADDL
	CAMGE K,BADDM
	MOVE K,BADDM
BAD4:	ADDI J,7(K)
	IDIVI J,5	;CLOBBERS K
	MOVE A,J
	ADDI A,(VP)
	HRLI A,10700
	MOVEI J,@SP
	CAIG J,1(A)
	ERR [ASCII /SCE/]
	SETZB G,(A)
	TLZ FF,REPLF

BADD3:	JUMPL F,BADD3A
	DBP7 B
	CAMN B,BADDB
	SOJA F,BADD3Z
	LDB D,B

BADD4:	CAIN D,".
BADD15:	AOJA G,BADD6
BADD17:	JUMPG F,BADD3B
	DBP7 C
	CAMN C,BADDC
	AOJA F,BADD3Y
	LDB E,C
BADD14:	CAIN E,".
	JRST BADD16
	CAIN E,"@
	JRST BD5A
BD5C:	CAIN D,"@
	JRST BD5B
BADD5:	ADDI D,-60(E)	;OR SUBI
	ADD D,BABR
	CAIGE D,"0
	JRST BADD5A
	CAILE D,"9
	JRST BADD5B
BADD6A:	SETZM BABR'
BADD6:	TLZE FF,REPLF
	MOVEI D,"@
	PUSHJ P,BAD6
	JRST BADD3
BAD6:	DBP7 A
	DPB D,A
	SKIPE B6RZ'
	POPJ P,
	MOVEM A,B6RZP'
	CAIN D,"@
	SETOM B6RA'
	CAIE D,"@
	CAIN D,".
	POPJ P,
	CAIE D,"0
	SETOM B6RZ
	POPJ P,

BADD7:	SKIPN C,BABR
	JRST BADD8
	SETOM B6RZ'
	MOVEI D,"1
	SKIPG C
	MOVEI D,"9
	DBP7 A
	DPB D,A

BADD8:	SETZM B10Z'
	SETZM B10A'
	SETZM B10P'
	SETZM B10RC'
	DBP7 A
	MOVE D,A
	JUMPL C,BADD9
	MOVE A,BADS
	CAIN A,"-
	JRST BADD8A
	CAMN VP,PLUSVP'
	JRST BADD10
BADD8A:	MOVE B,B6RZ
	IOR B,B6RA
	JUMPE B,PCLOSE	;EXIT: NO NUMERIC PART
	PVAK

BADD10:
B10A0:	CAMN D,B6RZP
	SETZM B10RC
	ILDB A,D
	JUMPE A,BADD11
	CAIE A,".
	JRST B10A1
	SKIPE B10A
	JRST BADD11
	SETOM B10P
	SKIPE B10Z
	JRST B10A1
	MOVE B,D
	ILDB B,B
	CAIE B,"@
	JRST B10A1
	MOVEI A,"0
	PVAK
	MOVEI A,".

B10A1:	CAIE A,"@
	JRST B10A2
	SKIPN B10Z
	JRST B10A4
	SKIPE B10P
	JRST B10B
	SETOM B10A
	JRST B10A2

B10A2:	SKIPE B10A
	MOVEI A,"@
	SKIPN B10RC
	JRST B10A5
	CAIE A,"@
	CAIN A,".
	JRST B10A5
	MOVNS A
	ADDI A,"9+"0
	CAMN D,B6RZP
	ADDI A,1

B10A5:	SKIPN B10Z
	CAIE A,"0
	JRST B10A3
	JRST B10A0

B10A4:	PVAK
	ILDB A,D
	CAIN A,"@
	JRST B10A4
	MOVEI A,".
	PVAK
	JRST PCLOSE	;EXIT: TOTALLY IMPRECISE

B10A3:	SETOM B10Z
	PVAK
	JRST B10A0

B10B:	PVAK
BADD11:	MOVEI A,"0
	SKIPN B10Z
	PUSHJ P,PVA
	JRST PCLOSE	;EXIT

BADD9:	MOVEI A,"-
	PVAK
BADD20:	SETOM B10RC
	JRST BADD10

BADD3Z:	IBP B
	SETOM BADDBE'
BADD3A:	MOVE D,FILL
	AOJA F,BADD4

BADD3Y:	IBP C
	SKIPE BADDBE
	JRST BADD7
BADD3B:	MOVE E,FILL
	SOJA F,BADD14

BADD5A:	SETOM BABR
	ADDI D,12
	JRST BADD6

BADD5B:	SETZM BABR
	AOS BABR
	SUBI D,12
	JRST BADD6

FILL:	"0

PLUS30:	MOVEI A,"+
	PVAK
	MOVEI A,"(
	PVAK
	JRST PLUS23

PLUS31:	ILDB A,B
	JUMPE A,PCLOSE	;EXIT
	CAIE A,"+
	CAIN A,"-
	JRST PLUS33
	MOVE C,A
	SKIPA A,["+
PLUS33:	ILDB C,B
	JUMPE C,PCLOSE	;EXIT
	CAIN C,"0
	JRST PLUS33
	PVAK
	MOVE A,C
	PVAK
	JRST PRIME1	;EXIT

BADD16:	JUMPN G,BADD17
	PUSH P,D
	MOVEI D,".
	PUSHJ P,BAD6
	POP P,D
	AOJA G,BADD17

BD5A:	RANDY 10.
	MOVEI E,60(K)
	TLO FF,REPLF
	JRST BD5C

BD5B:	RANDY 10.
	MOVEI D,60(K)
	TLO FF,REPLF
	JRST BADD5
NEGAT:	MOVEM B,NEGATB'
	EXCH B,(PP)
	MOVEM VP,NEGATV'
	MOVE D,(PP)
NEGAT2:	PUSHJ P,IGNSYM
NEGAT3:	ILDB A,(PP)
	JUMPE A,NEGAT4
	CAIN A,"*
	JRST NEGAT1
	CAIE A,"+
	CAIN A,"-
	JRST NEGAT2
NEGATA:	PUSHJ P,IGNSYM
	MOVE D,(PP)
	JRST NEGAT3

NEGAT4:	EXCH B,(PP)
	CAMN D,NEGATB
	JRST NEGAT5
	MOVEI A,"-
	PVAK
	MOVEI A,"(
NEGAT6:	PVAK
	CAMN D,NEGATB
	JRST NEGAT7
	ILDB A,NEGATB
	JRST NEGAT6

NEGAT7:	MOVEI A,")
	PVAK
	JRST NEGAT8

NEGAT5:	ILDB B,D
	JUMPE B,NEGAT9
	CAIE B,"+
	CAIN B,"-
	JRST NEGAT8
	MOVEI A,"-
	JRST NEGAT0
NEGAT8:	MOVEI A,"++"-
	ILDB B,NEGATB
	JUMPE B,NEGAT9
	SUB A,B
NEGAT0:	PVAK
	PUSHJ P,QUOSYM
	JRST NEGAT8

NEGAT9:	MOVE B,NEGATV
	MOVEI A,0
	PVAK
	HRLI VP,10700
	POPJ P,

NEGAT1:	MOVE A,(PP)
	ILDB A,A
	CAIN A,"+
	IBP (PP)
	CAIN A,"-
	IBP (PP)
	JRST NEGATA

TIMES:	TLZ FF,TINGF
	MOVE E,(PP)
	ILDB A,E
	CAIE A,"+
	CAIN A,"-
	JRST TIMES2
	JRST TIMES1

TIMES2:	MOVEM E,(PP)
	CAIN A,"-
	TLO FF,TINGF

TIMES1:	PUSHJ P,INFIX
	TLNE FF,EVCF+EVMF
	EXCH A,B
	MOVEM A,-2(PP)
	JUMPE B,PRIME0
	JUMPE A,PRIME1
	TLNN FF,TINGF
	JRST TIMES3
	PUSH P,A
	PUSHJ P,NEGAT
	POP P,A
	MOVEM VP,-5(PP)
TIMES3:	MOVEM A,TIMESA'
	MOVEM B,TIMESB'
	MOVEM VP,TIMSVP'
	MOVEM VP,TIMSP1'
	MOVE D,TIMESA

TIMS1:	MOVE B,D
	SETZM TIMSMA'
	ILDB E,D
	JUMPE E,TIMS17
	CAIE E,"+
	CAIN E,"-
	JRST TIMS2
	JRST TIMS2A

TIMS2:	ILDB E,D
	JUMPE E,TIMS17
TIMS2A:	CAIN E,"*
	JRST TIMS1
	SKIPL TIMSMA
	AOS TIMSMA
	CAIN E,"@
	JRST TIMS2
	SETOM TIMSMA
	CAIN E,".
	JRST TIMS2
	CAIL E,"0
	CAILE E,"9
	JRST TIMS3
	JRST TIMS2

TIMS3:	EXCH D,(PP)
	PUSHJ P,IGNSYM
	EXCH D,(PP)
TIMS1A:	MOVE B,D
	SETZM TIMSMA'
	JRST TIMS2

TIMS17:	DBP7 D
	SKIPLE TIMSMA
	MOVE B,D
	JRST TIMS10

TIMS11:	ILDB A,TIMESA
	PVAK	;THROUGH *
TIMS10:	CAME B,TIMESA
	JRST TIMS11
	CAIN A,"*
	MOVEM VP,TIMSVP
	CAIE A,"*
	PUSHJ P,TQSTR
	SETZM TIMSPF'
	MOVE D,TIMESB
	SETZM TIMSMA

TIMS12:	MOVE B,D
	ILDB E,D
	JUMPE E,TIMS20
	CAIE E,"+
	CAIN E,"-
	JRST TIMS13
	JRST TIM13A

TIMS13:	ILDB E,D
	JUMPE E,TIMS20
TIM13A:	CAIN E,"*
	JRST TIMS12
	SKIPL TIMSMA
	AOS TIMSMA
	CAIN E,"@
	JRST TIMS13
	SETOM PLUSMA
	CAIN E,".
	JRST TIMS13
	CAIL E,"0
	CAILE E,"9
	JRST TIMS14
	JRST TIMS13

TIMS14:	DBP7 D
	MOVE B,D
	EXCH D,(PP)
	PUSHJ P,IGNSYM
	CAME B,(PP)
	JRST TIMS15
	IBP (PP)
	PUSHJ P,IGNSYM
	SETOM TIMSPF

TIMS15:	EXCH D,(PP)
TIM12A:	MOVE B,D
	SETZM TIMSMA
	JRST TIMS13

TIMS20:	DBP7 D
	SKIPLE PLUSMA
	MOVE B,D
	MOVEM B,TIMSB1'
	CAMN VP,TIMSP1
	SETZM TIMSPF
	SKIPE D,TIMSPF
	JRST TIMS30	;() FORM
	CAMN B,TIMESB
	JRST TIMS24
	LDB A,B
	CAIE A,"*
	JRST TIMS23
	DBP7 B
	JRST TIMS23
TIMS22:	ILDB A,TIMESB
TIMS21:	PVAK
TIMS23:	CAME B,TIMESB
	JRST TIMS22
TIMS28:	JUMPE D,TIMS24
	MOVEI A,")
	LDB B,VP
	CAIN B,"*
	DPB A,VP
	CAIE B,"*
	PVAK

TIMS24:
TIM24X:	PUSHJ P,TQSTR
TIM24A:	MOVEM VP,TIMEVP'
	MOVE B,TIMESA
	MOVE C,TIMSB1
BMUL:	MOVE A,-16(PP)
	MOVEM A,BMULSG'
	SETOM BMULBP'
	SETOM BMULEP'
	SETZM BMULES'
	SETZM BMULVA'
	SETZM BMULVB'
	SETZM BMULAA'
	SETZM BMULAB'
	SETZM BMULPA'
	MOVEM B,BMULB'
	MOVEM C,BMULC'
	ILDB A,B
	CAIN A,"-
	SETCMM BMULES
	CAIE A,"-
	CAIN A,"+
	IBP BMULB
	MOVE B,BMULB
	ILDB E,B
	JUMPN E,BMULV1
	MOVE B,[(010700)[ASCII /1/]-1]
	MOVEM B,BMULB
BMULV1:	CAIN E,"0
	JRST BMULV2
	CAIN E,".
	JRST BMULV5
	CAIN E,"@
	JRST BMULW1
BMULV9:	ILDB E,B
	JUMPE E,BMULW3
	CAIN E,".
	JRST BMULW3
	AOS BMULAA
	JRST BMULV9

BMULW3:	ILDB A,C
	CAIN A,"-
	SETCMM BMULES
	CAIE A,"-
	CAIN A,"+
	IBP BMULC
	MOVE D,BMULC
	ILDB A,D
	JUMPN A,BMULV3
	MOVE D,[(010700)[ASCII /1/]-1]
	MOVEM D,BMULC

BMULV3:	CAIN A,"0
	JRST BMULV4
	CAIN A,".
	JRST BMULV6
	CAIE A,"@
	SKIPE BMULPA'
	JRST BMULW5
	MOVE B,BMULB'
	MOVE C,BMULC'
	MOVEI I,5
	MOVSI J,400000
	HRLI VP,10700
	SETZM BMULLB'
	SETZM BMULLC'

BMUL1:	MOVEM B,BMULX'
	MOVEM B,BMULX1'
	AOS I
	ILDB D,B
	CAIN D,"0
	JRST BMUL11
	CAIE D,".
	SETOM BMULLB'
BMUL11:	SKIPN BMULLB
	MOVEM B,BMULB
	SKIPL BMULEP
	AOS BMULEP
	CAIN D,".
	SETZM BMULEP
	CAIE D,".
	SKIPN BMULLB
	SKIPA
	ADDI J,1
	CAIN D,"@
	TLZA J,400000
	JUMPN D,BMUL1
	SOJL J,BMUL2Z
	SKIPL BMULSG
	CAMGE J,BMULSG
	MOVEM J,BMULSG
	MOVEI D,0
	SKIPL BMULEP
	DPB D,B

BMUL2Z:	MOVSI J,400000

BMUL2:	MOVEM C,BMULY'
	MOVEM C,BMULY1'
	ILDB D,C
	CAIN D,"0
	JRST BMUL12
	CAIE D,".
	SETOM BMULLC'
BMUL12:	SKIPN BMULLC
	MOVEM C,BMULC
	SKIPL BMULBP
	AOS BMULEP
	CAIE D,".
	JRST BMUL13
	SOSGE BMULEP
	SETZM BMULEP
	SETZM BMULBP
	JRST BMUL2

BMUL13:	SKIPE BMULLC
	ADDI J,1
BMUL2A:	CAIN D,"@
	TLZA J,400000
	JUMPN D,BMUL2
	MOVEM VP,BMULS'
	MOVEI A,"0
	PVAK
	MOVEM VP,BMULM'
	HRLI VP,10700
	SOJL J,BMUL3
	SKIPL BMULSG
	CAMGE J,BMULSG
	MOVEM J,BMULSG
	MOVEI D,0
	SKIPL BMULBP
	DPB D,C

BMUL3:	IDIVI I,5
	MOVEI K,0
	MOVSI J,10700
BMUL10:	ADD VP,I
	ADDI VP,(J)
	MOVEI E,@SP
	CAIG E,1(VP)
	ERR [ASCII /SCE/]
	MOVEM VP,BMULQ'
BMUL4:	MOVE E,BMULQ
	MOVE B,BMULY
BMUL4A:	CAMN B,BMULC
	JRST BMUL20
	LDB A,B
	DBP7 B
	MOVEM B,BMULY
	SUBI A,60
	JUMPL A,BMUL4A	;PERIOD
	MOVEI C,0
	MOVE F,BMULX
	MOVEI D,0
	MOVE B,K
BMUL5:	JUMPE B,BMUL6
	DPB D,E
	DBP7 E
	SOJA B,BMUL5

BMUL6:	CAMN F,BMULB
	JRST BMUL9
	LDB D,F
	DBP7 F
	SUBI D,60
	JUMPL D,BMUL6	;PERIOD
	IMUL D,A
	ADD C,D
BMUL7:	IDIVI C,10.
	DPB D,E
	DBP7 E
	JRST BMUL6

BMUL9:	JUMPN C,BMUL7
	ADD VP,I
	ADDI VP,(J)
	MOVEI C,@SP
	CAIG C,1(VP)
	ERR [ASCII /SCE/]
	MOVEM VP,BMULJ'
	JRST BMAD

BMULV5:	SETOM BMULVA
	SETOM BMULAA'
	JRST BMULV7

BMULV2:	SKIPN BMULVA
	MOVEM B,BMULB
	SKIPGE BMULAA
	SOS BMULAA
BMULV7:	ILDB E,B
	JUMPN E,BMULV1
BMULZ:	MOVE VP,TIMSP1
	MOVEM VP,-5(PP)
	MOVEI A,"0
	PVAK
	JRST PCLOSE

BMULV6:	SETOM BMULVB
	SETOM BMULAB'
BMULV4:	SKIPN BMULVB
	MOVEM D,BMULC
	SKIPGE BMULAB
	SOS BMULAB
	ILDB A,D
	JUMPN A,BMULV3
	JRST BMULZ

TIMS30:	MOVEI A,"(
	PVAK
	JRST TIMS23

TQSTR:	CAMN VP,TIMSVP
	POPJ P,
	MOVEI A,"*
	PVAK
	MOVEM VP,TIMSVP
	POPJ P,

BMULW0:	AOS BMULAA'
BMULW1:	SKIPGE BMULAA
	JRST BMULW2
	ILDB E,B
	JUMPE E,BMULW2
	CAIE E,".
	JRST BMULW0

BMULW2:	SETOM BMULPA'
	JRST BMULW3

BMULW4:	AOS BMULAB'
BMULW5:	SKIPGE BMULAB
	JRST BMULW6
	ILDB A,D
	JUMPE A,BMULW6
	CAIE A,".
	JRST BMULW4

BMULW6:	MOVEI A,"-
	SKIPE BMULES
	PUSHJ P,PVA
	SKIPGE B,BMULAA
	ADDI B,1
	ADD B,BMULAB
	SKIPGE BMULAB
	ADDI B,1
	AOJL B,BMULW7
	MOVEI A,"@

BMULW8:	PVAK
	SOJGE B,BMULW8
	MOVEI A,".
	PVAK
	JRST PCLOSE

BMULW7:	AOJL B,BMULT
	MOVEI A,"0
	PVAK
	MOVEI A,".
	PVAK
BMULT2:	MOVEI A,"@
	PVAK
	JRST PCLOSE

BMULT:	MOVEI A,".
	PVAK
	MOVEI A,"0
BMULT1:	PVAK
	AOJL B,BMULT1
	JRST BMULT2

BMAD:	SETZM BMULCE'
	SETZM BMADR'
	MOVE C,BMULQ
	MOVE D,BMULM

BMAD0:	CAMN C,E
	SETOM BMULCE
	MOVEI A,"0
	SKIPN BMULCE
	LDB A,C
	DBP7 C
	CAMN D,BMULS
	JRST BMAD1
	LDB B,D
	DBP7 D
BMAD3:	ADD A,B
	ADD A,BMADR
	SETZM BMADR
	CAILE A,"9
	JRST BMAD2
BMAD4:	DPB A,VP
	DBP7 VP
	JRST BMAD0

BMAD2:	SUBI A,10.
	AOS BMADR
	JRST BMAD4

BMAD1:	MOVEI B,"0
	SKIPN BMULCE
	JRST BMAD3
	SKIPN BMADR
	JRST BMAD5
	MOVEI A,"1
	DPB A,VP
	DBP7 VP
BMAD5:	SETZM BMADEL'
	MOVE E,BMULS
BMAD6:	CAMN VP,BMULJ
	JRST BMAD7
	ILDB A,VP
	IDPB A,E
	AOS BMADEL
	JRST BMAD6

BMAD7:	MOVEM E,BMULM
	MOVE VP,E
	HRLI VP,10700
	IBP J
	AOJA K,BMUL10

BMUL20:	SKIPN BMULES
	SKIPL BMULEP
	JRST BMUL21
BMUL32:	MOVE VP,TIMEVP
	MOVE J,BMULSG'
	MOVE B,BMULS
BMUL31:	SETZM BMULMF'
	SETZM BMUPF'
	SETZM BMULZF'
	SETZM BMULYF'
	JUMPE J,BMS
	ILDB A,B
	CAIE A,"1
	JRST BMUL34
	CAMN B,BMULM
	JRST BMUL35
	PVAK
	SETOM BMULZF
	SOJE J,BMS
	ILDB A,B
	SETOM BMULMF
	SETOM BMULYF
	CAIN A,".
	SKIPE BMULES'
	SETZM BMULMF
BMUL34:	CAIN A,".
	SETOM BMUPF'
	SKIPE BMULZF
	JRST BMU34A	;WHY GRONK 1.000?
	CAIN A,"0
	JRST BMU34B
	SETOM BMULZF

BMU34A:	CAIN A,"0
	JRST BMU34C
	CAIE A,".
	SETOM BMULYF
BMU34C:	PVAK
	SKIPN BMULYF
	JRST BMU34B
	CAILE A,".
	SOJE J,BMS

BMU34B:	CAMN B,BMULM
	JRST BMUL36
	ILDB A,B
	CAIE A,"0
	SETZM BMULMF
	JRST BMUL34

BMUL36:	SKIPN BMULMF'
	JRST PCLOSE
	MOVEI A,"1
BMUL35:	MOVE VP,TIMEVP
	LDB C,VP
	CAIE C,"*
	JRST BMUL33
	DBP7 VP
BMUL33:	CAMN VP,TIMSP1
	PVAK
	JRST PCLOSE

BMS:	SKIPE BMUPF
	JRST BMS0
	MOVEM VP,BMSVP'
BMS1:	CAME B,BMULM
	JRST BMS10
	ILDB A,B
	CAIN A,".
	JRST BMS2
	MOVEI A,"@
	PVAK
	JRST BMS1

BMS0:	MOVEI A,"@
BMS01:	PVAK
BMS10:	ILDB A,BMULX1'
	CAIN A,"@
	JRST BMS01
BMS11:	ILDB A,BMULY1'
	CAIE A,"@
	JRST BMS2
	PVAK
	JRST BMS11

BMS2:	CAME VP,BMSVP
	JRST PCLOSE
	MOVEI A,".
	PVAK
	MOVEI A,"@
	PVAK
	JRST PCLOSE

BMUL21:	MOVE D,BMULX1'
	MOVE A,BMULEP
BM21B1:	SOJLE A,BM21A
	ILDB C,D
	CAIN C,"@
	JRST BM21B1

BM21B:	DBP7 D
	MOVEM D,BMULX1
	MOVE D,BMULY1'
BM21B2:	ILDB C,D
	CAIE C,"@
	JRST BM21B3
	SOJLE A,BM21A1
	JRST BM21B2

BM21B3:	DBP7 D
BM21A1:	MOVEM D,BMULY1
	JRST BM21B4

BM21A:	MOVEM D,BMULX1
BM21B4:	MOVEM A,BMULEP
BM21B5:	MOVE D,BMULS
	MOVE E,BMULM
	HRLI E,10700
	SKIPN BMULES
	JRST BMUL22
	MOVEI A,"-
	IDPB A,E

BMUL22:	SKIPG A,BMULEP
	JRST BMUL23
	CAMG A,BMADEL
	JRST BMUL24
	MOVEI F,".
	IDPB F,E
	MOVEI F,"0
BMUL25:	IDPB F,E
	SOS A
	CAME A,BMADEL
	JRST BMUL25
	SETOM BMULEP
	JRST BMUL26

BMUL24:	CAME A,BMADEL
	JRST BMUL26
	MOVEI F,".
	IDPB F,E
	SETOM BMULEP

BMUL26:	AOS BMULEP
BMUL23:	CAMN D,BMULM
	JRST BMUL29
	ILDB F,D
	IDPB F,E
	JRST BMUL22

BMUL29:	EXCH E,BMULM
	HRLI E,10700
	MOVEM E,BMULS
	JRST BMUL32

;

SLASH:	TLZ FF,TINGF+SYPF
	MOVE E,(PP)
	ILDB A,E
	CAIE A,"+
	CAIN A,"-
	JRST SLASH2
	JRST SLASH1

SLASH2:	MOVEM E,(PP)
	CAIN A,"-
	TLO FF,TINGF

SLASH1:	PUSHJ P,INFIX
	TLNE FF,EVCF+EVMF
	EXCH A,B
	MOVEM A,-2(PP)
	SETZM NDIVQZ'
	SETZM NDIVQS'
	SETZM NDIVES'
	TLNN FF,TINGF
	JRST SLASH3
	PUSH P,A
	PUSHJ P,NEGAT
	POP P,A
	MOVEM VP,-5(PP)
SLASH3:	MOVEM A,SLASHA'	;DIVIDEND
	MOVEM B,SLASHB'	;DIVISOR
	PUSHJ P,SLSHT
	EXCH A,B
	PUSHJ P,SLSHT
	EXCH A,B
	CAIL B,2
	SOJA B,[SOJA B,PRIME0]	;DIVISOR NULL
	JUMPGE B,SLSHI
	CAIL A,2
	JRST SLSR	;RECIPROCAL
	JUMPGE A,SLSHI
	JRST SLDP
SLSR:	MOVE A,[(10700)[ASCII /1/]-1]
	MOVEM A,SLASHA
	TDCA I,I
SLDP:	MOVEI I,1
	SETZB H,SLASBH'
	MOVE A,-16(PP)
	MOVEM A,NDIVSD'
	MOVE A,SLASHB
	MOVE G,A
	PUSHJ P,SLSHS
	MOVEM G,NDIVR'
	MOVEM F,SLSHSM'
	MOVEM E,SLSHSN'
	MOVEM C,SLSHSQ'
	TLNN FF,TINGF
	JRST SLSHZ
SLDP2:	MOVEM VP,NDIVD'
	MOVEI H,1
	MOVE A,SLASHA
	PUSHJ P,SLSHS
	SKIPN NDIVSD
	JRST SLDP4
	SUB F,SLSHSM
	HRREI G,2(F)
	SUB E,SLSHSN
	SKIPN I
	SUB E,SLSHSM
	SKIPGE NDIVSD
	JRST SLDP3
	SUB F,NDIVSD
	SKIPGE F
	ADD E,F
SLDP3:	JUMPGE E,SLDP1
	MOVEI A,".
	SKIPE SLSHSD'
	JRST SLDP12
	MOVEM VP,SLSHSD'
	PVAK
SLDP12:	MOVE A,FILL
SLDP10:	PVAK
	AOJL E,SLDP10
SLDP1:	MOVEI A,0
	PVAK
	HRLI VP,10700
	MOVEM VP,-5(PP)
	SETZM NDIVSP'
	MOVEI A,"-
	SKIPE NDIVES
	PUSHJ P,PVA
	JUMPG G,SLDP9
	MOVEI A,".
	PVAK
	SETOM NDIVSP
	SETOM NDIVQS'
	MOVEI A,"0
SLDP11:	PVAK
	AOJLE G,SLDP11
	SOJA G,SLDP9

SLDP9:	SETZM NDIVSL'
	SETZM NASBH2'
	AOS NDIVSD'

NDIV1:	SETZM NDIV1M'
NDIV1P:	TLNE FF,EVNF
	CAILE G,1
	SKIPA A,NDIVR'
	JRST NDIV3
	SETZB E,NDIVC'
	MOVE B,NDIVD'
NDIV1A:	ILDB C,A	;DIVLP ENTEST
	JUMPE C,NDIV2X
	CAIN C,".
	JRST NDIV1A
NDIV1B:	ILDB D,B
	CAIN D,".
	JRST NDIV1B
	JUMPN D,NDIV1A

NDIV3:	SKIPE NDIVQZ
	JRST NDIV3A
	MOVE VP,-5(PP)
	MOVEI A,"0
	PVAK
	SOSN NDIVSD'
	MOVEM VP,NDIVSL
NDIV3A:	SKIPLE NDIVSD
	JRST NDIV33
	SKIPN NDIVSL
	JRST NDIV30
	EXCH VP,NDIVSL
NDIV31:	MOVEI A,"@
	DPB A,VP
	CAME VP,NDIVSL
	SKIPE NDIVSP
	JRST NDIV30
	ILDB A,VP
	CAIE A,".
	JRST NDIV31
	DBP7 VP
NDIV30:	MOVE C,NDIV1M
	MOVE A,NDIVD
	LDB B,A
	CAIE B,".
	JRST NDIV3B
	DBP7 A
NDIV3B:	ADDI C,60
	DPB C,A
	DBP7 A

NDIV3C:	SKIPN C,NASBH2'
	JRST NDIV35
	DBP7 C
	SKIPN D,SLASBH'
	JRST NDIV36
	ROTC C,6
	CAMG D,C
	EXCH C,D
	ROTC C,-6
NDIV36:	MOVE D,A
	ROTC C,6
	CAML D,C
	EXCH C,D
	ROTC C,-6
	SETZM ND38P'
	SKIPN D,SLSHSD'
	JRST ND38Y
	ROTC C,6
	CAMLE C,D
	SETOM ND38P
	ROTC C,-6
ND38Y:	SETZM ND38X'
	MOVEI B,"@
NDIV38:	ILDB D,C
	JUMPE D,NDIV37
	CAIN D,".
	JRST NDIV39
	DPB B,C
	SETOM ND38X
	SKIPN ND38P
	JRST NDIV38
	JRST NDIV3M

NDIV39:	SETOM ND38P'
	SKIPN ND38X
	JRST NDIV38
NDIV3M:	MOVEI B,0
	IDPB B,C
	JRST NDIV37

NDIV35:	SKIPE C,SLASBH
	JRST NDIV36
NDIV37:	SKIPN B,SLSHSD'
	JRST NDIV3D
	ROTC A,6
	CAMG B,A
	EXCH A,B
	ROTC A,-6
NDIV3D:	MOVE G,A
	MOVEI H,0
	PUSHJ P,SLSHS
	MOVE A,G
	SKIPN B,SLSHSD'
	JRST NDIV3Q
	ROTC A,6
	CAMG B,A
	EXCH A,B
	ROTC A,-6

NDIV3Q:	MOVE B,A
	ILDB C,B
	CAIN C,".
	JRST NDIV3L
	CAIE C,"@
	JRST NDIV3F
NDIV3P:	ILDB C,B
	CAIN C,"@
	JRST NDIV3P
	JUMPN C,NDIV3F
	MOVEI C,".
	DPB C,B
	MOVEI C,0
	IDPB C,B
	JRST NDIV3F

NDIV3L:	ILDB C,B
	CAIE C,"@
	JRST NDIV3F
	MOVEI C,"0
	DPB C,A
	DBP7 A

NDIV3F:	JUMPG F,NDIV3E
	JUMPN E,NDIV3E
	MOVE A,[(10700)[ASCII /0/]-1]
	JRST NDIV3I
NDIV3E:	ILDB C,SLASHA
	CAIE C,"-
	JRST NDIV3I
	DPB C,A
	DBP7 A

NDIV3I:	MOVE B,[(10700)[ASCII /\/]-1]
	MOVSI F,400000
	IORM F,-4(PP)
	MOVE F,V
	PUSH P,[PCLOSE]
	PUSH P,[0
	PUSH P,B
	MOVEM B,DSYNP
	JRST DSYIN

NDIV2X:	MOVEM A,NDIVRE'
	MOVEM B,NDIVDE'
	SETZM NASBH1'
NDIV2W:	HLL B,NDIVD
	SUB B,NDIVD
	HRRZ C,VP
	ADDI C,1
	ADD B,C
	MOVEI F,@SP
	CAIG F,1(B)
	ERR [ASCII /SCE/]
	HRL C,NDIVD
	BLT C,(B)
	MOVE B,NDIVDE
	JRST NDIV2

NDIV2Y:	SETZM NDIVC'
NDIV2Z:	DPB D,B
	DBP7 B
NDIV2:	DBP7 A
	CAMN A,NDIVR
	JRST NDIV4
	LDB C,A
	CAIN C,".
	JRST NDIV2
NDIV2L:	LDB D,B
	CAIN D,".
	JRST NDIV2M
	CAIN C,"@
	JRST NDIV20
NDIV21:	ADD D,NDIVC'
	SUBI D,-60(C)
	CAIL D,"0
	JRST NDIV2Y
	SETOM NDIVC
	ADDI D,12
	JRST NDIV2Z

NDIV2M:	DBP7 B
	JRST NDIV2L

NDIV4:	MOVE D,NDIVC'
	SETZM NDIVC
	ADDB D,NDIV1M'
	JUMPL D,NDIV5
	MOVE A,NASBH1'
	SKIPN NASBH2'
	MOVEM A,NASBH2
	MOVE A,NDIVRE
	MOVE B,NDIVDE
	AOJA E,NDIV2W

NDIV20:	SKIPN NASBH1
	MOVEM B,NASBH1
	MOVEI C,"0
	JRST NDIV21

NDIV5:	HRRZ B,NDIVDE
	HRL C,VP
	HRR C,NDIVD
	ADD C,[(1)
	BLT C,(B)
	SOJN G,NDIV5Z
	MOVEI A,".
	PVAK
	SKIPN NDIVSL'
	SETOM NDIVSP'
	SETOM NDIVQS'
NDIV5Z:	MOVEI A,60(E)
	CAIE A,"0
	SETOM NDIVQZ'
	CAIN A,"0
	SKIPE NDIVQS'
	JRST NDIV5B
NDIV5C:	CAIE A,"0
	SETOM NDIVQS
NDIV5A:	ILDB A,NDIVD
	CAIN A,".
	JRST NDIV5A
	SUBI A,60
	MOVEM A,NDIV1M'
	MOVEI A,"0
	DPB A,NDIVD
	JRST NDIV1P

NDIV5B:	PVAK
	SKIPE NDIVQZ
	SOSE NDIVSD'
	JRST NDIV5C
	MOVEM VP,NDIVSL'
	JRST NDIV5C

NDIV33:	TLNE FF,EVNF
	JRST NDIV30
	SKIPE NDIVSP
	JRST NDIV34
	MOVEI A,".
	PVAK
NDIV34:	MOVEI A,"@
	PVAK
	JRST NDIV30
SLSHS:	SETZB E,SLSHSF'
	SETZB J,SLSHSD'
	MOVEI C,0
	TLZ FF,SLSHSS+SLSHSC+TINGF+REPLF
	ILDB B,A
	CAIE B,"+
	CAIN B,"-
	JRST SLASH4
	JRST SLSH4A
SLASH4:	CAIN B,"-
	SETCMM NDIVES
SLSH4Z:	JUMPN H,SLSH4X
	IBP G
SLSH4Y:	JUMPE H,SLSH4X
	PUSHJ P,PVB
SLSH4X:	ILDB B,A
SLSH4A:	JUMPE B,SLASH5
	CAIN B,"@
	JRST SLSH4K
	CAIN B,".
	JRST SLSH4B
	CAIE B,"0
	TLO FF,SLSHSC+SLSHSS+TINGF+REPLF
SLSH4L:	TLNE FF,REPLF
	ADDI C,1
	SKIPE SLSHSF
	SUBI C,1
	TLNN FF,SLSHSC
	JRST SLSH4C
	TLNE FF,SLSHSS
	ADDI E,1
SLSH4J:	TLNN FF,TINGF
	JRST SLSH4Y
	AOJA J,SLSH4Y

SLSH4B:	MOVEM VP,SLSHSD'
	JUMPN H,SLSH4R
	MOVE D,A
	DBP7 D
	MOVEM D,SLSHSD'
SLSH4R:	MOVEM E,F
	SETOM SLSHSF'
	MOVEI E,0
	TLO FF,SLSHSS
	JUMPN H,SLSH4D
	TLNN FF,SLSHSC
	JRST SLSH4Z
SLSH4D:	TLO FF,SLSHSC
	JRST SLSH4Y

SLASH5:	SKIPE SLSHSF
	POPJ P,
	MOVE F,E
	MOVEI E,0
SLSH5A:	POPJ P,

SLSH4C:	TLNN FF,SLSHSS
	JRST SLSH4Z
	SUBI F,1
	AOJA E,SLSH4Z

SLSH4K:	TLO FF,REPLF+SLSHSS+SLSHSC
	JUMPL J,SLSH4L
	SKIPLE H
	MOVEM VP,SLASBH'
	SKIPL NDIVSD'
	CAMGE J,NDIVSD'
	MOVEM J,NDIVSD
	MOVSI J,400000
	MOVEI B,"0
	JRST SLSH4L

SLSHZ:	MOVEI A,"/
	PVAK
	MOVEI A,"0
	JRST SLSHI3

PVB:	EXCH A,B
	PVAK
	EXCH A,B
	POPJ P,

SLDP4:	HRLI VP,10700
	MOVEM VP,-5(PP)
	SUB C,SLSHSQ'
	AOJLE C,SLDP41
	MOVEI A,"@
SLDP40:	PVAK
	SOJG C,SLDP40
	MOVEI A,".
	PVAK
	JRST NDIV3C

SLDP41:	JUMPN C,SLDP42
	MOVEI A,"0
	PVAK
SLDP42:	MOVEI A,".
SLDP43:	PVAK
	MOVEI A,"0
	AOJLE C,SLDP43
	MOVEI A,"@
	PVAK
	JRST NDIV3C

;VALUE IN A: +2=NULL; +1=NON-ITEM; 0=NON-NUM.; -1=NUMBER
;USES A, C

SLSHT:	JUMPE A,SLSHT4
	SETOM SLSHTN'
	SETZM SLSHTA'
	ILDB C,A
	JUMPE C,SLSHT4
	CAIE C,"+
	CAIN C,"-
	JRST SLSHT1

SLSHT0:	JUMPE C,SLSHT3
	CAIN C,"@
	JRST SLSHT1
	SETOM SLSHTA
	CAIN C,".
	JRST SLSHT1
	CAIL C,"0
	CAILE C,"9
	SETZM SLSHTN'
	SKIPGE TYPE(C)
	JRST SLSHT2
SLSHT1:	ILDB C,A
	JRST SLSHT0

SLSHT2:	MOVEI A,1
	POPJ P,
SLSHT3:	SKIPGE A,SLSHTN
	MOVE A,SLSHTA
	POPJ P,

SLSHT4:	MOVEI A,2
	POPJ P,

;INDICATED DIVISION

SLSHI0:	PVAK
SLSHI:	ILDB A,SLASHA'
	JUMPN A,SLSHI0
	MOVEI A,"/
	PVAK
	JUMPLE B,SLSHI1
	MOVEI A,"(
SLSHI2:	PVAK
SLSHI1:	ILDB A,SLASHB'
	JUMPN A,SLSHI2
	MOVEI A,")
	SKIPLE B
SLSHI3:	PVAK
	MOVE A,[(10700)[0]-1]
	JRST NDIV3I

QUOSYM:	SETZM IGNSYC'
QUOSN:	ILDB A,NEGATB'
QUOSY2:	CAIN A,""
	JRST QUOQ
	CAIN A,"[
	JRST QUOS
	CAIN A,"(
	AOS IGNSYC
	CAIE A,")
	JRST QUOSY1
	SKIPLE IGNSYC
	SOS IGNSYC
QUOSY1:	CAIN A,".
	JRST QUOSM
	SKIPN IGNSYC
	SKIPL TYPE(A)
	JRST QUOSM
QUOS0:	MOVE C,NEGATB
	DBP7 C
	MOVEM C,NEGATB
	POPJ P,

QUOQ:	PVAK
	ILDB A,NEGATB
	JUMPE A,[ERR [ASCII /UCQ/]]
	CAIE A,40
	JRST QUOQ
QUOSM:	PVAK
	JRST QUOSN

QUOS:	PVAK
	ILDB A,NEGATB
	JUMPE A,QUOS0
	CAIE A,"[
	JRST QUOSY2
	PVAK
	MOVE A,[ILDB A,NEGATB]
	PUSHJ P,SUPER0
	  PVAK
	JRST QUOSN
GOTO:	MOVE B,-14(PP)
	MOVE E,(PP)
	MOVE A,E
	ILDB F,A
	MOVE F,TYPE(F)
	JUMPGE F,GOTO8
	MOVE E,-2(PP)
	MOVE A,E
	ILDB F,A
	MOVE F,TYPE(F)
GOTO8:	MOVEI G,0
	SKIPN GOTOF
	JRST GOTO0

GOTOP:	MOVEI C,"\
	TRO FF,IOE1
	PUSHJ P,TYO
	PUSHJ P,TYO
	MOVE A,E
	PUSHJ P,TYAL
	MOVEI C,"\
	PUSHJ P,TYO
	PUSHJ P,TYO
	PUSHJ P,CRLF
	TRZ FF,IOE1

GOTO0:	MOVE A,E
GOTO1:	MOVE C,(PP)
	ROTC B,6
	CAMG C,B
	JUMPL F,GOTO5
GOTO6:	ROTC B,-6
	ILDB D,B
	JUMPE D,GOTON
	CAIN D,""
	JRST GOTOIQ
	CAIE D,"[
	JRST GOTO1
	ILDB D,B
	CAIN D,"[
	JRST GOTOS
	SKIPA
GOTO2:	ILDB D,B
	ILDB C,A
	SKIPL TYPE(C)
	JRST GOTO3
	SKIPL TYPE(D)
	JRST GOTO0
	DBP7 B
	JUMPL F,GOTO4
	MOVEM B,(PP)
	JRST SPACE0

GOTO3:	CAMN C,D
	JRST GOTO2
	DBP7 B
	JRST GOTO0

GOTON:	CAME E,-2(PP)
	ERR [ASCII /UDT/]
	JRST EV0

GOTOI9:	CAIN D,40
	JRST GOTO0
GOTOIQ:	ILDB D,B
	JUMPN D,GOTOI9
	ERR [ASCII /UCQ/]

GOTOS:	EXCH B,(PP)
	PUSHJ P,SUPER
	  JFCL
	EXCH B,(PP)
	JRST GOTO0

GOTO4:	MOVE G,B
	JRST GOTO0

GOTO5:	JUMPN G,GOTO7
	MOVEI F,0
	JRST GOTO6

GOTO7:	MOVEM G,(PP)
	JRST SPACE0

GOTOF:	0	;TRACE FLAG

;

REMDR:	MOVE B,[(10700)[ASCII /\/]-1]
	PUSHJ P,SYCH
	JUMPE B,SPACE1
	JRST SYMBL3

TYIAZ:	PUSHJ P,TYI
	SKIPN A,C
	MOVEI A,33
	TLNN FF,EVNF
	POPJ P,
	CAIE A,15
	CAIN A,12
	JRST TYIAZ
	POPJ P,

TYIA:	PUSHJ P,TYI
	MOVE A,C
	POPJ P,

TYI:	TRNN FF,IOQ
	JRST TYIT
	JRST UTI

TYO:	JUMPE C,CPOPJ
	PUSH P,A
	CAIN C,15
	SETZM LINPOS
	MOVE A,LINPOS
	CAML A,LINEL1
	TLNN FF,TYDF
	JRST TYOK1
TYOK2:	PUSH P,C
	PUSH P,FF
	MOVEI C,15
	PUSHJ P,TYO
	MOVE FF,(P)
	MOVEI C,12
	PUSHJ P,TYO
	POP P,FF
	POP P,C
TYOK1:	TRNE FF,ION+IO%
	TRNN FF,IOV
	TRNE FF,IOE1+IOE2+IOE3
	PUSHJ P,TYOT
	CAIL C,40
	CAILE C,173
	JRST .+2
	AOS LINPOS
	TRZ FF,IOE3
	TRNN FF,ION+IO%
	JRST TYOP
	SKIPE UTOOK'
	TRNN FF,IOR
	JRST TYO1

IFE TS,[
	PUSHJ P,UWR"
	ERR [ASCII /NFB/]
TYO1:	EXCH A,C
	TRNE FF,IOB
	PUSHJ P,PILPT"
	EXCH A,C
]
IFN TS,[
	.IOT UTOC,C
TYO1:	TRNE FF,IOB
	.IOT LPTC,C
]
TYOP:	POP P,A
	POPJ P,

TYOD:	CAIL C,40
	JRST TYOD1
	PUSH P,C
	MOVEI C,"^
	PUSHJ P,TYOD1
	POP P,C
	ADDI C,100
TYOD1:	PUSHJ P,TYO
	SOJG D,CPOPJ
CRLFD:	MOVE D,LINEL
CRLF:	MOVEI C,15
	PUSHJ P,TYO
	MOVEI C,12
	JRST TYO

TYAL:	JUMPE A,CPOPJ
TYAL1:	ILDB C,A
	JUMPE C,CPOPJ
	CAIE C,12
	CAIN C,15
	POPJ P,
	PUSHJ P,TYO
	JRST TYAL1

DPT:	IDIVI B,12
	JUMPE B,TYD
	PUSH P,C
	PUSHJ P,DPT
	POP P,C
TYD:	ADDI C,60
	JRST TYO

LINEL:	72.

TYA:	JUMPE A,CPOPJ
TYA1:	ILDB C,A
	JUMPE C,CPOPJ
	PUSHJ P,TYO
	JRST TYA1

TYAS:	JUMPE A,CPOPJ
TYAS1:	ILDB C,A
	JUMPE C,CPOPJ
	CAIN C,40
	POPJ P,
	PUSHJ P,TYO
	JRST TYAS1

IFE TS,[
TYOT0:	PUSH P,A
	MOVE A,TYOTI
	IBP A
	CAMN A,[(350700)TYOBE]
	HRRI A,TYOB
TYOT1:	CAMN A,TYOTO
	JRST TYOT1
	DPB C,A
	MOVEM A,TYOTI
	POP P,A
	POPJ P,
]

TYOT:	CAIE C,33
	TRNE FF,IOX
	POPJ P,
IFE TS,[
	PUSHJ P,TYOT0
TYOT2:	CONSO TTY,30
	CONO TTY,10+TTYCHN
	POPJ P,
]
IFN TS,[
TYOT0:	.IOT TYOC,C
	POPJ P,
]

TYBK:	0
	PUSH P,A
TYBK1:
IFE TS,[
	CONSZ TTY,20	;TYO BUSY
	JRST TYBKI
	MOVE A,TYOTO
	CAMN A,TYOTI
	JRST TYBK9
	IBP A
	CAMN A,[(350700)TYOBE]
	HRRI A,TYOB
	MOVEM A,TYOTO
	LDB A,A
	CAIGE A,175
	CAIN A,33
	JRST TYBK1
	DATAO TTY,A
TYBK9:	CONO TTY,TTYCHN+200	;TYO DONE OFF
]
TYBKI:
IFE TS,[
	CONSO TTY,40	;TYI DONE
	JRST TYBKD
	DATAI TTY,A
	ANDI A,177
]
IFN TS,[
	.ITYI A,
	JRST TYBKD
]
	SKIPN TYCIP'
	JRST TYBKI1
	JUMPE A,TYBKD
	SETZM TYCIP
IFE TS,[
	CAIN A,177
	JRST TYBKD
	CAIGE A,175
	CAIN A,33
	JRST TYBKC0
	CAIN A,15
	JRST TYBKCR
	CAIN A,^P
	JRST TYBKI3
	CAIE A,^Q
	JRST TYBKC
TYBKI3:	PUSH P,A
	MOVEI A,^P
	PUSHJ P,TYIP
	POP P,A
	JRST TYBKC
]
IFN TS,JRST TYBKC

TYBKI1:	CAIL A,40
	JRST TYBKC
	JRST @TYBKT(A)

TYBKT:	TYBKD
	TYBKC
	TYBKIB
	TYBKC
	TYBKC
	TYBKIE
	TYBKIF
	TYBKIG

	REPEAT 6,TYBKC
	TYBKIN
	TYBKIO

	TYBKIP
	TYBKC
	TYBKIR
	TYBKIS
	TYBKIT
	TYBKC
	TYBKIV
	TYBKIW

	TYBKIX
	REPEAT 7,TYBKC

IFE TS,[
TYBKC0:	MOVEI A,0
]
TYBKC:
IFE TS,PUSHJ P,TYIP
TYBKD:	POP P,A
IFE TS,JRST 12,@TYBK
IFN TS,JRST 2,@TYBK

IFE TS,[
TYIP:	PUSH P,B
	MOVE B,TYIPI
	IBP B
	CAMN B,[(350700)TYIBE]
	HRRI B,TYIB
	CAMN B,TYIPO
	JRST TYIPD	;LOST
	DPB A,B
	MOVEM B,TYIPI
TYIPD:	POP P,B
	POPJ P,
]

TYBKIP:	SETOM TYCIP
	JRST TYBKD
IFE TS,[
TYIT:	SETZM TYITP'
	SKIPN TYITC'
	JRST TYIT1
	SETZM TYITC
	MOVEI C,12
	JRST TYIT2

TYIT1:	MOVE C,TYIPO
	CAMN C,[(350700)TYIBE]
	HRRI C,TYIB
	MOVEM C,TYIPO
	CAMN C,TYIPI
	JRST .-1
	ILDB C,TYIPO
	SKIPE TYITP
	JRST TYIT2
	CAIN C,15
	SETOM TYITC
	CAIN C,^P
	JRST TYIT3
	CAIE C,^Q
	JRST TYIT2
	MOVEI C,"^
	PUSHJ P,TYOT0
	MOVEI C,"Q
	PUSHJ P,TYOT0
	TRO FF,IOQ
	JRST UTI

TYIT3:	SETOM TYITP
	JRST TYIT1
TYIT2:	TRO FF,IOE3
	JRST TYO
]

IFN TS,[
TYIT:	MOVEI C,12
	AOSE TYITC'
	.IOT TYIC,C
	CAIN C,^Q
	JRST TYIT1
	CAIN C,15
	SETOM TYITC
	CAIL C,40
	JRST TYIT8
	CAIN C,^P
	JRST TYIT5
	MOVEM C,TYITSC'
	HRRZ C,TYBKT(C)
	CAIE C,TYBKC
	JRST TYIT
	MOVE C,TYITSC
	JRST TYIT8

TYIT5:	.IOT TYIC,C
	CAIN C,177
	JRST TYIT
	CAIGE C,175
	CAIN C,33
	JRST TYIT7
	CAIE C,15
	JRST TYIT8
	PUSHJ P,TYOT0
	MOVEI C,12
	PUSHJ P,TYOT0
	JRST TYIT

TYIT7:	TDCA C,C
TYIT6:	.IOT TYIC,C
TYIT8:	TRO FF,IOE3
	JRST TYO

TYIT1:	MOVEI C,"^
	PUSHJ P,TYOT0
	MOVEI C,"Q
	PUSHJ P,TYOT0
	TRO FF,IOQ
UTI:	SKIPN UTIOK
	JRST UTI1
	.IOT UTIC,C
	SKIPGE C
UTI1:	TRZA FF,IOQ
	JRST TYO
	SETZM UTIOK
	JRST TYIT
]

TYBKIR:	TROA FF,IOR
TYBKIT:	TRZ FF,IOR
	JRST TYBKX

TYBKIB:
IFN TS,[
	LPTC==5
	.OPEN LPTC,[SIXBIT /  !LPT/]
	ERR [ASCII /LIU/]
]
	TRO FF,IOB
	JRST TYBKX

TYBKIE:
IFN TS,.CLOSE LPTC,
	TRZ FF,IOB
	JRST TYBKX

TYBKIS:	TRZ FF,IOQ
	JRST TYBKX

TYBKIN:	TROA FF,ION
TYBKIF:	TRZ FF,ION
	JRST TYBKX

TYBKIO:	MOVEI A,PPDL
	CAIG A,(PP)
	JRST TYBKD
IFE TS,[
	JRST 10,.+1
	PUSHJ P,UWAIT"
	PUSHJ P,LPTWAT"
	JRST DDT
]
IFN TS,[
	.DISMI [.+1]
	.VALUE
]

TYBKIX:	PUSH P,C
	MOVE C,TYIPI
	MOVEM C,TYIPO
	JRST TYBKY

TYBKIW:	PUSH P,A
	MOVE A,TYOTO
	MOVEM A,TYOTI
	POP P,A
	TRZA FF,IOV
TYBKIV:	TRO FF,IOV
TYBKX:	PUSH P,C
TYBKY:	PUSH P,A
	MOVEI C,"^
	PUSHJ P,TYOT0
	POP P,C
	ADDI C,100
	PUSHJ P,TYOT0
	POP P,C
	JRST TYBK1

TYBKCR:	PUSHJ P,TYOT0
	MOVEI C,12
	PUSHJ P,TYOT0
	JRST TYBK1

IFE TS,[
UTI:	SKIPE UTIOK
	PUSHJ P,URED"
	TRZA FF,IOQ
	JRST TYO
	SETZM UTIOK
	JRST TYIT
]
TYBKIG:
IFE TS,[
	MOVE A,TYOTO
	MOVEM A,TYOTI
	MOVE A,TYIPI
	MOVEM A,TYIPO
]
IFN TS,[
	.RESET TYOC,
	.RESET TYIC,
]
	TRNE FF,IOX
	JRST TYBG3
	TRNE FF,IOE1+IOE2+IOE3
	JRST TYBG1
TYBG3:
IFE TS,[
	POP P,A
	JRST 10,TYBKM
]
IFN TS,[
	MOVEI A,TYBKM
	MOVEM A,TSINT+1
	JRST TYBKD
]

TYBKM:	TRO FF,IOE1
	PUSHJ P,CRLF
	MOVEI C,"\
	PUSHJ P,TYO
	TRZ FF,IOX
	PUSHJ P,TYO
	JRST ERROR1

TYBG1:	TRNE FF,IO%
	JRST TYBG3
	TRO FF,IOX
	PUSH P,C
	MOVEI C,15
	PUSHJ P,TYOT0
	MOVEI C,12
	PUSHJ P,TYOT0
	MOVEI C,"\
	PUSHJ P,TYOT0
	POP P,C
IFE TS,JRST TYBK1
IFN TS,JRST TYBKD

TYOB:	BLOCK 200
TYOBE:
TYIB:	BLOCK 40
TYIBE:
TYIPI:	(350700)TYIB
TYIPO:	(350700)TYIB
TYOTI:	(350700)TYOB
TYOTO:	(350700)TYOB

IFN TS,[
TSINT:	0
	0
	EXCH A,TSINT
	TRNE A,1
	JSR TYBK
	TRNE A,200000
	JSR APRBK
	MOVE A,TSINT
	.DISMI TSINT+1
]

CHINB:	TROA FF,IOB
CHINE:	TRZ FF,IOB
	JRST EV0

CHINN:	TROA FF,ION
CHINF:	TRZ FF,ION
	JRST EV0

CHINQ:	TROA FF,IOQ
CHINS:	TRZ FF,IOQ
	JRST EV0

CHINR:	TROA FF,IOR
CHINT:	TRZ FF,IOR
	JRST EV0

CHINV:	TROA FF,IOV
CHINW:	TRZ FF,IOV
	JRST EV0

CHINX:	MOVE C,TYIPI
	MOVEM C,TYIPO
	JRST EV0

CHING:	ERR [ASCII /\\\/]

ERROR:	0
	MOVEM A,ERRRA'
	MOVEM B,ERRRB'
	MOVEM C,ERRRC'
	MOVEM D,ERRRD'
	LDB A,[(331100)40
	MOVEI B,[ASCII /UUO/]
	CAIE A,ERR_-27.
	MOVEM B,40
	MOVE A,40
IFN TS,[
ERROR4:	MOVE B,(A)
	CAME B,[ASCII /SCE/]
	JRST ERROR3
	AOS C,CORSIZ
	.CORE (C)
	JRST ERROR3

ERROR5:	MOVE B,SSEND
	MOVEI A,2000
	ADDB A,SSEND
	SETZM (A)
	PUSHJ P,SYCL
ERRAR1:	HRRZ A,ERROR
	CAIL A,DSY1
	CAIL A,DSY9
	JRST ERRRST
	POP P,A	;FLUSH RETURN
	SKIPN -1(P)
	PUSH P,LSYLSY'
	POP P,A	;FLUSH IT
	JRST DSY1

ERRRST:	MOVE A,ERRRA'
	MOVE B,ERRRB'
	MOVE C,ERRRC'
	MOVE D,ERRRD'
	JRST @ERROR

ERROR3:	MOVE A,40

]
	TRZ FF,IO%
	TRO FF,IOE1
	HRLI A,440700
	PUSHJ P,TYA
	MOVE B,V
	PUSHJ P,DPT
	PUSHJ P,CRLFD
	JUMPE V,ERROR1
	SKIPN ERRORB
	JRST ERROR1

ERROR9:	IBP -1(PP)
	MOVEI B,PPDL+PPBL-1-1
ERROR6:	CAIL B,(PP)
	JRST ERROR0
	MOVE A,(B)
	LDB C,A

ERROR2:	SKIPE C
	PUSHJ P,TYOD
ERROR8:	ILDB C,A
	CAIE C,".
	SKIPL TYPE(C)
	JRST ERROR2

ERROR7:	CAIN C,"(
	PUSHJ P,TYOD
	MOVEI C,40
	PUSHJ P,TYOD
	ADDI B,PPBL
	JRST ERROR6

ERROR0:	PUSHJ P,CRLF
ERROR1:	PUSHJ P,FSY
	JRST GO

ERRORB:	-1	;BACKTRACE FLAG

FLUSH:	MOVE A,SSEND
	MOVEM A,SP
	JRST BEG
APRBK:	0
IFE TS,[
	CONSO 200000	;PDL OV
	JRST APRBK1
	CONO 400000+APRCHN
]
	MOVE P,[(-PDLL)PDL
	MOVEI A,APRPE
IFE TS,HRRM A,APRBK
IFN TS,HRRM A,TSINT+1
IFE TS,JRST 12,@APRBK
IFN TS,JRST 2,@APRBK

APRPE:	ERR [ASCII /POV/]
IFE TS,[
APRBK1:	CONSO 1000	;CLOCK
	JRST .
	JSR APRBRK"
	CONO 1000+APRCHN	;CLOCK FLAG OFF
	JRST 12,@APRBK
]
RANDMM:	27*27*27*27*27
RANDMS:	27*27*27*27*27
SGNF0:	-1
LINPOS:	0
LINEL1:	54+<34*<1-TS>>

	VARIAB
CON:	CONSTA

DDT=174000

%=(400000)
TYPE:	% ZERO
	% ILLEG
	% CHINB
	REPEAT 2,% ILLEG
	% CHINE
	% CHINF
	% CHING

	% ILLEG
	REPEAT 4,% SPACE
	% IGNOR
	% CHINN
	% ILLEG

	% ILLEG
	% CHINQ
	% CHINR
	% CHINS
	% CHINT
	% ILLEG
	% CHINV
	% CHINW

	% CHINX
	REPEAT 7,% ILLEG

	% SPACE
	% EXCLM
	% QUOTE
	% NSIGN
	% DOLAR
	% PERCT
	% AMPER
	% PRIME
	% PAREN
	% CPARN
	% TIMES
	% PLUS
	% COMMA
	% MINUS
	% POINT
	% SLASH
	REPEAT 12,DIGIT
	% COLON
	% SEMCL
	% LESS
	% EQUAL
	% GREAT
	% QMARK

	ATSGN
	REPEAT 32,LETER
	% OPBRK
	% REMDR
	% GOTO
	% UPARR
	% SERCH

	% ILLEG
	REPEAT 32,LETER
	REPEAT 5,% ILLEG

CORSIZ:	<THEEND+1777>/2000

PATCH:	BLOCK 100

SSEND:	(10700)SWEND
SP:	(10700)SWEND

PDL:	BLOCK PDLL
PPDLB:	REPEAT PPBL-6,0
PPDLV:	REPEAT 6,0
PPDL:	BLOCK PPDLL
VS:	BLOCK VSL-1

SWEND:	0
THEEND:

END BEG
,TYBKC
	JRST TYIT
	MOVE C,TYITSC
	JRST TYIT8

TYIT5:	.IOT TYIC,C
	CAIN C,177
	JRST TYIT
	CAIGE C,175
	CAIN C,33
	JRST TYIT7
	CAIE C,15
	JRST TYIT8
	PUSHJ P,TYOT0
	MOVEI C,12
	PUSHJ P,TYOT0
	JRST TYIT

TYIT7:	TDCA C,C
TYIT6:	.IOT TYIC,C
TYIT8:	TRO FF,IOE3
	JRST TYO

TYIT1:	MOVEI C,"^
	PUSHJ P,TYOT0
	MOVEI C,"Q
	PUSHJ P,TYOT0
	TRO FF,IOQ
UTI:	SKIPN UTIOK
	JRST UTI1
	.IOT UTIC,C
	SKIPGE C
UTI1:	TRZA FF,IOQ
	JRST TYO
	SETZM UTIOK
	JRST TYIT
]

TYBKIR:	TROA FF,IOR
TYBKIT:	TRZ FF,IOR
	JRST TYBKX

TYBKIB:
IFN TS,[
	LPTC==5
	.OPEN LPTC,[SIXBIT /  !LPT/]
	ERR [ASCII /LIU/]
]
	TRO FF,IOB
	JRST TYBKX

TYBKIE:
IFN TS,.CLOSE LPTC,
	TRZ FF,IOB
	JRST TYBKX

TYBKIS:	TRZ FF,IOQ
	JRST TYBKX

TYBKIN:	TROA FF,ION
TYBKIF:	TRZ FF,ION
	JRST TYBKX

TYBKIO:	MOVEI A,PPDL
	CAIG A,(PP)
	JRST TYBKD
IFE TS,[
	JRST 10,.+1
	PUSHJ P,UWAIT"
	PUSHJ P,LPTWAT"
	JRST DD