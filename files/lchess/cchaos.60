;CHESS CHAOS NET ROUTINES.   -*-LISP-*-

;PACKET ORIENTED.  16 BIT WORD ORIENTED.
;  52. IS CURRENTLY MAX POSSIBLE WORDS PER PACKET.

;PROTOCOL FUNCTIONS
;  LOAD CHEOPS ARG BLOCK  
;	PACKET-TYPE		     (1-WORD)
;	CHEOPS BUFFER-MEMORY LOC     (1-WORD)
;       PRIORITY-OPTION (NONE,BACKGROUND,PRIORITY)  (1-WORD)
;	    NONE PRIORITY, ADD-TO-BACKGROUND, ADD-TO-BACKGROUND-AND-START-BACKGROUND
;	PARAMETER SECTION
;	  WD2 THRU WD7 OF ARG BLOCK  (6-WORDS)
;       POSITION 
;	  REST OF PACKET. TERMINATED BY 0  (MAX 32. WORDS)
;  RETURN CHEOPS RESULTS  LOC
;	PACKET-TYPE
;	WD3 (SETD)
;	PAD TO ALIGN WITH PDP10 WORD BOUNDARY (1 WORD)
;       WD16 THRU WD36 (21 (OCTAL) WORDS)

;  MISC FUNCTIONS ...


(DECLARE (SPECIAL CHEOPS-CHAOS-SERVER-PROCESS 
		  CHEOPS-CHAOS-SERVER-CONNECTION 
		  CCP-WORD-NAMES CCR-WORD-NAMES
		  CCHAOS-N-PACKET-TYPES CCHAOS-PACKET-RECEIVE-DISPATCH
		  CCR-NBYTES))

(DECLARE (SPECIAL CHEOPS-BUFFER-MEMORY CHEOPS-WAIT-BADR 
		  CHEOPS-BUFFER-MEMORY-RING-HIGH-BADR  ;ADR AT WHICH BUFFER IS LOOPED
						       ;IN BACKGROUND MODE.
		  CHEOPS-BUFFER-MEMORY-PRIORITY-BLOCK-POINTER
		  CHEOPS-BUFFER-MEMORY-BACKGROUND-BLOCK-POINTER
		  CHEOPS-BUFFER-MEMORY-UNIBUS-WD-ADR))

(SETQ CHEOPS-BUFFER-MEMORY-RING-HIGH-BADR 200)  ;INITIALIZE TO SOMETHING VAGUELY REASONABLE

(DECLARE (SPECIAL CHEOPS-ARG-FLAG CHEOPS-ARG-UNUSED CHEOPS-ARG-MODES 
		  CHEOPS-ARG-SETD CHEOPS-ARG-ALPHA CHEOPS-ARG-BETA 
		  CHEOPS-ARG-FLAGWORD CHEOPS-ARG-CHAIN-POINTER))

;NAME IS A FUNCTION OF ONE ARG TO HANDLE THIS TYPE PACKET ON RECEIVE
(EVAL-WHEN (COMPILE EVAL LOAD)
 (SETQ CCHAOS-PACKET-TYPES 
      '(CCHAOS-PKT-ERROR CCHAOS-PKT-POSITION CCHAOS-PKT-REQUEST-RESULT 
	CCHAOS-PKT-RESULTS CCHAOS-PKT-CLOSE-RING-AND-START-BACKGROUND)) )

  (SETQ CCHAOS-N-PACKET-TYPES (LENGTH CCHAOS-PACKET-TYPES))
  (SETQ CCHAOS-PACKET-RECEIVE-DISPATCH (MAKE-ARRAY NIL ART-Q CCHAOS-N-PACKET-TYPES))
     (FILLARRAY CCHAOS-PACKET-RECEIVE-DISPATCH CCHAOS-PACKET-TYPES)

(DEFMACRO CCHAOS-PACKET-CODE (TYPE)
    `,(FIND-POSITION-IN-LIST TYPE CCHAOS-PACKET-TYPES))

(EVAL-WHEN (COMPILE LOAD)
 (SETQ CCP-WORD-NAMES  ;WORDS IN POSITION PACKET
      '(OP-CODE BUFFER-MEMORY-LOC PRIORITY-OPTION PS-2 PS-3 PS-4 PS-5 PS-6 PS-7
	POS-WD0))
 (SETQ CCR-WORD-NAMES 
      '(OP-CODE BUFFER-MEMORY-LOC PS-3  PAD PS-16))  ;THENCE CONSECUTIVE THRU PS-36 (OCTAL)
 (SETQ CCC-WORD-NAMES 
      '(OP-CODE BUFFER-MEMORY-LOC-HEAD BUFFER-MEMORY-LOC-TAIL))
)

(DEFMACRO CCP-PKT-WD (WORD-NAME)
  `,(+ CHAOS:FIRST-DATA-WORD-IN-PKT 
       (FIND-POSITION-IN-LIST WORD-NAME CCP-WORD-NAMES)))

(DEFMACRO CCR-PKT-WD (WORD-NAME)
  `,(+ CHAOS:FIRST-DATA-WORD-IN-PKT 
       (FIND-POSITION-IN-LIST WORD-NAME CCR-WORD-NAMES)))

(DEFMACRO CCC-PKT-WD (WORD-NAME)
  `,(+ CHAOS:FIRST-DATA-WORD-IN-PKT 
       (FIND-POSITION-IN-LIST WORD-NAME CCC-WORD-NAMES)))

(SETQ CCR-NBYTES		;USE THIS FOR N BYTES FIELD OF CHEOPS RESULTS PACKET
      (* 2 (+ (CCR-PKT-WD PS-16) 21)))

(COND ((NULL (BOUNDP 'CHEOPS-CHAOS-SERVER-PROCESS))
       (SETQ CHEOPS-CHAOS-SERVER-PROCESS
	     (PROCESS-CREATE "CHEOPS Server Process"))))

(DEFUN START-CHEOPS-CHAOS-SERVER NIL 
       (PROCESS-DISABLE CHEOPS-CHAOS-SERVER-PROCESS)
       (PROCESS-PRESET CHEOPS-CHAOS-SERVER-PROCESS (FUNCTION CHEOPS-CHAOS-SERVER))
       (PROCESS-ENABLE CHEOPS-CHAOS-SERVER-PROCESS))

(DEFUN STOP-CHEOPS-CHAOS-SERVER NIL 
       (PROCESS-DISABLE CHEOPS-CHAOS-SERVER-PROCESS))

(DECLARE (SPECIAL BAD-PACKET))
(COND ((NULL (BOUNDP 'BAD-PACKET))
       (SETQ BAD-PACKET NIL)))

(DEFUN CHEOPS-CHAOS-SERVER NIL 
  (PROG (STATE IN-PKT PKT-CH-OPCODE)
    TOP	(SETQ CHEOPS-CHAOS-SERVER-CONNECTION 
	      (CHAOS:LISTEN "CHEOPS-SERVER"))
	(PROCESS-WAIT "Waiting for Connection" 
		      (FUNCTION (LAMBDA NIL 
				  (NOT (EQ (CHAOS:STATE CHEOPS-CHAOS-SERVER-CONNECTION)
					   'CHAOS:LISTENING-STATE)))))
	(COND ((NOT (EQ (SETQ STATE (CHAOS:STATE CHEOPS-CHAOS-SERVER-CONNECTION))
			'CHAOS:RFC-RECEIVED-STATE))
	       (FORMAT T "~%BAD STATE ~S" STATE)
	       (CHAOS:CLOSE CHEOPS-CHAOS-SERVER-CONNECTION "Bad State")
	       (GO TOP)))
	(CHAOS:ACCEPT CHEOPS-CHAOS-SERVER-CONNECTION)
IN-LOOP	(SETQ IN-PKT (CHAOS:GET-NEXT-PKT CHEOPS-CHAOS-SERVER-CONNECTION))
	(COND ((< (SWAP-8 (CHAOS:PKT-OPCODE-LEFT-JUSTIFIED IN-PKT)) CHAOS:DAT-OP)
	       (GO NOT-DAT)))
	(SETQ PKT-CH-OPCODE (SWAP-8 (AR-1 IN-PKT (CCP-PKT-WD OP-CODE))))
	(COND ((OR (< PKT-CH-OPCODE 0)
		   (NOT (< PKT-CH-OPCODE CCHAOS-N-PACKET-TYPES)))
	       (GO IN-LOSE)))
	(FUNCALL (AR-1 CCHAOS-PACKET-RECEIVE-DISPATCH PKT-CH-OPCODE) IN-PKT)
   X	(CHAOS:RETURN-PKT (PROG1 IN-PKT (SETQ IN-PKT NIL)))
	(GO IN-LOOP)
  NOT-DAT (COND ((NOT (= (SWAP-8 (CHAOS:PKT-OPCODE-LEFT-JUSTIFIED IN-PKT)) CHAOS:CLS-OP))
		 (GO IN-LOSE)))
	  (CHAOS:CLOSE CHEOPS-CHAOS-SERVER-CONNECTION "")
	  (CHAOS:RETURN-PKT (PROG1 IN-PKT (SETQ IN-PKT NIL)))
	  (GO TOP)
  IN-LOSE (FORMAT T "~% bad input packet ")
  	  (CHAOS:PRINT-PKT IN-PKT)
          (COND ((NULL BAD-PACKET)
                 (SETQ BAD-PACKET IN-PKT)))
	  (GO IN-LOOP)
))

(DEFUN CCHAOS-PKT-POSITION (PKT)
  (PROG (I BADR BPNTR PRIORITY-OPTION)
	(SETQ BADR (SWAP-8 (AR-1 PKT (CCP-PKT-WD BUFFER-MEMORY-LOC))))
	(COND ((OR (NOT (ZEROP (LOGAND BADR 77)))
		   (NOT (< BADR 10000)))
	       (FORMAT T "~%CCHAOS-PKT-POSITION Received Bad BADR Address ~O" BADR)
	       (RETURN NIL)))
	(COND ((ZEROP (SETQ PRIORITY-OPTION (SWAP-8 (AR-1 PKT (CCP-PKT-WD PRIORITY-OPTION)))))
	       (GO T1))	;DONT HACK PRIORITY BLOCK POINTERS
	      ((EQ PRIORITY-OPTION 1)
	       (AS-1 177777 CHEOPS-BUFFER-MEMORY CHEOPS-BUFFER-MEMORY-PRIORITY-BLOCK-POINTER)
	       (CHEOPS-ABORT-PRIORITY))
	      (T (CHEOPS-ABORT-BACKGROUND-AND-CONFIRM)))
   T1	(AS-1 (SWAP-8 (AR-1 PKT (CCP-PKT-WD PS-2)))
	      CHEOPS-BUFFER-MEMORY (+ BADR CHEOPS-ARG-MODES))
	(AS-1 (SWAP-8 (AR-1 PKT (CCP-PKT-WD PS-3)))
	      CHEOPS-BUFFER-MEMORY (+ BADR CHEOPS-ARG-SETD))
	(AS-1 (SWAP-8 (AR-1 PKT (CCP-PKT-WD PS-4)))
	      CHEOPS-BUFFER-MEMORY (+ BADR CHEOPS-ARG-ALPHA))
	(AS-1 (SWAP-8 (AR-1 PKT (CCP-PKT-WD PS-5)))
	      CHEOPS-BUFFER-MEMORY (+ BADR CHEOPS-ARG-BETA))
	(AS-1 (SWAP-8 (AR-1 PKT (CCP-PKT-WD PS-6)))
	      CHEOPS-BUFFER-MEMORY (+ BADR CHEOPS-ARG-FLAGWORD))
	(AS-1 (SWAP-8 (AR-1 PKT (CCP-PKT-WD PS-7)))
	      CHEOPS-BUFFER-MEMORY
	      (+ BADR CHEOPS-ARG-CHAIN-POINTER))
	(SETQ BPNTR (+ BADR 37))
  	(SETQ I (CCP-PKT-WD POS-WD0))
   L	(COND ((NOT (ZEROP (AS-1 (SWAP-8 (AR-1 PKT I))
				 CHEOPS-BUFFER-MEMORY 
				 BPNTR)))
	       (SETQ I (1+ I) BPNTR (1+ BPNTR))
	       (GO L)))
   	(AS-1 177777 CHEOPS-BUFFER-MEMORY BADR)
	(COND ((= PRIORITY-OPTION 1)
	       	(AS-1 (+ CHEOPS-BUFFER-MEMORY-UNIBUS-WD-ADR BADR)
		      CHEOPS-BUFFER-MEMORY 
		      CHEOPS-BUFFER-MEMORY-PRIORITY-BLOCK-POINTER)
		(CHEOPS-WRITE-DOORBELL 3))
	      ((= PRIORITY-OPTION 3)
	       (AS-1 (+ CHEOPS-BUFFER-MEMORY-UNIBUS-WD-ADR BADR)
		     CHEOPS-BUFFER-MEMORY 
		     CHEOPS-BUFFER-MEMORY-BACKGROUND-BLOCK-POINTER)))
	(RETURN T)
))

(DEFUN CCHAOS-PKT-REQUEST-RESULT (PKT)
  (PROG (BADR OUT-PKT ISETD)
	(SETQ BADR (SWAP-8 (AR-1 PKT (CCP-PKT-WD BUFFER-MEMORY-LOC))))
	(SETQ CHEOPS-WAIT-BADR BADR)    ;SPECIAL AND FREE SO CAN BE EXAMINED FROM OTHER
					; PROCESS IF THINGS HANG UP.
	(COND ((OR (NOT (ZEROP (LOGAND BADR 77)))
		   (NOT (< BADR 10000))
		   (> BADR CHEOPS-BUFFER-MEMORY-RING-HIGH-BADR))
     (FORMAT T "~%CCHAOS-PKT-REQUEST-RESULT Received Bad BADR Address ~O, HIGH-BADR ~O"
		       BADR CHEOPS-BUFFER-MEMORY-RING-HIGH-BADR)
	       (RETURN NIL)))
	(SETQ OUT-PKT (CHAOS:GET-PKT))
	(SETF (CHAOS:PKT-OPCODE-LEFT-JUSTIFIED OUT-PKT) (SWAP-8 CHAOS:DAT-OP))
   L	(COND ((NOT (= (AR-1 CHEOPS-BUFFER-MEMORY BADR)
		       1))
	       (PROCESS-WAIT "CHEOPS-WAIT" 
			     (FUNCTION 
			      (LAMBDA NIL 
				      (= (AR-1 CHEOPS-BUFFER-MEMORY CHEOPS-WAIT-BADR)
					 1))))
	       (GO L)))
   	(AS-1 (SWAP-8 (CCHAOS-PACKET-CODE CCHAOS-PKT-RESULTS))
	      OUT-PKT (CCR-PKT-WD OP-CODE))
	(AS-1 (SWAP-8 BADR)
	      OUT-PKT (CCR-PKT-WD BUFFER-MEMORY-LOC))
   L1	(AS-1 (SWAP-8 (SETQ ISETD (AR-1 CHEOPS-BUFFER-MEMORY (+ BADR CHEOPS-ARG-SETD))))  ;SETD
	      OUT-PKT
	      (CCR-PKT-WD PS-3))
	(DO ((BADRL (+ BADR 16) (1+ BADRL))
	     (PKTL (CCR-PKT-WD PS-16) (1+ PKTL))
	     (ENDTST (+ BADR 36)))
	    ((= BADRL ENDTST))
	  (AS-1 (SWAP-8 (AR-1 CHEOPS-BUFFER-MEMORY BADRL)) OUT-PKT PKTL))
	(COND ((NOT (= ISETD (AR-1 CHEOPS-BUFFER-MEMORY (+ BADR CHEOPS-ARG-SETD))))
	       (GO L1)))	;TIMING ERROR, TRY AGAIN.
	(SETF (CHAOS:PKT-NBYTES OUT-PKT) CCR-NBYTES)
	(CHAOS:SEND-PKT CHEOPS-CHAOS-SERVER-CONNECTION
			(PROG1 OUT-PKT (SETQ OUT-PKT NIL)))
))

(DEFUN CCHAOS-PKT-CLOSE-RING-AND-START-BACKGROUND (PKT)
  (PROG (H-BADR T-BADR)
	(SETQ H-BADR (SWAP-8 (AR-1 PKT (CCC-PKT-WD BUFFER-MEMORY-LOC-HEAD))))
	(SETQ T-BADR (SWAP-8 (AR-1 PKT (CCC-PKT-WD BUFFER-MEMORY-LOC-TAIL))))
	(AS-1 (+ H-BADR CHEOPS-BUFFER-MEMORY-UNIBUS-WD-ADR)
	      CHEOPS-BUFFER-MEMORY
	      (+ T-BADR CHEOPS-ARG-CHAIN-POINTER))
	(AS-1 (+ H-BADR CHEOPS-BUFFER-MEMORY-UNIBUS-WD-ADR)
	      CHEOPS-BUFFER-MEMORY 
	      CHEOPS-BUFFER-MEMORY-BACKGROUND-BLOCK-POINTER)
	(SETQ CHEOPS-BUFFER-MEMORY-RING-HIGH-BADR T-BADR)  ;ANY BUFFER MEMORY ADDRESS HIGHER
							   ; THAN THIS IS GARBAGE
))

(DEFUN SWAP-8 (N)
  (DPB N 1010 (LDB 1010 N)))
