;;;		FRIDAY  JAN 20,1978  04:30:22
.FASL
IF1,.INSRT SYS:.FASL DEFS

GETDT0	WTA [ILLEGAL DATE LIST!]
GETDAT: PUSHJ P,GETDT1
	  JRST GETDT0
	POPJ P,

GETDT1: SETZ TT,D
	MOVEI B,(A)
	MOVEI F,99.
	JSP R,FXCAR
	DPB T,[330700,,TT]
	HRRZ B,(B)
	MOVEI F,12.
	MOVEI D,1
	JSP R,FXCAR
	DPB T,[270400,,TT]
	HRRZ B,(B)
	MOVEI F,31.
	JSP R,FXCAR
	DPB T,[220500,,TT]
	AOS (P)
	POPJ P,

FXCAR:	MOVEI T,(B)
	LSH T,-11
	SKIPL ST(T)
	  POPJ P,
	HLRZ T,(B)
	LSH T,-11
	HRRZ T,ST(T)
	CAIE T,.ATOM FIXNUM
	  POPJ P,
	HLRZ T,(B)
	MOVE T,(T)
	CAIL T,(D)
	  CAILE T,(F)
	    POPJ P,
	JRST (R)

.ENTRY DDATE SUBR 002
	PUSH P,[FIX1]
	JRST GETDAT

.ENTRY GETTIM SUBR 002
	PUSH P,[FIX1]
GETTIM:	PUSHJ P,GETTI0
	  JRST [WTA [ILLEGAL TIME LIST!] ? JRST GETTIM]
	POPJ P,

GETTI0:	SETZB TT,D
	MOVEI B,(A)
	MOVEI F,24.
	JSP R,FXCAR
	MOVEI F,60.
	HRRZ B,(B)
	JSP R,FXCAR
	IMULI TT,60.
	ADDI TT,(T)
	HRRZ B,(B)
	JSP R,FXCAR
	IMULI TT,60.
	ADDI TT,(T)
	IMULI TT,2.
	AOS (P)
	POPJ P,

.ENTRY *SET-FILE-REFERENCE-DATE SUBR 003
	;; File object, date-list (<Year> <Month> <Day>)
	;; Returns NIL if successful, else error code.
	PUSH P,A
	MOVEI A,(B)
	PUSHJ P,GETDAT
	MOVE D,TT
	POP P,A
	MOVEI AR1,(A)
	JSP TT,XFILEP
	  JRST [WTA [NOT A FILE - *SET-FILE-REFERENCE-DATE!] ? JRST .-1]
	MOVEI TT,F.CHAN
	MOVE T,@TTSAR(A)
	.CALL SRDATE
	  SKIPA F,[FIX1]
	   MOVEI F,CPOPJ
	SETZ A,
	JRST (F)

SRDATE:	SETZ
	SIXBIT \SRDATE\
	MOVE T
	MOVE (FXP)
	SETZB TT

.ENTRY SET-FILE-REFERENCE-DATE SUBR 003
	;; Filename, date-list.
	PUSH P,A
	MOVEI A,(B)
	PUSHJ P,GETDAT
	PUSH FXP,TT
	POP P,A
	SETZ B,
	CALL 2,.FUNCTION MERGEF
	PUSHJ P,FIL6BT
	LOCKI
	.CALL [SETZ ? SIXBIT \OPEN\ ? MOVSI .UAI+8. ? MOVEI 0 
		MOVE -4(FXP)
		MOVE -2(FXP)
		MOVE -1(FXP)
		MOVE -3(FXP)
		SETZB TT]
	  JRST [MOVEI B,.ATOM OPEN ? JRST SRDAT1]
	SETZB T,A
	.CALL SRDATE
	  JRST [MOVEI B,.ATOM SRDATE ? JRST SRDAT1]
SRDAT0:	.CLOSE 0,
	PUSHJ P,INTREL
	SUB FXP,[5,,5]
	POPJ P, 

SRDAT1:	JSP T,FXCONS
	CALL 2,.FUNCTION XCONS
	JRST SRDAT0 

.ENTRY *SET-FILE-CREATION-DATE SUBR 003
	;; File object, date-list, time-list
	