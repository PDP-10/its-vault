TITLE LOGO
;         TM
;(C) COPYRIGHT,1970,BOLT BERANEK AND NEWMAN INC., CAMBRIDGE, MASS.

MLON

;CONFIGURATION PARAMETERS 

TEN50==1
TENEX==2
BBN50==4
TURTLE==10
SAVBRK==20	;IF ON, SAVEUP THE STATE OF LOGO SO THAT "GO" PROCEEDS
OFILE==40	;OLD STYLE FILES
NFILE==100	;NEW STYLE FILES
LEVELC==200
DRIBBLE==400
CONFIG==TEN50!BBN50!NFILE

ERPDLL==20	;LENGTH OF ERROR PDL, NEEDS TO BE LONG ENOUGH TO
		; AVOID PDL OVERFLOW TRAP

;ACCUMULATOR ASSIGNMENTS
F=0	;FLAGS AND BITS 
A=1
B=2
C=3
D=4
E=5
G=7
H=10
R=11
L=12
M=13
N=14
W=15
S=16	;ARGUMENT PUSHDOWN STACK
P=17	;CONTROL PUSHDOWN STACK

;FLAGS FOR THE LH OF F

LDONF==1	;LINE DONE FLAG FOR EDIT MODE
EDITF==2
UPFF==4		;COMEXR CALLED FROM RETURN CODE, NOT FROM COMEX
TIF==10
BREAKF==20
TOF==40		;TYPE OR PRINT IN PROGRESS
GOF==100	;USER TYPED  DIRECT GO
RQF==200	;A REQUEST IN PROGRESS
COMF==400	;1 IF USER PROCEDURE BEING LEFT SHOULD BE TREATED LIKE COMMAND
GCF==1000	;1 IF THIS K HAS NOT BEEN GARBAGE COLLECTED YET
TITLEF==2000	;DOING TITLE DURING A TO
BROKE==4000	;ERROR WAS NON-TYI BREAK KEY
GETF==10000	;GET IN PROGRESS
SAVEF==20000	;SAVE IN PROGRESS, FOR ALTERNATE FORMS OF TYPEOUT
NOBREAK==100000	;TO TEMPORARILY INHIBIT BREAK KEY
FCHARF==200000	;FOR COMPUTING THE TIME BETWEEN _ AND CHAR 1
GCCSF==400000	;PROGRAM EDIT DONE, CODE PTRS ON STACK MAY BE INVALID


;FLAGS FOR THE RIGHT HALF OF F

TF==1	;A TEMPORARY FLAG FOR MANY ROUTINES
PMF==2	;+- SIGN
NWF==4	;NOT WORD INPUT
CRF==10
FSYMF==20	;0 IF FIRST ELEMENT OF LINE BEING COMPILED,USED W NNUMF
NNUMF==40	;NON-NUMERIC INPUT
PREFIX==200	;FOR PWORD
SUFFIX==400	;ALSO FOR PWORD
MAKEF==1000	;COMPIL CALLED WITH THIS=1 MEANS CALLED FROM MAKE
STORED==2000
FLUSHF==4000	;GETTING AN ALREADY DEFINED PROCEDURE
EABBRF==10000	;DOING LIST ENTRY ABBREVIATIONS
ECONTF==20000	;  "    "     "   CONTENTS
EENTRF==40000	;  "    "     "   ENTRY
ENAMEF==100000	;  "    "     "   NAMES
EELSEF==200000	; 1_ELSE IS OK LINE TERMINATOR
ESCHF==400000	;SEEN A NON-PRINTING CHAR IN PRODNM


;FLAG DEFINITIONS FOR LH OF STRING POINTER

IMMEDIATE==400000	;POINTER IS NOT A POINTER, BUT IS DATA
COMPOUND==200000	; ELEMENT POINTED TO IS A LIST OF POINTERS
WORDF==100	;STRING IS A WORD
SENTF==200	;SENTENCE
EMPTYF==400	;EMPTY
UNBOUN==40000	;FOUND  IN VP AND DP, 1_NOT EXPLICITLY SET
GLOBLF==100000	; "      " "   "  " , 1_TOPMOST BINDING, DONE BY MAKE

;FLAG DEFINITIONS FOR LH OF COMPILED ELEMENT

MPF==100000
UPRF==40000
VARF==20000
LITF==10000
INFOP==4000	;IS AN INFIX OPERATOR
MVARF==2000	; LIKE VARF, BUT TABLE APPEARS IN CODE
COMMTF==1000	;IS A COMMENT
ANDF==400	;USED IN TO LINES, IMPLIES THIS DUMMY PRECEDED BY AND
SCHF==20000	;USED IN USER PROCEDURE NAMES, 1_NOT PROPER FORM
MAKESC==40	;MEANS 2 LINE MAKE AND END OF NAME:
;ALSO IN THE LEFT HALF OF COMPILED ELEMENT CAN BE STRING BITS


;FLAG DEFINITIONS FOR LEFT HALF OF FIRST WORD OF RP PAIR

TRACEF==100000	;THIS PROCEDURE IS BEING TRACED

;FLAG DEFINITION FOR LH OF 2ND WORD OF MACHINE NAME PAIR

COMPUT==400000	;IF ONE, THIS NAME MUST BE COMPUTED, ADDR IN RH


;UUO DEFINITIONS

OPDEF ERROR [1B8]
OPDEF EXPAND [2B8]
OPDEF GARBAGE [3B8]
OPDEF SQUEZE [4B8]
OPDEF PJRST [254B8]

;STORAGE ALLOCATION TABLE DEFINITIONS

DEFINE TABLES
<TT RP,40,<[EXP RPA,0]>	;THESE THREE MAY BE IN ANY ORDER
 TT VP,40,<[EXP VPA,0]>
 TT UA,40
 TT PS,200,<[EXP PSA,PSD,PSM,0]>	;THESE THREE MUST BE IN THIS ORDER
 TT DP,200,<[EXP DPC,0]>
 TT SP,40,<[EXP UUOACS+S,0]>	;THESE TWO MUST OCCUR IN THE SAME ORDER AS IN ACS
 TT PP,200,<[EXP UUOACS+P,0]>
FOR TENEX,< TT WS,2000,<[EXP W,WSA,WSM,WSB,WSD,UUOACS+W,0]>>
FOR TEN50,<TT WS,2000,<[EXP W,WSA,WSM,WSB,WSD,UUOACS+W,BUFLOC,BUFLOC+1,FPTR,0]>>
;THIS ONE MUST ALWAYS BE LAST SO MEM TRAP CAN BE USED AS END TEST
>
DEFINE POINTR
<MM RP,A
 MM VP,A
 MM PS,A
 MM PS,D
 MM PS,M
 MM DP,C
 MM WS,A
 MM WS,M
 MM WS,D
 MM WS,B>

DEFINE U(A1)<UU(A1,1)>
DEFINE UU(A1,A2)<
A1:	BLOCK A2 >
LOC 140

;MACRO TO MAKE CONFIGURATION DEPENDENT STUFF READ LIKE ENGLISH

DEFINE FOR (WHO,WHAT)
<IFN CONFIG&WHO, <WHAT>>

DEFINE NOTFOR (WHO,WHAT)
<IFE CONFIG&WHO, <WHAT>>

DEFINE UNDEX (AC)
< ADD	AC,[XWD 070000,0]
  CAIG	AC,0
  SUB	AC,[XWD 430000,1]>


;UNSHARED AREA, NON-ZERO STUFF FIRST

FURST==.	;IN CASE DIFFERENT SYSTEMS PUT DATA IN DIFFERENT PLACES
UU UUOTRP,2;	;Z
		;JRST DOUUO
DEFINE MM (A1,A2)
<U A1'A2>
POINTR
U PTOP;	;ORDER OF THE NEXT THREE IS EXTREMELY IMPORTANT, MUST BE
U DTOP;
U WTOP;
U RANNO;	;STUFF TO COMPUTE PSEUDO-RANDOM DIGIT

FOR TENEX,<	UU JFNTAB,10;	;FOR OPENNING FILES
		UU CHNTAB,^D17>	;PSEUDO-INTERRUPT CHANNEL DISPATCHES
;OTHER GOOD FILE STUFF

U CHIN;		;INSTRUCTION TO EXECUTE TO READ A CHARACTER
U CHOUT;	;INSTRUCTION TO EXECUTE TO WRITE A CHARACTER
U INFILE;
FOR OFILE,<U OUTFILE>
FOR NFILE,<
U FPTR;		;BYTE PTR INTO A BUFFER (ALWAYS ASSOCIATED WITH LBUF+1)
U CCOUNT;	;COUNT OF CHARS REMAINING UNUSED IN LBUF+1
U TANDD;	;TIME AND DATE OF STARTUP
U FILEAD;	;FIRST BLOCK OF ENTRY BEING WRITTEN
U FILECT;	;COUNT OF BLOCKS USED IN WRITING THIS FILE
U LABS;		;WHERE PTR TO TOP OF FREE SPACE IS STORED DURING OPERATION
U LDIRNO;	;BLOCK NUMBER OF PREVIOUS DIRECTORY
U LDIRL;	;NUMBER OF ENTRIES IN LDIRNO
U FILSIZ;	;SIZE IN DISC BLOCKS OF FILE
UU DMPLST,2

;DATA FOR EACH PHYSICAL BUFFER

UU BUFLOC,2;	;LOCATION IN CORE
UU PBLOCK,2;	;LOGICAL BLOCK NUMBER IN THE BUFFER
UU CHANGE,2;	;0 IF NOT ALTERED

;DATA FOR EACH LOGICAL BUFFER

UU LBLOCK,2;	;LOGICAL BLOCK NUMBER IN THIS LOGICAL BUFFER
UU LBUF,2;	;PHYSICAL BUFFER USED BY LOG BUFFER (0 OR 1)

FOR TEN50,<
UU OPNPAR,3;	;THIS MUST PRECEDE LUKDAT FOR COPY TO CLEAR!
UU LUKDAT,4
UU BUFADR,2;	;STRING POINTER LOCATIONS OF FILE BUFFERS
>>
FOR DRIBBLE,<
U DRIBFL;	;FILE NUMBER OF DRIBBLE FILE
U DRIBTM;	;TIME OF TYPING _ FOR THE TIME STAMP
>

DEFINE TT(A1,A2,A3)
<U A1>
TABLES
U CHARNO;	;THESE FOR INPUT SECTION
U BCHAR
U TRACEM;	;LIKE BCHAR BUT ONLY FOR TRACE OUTPUT
U SCOUNT
U LINENO
U PRODNM;	;NAME OF PROCEDURE IN PROGRESS
U TRUTH
U CBOT;	;BASE OF LINE PTED TO BY CPP
U CSTOCT;	;COUNT OF ROOM LEFT IN LINE BEING COMPILED
U CPP;		;POINTER INTO COMPILED LINE
U SPP;		;CONTENTS OF S AND P AT START OF LINE
U LINBOT
U SRCBOT
U NEWBOT
U NXLINE;	;PTR TO NEXT LINE IN CURRENT PROCEDURE
U THISPR;	;MACHINE PROCEDURE LAST IN, PTR TO 2ND ENTRY IN CMPT
U TOPROD;	;NAME OF PROCEDURE BEING DEFINED
U BRKTEM;	;TEM STORAGE FOR BREAKY ROUTINE
FOR TENEX,<U BRKTEB>	;MORE TEM FOR BREAK KEY
U RADIX;	;RADIX FOR CURRENT NUMBER CONVERSION
U GODEPT;	;THE NUMBER OF GO'S IN THE STACK
U TOFDAY;	;TIME OF STARTUP IN SECS SINCE MIDNIGHT OR RESET CLOCK
U BSP;		;SPP ASSOCIATED WITH GODEPTH, STACKS AT LAST SAVED BREAK
U GCTEM;	;TEM STORAGE FOR GC AND ALLOCATOR
UU UUOACS,20
UU ERRPDL,ERPDLL;	;FIXED PUSHDOWN FOR ERROR HANDLER
FOR TENEX,<	UU LEVLTB,3>	;FOR PSI SYSTEM
U GETDST;	;STUFF FOR COPY
U SAVSRC
FOR TENEX,<U COPJFN>
FOR TEN50,<
U FPTR2
U CCNT2
U BUFLC2;	;ABS ADDR OF COPY BUFFER
U BUFAD2>	;WS ADDR OF COPY BUFFER
BLAST==.-1

EXTERNAL JOBUUO,JOB41,JOBSA,JOBREL

FOR TEN50,<
EXTERNAL JOBREN,JOBAPR,JOBOPC
EXTERNAL JOBDDT,JOBFF
EXTERNAL JOBCNI,JOBTPC

HISEG>
FOR TENEX,<RELOC>	;PAGE 0 WILL BE WRITABLE,1-LOGEND NOT

LOGO:	MOVEI	F,0	;CLEAR FLAGS, MAY BE SKIPPED ON CERTAIN RESTARTS
FOR TEN50,<
	CALLI	0	;RESET
	MOVEI	A,BREAKY
	MOVEM	A,JOBREN	;FOR BREAK KEY
	MOVEI	A,ALLOC
	MOVEM	A,JOBAPR	;FOR ILLEGAL MEMORY REFS
	HLRZ	A,JOBSA		;INITIALIZE THE LOW SEG
	HRLI	A,(A)
	ADDI	A,1
	HRRZ	B,JOBREL	;TOP OF HIGHEST K ASSIGNED
	SETZM	-1(A)
	BLT	A,(B) >		;CLEAR FROM JOBFF TO TOP OF ASSIGNED CORE

FOR TENEX,<
	RESET
	MOVE	A,[SIXBIT /LOGO/]
	SETNM				;SET UP NAME FOR SYSTAT
	MOVE	A,[XWD 20000,20001]
	SETZM	20000
	BLT	A,21777 >
FOR TEN50,<
	MOVE	1,[SIXBIT /LOGO/]
	CALL	1,[SIXBIT /SETNAM/] >

	MOVE	A,[JSR UUOTRP]
	MOVEM	A,JOB41		;FOR UUOS

	MOVE	A,[XWD FURST,FURST+1]
	SETZM	FURST
	BLT	A,BLAST	;CLEAR FROM START OF CORE TO EITHER DDT OR JOBFF

	MOVE	A,[XWD SPFRST,FURST]
	BLT	A,FURST+SPLLEN-1 ;SET UP NON-ZERO PART OF UNSHARED CORE

FOR TEN50,<	HLRZ	B,JOBSA>	;ALWAYS THE FIRST FREE LOCATION
FOR TENEX,<	MOVEI	B,20000>	;ENOUGH ROOM FOR CODE AND SYMBOLS
	HRRZI	A,RP
INILU:	HLRZ	C,ALOCTB-RP(A)	;POINTER TO LIST OF CELLS TO BE SET UP

	HRRZM	B,(A)		;RP ETC.
	JUMPE	C,INILUR	;NO SUBSIDIARY LIST

	MOVE	E,(C)
	JUMPE	E,INILUR	;END OF SUBSIDIARY LIST
	HRRM	B,(E)		;NO, RPA ETC.
	AOJA	C,.-3		;NEXT IN THIS LIST

INILUR:	ADD	B,ALOCTB-RP(A)	;HOW MUCH TO GIVE TO THIS SPACE
	CAIE	A,WS
	AOJA	A,INILU		;MORE SPACES TO GO

	MOVEI	A,0
	JSP	C,SETPDL

FOR TEN50,<
	HRRZ	A,JOBREL
	CAIGE	A,@WTOP	;IS WTOP INSIDE CURRENT ALLOCATION?
	EXPAND	WS	;NO, GET A K SO WS WILL FIT
	PUSHJ	P,SETTTM	;SET TELETYPE MODE TO BBN'S TELCOMP MODE
	MOVEI	A,220000
	CALL	A,[SIXBIT /APRENB/]	;REQUEST PDL AND ILL MEM REF TRAPS
>

FOR TENEX,<
	MOVEI	A,@WTOP
	JSP	E,SETMEM >	;LOCK PAGE ABOVE ABOVE ADDRESS

	HRRZI	A,(W)
	HRLI	A,.WBASE
	BLT	A,.WTOP-.WBASE-1(W)
	HRRZI	A,FLUSHM
	HRRM	A,JOBSA	;DON'T REDO THE INITIAL STORAGE ALLOCATION

	PUSHJ	P,TIMDAY	;IN SECONDS SINCE MIDNIGHT
	TRNN	F,TF
	MOVEM	C,TOFDAY	;TIME OF LAST RESET CLOCK

	MOVEI	B,[ASCIZ /
LOGO

/]
	TRNN	F,TF	;1 IF CALLED BY ERASE ALL
	PUSHJ	P,TOSS


RESET:	SETZM	TRACEM		;TRACE MARGIN 

FOR TEN50,<	PUSHJ	P,SETTTM	;ENTER TELCOMP MODE
	MOVEI	A,220000
	CALLI	A,16>		;APR ENABLE

FOR TENEX,<
	CIS		;CLEAR PSI SYSTEM
	MOVEI	A,400000	;DENOTE THIS FORK
	MOVE	B,[XWD LEVTAB,CHNTAB]
	SIR			;SET UP PSI SYSTEM
	MOVEI	A,0		;BREAK TO CHANNEL 0
	ATI			;ASSIGN TERMINAL INTERRUPT
	MOVEI	A,400000	;THIS FORK
	MOVE	B,[XWD 400702,0];CHANNELS 0,9,10,11,16
	AIC			;ACTIVATE THESE CHANNELS
	EIR			;ENABLE THE SYSTEM
	MOVEI	A,101
	MOVE	B,[EXP 525252525252]
	MOVE	C,B
	SFCOC		;SET ALL CONTROL CHAR TYPEOUTS TO BE THEMSELVES
	MOVEI	A,100
	RFMOD			;READ THE TERMINAL INPUT MODES
	ANDCMI	B,770000	;ZERO OUT THE WAKEUP SET
	IORI	B,140000	;SET THE WAKEUP SET TO ALL CONTROLS
	SFMOD >			;SET THEM


	SQUEZE			;TAKE OUT ANY EXTRA ALLOCATION

	TRZE	F,TF	;1 IF CALLED BY ERASE ALL
	JRST	MAINL
FOR DRIBBLE,<		;INITIALIZE THE DRIBBLE FILE
FOR TENEX,<
	TLO	F,NOBREAK	;DO NOT ALLOW BREAK KEY HERE
	MOVEI	B,[ASCIZ /INITIALS PLEASE: /]
	PUSHJ	P,TIS		;REQUEST INITIALS
	 JRST	.-2		;RUBOUT
	MOVEI	A,[ASCIZ /DRB/]
	MOVEM	A,JFNTAB+5
	MOVE	B,0(S)
	MOVEI	A,10
	CAMGE	A,@WSB
	 ERROR	FILER9
	ADDI	B,1(W)
	HRRZM	B,JFNTAB+4
	MOVE	A,[ASCIZ /NMI/]
	CAMN	A,(B)		;TO OVERRIDE DRIBBLE CAPABILITY?
	 JRST	DRIBEND		;YES
;FIX UP THE INPUT NAME
	MOVEI	A,1
	MOVEM	A,JFNTAB
	MOVEI	A,JFNTAB
	SETZ	B,
	GTJFN
	 ERROR	IOPERR
	MOVE	B,[XWD 070000,020000]	;FOR APPENDING
	OPENF
	 ERROR	IOPERR
	MOVEM	A,DRIBFL

	MOVEI	B,";"
	BOUT
	SETO	B,
	SETZ	C,
	ODTIM
	MOVEI	B," "
	BOUT
	MOVE	D,0(S)
	HRLI	D,010700+W
	ILDB	B,D
	JUMPN	B,.-2
	MOVEI	B,37
	BOUT
DRIBEND:	POP	S,A
	TLZ	F,NOBREAK
>>

MAINL:	MOVEI	B,[ASCIZ /_/]
	SKIPE	TOPROD	;ARE WE DEFINING A PROCEDURE NOW?
	MOVEI	B,[ASCIZ /@/]	;YES, READY CHAR IS @
	PUSHJ	P,TIS
	 JRST	MAINL
MAINL1:	PUSHJ	P,COMPIL
	 JRST	MAINL	;GOT A STORED LINE
	PUSHJ	P,EXECUT
	JRST	MAINL


DOUUO:	MOVEM	17,UUOACS+17
	HRRZI	17,UUOACS
	BLT	17,UUOACS+16
	MOVE	17,UUOACS+17
	HLRZ	A,JOBUUO
	ASH	A,-11	;FLUSH AC, IF ONE
	CAILE	A,4	;SQUEZE IS THE HIGHEST KNOWN UUO
	JRST	ILLUUO
	JRST	@.(A)	;ZERO CAUGHT BY MONITOR
	 ERRORR
	 ALLOCATOR
	 CALGAR
	 CMPRSS
CALGAR:	JSP	P,GARCOL	;NO PUSHES OR POPS IN GC
	JRST	UUORET

FOR TEN50,<
CALDDT:
FOR BBN50,<MOVEI A,20000	;LEAVE TELCOMP MODE IF IN IT
	TTCALL	6,A
	TLZ	A,600000
	TTCALL	7,A >
	HRRZ	A,JOBDDT
	JUMPE	A,[ERROR .]
	PUSHJ	P,(A)	;SO DEBUGGER MAY POPJ P,$X TO RETURN
	PUSHJ	P,SETTTM
	JRST	COMEX

SETTTM:
FOR BBN50,<	MOVEI	A,20000
	TTCALL	6,A
	TLO	A,600000
	TTCALL	7,A >
	POPJ	P,

BREAKY:	TTCALL	11,
	TTCALL	12,
	TLNE	F,NOBREAK	;IGNORE BREAK KEY?
	JRST	.+3		;YES
	TLNE	F,TIF	;LEAVE IT SET FOR BRAKER ROUTINE
	ERROR	BREAK	;BREAK IMMEDIATELY IF TYPE IN HUNG
	TLO	F,BREAKF	;OTHERWISE, JUST FLAG IT FOR LATER
	JRST	2,@JOBOPC >

FOR TENEX,<
BREAKY:	MOVEM	A,BRKTEM
	MOVEM	B,BRKTEB
	HRLZI	A,400000	;DENOTE THIS FORK
	MOVE	B,A		;DENOTE INTERRUPT CHANNEL 0
	DIC			;DEACTIVATE BREAK KEY
	MOVE	B,BRKTEB
	MOVEI	A,100
	CFIBF		;CLEAR THE INPUT BUFFER
	MOVEI	A,101
	CFOBF		;OUTPUT BUFFER TOO
	MOVE	A,BRKTEM
	TLNE	F,NOBREAK	;IGNORE BREAK?
	JRST	.+3		;YES
	TLNE	F,TIF
	ERROR	BREAK
	TLO	F,BREAKF
	DEBRK			;RETURN TO STOPPED PROCESS

CALDDT:	PUSHJ	P,770000	;FOR NOW, ASSUME RESIDENT DDT
	JRST	COMEX >

COMPIL:	TRZ	F,STORED
	MOVEI	A,10
	MOVEM	A,CSTOCT
	PUSHJ	P,MAKELM
	TLO	B,COMPOUND
	MOVEM	B,CBOT
	MOVEM	B,CPP		;MAKE AN ELEMENT TO PUT COMPILED LINE IN
CMCOMP:	TRZ	F,FSYMF		;ENTER HERE FROM CMAKE
	POP	S,L
	MOVE	M,UA		;IF THE DEPTH OF ABBREVIATION EXPANSION
	SUB	M,UA+1		;IS GREATER THAN THE NUMBER OF ABBS
	LSH	M,-1		;THEN THERE IS A LOOP IN ABB DEFINITION
	HRLZI	M,-2(M)		;-MAX NO OF ABBREVIATIONS,,0
COMPAB:	HRLI	L,10700+W
	MOVEM	L,LINBOT
	IBP	L
GNS:	TRZ	F,NNUMF!ESCHF
	SETZB	N,SCOUNT	;CLEAR TYPE OF SYMBOL (N) AND CHAR COUNT
	PUSHJ	P,NEWSTR
GNS1:	LDB	C,L		;PICK UP PREV TERMINATR, MAY BE RELEVANT
	JRST	.+2
	ILDB	C,L
	CAIN	C," "
	JRST	.-2
	JRST	GNS2		; HAVE FIRST NON-SPACE CHAR OF SYMBOL

GNSLET:	CAIN	C,"_"		; IS IT ASSIGNMENT OPERATOR?
	JRST	GNSMAK		; YES
GNSABC:	TRO	F,NNUMF		; NON-NUMERIC CHAR SEEN
GNSB:	IDPB	C,B		;STORE CHAR IN SYMBOL
	AOS	SCOUNT		; UPDATE LENGTH OF SYMBOL
	ILDB	C,L		; NEXT CHAR OF INPUT
GNS2:	JUMPE	C,GNSEND	; POSSIBLE END OF INPUT STRING
	CAIL	C,77		; NOT, ? AND ABOVE ARE ALL PRINTING
	JRST	GNSLET		; CHARS AND LEGAL SYMBOL CONSTITUENTS
	CAIL	C,40		; BELOW 40 ARE ALL CONTROL CHARS
	JRST	GNSSCH		; NOT, THE REST TAKE SPECIAL CARE
	CAIN	C,24		; IS A CONTROL, IS IT CONTROL-T?
	JRST [	MOVEI D,GNSLIT	; YES, THEN IT IS THE QUOTE FOR FILES
		JRST GNSCH1 ]
GNODCH:	TRO	F,ESCHF		; THE REST THAT GOT THRU ARE SYMBOL
	JRST	GNSABC		; CONSTITUENTS BUT NOT FOR PROCEDURE NAM


GNSSCH:	MOVE	D,INFCTB-40(C)	; TABLE ENTRY FOR THE CHAR
	TLNN	D,TERM		; IS IT A TERMINATOR FOR PREVIOUS SYMBOL
	JRST	(D)		; NO, THEN DISPATCH ON ITS TYPE
GNSCH1:	SKIPE	SCOUNT		; IS THERE A PREVIOUS SYMBOL?
	JRST	GNSEND		; YES, THEN FIND OUT WHAT KIND IT IS
	TLNN	D,FOP		; IS IT AN INFIX OPERATOR?
	JRST	(D)		; NO, THEN IT HAS A SPECIAL HANDLER
	IBP	L		; YES, LETS NOT SEE THIS ONE AGAIN
	HRR	N,D		; YES, RH IS PTR INTO PRINTNAME TABLE
	HRLI	N,INFOP!IMMEDI	; LH OF COMPILED ELEMENT SAYS INFIX OP
	JRST	CMPLA		; AND WE STORE IT IN THE COMPILED CODE

GNSMAK:	MOVE	N,[XWD INFOP!IMMEDI,IMAKEL+1]
	JRST	CMPLA

GNSEND:	SKIPN SCOUNT	;NEW SYMBOL EMPTY?
	JRST ABBJ	;YES, AT END OF A WHOLE INPUT STRING
	PUSHJ	P,ENDSTR	;GOT A SYMBOL, FINISH IT
	POP	S,A		;THIS IS IT
	TRNN	F,NNUMF	;MIGHT IT BE A NUMBER, IE A LITERAL
	JRST	GNSN	;IT IS A POSSIBLE LINE NUMBER


;CHECK ELEMENT TO SEEE IF IT IS AN ABBREVIATION

ABBA:
	HRRZ	B,UA	;USER DEFINED ABBREVIATIONS FIRST
	PUSHJ	P,LOOKY
	 JRST	NOABBS
	MOVE	C,1(N)
	CAMN	C,[EXP -1]
	JRST	NOABBS	;A DELETED ABBREVIATION
	JUMPE	C,GNSP3	;A DELETED SYSTEM ABBREV, MUST BE A UPROC
	PUSH	S,LINBOT
	SUB	L,LINBOT
	PUSH	P,L	;SAVE RELATIVE POINTER INTO SOURCE STRING
	HRRZ	L,1(N)	;USE THE VALUE OF THE ABBREV AS THE NEW SOURCE
	AOBJN	M,COMPAB	;WE ARE NOW ONE LEVEL DEEPER IN ABBREVS
	ERROR	ABBER1		;LOOP IN ABBREVIATION DEFINITION


ABBJ:	TRNN	M,-1	;TOP LEVEL ABBREVIATION?
	JRST	CMPLA	;YES, DONE WITH WHOLE INPUT, ADD TERMINATOR
	POP	S,LINBOT	;NOT TOP LEVEL
	POP	P,L
	ADD	L,LINBOT
	SUB	M,[XWD 1,1]	;UP ONE LEVEL
	JRST	GNS1	;CONTINUE WITH PREVIOUS INPUT STRING

DEFINE XOP (RH,LH)
<XWD LH,RH>
TERM==1
FOP==2

INFCTB:
XOP GNSEND,TERM		;SPACE
XOP GNSABC		; !
XOP GNSLIT,TERM		; "
XOP GNODCH		; #  ,NOT LEGAL IN PNAME BECAUSE IT ECHOES AS SP
REPEAT 4,<XOP GNSABC>	; $%&'
XOP GNSLP,TERM		; (
XOP GNSRP,TERM		; )
XOP ITIMES+1,TERM+FOP	; *
XOP IPLUS+1,TERM+FOP	; +
XOP GNSABC		; ,
XOP IMINUS+1,TERM+FOP	; -
XOP GNSABC		; .
XOP IQUOT+1,TERM+FOP	; /
REPEAT 12,<GNSB>	; DIGITS 0-9
XOP GNSVAR,TERM		; :
XOP GNSCOM,TERM		; ;
XOP ILTHAN+1,TERM+FOP	; LEFT ANGLE BRACKET
XOP IEQUAL+1,TERM+FOP	; =
XOP IGRTR+1,TERM+FOP	; RIGHT ANGLE BRACKET

;TO RESTORE LOGO TO A PREFIX ONLY LANGUAGE, ALL "TERM+FOP"S 
; SHOULD BE CHANGED TO GNSABC, IE LEGAL SYMBOL CONSTITUENTS


GNSLP:	SKIPA	N,[LPREN]
GNSRP:	MOVEI	N,RPREN
	IBP	L
CMPLMP:	TLO	N,MPF!IMMEDIATE
	HRLZI	C,200000
	TDNE	C,(N)	;IS THIS MACHINE PROCEURE "MAKE"?
	JRST	CMAKE	;YES
	ADDI	N,1	;NO, STORE MACHINE PROCEDURE CALL
CMPLA:	PUSHJ	P,CSTORE
	TRO	F,FSYMF	;NO LONGER FIRST ELEMENT
	JUMPN	N,GNS
CMPEND:	TRNE	F,MAKEF
	POPJ	P,
	MOVE	A,CPP
	MOVE	B,CBOT
	SUBI	A,(B)
	HRRZM	A,@WSB		;SHORTEN THE ELEMENT
	MOVEM	B,CPP		;AND MAKE CPP POINT AT BEG OF LINE
	TRZE	F,STORED
	JRST	TOLINE
	JRST	CPOPJ1



GNSN:	HRLZI	N,LITF!WORDF+W
	HRR	N,NEWBOT
	TRNN	F,FSYMF!MAKEF	;IS IT FIRST ELEMENT AND NOT IN 2 LINE MAKE?
	TRO	F,STORED	;YES, NUMBER+FIRST_STORED LINE
	JRST	CMPLA

;NOT AN ABBREVIATION, CHECK FOR EXISTING PROCEDURE NAMES

NOABBS:	MOVE	A,NEWBOT
	PUSHJ	P,SYSLKP
	 JRST	GNSP3
	SKIPL	(N)	;IS IT AN ABBREVIATION
	JRST	CMPLMP	;NO, A MACHINE PROCEDURE NAME
	PUSH	S,LINBOT
	SUB	L,LINBOT
	PUSH	P,L
	HRRZ	L,1(N)
	HRLI	L,350700
	SETZM	LINBOT
	AOBJN	M,GNS
	ERROR	IOPERR		;SYSTEM ABB MUST TERMINATE THE LOOP


GNSP3:	MOVE	A,NEWBOT
	HRRZ	B,RP
	PUSHJ	P,LOOKY
	 JRST	.+2
	JRST	GNSP1	;AND CALL THIS A USER PROCEDURE

	MOVEI	E,2(N)	;SPACE FOR THE NEW ENTRY IN TABLE
	CAML	E,RP+1	;IS IT THERE ALREADY?
	EXPAND	RP
	MOVE	A,NEWBOT
	MOVEM	A,(N)	;NAME OF NEW PROCEDURE
	SETOM	1(N)	;NOT YET TO'D
	SETZM	2(N)	;NEW END OF TABLE
GNSP1:	SUB	N,RP
	TLO	N,UPRF!IMMEDIATE
	TRZE	F,ESCHF
	TLO	N,SCHF
	AOJA	N,CMPLA

;COMMENTS, LITERALS, AND VARIABLE NAMES GET COPIED AND EXTRA SPACES FLUSHED

GNSLIT:	TLO N,LITF
	JRST WEFA
GNSCOM:	TLOA	N,COMMTF
GNSVAR:	TLO	N,VARF
WEFA:	MOVEI	D,(C)	;SAVE THE PROPER TERMINATOR
	PUSHJ	P,WEFS
	IBP	L
WEFAB:	TLNE	N,VARF		;STARTED BY : ?
	JRST	VAREND		; ENDING A VERIABLE IS SPECIAL
	POP	S,B
	IOR N,B
	JRST	CMPLA

WEFS:	TRZ	F,TF!NWF	;MAKE A WELL-FORMED STRING,
				; I.E. NO LEADING SPACES, NO TRAILING,
				; AND AT MOST ONE IN THE MIDDLE
	ILDB	C,L
	CAIN	C," "
	JRST	.-2
WEFC:	CAIN	C,(D)	;DONE YET?
	JRST	WEFB
	JUMPE	C,WEFD	;NO SECOND " / OR ;
	TRO	F,TF	;HAVE SEEN A REAL CHAR
	IDPB	C,B
	ILDB	C,L
	CAIE	C," "
	JRST	WEFC
	ILDB	C,L
	CAIN	C," "
	JRST	.-2	;SAD SPC
	CAIN	C,(D)
	JRST	WEFB
	JUMPE	C,WEFD
	MOVEI	C," "
	IDPB	C,B
	LDB	C,L	;GET FIRST CHAR OF NEXT WORD AGAIN
	TRO	F,NWF	;GOT MORE THAN ONE WORD
	JRST	WEFC

WEFD:	TLNN	N,COMMTF	;IF A COMMENT, EOL IS OK
	ERROR	INERR1	;BUT NOT LITS OR VARS
WEFB:	TRNE	F,TF	;IS THIS THING EMPTY?
	JRST	ENDSTR	;NO, FINISH THE STRING NORMALLY
	PUSH	S,[XWD W+WORDF!SENTF!EMPTYF!,]	;EMPTY
	POPJ	P,

VAREND:	MOVE	A,(S)		; GOT A VARIABLE NAME
	TLNE	A,EMPTYF	; IS IT EMPTY?
	 ERROR	NMERR5		; YES, :: NOT PERMITTED
	MOVEI	B,MV
	PUSHJ	P,SYSLUK	; IS IT ONE OF THE BUILT IN NAMES?
	 JRST	VAREN2		; NO
	POP	S,A		; FLUSH THE GENERATED SYMBOL
	TLO	N,MVARF!IMMEDI	; CALL IT A MACHINE VARIABLE
	AOJA	N,CMPLA		; AND STORE ABS PTR TO VALUE WD IN MV

VAREN2:	MOVE	A,(S)
	MOVE	B,VP		; SEE IF IT EXISTS IN OBLIST
	PUSH	S,[XWD W+WORDF!SENTF!EMPTYF!UNBOUN!GLOBLF,0]
	PUSHJ	P,LOOKY
	 PUSHJ	P,MAKEN	; NOT ALREADY THERE, ADD IT, VALUE AS ABOVE
	POP	S,A
	POP	S,A
	SUB	N,VP		; COMPILED ELEM IS PTR REL TO VP
	TLO	N,VARF!IMMEDI	; AND IS FLAGGED AS A VARIABLE
	AOJA	N,CMPLA

CMAKE:	TRNE	F,MAKEF	;ALREADY IN A 2 LINE MAKE?
	AOJA	N,CMPLA	;YES
	LDB	C,L
	JUMPE	C,CMAKE2
	MOVE	A,L
	ILDB	C,A
	CAIN	C," "
	JRST	.-2	;SAD SPC
	CAIE	C,";"	;MAKE-COMMENT-CR
	AOJA	N,CMPLA
	PUSHJ	P,NEWSTR
	MOVE	C,LINBOT
	MOVEM	C,SRCBOT	;FOR GC
	PUSH	S,[XWD W+COMMTF,0]
	PUSHJ	P,BTFCP1
	ADD	N,[XWD MAKESC,1]
	PUSHJ	P,CSTORE
	POP	S,N
	JRST	CMAKE3
CMAKE2:	ADD	N,[XWD MAKESC,1]
CMAKE3:	TRO	F,MAKEF
	PUSHJ	P,CSTORE
	MOVEI	B,[ASCIZ /   NAME: /]
	PUSHJ	P,TIS		;ASK FOJ IT
	JRST	.-2
	PUSHJ	P,CMCOMP
	SOS	A,CPP
	AOS	CSTOCT
	MOVSI	B,MAKESC
	IORM	B,@WSA
	MOVEI	B,[ASCIZ /   THING: /]	;CONTINUE TO ASK
	PUSHJ	P,TIS
	JRST	.-2
	PUSHJ	P,CMCOMP
	TRZ	F,MAKEF
	JRST	CMPEND


CSTORE:	AOS	CPP
	MOVEM	N,@CPP
	SOSE	CSTOCT
	POPJ	P,
	MOVEI	A,10		;ADD THIS MUCH ROOM TO LINE ELEMENT
	MOVEM	A,CSTOCT
	MOVE	B,CBOT
	PUSHJ	P,COPYUP
	TLO	B,COMPOUND
	MOVEM	B,CBOT		;LOCATION OF NEW ELEMENT
	MOVEM	A,CPP		;NEW PTR TO OLD END
	POPJ	P,

EXECUTE:
	TRZ	F,EELSEF	; 1_LEGAL TO END COMMAND LINE WITH ELSE
	TLZE F,BREAKF
	ERROR	BREAK
	PUSH	P,[EXP [EXP [ERROR NOCMD]]]
EXECU1:	AOS	A,CPP
	SKIPN	A,@WSA	;IS IT EOL?
	PUSHJ	P,SCOMEX	;YES, CALL THE EXIT
	TLNN	A,COMMTF	;IS IT A COMMENT?
	JRST	EXCTL1		;NO, GO TO IT
	JRST	EXECU1		;IGNORE COMMENT

EVAL:	PUSH	P,[EXP [EXP APOPJ]]
	SKIPA	A,CPP
EXCTLL:	AOS	A,CPP	;POINT TO NEXT ELEMENT
	MOVE	A,@WSA
EXCTL1:	TLZ	A,IMMEDIATE!COMPOUND	;IRRELEVANT BITS IN PTR
	JFFO	A,.+2
	ERROR	ERMSG1	;END OF LINE, SOMETHING MISSING
	JRST	@.-1(B)
	 EXCTMP
	 EXCTUP
	 EXCTV
	 EXCTL
	 EXCTIF	;INFIX OP IN PREFIX POSITION,MUST BE UNARY +-
	 EXCTMV	; MACHINE VARIABLE, ABS PTR TO VALUE WORD
	 EXCTLL	;COMMENT, TREATED JUST LIKE IT DOESN'T EXIST


EXCTP4:	POP	P,THISPR
	MOVE	A,(A)		;FETCH ADDRESS OF ROUTINE
EXCTP5:	PUSHJ	P,(A)	;CALL PROCEDURE

OPEX:	AOS	A,CPP	;LETS LOOK AT THE NEXT ELEMENT
	MOVE	A,@WSA	;TO SEE IF IT IS AN INFIX OP
	TLNE	A,COMMTF
	JRST	OPEX	; IGNORING COMMENTS
	TLNE	A,INFOP	; IS IT INFIX
	JRST	INFNXT	; YES, CHECK PRECEDENCE TO SEE WHO GETS INPUT
	MOVE B,[XWD MPF!IMMEDI,ANDL+1] ;AND BETWEEN TWO PREFIX INPUTS IS OPTIONAL
OPEX1:	SOS	CPP	; NO, PRECEDING OPERATOR GETS FIRST CRACK
	HRLZI	A,-1
	ADDB	A,0(P)
	JRST	EXCTP2

EXCTMP:	HLL	A,(A)		;FETCH NUMBER OF ARGUMENTS
EXCTP1:	PUSH	P,A
	MOVE	B,[XWD MPF!IMMEDI,OFL+1]	;TEST FOR OPTIONAL OF
EXCTP2:	JUMPL	A,EXCTP4	;NEED NO ARGS
EXCTP3:	AOS	A,CPP
	MOVE	A,@WSA
	CAMN	B,A		;OF OR AND
	JRST	EXCTLL	;IS OF
	JRST	EXCTL1	;NOT OF

EXCTUP:	HLL	A,@RPA	;FETCH NO ARGS-1
	PUSH	P,A
	HRRI A,UPRODL+1	;ENTRY IN PRINTNAME TABLE FOR GENERIC PROCEDURE
	JRST	EXCTP1

EXCTMV:	SKIPGE	A,(A)		; FETCH VAL AND IS IT COMPUTATIONAL?
	JRST	EXCTP5		; YES, GO COMPUTE
	JRST	EXCTL		; NO, HAVE A VALUE

EXCTV:	MOVE	A,@VPA		; FETCH THE VALUE
	TLZ	A,GLOBLF!UNBOUN	; UNBOUN SHOULD BE CHECKED, NOT FLUSHED
EXCTL:	PUSH	S,A
	JRST	OPEX

EXCTLP:	PUSHJ	P,GNE
	 ERROR	PRNER2
	MOVEI	B,[EXPARR]
	MOVEM	B,0(P)		;KEEPING A TRANSPARENT
	JRST	EXCTL1
EXCTRP:	POP	P,A
	POP	P,A
	CAIE	A,[EXP EXPARR]	; I.E. ONE INPUT,,EXPARR
	ERROR	PRNER1	;MATCHING (?
	PUSH	S,MV+1		; WAS (), AN EMPTY EXPRESSION
	JRST	OPEX

EXPARR:	PUSHJ	P,GNE
	 SKIPA
	CAME	A,[XWD MPF!IMMEDI,RPREN+1]
	ERROR	PRNER2	;MISSING )?
	POPJ	P,	;PRENS MATCH

SPECWD:	ERROR	EVER5
EXIT:	ERROR	XITERR	;USER DEFINED ERROR MESSAGE.
			;  USEFUL IN HOARDED PROCEDURES


EXCTIF:	MOVEI	A,(A)	;FLUSH BITS IN LH
	CAIN	A,IPLUS+1	; UNARY PLUS?
	JRST [	PUSH P,[INFUPL+1]
		JRST EXCTLL ]	;INFIX OP WITH HIGHER PRECEDENCE
	CAIE	A,IMINUS+1	; UNARY MINUS?
	 ERROR	INFERR	;A NONO
	PUSH	P,[INFUMN+1]	; INFIX OP WITH A HIGHER PRECEDENCE
	JRST	EXCTLL

INFNXT:	HLRZ	B,-1(A)		; GET PRECEDENCE OF NEW OPERATOR
	ANDI	B,17		; ONLY NEED 4 BITS
	MOVE	C,0(P)		; FETCH PREVIOUS OPERATOR
	HLRZ	C,-1(C)		; AND GET ITS PRECEDENCE
	ANDI	C,17

	CAIG	B,(C)		; NEW : OLD
	JRST	OPEX1		; NEW LE OLD, IE INPUT GOES WITH LH ONE
	HLL	A,(A)		; GOES WITH THIS ONE, GET # OF ARGS
	PUSH	P,A		; PUT OP ON STACK
	JRST	EXCTLL		; AND BACK FOR MORE

CALLDO:	JSP	D,COMEXR ;CHECK IF AT BEG OF LINE, IF NOT DON'T RETURN
	PUSHJ	P,COMPIL
	 POPJ	P,	;STORED THE LINE, NO NEED TO EXECUTE
	JRST	EXECUTE

SCOMEX:	SOS	CPP
COMEX:	JSP	D,COMEXR
	POPJ	P,

COMEXU:	TLOA	F,UPFF	;DENOTE CALLED BY USER COMMAND
COMEXR:	TLZ	F,UPFF	;DENOTE ROUTINE CALLED BY MACHINE COMMAND
	POP	P,A	;THE RETURN TO OPEX
COMXRA:	POP	P,A
	CAIE	A,[[ERROR NOCMD]]
	 ERROR	COMERR
COMXRC:	PUSHJ	P,GNE	;NEXT ELEMENT
	 JRST	COMXRD	;NO MORE
	TRZE	F,EELSEF
	CAME	A,[XWD MPF!IMMEDI,ELSEL+1]
	 ERROR	ERXTRA

COMXRD:	SETZM	CBOT	;DONE WITH THIS LINE
	JRST	(D)


;TABLE ADDRESS IN B
;SYMBOL POINTER IN A
;INDEX FOR ENTRIES IN TABLE B IN LH OF C IF NECESSARY



SYSLKP:	HRLZI	B,010700+W
	HRRI	B,(A)
	ILDB	C,B	;FIRST CHAR OF THIS ELEM
	CAIL	C,"A"
	CAILE	C,"Z"
	 POPJ	P,	;NOT IN RANGE FOR RESERVED NAME
	MOVE	B,MNPT-"A"(C)	;THE NAME TABLE FOR THAT LETTER
SYSLUK:	MOVEI	C,0
	JRST	LOOK0

LOOKY:	HRLZI	C,W
LOOK0:	MOVEI	E,2
	MOVEI	N,(B)	;USE N FOR STEPPING THRU TABLE

	ADDI	A,(W)	;MAKE A ABSOLUTE
	MOVN	B,(A)	;GET THE LENGTH OF THE WS ELEMENT
	HRLI	A,-1(B)	;PUT -L-1 OF WS ELEMENT IN LH OF A
	TLNN	C,W
	AOBJN	A,.+1

LOOKL:	MOVE	B,A	;USE B FOR CHANGING A
	HRR	C,(N)	;GET THE NAME OF THIS ENTRY IN THIS TABLE
	TRNN	C,-1	;0 AT END OF TABLE
	POPJ	P,

	MOVE	D,@C	;GET A WORD OF THIS ENTRY
	CAME	D,(B)	;IS IT THE SAME AS THIS WORD OF NEW ELEMENT
	JRST	LOOKN	;NO, NOT A MATCH FOR THIS SYMBOL
	ADDI	C,1	;POINT AT NEXT WORD OF THIS ENTRY
	AOBJN	B,.-4	;IF NOT DONE WITH THIS ELEMENT, GO BACK
	JRST	CPOPJ1	;DONE WITH THIS SYMBOL, COMPLETE MATCH

LOOKN:	ADD	N,E
	JRST	LOOKL	;NEXT ENTRY
                                                                                                                                                                                                                                                                                                                                                                                                                

THING:	MOVE	A,(S)
THING1:	MOVE	B,VP
	PUSHJ	P,LOOKY
	 JRST	THING3	;NOT A GLOBAL VARIABLE
	JRST	THING4

THING3:	MOVE	A,(S)
	MOVEI	B,MV
	PUSHJ	P,SYSLUK
	 MOVEI	N,MV	;FIRST MACHINE VAR IS EMPTY
	MOVE	A,1(N)	;MACHINE VARIABLES MUST BE CHECKED
	JUMPGE	A,THING4
	POP	S,B	;THIS ONE MUST BE COMPUTED, (FLAG IS SIGN BIT)
	JRST	(A)
THING4:	MOVE	A,1(N)
	MOVEM	A,(S)
	POPJ	P,


CCONTE:	PUSHJ	P,NEWSTR
	TRZ	F,TF
	TRO	F,NWF		; :CONTENTS: ALWAYS A SENTENCE
	MOVEI	C,0
	MOVE	D,RP
	MOVNI	E,1
CCNTE1:	SKIPN	A,(D)		;ANY MORE PROCEDURE NAMES?
	JRST [	TRNE F,TF	;NO MORE PROCEDURES, WERE THERE ANY?
		JRST ENDST1	; YES
		PUSH S,MV+1	; NO, RETURN EMPTY
		POPJ P, ]
	MOVEI	D,1(D)
	CAMN	E,(D)		;IS THIS ONE DEFINED?
	AOJA	D,CCNTE1	;NO
	MOVEI	C," "
	TROE	F,TF		;FIRST ONE?
	DPB	C,B		;NO REPLACE EOM WITH SPACE
	PUSHJ	P,NEWSR0
	PUSHJ	P,COPYAB
	AOJA	D,CCNTE1

FIRST:
	PUSHJ	P,NEWOPS	;CHECK EMPTY,SET UP SRCBOT+NEWBOT
	JRST	FSTSNT	;NO, SENTENCE

	ILDB	C,A	;FIRST BYTE
	IDPB	C,B	;BOMBS HERE ON FULL WORKSPACE

	MOVEI	C,0	;FILL ZEROES AND RETURN NEW ARG
	JRST	BTFCLO

FSTSNT:	ILDB	C,A	;FIRST OF SENTENCE
	IDPB	C,B	;COPY CHARACTERS TO END OF FIRST WORD
	CAIE	C," "	;IS IT THE END OF A WORD
	JUMPN	C,FSTSNT	;JUMP IF NOT THE ONE WORD SENTENCE

	MOVEI	C,0	;CLEAR " " OR 177
	DPB	C,B	;CLOBBER OTHER TERMINATOR

	HRLZI	A,W+WORDF
	HLLM	A,0(S)
	JRST	BTFCL1


BUTFIRST:
	PUSHJ	P,NEWOPS
	JRST	BTFSNT	;NOT A WORD OR EMPTY, IE SENTENCE

	IBP	A		;BUTFIRST OF WORD, SKIP OVER FIRST CHAR
	ILDB	C,A		;THIS IS THE SECOND CHAR
	JUMPN	C,BTFCOP	;MORE THAN ONE CHAR, COPY TO END OF STRING

BTFEMP:	HRLZI	A,W+WORDF!SENTF!EMPTYF	;ONE OF THE MANY WAYS T
	MOVEM	A,(S)	;TO GET A POINTER TO EMPTY
	POPJ	P,

BTFSNT:	ILDB	C,A	;SKIP OVER FIRST WORD
	JUMPE	C,BTFEMP	;WAS ONLY WORD, RESULT IS EMPTY
	CAIE	C," "	;END OF WORD?
	JRST	BTFSNT	;CONTINUE SKIPPING OVER FIRST WORD

BTFCP1:	ILDB	C,A
BTFCOP:
BTFCLO:	IDPB	C,B
	JUMPN	C,.-2	;COPY THRU END OF ARG

BTFCL1:	PUSHJ	P,ENDST1
	POP	S,A
	HRRM	A,(S)	;NEW STRING, OLD TYPE
	POPJ	P,

LAST:
	PUSHJ	P,NEWOPS
	JRST	LSTSNT

	ILDB	C,A
	IDPB	C,B	;COPY FIRST CHAR, BUMPING THE CHAR PTR IN B ONCE

	ILDB	C,A
	JUMPE	C,BTFCLO	;DONE WITH THE WORD
	DPB	C,B	;PUT THIS CHAR ON TOP OF PREVIOUS ONE
	JRST	.-3

LSTSNT:	MOVE	E,A	;SAVE POINTER TO BEGINNING OF WORD
LSTSN2:	ILDB	C,A	;CONTINUE WITH THE CURRENT WORD
	CAIN	C," "
	JRST	LSTSNT	;END OF CURRENT WORD, NOT LAST ONE
	JUMPN	C,LSTSN2	;NULL_END OF SENT, ELSE NOT END OF ANY WORD

	MOVE	A,E
	JRST	FSTSNT

BUTLAST:
	PUSHJ	P,NEWOPS
	JRST	BTLSNT

	ILDB	C,A	;BUTLAST OF WORD, COPY WORD AND CLOBBER LAST CHAR
	IDPB	C,B
	ILDB	C,A
	JUMPE	C,BTFEMP	;WAS A ONE CHAR WORD, MAKE AN EMPTY
	IDPB	C,B
	ILDB	C,A
	JUMPN	C,.-2	;COPY REST OF WORD
BTLCLO:	DPB	C,B	;CLOBBER LAST CHAR
	JRST	BTFCL1	;FILL TO END OF WORD

BTLSNT:	MOVE	E,A	;SAVE POINTER TO BEGINNING
BTLSN1:	ILDB	C,A	;STEP THRU FIRST WORD
	JUMPE	C,BTFEMP	;ALSO LAST, MAKE AN EMPTY
	CAIE	C," "
	JRST	BTLSN1	;CONTINUE WITH FIRST WORD

BTLSN2:	MOVE	A,E
BTLSN9:	ILDB	C,A		;COPY STEPPED OVER WORD
	IDPB	C,B
	CAIE	C," "
	JRST	BTLSN9

	MOVE	E,A	;SAVE POINTER TO NEXT WORD
BTLSN4:	ILDB	C,A
	JUMPE	C,BTLCLO	;THIS IS END OF LAST WORD
	CAIN	C," "
	JRST	BTLSN2	;END OF WORD, NOT LAST ONE, COPY IT
	JRST	BTLSN4	;NOT END OF WORD, FIND IT

WORD:
	HRLZI	A,WORDF
	TDNE	A,0(S)
	TDNN	A,-1(S)
	ERROR	WRDERR	;YOU CAN'T MAKE A WORD OUT OF A SENTENCE

	HRLZI	C,EMPTYF
	TDNN	C,-1(S)	;IS FIRST ARG EMPTY?
	JRST	WORD0	;NO
	POP	S,-1(S)	;YES, THEN SECOND ARG IS THE RESULT
	POPJ	P,

WORD0:	TDNE	C,(S)
	JRST	SPOPJ
	PUSHJ	P,NEWSTR	;SET UP B AND NEWBOT
	PUSHJ	P,NEWSR1	;SET UP A TO -1(S) AND SRCBOT

	ILDB	C,A
	JUMPE	C,WORD2	;DON'T COPY EOM FROM FIRST ARG
	IDPB	C,B
	JRST	.-3	;COPY WHOLE FIRST ARG

WORD2:	PUSHJ	P,NEWSRC	;SET UP A TO 0(S) AND SRCBOT
	ILDB	C,A
	IDPB	C,B	;COPY SECOND ARG, INCLUDING EOM
	JUMPN	C,.-2
	POP	S,A	;FLUSH SECOND ARG
	JRST	BTFCL1	;FILL ZEROES AND RETURN ONE ARG

SENTENCE:
	HRLZI	E,EMPTYF
	TDNE	E,0(S)
	TDNN	E,-1(S)
	JRST	SENT1

SPOPJ:	POP	S,A	;BOTH EMPTY
	POPJ	P,	;RETURN EMPTY

SENT1:	PUSHJ	P,NEWSTR
	PUSHJ	P,NEWSR1

SENT2:	ILDB	C,A
	JUMPE	C,SENT3
	IDPB	C,B
	JRST	SENT2

SENT3:	MOVEI	C," "
	PUSHJ	P,NEWSRC
	TDNN	E,-1(S)		;IF FIRST ARG IS EMPTY, DON'T SPACE
	TDNE	E,0(S)		;OR IF SECOND IS
	SKIPA			;IN EITHER CASE, DON'T SPACE

SENT4:	IDPB	C,B
	ILDB	C,A
	JUMPN	C,SENT4

	POP	S,A	;FLUSH SECOND ARG
	HRLZI	A,W+SENTF	;CALL NEW THING A SENTENCE
	HLLM	A,0(S)
	JRST	BTFCLO

SENTCS:	SKIPA	A,[SENTCL+1]
WORDS:	MOVEI	A,WORDL+1
	PUSH	P,A		; ROUTINE TO CALL GOES ON STACK
	PUSH	S,MV+1
WORDS1:	PUSHJ	P,GNE		; NEXT INPUT TO SS OR WS
	 JRST	WORDS2		; END OF LINE
	CAMN	A,[XWD MPF!IMMEDI,RPREN+1]
	 JRST	WORDS2		; OR ) TERMINATE INPUTS TO SS
	PUSHJ	P,EVAL		; NEXT INPUT
	MOVE	A,(P)
	MOVEM	A,THISPR
	MOVE	A,(A)
	PUSHJ	P,(A)		; CALL WORD OR S, PR NAME SET UP FOR ERR
	JRST	WORDS1		; GO BACK FOR MORE

WORDS2:	SOS	CPP		; DON'T WANT TO EAT UP THE TERMINATOR
	JRST	APOPJ		; GET RID OF ROUTINE NAME AND EXIT


COUNT:	MOVE	A,(S)	;THING TO COUNT
	MOVEI	B,0	;COUNT INITIALLY ZERO
	TLNE	A,EMPTYF	;IS IT EMPTY?
	JRST	COUNT2	;COUNT=0
	TLNN	A,WORDF	;IS IT A WORD?
	AOJA	B,COUNT1	;NO, IT IS A NON-EMPTY SENTENCE

	PUSHJ	P,COUNTW
	JRST	COUNT2

COUNT1:	HRLI	A,010700+W
COUNTL:	ILDB	C,A
	JUMPE	C,COUNT2
	CAIN	C," "
	AOJA	B,COUNTL	;COUNT WORDS OF SENT AT BEG OF WORD
	JRST	COUNTL		;MORE OF THE SAME WORD

COUNT2:
	MOVE	M,B	;ARG TO SNM IN M
	PUSHJ	P,SNM
	MOVSI	C,W+WORDF
	HLLM	C,(S)
	JRST	BTFCLO

COUNTW:	HRLI	A,440700+W
	MOVNI	B,1
	ADD	B,@A	;NOW NUMBER OF WORDS -1
	IMULI	B,5	;NUMBER OF CHARS IN ALL BUT LAST WORD
	ADD	A,@A	;NOW A POINTS AT LAST WORD
	ILDB	C,A	;WORD
	JUMPE	C,CPOPJ
	AOJA	B,.-2


RANDOM:	MOVE	C,[EXP 1000003]
	IMULB	C,RANNO
	TLZ	C,400000	;MAKE IT POSITIVE
	MULI	C,12
	ADDI	C,"0"
	PUSHJ	P,NEWSTR
	IDPB	C,B
	JRST	ENDSTR


EITHER:	PUSHJ	P,GNE	;GOT 1 INPUT SO FAR, NOW DO NOISE WORD CHECK
	 ERROR	ERMSSG
	CAME	A,[XWD MPF!IMMEDI,ANDL+1]
	CAMN	A,[XWD MPF!IMMEDI,ORL+1]
	AOS	CPP	;SKIP THE NOISE WORD
	PUSHJ	P,EVAL	;AND GET THE SECOND INPUT
	TROA	F,TF
BOTH:	TRZ	F,TF
	MOVE	A,(S)
	PUSHJ	P,PREDIQ
	 ERROR	PREDR2
	MOVE	A,-1(S)
	PUSHJ	P,PREDIQ
	 ERROR	PREDR2
	POP	S,B	;SECOND ARG
	MOVEI	A,1	;LENGTH OF "TRUE"
	TRZN	F,TF	;SKIP IF DOING EITHER
	MOVEI	A,2	;LENGTH OF "FALSE"
	CAMN	A,@WSB	;COMPARE AGAINST LENGTH OF SECOND ARG
	MOVEM	B,(S)	;JAM IF EITHER AND TRUE OR BOTH AND FALSE
	POPJ	P,


EMPTYP:	HRLZI	A,EMPTYF
PREDS1:	POP	S,B
	TDNN	A,B
	JRST	ISFALSE
	JRST	ISTRUE
SENTP:	HRLZI	A,SENTF
	JRST	PREDS1
WORDP:	HRLZI	A,WORDF
	JRST	PREDS1

IS:	POP	S,A
	POP	S,B
IS1:	MOVE	C,@A
	CAME	C,@B
	JRST	ISFALSE
	ADDI	A,1
	TRNE	C,377
	AOJA	B,IS1

ISTRUE:	PUSH	S,[XWD WORDF+W,TRUEV]	;POINTER TO "TRUE"
	POPJ	P,
ISFALSE:	PUSH	S,[XWD WORDF+W,FALSEV]	;WS PTR TO "FALSE"
	POPJ	P,

NUMBRP:	POP	S,A
	PUSHJ	P,NUMBRQ
	JRST	ISFALSE
	JRST	ISTRUE

NOT:	MOVE	A,(S)	;GET BOOL, LEAVING IT ON STACK FOR ERROR
	PUSHJ P,PREDIQ	;MAKE SURE IT IS A BOOL
	 ERROR PREDR1
	POP	S,B	;FLUSH IT NOW
	SKIPE	@A	;A IS THE REL PTR TO THE WORD CONTAINING THE EOM
	 JRST	ISFALSE	; THE SECOND WORD OF THE STRING
	 JRST	ISTRUE

;THIS GRATRP DOES NOT GENERATE ANY NEW STRINGS, DOES NOT PERFORM AN
;ADDITION, AND SHOULD END QUICKLY ON NUMBERS WHICH HAVE
;DIFFERENT ORDERS OF MAGNITUDE OR ARE OF DIFFERENT SIGN

LESSP:	PUSHJ	P,NUMRQS	;TEST INPUTS BEFORE REVERSING THEM
	PUSHJ	P,SWITCH	; EXCH 0(S) AND -1(S)
	JRST	.+2
GRATRP:	PUSHJ	P,NUMRQS	;ARE THE INPUTS NUMBERS?
	MOVE	A,(S)
	PUSHJ	P,COUNTW
	POP	S,A
	PUSHJ	P,FINDN		;SUBTRACT OFF SIGNS AND LEADING ZEROES
	MOVE	E,A		;PTR TO FIRST SIGNIFICANT DIGIT
	MOVEI	D,(B)		;COUNT OF SIGNIFICANT DIGITS
	MOVE	H,F		;STATE OF PMF

	MOVE	A,(S)
	PUSHJ	P,COUNTW
	POP	S,A
	PUSHJ	P,FINDN
	XOR	H,F
	TRNE	H,PMF		;SIGNS THE SAME?
	JRST	GRATRA		;NO, CAN QUIT NOW

	SUB	B,D		;SAME NUMBER OF SIGNIF DIGITS?
	JUMPN	B,GRATRD	;NO, QUIT NOW
	LDB	B,A		; 
	LDB	C,E
GRATRC:	JUMPE	B,GRATRB	;END OF ONE NUMBER
	SUB	B,C
	JUMPN	B,GRATRD	;SIGN IS MEANINGFUL
	ILDB	B,A
	ILDB	C,E
	JRST	GRATRC

GRATRD:	CAIGE	B,0
	TRC	F,PMF
GRATRF:	TRZE	F,PMF
	JRST	ISFALSE
	JRST	ISTRUE	;
GRATRA:	LDB	C,A
	JUMPN	C,GRATRF
	LDB	C,E
GRATRB:	JUMPE	C,ISFALSE
	JRST	GRATRF

FINDN:	TRZ	F,PMF
	HRLI	A,010700+W
FINDNA:	ILDB	C,A
	CAIN	C,"0"
	SOJA	B,FINDNA
	CAIN	C,"+"
	SOJA	B,FINDNA
	CAIE	C,"-"
	POPJ	P,
	TRO	F,PMF		;NUMBER IS NEG
	SOJA	B,FINDNA

EQUALP:	PUSHJ	P,DIFF	;EQUALP IS ZEROP OF DIFF OF
ZEROP:	MOVE	A,(S)
	PUSHJ	P,NUMBRQ
	 ERROR	ZERERR
	POP	S,A
	HRLI	A,010700+W
	ILDB	C,A		;GET THE FIRST CHAR
	CAIE	C,"+"	;IF A SIGN, GET ANOTHER CHAR
	CAIN	C,"-"
ZEROP1:	ILDB	C,A
	JUMPE	C,ISTRUE	;NUMBRQ WOULD FAIL ON SIGN ONLY OR EMPTY
	CAIN	C,"0"
	JRST	ZEROP1
	JRST	ISFALSE


MINIM:	SKIPA	A,[LESSP]
MAXIM:	MOVEI	A,GRATRP
	PUSH	P,A		; WHICH WAY THE TEST GOES
	PUSH	S,-1(S)
	PUSH	S,-1(S)		; EXTRA COPY FOR THE ONES THAT PRED EATS
	PUSHJ	P,@0(P)		; CALL THE APPROPRIATE PREDICATE
	POP	P,A		; FLUSH THE PARAM
	POP	S,B		; THE OUTPUT FROM PRED
	MOVEI	A,1		; LENGTH OF "TRUE"
	POP	S,C		; SECOND INPUT TO MAX OR MIN
	CAME	A,@WSB		; MAX AND FIRST .GTR. SECOND?
	MOVEM	C,0(S)		; NO, THEN SECOND IS THE ANSWER
	POPJ	P,

COMPLM:	MOVE	A,(S)
	PUSHJ	P,NUMBRQ
	 ERROR	ZERERR
	POP	S,A
	PUSHJ	P,NEWSR0
	PUSHJ	P,NEWSTR
	MOVEI	D,"-"
	ILDB	C,A
	CAIE	C,"-"		;IS THE OLD STRING NEG?
	IDPB	D,B		;NO, MAKE NEW ONE NEG
	CAIE	C,"+"		;SKIP EXPLICIT +
	CAIN	C,"-"		; OR -
	SKIPA
	IDPB	C,B		;OTHERWISE STORE FIRST CHAR OF NUM
	PUSHJ	P,COPYAB	;COPY REST OF STRING
	PJRST	ENDST1		;AND FINISH IT OFF

UNPLUS:	MOVE	A,(S)
	PUSHJ	P,NUMBRQ
	 ERROR	ZERERR
	POPJ	P,

DIFF:	TROA	F,PMF
SUM:	TRZ	F,PMF
	PUSHJ	P,NUMRQS
	PUSHJ	P,REVERS
	CAIE	D,"-"
	TRON	F,PMF	;SKIP IF SECOND ARG + AND IS SUBTRACTION
	TRON	F,PMF	;SKIP IF ARG2 - & DIFF OR ARG2 + & SUM
	PUSHJ	P,TENCOM	;CALLED WITH PMF=1_FILL 9'S

	PUSHJ	P,SWITCH

	PUSHJ	P,REVERS
	CAIN	D,"-"
	PUSHJ	P,TENCOM

	PUSHJ	P,SUMMER
	TRZ	F,PMF
	CAIE	G,"8"	;WHEN 9+9 AND NO CARRY INTO
	CAIN	G,"9"	;RESULT NEG IFF HIGH ORDER DIG =9
	PUSHJ	P,TENCOM	;CALLED WITH PMF=0_PUT A "-" AT THE END

	POP	S,A	;FLUSH EXTRA LEADING ZEROES
SUM7:	HRLI	A,350700+W	;SKIPPING OVER FIRST CHAR
	AOJA	A,.+2
SUM8:	CAIE	C,"0"	;CONSECUTIVE 0?
	MOVE	B,A	;NO, SAVE POINTER TO HIGHEST SIGNIFICANT DIG SO FAR
	ILDB	C,A
	JUMPE	C,.+4	;END OF STRING
	CAIE	C,"-"	;MINUS SIGN LIKE A TERMINATOR FOR NEG STRING
	JRST	SUM8
	IDPB	C,B	;PUT TERMINATOR (IN C) ABOVE MOST SIGNIFICANT DIGIT
	PUSHJ	P,ENDSTR

;FALL INTO REVERB - REVERSE BACK - AND EXIT FROM SUM FROM REVERB


REVERB:	TROA	F,TF	;REVERSING BACK, NO MODIFICATION TO STRING
REVERS:	TRZ	F,TF	;FIRST REVERSE OF NUMBER, FIDDLE WITH SIGNS
	PUSHJ	P,NEWSTR
REVERA:	PUSHJ	P,NEWSRC
	MOVE	D,@A
	ADDI	A,(D)	;A POINTS AT LAST WORD
	MOVEI	G,0
REVERY:	MOVE	C,@A
	JUMPE	C,REVERZ
	ROT	C,6
	ROT	C,-7
	TRNN	C,177
	AOJA	G,.-2
	ROT	G,1	;TIMES TWO
	JRST	.+4(G)
REVERL:	SUBI	A,1
	MOVE	C,@A
	ROT	C,-1	;FLUSH BIT 35
	IDPB	C,B
	ROT	C,-7
	IDPB	C,B	;D
	ROT	C,-7
	IDPB	C,B	;C
	ROT	C,-7
	IDPB	C,B	;B
	ROT	C,-7
	IDPB	C,B	;A
REVERE:	SOJG	D,REVERL	;MORE WORDS TO REVERSE
	TRNE	F,TF
	JRST	REVERO
	MOVEI	D,177
	ANDI	D,(C)	;SAVE STATE OF SIGN IN D
	MOVEI	C,"0"
	CAIE	D,"+"
	CAIN	D,"-"
	SKIPA			;SIGNED, OVERWRITE THE SIGN
	IBP	B		;UNSIGNED, ADD A LEADING 0 DIGIT
	DPB	C,B	;REPLACE THE SIGN WITH A "0"

REVERO:	MOVEI	C,0
	JRST	BTFCLO

REVERZ:	SUBI	A,1
	SOJG	D,REVERY
	ERROR	IOPERR	;INPUT WAS NOT A NUMBER


TENCOM:
	PUSHJ	P,NEWSRC
	TRO	F,TF	;SET CARRY INTO LOW ORDER DIGIT
	ILDB	C,A
	JUMPE	C,[ERROR]
NINECM:	MOVNS	C
	ADDI	C,"9"+"0"
	TRZE	F,TF	;WAS THERE A CARRY INTO THIS POSITION?
	ADDI	C,1	;YES
	CAIG	C,"9"	;IS THERE A CARRY OUT OF THIS POS?
	JRST	.+3	;NO
	SUBI	C,12
	TRO	F,TF
	DPB	C,A
	ILDB	C,A
	JUMPN	C,NINECM

	TRNE	F,PMF		;NEGATIVE RESULT?
	POPJ	P,		;NO
	MOVEI	D,"-"
	DPB	D,A
	MOVE	B,A
	PJRST	ENDSTP		;LENGTH OF THE STRING MAY HAVE CHANGED

SUMMER:	MOVE	L,-1(S)
	TRZ	F,TF!PMF
	HRLI	L,010700+W
	MOVEM	L,LINBOT
	PUSHJ	P,NEWSRC
	PUSHJ	P,NEWSTR

SUMMRL:	ILDB	C,A
	JUMPE	C,SUMMR2
	SUBI	C,"0"
	ILDB	E,L
	JUMPE	E,SUMMR3
	ADDI	C,(E)
	TRZE	F,TF	;CARRY?
	ADDI	C,1	;YES
	CAIG	C,"9"	;CARRY OUT OF THIS POSITION?
	JRST	.+3	;NO
	SUBI	C,12
	TRO	F,TF
	IDPB	C,B
	MOVEI	G,(C)
	JRST	SUMMRL

SUMMR2:	MOVE	C,SRCBOT
	EXCH	C,LINBOT
	MOVEM	C,SRCBOT
	EXCH	A,L
	IBP	A
SUMMR3:	UNDEX	L
	LDB	C,L	;GET PREVIOUS CHAR, (WAS 0 OR 9)
	CAIN	C,"9"
	TRO	F,PMF	;NO, IT WAS TEN'S COMPLEMENT
	LDB	C,A
	JUMPE	C,SUMMR5

SUMMR4:	TRZE	F,TF
	ADDI	C,1
	TRNE	F,PMF	;WAS IT NEGATIVE?
	ADDI	C,11	;YES, LEADING NINES
	CAIG	C,"9"
	JRST	.+3
	SUBI	C,12
	TRO	F,TF
	IDPB	C,B
	MOVEI	G,(C)
	ILDB	C,A
	JUMPN	C,SUMMR4

SUMMR5:	POP	S,A
	SETZM	LINBOT
	JRST	BTFCLO

PRODUCT:
	PUSHJ	P,NUMRQS
	MOVE	A,@(S)		;LENGTH OF ONE INPUT
	CAILE	A,@-1(S)	;.GTR. THAN OTHER INPUT?
	PUSHJ	P,SWITCH	; NOW FIRST ARG (-1(S)), .GE. SECOND
	PUSHJ	P,REVERS	;REVERSE SHORTER STRING
	CAIE	D,"-"		;SIGN OF IT
	TRZA	F,PMF		;IS +
	TRO	F,PMF		;IS -
	PUSHJ	P,SWITCH
	PUSHJ	P,REVERS	;REVERSE THE LONGER INPUT
	CAIN	D,"-"		;SIGN OF OTHER
	TRC	F,PMF		;IF - COMPLEMENT SIGN OF RESULT
	MOVE	L,-1(S)		;THE SHORTER ONE
	HRLI	L,010700+W
	MOVEM	L,LINBOT
	PUSHJ	P,NEWSTR	;PLACE FOR PRODUCT REVERSED
	PUSHJ	P,NEWSRC	;MULTIPLICAND

PRODLA:	MOVE	B,L
	ADD	B,NEWBOT
	SUB	B,LINBOT	;LINE UP PARTIAL RESULT
	ILDB	C,L		;NEXT CHAR FROM MULTIPLIER
	JUMPE	C,PRDED1	;END OF MULTIPLIER
	SUBI	C,"0"
	JUMPE	C,PDZERO	;BRING DOWN A 0
	MOVE	A,SRCBOT	;MULTIPLICAND AGAIN
	SETZ	D,		; CARRY FROM LAST DIGIT
PRODLB:	ILDB	E,A		;NEXT CHAR IN MULTIPLICAND
	JUMPE	E,PRDED2	;ICAND DONE, PROPAGATE CARRY
	SUBI	E,"0"
	IMULI	E,(C)		;DIG*DIG, .LE. 81
	PUSHJ	P,PCARRY	;ADD IN THE CARRY ETC
	JRST	PRODLB

PCARRY:	ILDB	H,B		;CURRENT PARTIAL RESULT, THIS PLACE
	CAIE	H,0		;PAST END OF PREVIOUS PARTIAL RESULT?
	SUBI	H,"0"		;NO,REMOVE CHAR OFFSET
	ADDI	D,(H)
	ADDI	D,(E)		;MAX IN D IS 81+9+9=99
	IDIVI	D,12		;CARRY_D
	ADDI	E,"0"		;PARTIAL RESLUT INTO E
	DPB	E,B		;BACK INTO PRODUCT
	POPJ	P,

PRDED2:	JUMPE	D,PRODLA	;PROPAGAE CARRY AS LONG AS NON-ZERO
	MOVEI	E,0		;NO NEW RESULT
	PUSHJ	P,PCARRY
	JRST	PRDED2

PDZERO:	ILDB	E,B		;MAKE SURE A PLACE HOLDER EXISTS
	CAIN	E,0		;IN THIS POSITION IN RESULT
	MOVEI	E,"0"
	DPB	E,B
	JRST	PRODLA

PRDED1:	TRZ	F,TF		;SEARCH FOR END AND NON-ZERO
	MOVE	B,NEWBOT
PRDED4:	ILDB	C,B
	JUMPE	C,PRDED3	;END OF RESULT
	CAIE	C,"0"
	TRO	F,TF		;SEEN A NON-ZERO DIGIT
	JRST	PRDED4
PRDED3:	TRZN	F,TF
	TRZ	F,PMF		;DON'T ALLOW -0
	MOVEI	E,"-"
	TRZE	F,PMF
	DPB	E,B
	SETZM	LINBOT
	POP	S,A
	POP	S,A
	MOVE	A,NEWBOT
	JRST	SUM7		;DELETE LEADING ZEROES AND REVERSE BACK

QUOTIENT:
	PUSHJ	P,QUOT
	POP	S,-1(S)		;FLUSH REMAINDER
	POPJ	P,

REMAINDER:
	PUSHJ	P,DIVREM		;GET QUOTIENT, REMAINDER
	POP	S,-1(S)		;AND FLUSH THE QUOTIENT
	POPJ	P,

DIVISION:
	PUSHJ	P,DIVREM	;GET QUOTIENT, REMAINDER
	PJRST	SENTENCE	;AND MAKE A SENTENCE OUT OF THEM

DIVREM:	PUSHJ	P,QUOT
	PUSHJ	P,SWITCH

	HRLZI	L,010700+W
	HRRI	L,(A)		;SWITCH USES A
	JSP	D,NUMDIG
	 SKIPA
	JRST	.+4
	MOVE	B,(S)
	MOVEI	B,1(B)
	HRLI	B,350700+W
	IDPB	C,B
	TLNE	B,760000
	JRST	.-2
	TRO	F,TF		;FOR REVERSING BACK
	PUSHJ	P,NEWSTR
	PJRST	REVERA		;REVERSE BACK


NUMDIG:	MOVE	A,L		; COUNT SIGNIF DIGS IN L STRING
	MOVE	B,L		; SPARE COPIES OF PTR TO A AND B
	ILDB	C,A		; FIND MOST SIGNIFICANT DIGIT
	JUMPE	C,.+4		; B NOW HAS PTR TO MOST SIGNIF DIGIT
	CAIE	C,"0"
	MOVE	B,A		; THIS ONE IS MORE SIGNIFICANT
	JRST	.-4

	SKIPA	A,[0]		; NOW COUNT IN A HOW MANY
	IBP	L
	CAME	B,L		; REACHED THE END?
	AOJA	A,.-2		; NO, IS SIGNIF
	JUMPE	A,(D)
	JRST	1(D)

QUOT:	PUSHJ	P,NUMRQS
	PUSHJ	P,SWITCH

	PUSHJ	P,REVERS	;DIVIDEND
	CAIE	D,"-"
	TRZA	H,PMF		;+
	TRO	H,PMF		;-

	PUSHJ	P,SWITCH
	PUSHJ	P,REVERS	;DIVISOR
	CAIN	D,"-"
	TRC	H,PMF		;-

	PUSHJ	P,NEWSRC
	PUSHJ	P,NEWSTR
	PUSHJ	P,COPYAB
	PUSHJ	P,ENDST1
	TRO	F,PMF
	PUSHJ	P,TENCOM	;+DIVD,+DIVSR,-DIVSR ON PDL

	PUSHJ	P,NEWSR1
	MOVE	L,A
	JSP	D,NUMDIG
	 ERROR	DIVERR
	MOVE	E,A		;NUMBER OF NON-ZERO DIGITS IN DIVISOR
	MOVE	L,-2(S)
	HRLI	L,010700+W
	MOVEM	L,LINBOT
	JSP	D,NUMDIG
	 JRST	QUOTZR
	SUB	A,E		;DIGS IN DIVD- DIGS IN DIVSR
	JUMPL	A,QUOTZR	;DIVISOR .GTR. DIVIDEND
	IDIVI	A,5
	ADD	A,LINBOT
	JUMPE	B,.+3
	IBP	A
	SOJG	B,.-1		;WHERE TO START SUBTRACTION PTR IN A

	TRZ	F,NWF!PMF	;NO SUCCESSFUL SUBTRACTIONS DONE YET
	PUSHJ	P,NEWSTR	;QUOTIENT APPEARS HIGH DIG TO LOW
	MOVEI	C,"-"
	TRNE	H,PMF		;IS RESULT NEG?
	IDPB	C,B		;YES, STORE A -

	MOVE	L,A
QUOTLP:	MOVEI	C,"0"
	IDPB	C,B		;START OF NEXT DIGIT
ADDR:	MOVE	E,(S)
ADDR0:	MOVE	A,L
	HRLI	E,010700+W
	TRZ	F,TF		;CARRY
ADDR1:	ILDB	C,A
	JUMPE	C,ADDR2	;END OF LONGER ONE GETS PROPER CARRY
	SUBI	C,"0"
	ILDB	D,E
	ADDI	C,(D)
	TRZE	F,TF
	ADDI	C,1
	CAIG	C,"9"
	JRST	.+3
	SUBI	C,12
	TRO	F,TF
	DPB	C,A
	JRST	ADDR1

ADDR2:	TRNE	F,PMF
	TRC	F,TF		;INVERT SENSE OF CARRY
	TRZN	F,TF		;IF NO CARRY
	JRST	ADDR3		;THEN WE'RE DONE WITH THIS SIGNIF DIGIT
	LDB	C,B
	MOVEI	C,1(C)
	DPB	C,B		;BUMP THIS DIGIT
	TRO	F,NWF		;NON-ZERO RESULT
	JRST	ADDR		;REPEAT THE SUBTRACTION

ADDR3:	MOVE	E,-1(S)
	TRCN	F,PMF		;ARE THE DIVISORS + - ?
	JRST	ADDR0		;NO, ADD BACK IN
	CAMN	L,LINBOT
	JRST	QUDONE
	UNDEX	A
	DPB	C,A		;C IS 0, SHORTEN THE DIVIDEND
	UNDEX	L
	TRNE	F,NWF		;JUST DONE A LEADING 0?
	JRST	QUOTLP		;NO, NEXT DIGIT OF QUOTIENT
	JRST	ADDR		;YES, DO THIS ONE OVER

QUDONE:	TRZE	F,NWF		;ANY SUCCESSFUL SUBTRACTION?
	JRST	.+4		;YES
QUOTZR:	MOVE	B,NEWBOT
	MOVEI	C,"0"
	IDPB	C,B
	POP	S,C		;FLUSH THE TWO DIVISORS
	PJRST	ENDSTP	;EXIT WITH REV(REM) AND QUOT ON S STACK

DATE:
FOR TENEX,<SETO	B,
	SETZ	D,
	ODCNV
	HLRZ	H,C		;DAY TO H
	HRRZI	E,(B)		;MON TO E
	HLRZ	D,B		;YR  TO D
>
FOR TEN50,<CALL	G,[SIXBIT /DATE/]
	IDIVI	G,^D31		;DAY TO H
	MOVEI	D,(G)
	IDIVI	D,^D12	;MON TO E
	ADDI	D,^D1964	;YR  TO D
>
	MOVEI	M,1(E)		;IT WAS MON-1
	PUSHJ	P,SNM
	MOVEI	E,"/"
	IDPB	E,B
	MOVEI	M,1(H)		;IT WAS DAY-1
	PUSHJ	P,SNM0		;DON'T MAKE A NEW STRING
	IDPB	E,B
	MOVEI	M,(D)		;YEAR
	PUSHJ	P,SNM0
	PJRST	ENDSTR


TIME:	PUSHJ	P,TIMDAY
	IDIVI	C,^D3600	;SECONDS PER HOUR
	MOVE	M,C		;HOUR TO M
	TRZ	F,PMF		;AM OR PM
	CAIGE	M,^D12
	JRST	.+3
	SUBI	M,^D12
	TRO	F,PMF		;PM
	SKIPN	M
	MOVEI	M,^D12		;MIDNIGHT AND NOON ARE 12
	PUSHJ	P,SNM
	MOVEI	C,":"
	IDPB	C,B

	IDIVI	D,^D60		;CONVERT SECONDS TO MINUTES
	MOVE	M,D
	PUSHJ	P,SNM0
	MOVEI	A,[ASCIZ / AM/]
	TRZE	F,PMF
	MOVEI	A,[ASCIZ / PM/]
	HRLI	A,440700
	PUSHJ	P,COPYAB
	PJRST	ENDSTR

TIMDAY:
FOR TENEX,<SETO	B,
	SETZ	D,
	ODCNV
	HRRZI	C,(D) >		;D HAD HIGH BITS SET
FOR TEN50,<CALL	C,[SIXBIT /MSTIME/]
	IDIVI	C,^D1000 >	;INDEPENDENT OF LINE FREQUENCY
	POPJ	P,


CLOCK:	PUSHJ	P,TIMDAY
	SUB	C,TOFDAY
	CAIGE	C,0
	ADDI	C,^D24*^D3600	;WENT PAST MIDNIGHT
	MOVE	M,C
	PUSHJ	P,SNM
	PJRST	ENDSTR

RESETC:	PUSHJ	P,TIMDAY
	MOVEM	C,TOFDAY
	JRST	COMEX

;TEXT - AN OPERATION ON TWO INPUTS
; 1) NAME OF A PROCEDURE
; 2) A LINE NUMBER IN THAT PROCEDURE
;IF EITHER INPUT LIES, THE OUTPUT IS EMPTY

TEXT:	POP	S,L		;SECOND INPUT, A LINE NUMBER
	PUSHJ	P,DNM
	 JRST	BTFEMP		;NOT A NUMBER, RETURN EMPTY
	MOVE	A,(S)
	MOVE	B,RP
	PUSHJ	P,LOOKY	;FIRST INPUT, A PROCEDURE NAME
	 JRST	BTFEMP		;NOT A PROCEDURE
	HRRZ	A,1(N)
	CAIN	A,-1		;IS IT DEFINED?
	 JRST	BTFEMP		;NO
	JUMPE	M,TEXT1		;IS IT LINE 0, IE TITLE
	JSP	C,SRCHLL	;FIND THE LINE
	 CAIE	B,(M)
	 JRST	BTFEMP		;NOT FOUND, RETURN EMPTY
	POP	S,B
	PJRST	LINGEN		;RETURN THE TEXT OF THE LINE

TEXT1:	POP	S,B
	MOVEI	D,1(A)
	MOVE	M,@PSD
	PUSHJ	P,NEWSTR
	MOVEI	D,TITLEL+1
	PUSHJ	P,LNGMP		;COPY IN BUILT-IN COMMAND NAME
	PJRST	LINGE0		;REST OF THE LINE

;LINES - A PROCEDURE OF ONE INPUT, A PROCEDURE NAME
;IF DEFINED, RETURNS THE SENTENCE OF ALL LINE NUMBERS, ELSE EMPTY

LINES:	MOVE	A,(S)
	MOVE	B,RP
	PUSHJ	P,LOOKY
	 JRST	BTFEMP		;PROCEDURE NOT EXTANT
	HRRZ	A,1(N)
	CAIN	A,-1
	 JRST	BTFEMP		;NOT DEFINED
	MOVEI	A,2(A)		;SKIP OVER TITLE
	SKIPN	M,@PSA		;ANY LINES?
	 JRST	BTFEMP		;NONE
	POP	S,B
	PUSHJ	P,SNM		;FIRST LINE NUMBER
	TRO	F,NWF		;SP OF LINES OF "X" IS ALWAYS "TRUE"
LINES1:	MOVEI	A,2(A)
	SKIPN	M,@PSA		;ANY MORE LINES?
	 PJRST	ENDSTR		;NO
	PUSHJ	P,DSPACE
	PUSHJ	P,SNM0		;ADDITIONAL LINE NUMBERS
	JRST	LINES1

                                                                                                                                                     
;HERE FOLLOW THE COMMANDS

TYPE:	TRZA	F,CRF	;DON'T DO CRLF AFTER TOS
PRINT:	TRO	F,CRF
	TLO	F,TOF	;DENOTE TYPEOUT FOR BREAKY ERROR
	SETZM	BCHAR
	POP	S,A	;GET THE THING TO TYPE OR PRINT
	PUSHJ	P,PTOSS
	TRZE	F,CRF	;CALLED BY TYPE OR PRINT?
LISTXT:	PUSHJ	P,CRLF	;PRINT
	TLZ	F,TOF	;NO LONGER TYPING OUT
	JRST	COMEX

ENND:	SKIPN	A,TOPROD
	ERROR	NOPERR		;END WHAT? YOU ARE NOT DEFINING ANYTHING
	SKIPN	PRODNM		;IF THE "END" WAS STORED, OR
	TLNE	F,GETF		;IN ANY CASE, IF GETTING
	JRST	ENND1		;DO NOT TYPE "DEFINED"
	JSP	H,ETUP
	MOVEI	A,[ASCIZ / DEFINED/]
	PUSHJ	P,PTOSSM
	PUSHJ	P,CRLF
ENND1:	SETZB	A,TOPROD
	TRZN	F,FLUSHF	;IF FLUSHING IT,  NOT REALLY OPEN
	PUSHJ	P,MOVEMP
	JRST	COMEX


ABBREVIATE:
	AOS	CPP
	PUSHJ	P,EVAL		;EVAL FIRST ARG
	PUSHJ	P,GNE
	 ERROR	ERMSSG		;SOMETHING MISSING, IE SECOND ARG
	CAMN	A,[XWD MPF!IMMEDI,ASL+1]	;IS IT "AS"?
	AOS	CPP		;YES, SKIP IT
	PUSHJ	P,EVAL		;GET SECOND ARG

ABBRV0:	MOVE	A,0(S)		;SECOND ARG, ABBREVIATION
	TLNE	A,EMPTYF	;IS IT EMPTY
	 ERROR	ABBER2		;DON'T USE THE EMPTY THING AS AN ABB
	MOVE	B,UA		;USER ABBREVIATION TABLE
	PUSHJ	P,LOOKY
	 JRST	ABBRV1
	POP	S,A		;FOUND IT, FLUSH ABBREVIATION
	POP	S,1(N)		;SAVE ITS NEW VALUE
	JRST	COMEX

ABBRV1:	MOVEI	A,2(N)		;NOT FOUND, INSERT AT END OF TABLE
	CAML	A,UA+1		;ROOM FOR TWO MORE WORDS AT END?
	EXPAND	UA		;NO, MAKE ROOM
	POP	S,0(N)		;ABBREVIATION FIRST
	POP	S,1(N)		;VALUE SECOND
	SETZM	2(N)		;REPLACE END CONDITION
	JRST	COMEX

TEST:	MOVE	A,(S)		;LEAVE ARG ON STACK FOR ERROR
	PUSHJ	P,PREDIQ	;IS THE INPUT A PREDICATE?
	 ERROR	PREDR1		;NO
	POP	S,B		; OK TO FLUSH NOW
	MOVEI	B,0
	SKIPN	@A	;0 FOR "FALSE"; "TRUE" FOR "TRUE"
	MOVNI	B,1
	MOVEM	B,TRUTH
	JRST	COMEX

IF:	MOVE	A,(S)
	PUSHJ	P,PREDIQ	;IS INPUT TO IF A PREDICATE?
	 ERROR	PREDR1		;NO, INPUT MUST BE A PREDICATE
	POP	S,B		;FLUSH IT
	TRO	F,EELSEF	;DENOTE THAT AN ELSE MAY APPEAR
	SKIPE	@A		;IS IT FALSE
	 JRST	IF2		;NO, TRUE
	AOS	A,CPP
	SKIPN	B,@WSA		;HOOK FOR A TRAILING ELSE
	JRST	IFXFS2		;DIDN'T FIND ONE
	CAME	B,[XWD MPF!IMMEDI,ELSEL+1]
	JRST	.-4		; NOT ELSE, GO BACK FOR NEXT ELEM
	JRST	IFX2		;FOUND ELSE, USE THE REST
IF2:	POP	P,0(P)		;FLUSH PTR TO OPEX
	MOVE	B,[XWD MPF!IMMEDI,THENL+1] ; OPTIONAL THEN
	JRST	EXCTP3		;EXECUTE THE THEN PART

IFFALSE:	SKIPE TRUTH
	JRST IFX2	; IF FALSE, AND IN FACT TRUTH IS FALSE
IFXFLS:	MOVE	A,CBOT
	ADD	A,@WSA
IFXFS2:	SUBI	A,1
	MOVEM	A,CPP
	JRST	COMEX

IFTRUE:	SKIPE TRUTH
	JRST IFXFLS	; IF TRUE, AND TRUTH=FALSE
IFX2:	POP	P,0(P)	;DO REST OF LINE AS IF IF WASN'T THERE
	JRST	EXCTLL


GOODBYE:	JSP	D,COMEXR	;MAKE SURE LINE IS OK FIRST, THEN
FOR TENEX,<	MOVEI	A,100	;PRIMARY INPUT FILE
	CFIBF		;FLUSH ANY OTHER JUNK
	MOVE	C,[ASCII /LOGO
/]
	MOVNI	D,5
	SETZ	B,
	ROTC	B,7
	STI		;SIMULATE TERMINAL INPUT TO EXEC
	AOJL	D,.-3
	HALTF >		;AND RETURN TO EXEC
FOR TEN50,<	CALL	[SIXBIT /EXIT/]>

TYPEIN:	PUSHJ P,REQUEST	;PUSH SECOND ARG TO MAKE
MAKE:	MOVE	A,-1(S)
	TLNE	A,EMPTYF
	ERROR	NMERR5	;NAME NOT ALLOWED TO BE EMPTY
	HRRZI	B,MV	
	PUSHJ	P,SYSLUK
	 JRST	.+2
	ERROR	TOERR3	;X IS USED BY LOGO
	MOVE	A,-1(S)
	HRRZ	B,VP
	PUSHJ	P,LOOKY
	 PUSHJ	P,MAKEN	;NOTA, MAKE A NEW GLOBAL
MAKE1:	POP	S,A	; GET THE VALUE
	DPB A,[XWD 004100,1(N)]	;PUT IT AWAY, NOT CHANGING STATE OF GLOB
IGNORE:	POP	S,A	;FLUSH ONE INPUT, THE NAME IF MAKE
	JRST COMEX

MAKEN:	MOVEI	B,2(N)
	CAML	B,VP+1
	EXPAND	VP
	MOVE	A,-1(S)
	MOVEM	A,(N)
	MOVE	A,[XWD W+WORDF!SENTF!EMPTYF!GLOBLF!UNBOUN,0]
	MOVEM	A,1(N)
	SETZM	2(N)
	POPJ	P,



LOCAL:	SKIPN	PRODNM
	 ERROR	STOERR	;LOCAL CAN ONLY BE STORED
	MOVSI	A,EMPTYF
	TDNE	A,0(S)		;IS THE LOCAL NAME EMPTY
	 ERROR	NMERR5		; NAMES CANNOT BE EMPTY
	MOVE	A,(S)
	MOVEI	B,MV
	PUSHJ	P,SYSLUK	; IS IT A BUILT IN
	 JRST	.+2
	ERROR	LCLERR		; CAN'T USE MV AS LOCAL NAME
	MOVE	A,(S)
	PUSH	S,A		; SO MAKEN CAN USE "-1(S)" FOR NAME
	MOVE	B,VP
	PUSHJ	P,LOOKY	; DOES THE NAME ALREADY EXIST?
	 PUSHJ	P,MAKEN		; NO, GO DO IT
	POP	S,A
	POP	S,A		; GET RID OF JUNK

	MOVE	E,DTOP
	MOVE	D,DP+1
	SUB	D,DP	;END RELATIVE TO BEGINNING
	CAIG	D,2(E)	;WILL ANOTHER ENTRY FIT
	EXPAND	DP	;NO
	ADD	E,DP	;MAKE ABSOLUTE
	MOVE	B,[XWD W+WORDF!EMPTYF!SENTF!UNBOUN,0]
	EXCH	B,1(N)		; TOP LEVEL BINDING NOT SET, ONLY DECLARED
	SUB	N,VP
	ADDI	N,1
	MOVEM	N,(E)		; NAME PTR IN DP IS REL PTR TO VP
	MOVEM	B,1(E)		; AND VALUE IS THE ONE IN VP PREVIOSLY
	MOVEI	A,2
	ADDM	A,DTOP	;TOP OF LIST IS HIGHER
	JRST COMEX	;ONLY ALLOW ONE INPUT

;HERE FOLLOWS ALL THERE IS TO USER DEFINED PROCEDURES
;DEFINING THEM, EXECUTING THEM, CHANGING TITLES AND TRACING

TITLE:	SKIPN	TOPROD	;IS THERE A PROCEDURE OPEN?
	ERROR	NOPERR	;NO, CANNOT RETITLE NO PROCEDURE
	PUSHJ	P,GNE
	 ERROR	ERMSSG
	CAME	A,[XWD MPF!IMMEDI,TOL+1]	;IS THE NEXT THING "TO"
	ERROR	TITER2	;WORD AFTER TITLE MUST BE TO
	TLO	F,TITLEF
	JRST	TO0
TO:	TLZ	F,TITLEF
	SKIPE	TOPROD	;IS THERE A PROCEDURE ALREADY OPEN
	ERROR	TOERR4	;YES
TO0:	MOVE	H,CPP	;WHERE TO START SAVING THE CODE OF TITLE LINE
	SUB	H,CBOT	;ALWAYS RELATIVE TO BEG OF LINE!!
	PUSHJ	P,GNE	;NEXT NON-COMMENT ELEMENT
	 ERROR	ERMSSG	;MUST HAVE A PROCEDURE NAME
	TLNN	A,UPRF
	 ERROR	TOERR5	;CANNOT BE USED AS A PROCEDURE NAME
	TLNE	A,SCHF		; DOES IT HAVE NON-PRINTING CHARS
	 ERROR	TOERR7	; YES, NOT A LEGAL NAME
	MOVEI	E,(A)
	MOVE	G,@RPA
	CAMN	G,[EXP -1]	;IS THE PROCEDURE ALREADY DEFINED?
	JRST	TO1		;NO, ALWAYS OK
	TLNE	F,GETF	;DOING A GET?
	JRST	TOGET		;YES
	HRRZ	B,TOPROD
	TLNE	F,TITLEF	;ARE WE DOING "TITLE"?
	CAIE	B,(A)		;YES, IS IT THIS PROCEDURE?
	 ERROR	TOERR6	;NO TO EITHER, THE PROCEDURE IS ALREADY DEFINED
TO1:	SETZ	M,	;COUNT OF DUMMIES ENCOUNTERED
	PUSHJ	P,GNE	;NEXT NON-COMMENT ELEMENT
	 JRST	TOA	;NO DUMMIES
TO2:	TLNE	A,MVARF	; IS THE FORMAL NAME RESERVED?
	 ERROR	TOERR3	; NOT PERMITTED
	TLNN	A,VARF		;IS IT A DUMMY?
	ERROR	TOERR2	;NO
	MOVEI	M,1(M)	;YES, COUNT IT
TO3:	PUSHJ	P,GNE	;CHECK FOR "AND"
	 JRST	TOA	;NOTHING, SO NO AND
	CAME	A,[XWD MPF!IMMEDI,ANDL+1]	;IS IT "AND"?
	JRST	TO2		;NOT AND, TRY DUMMY
	PUSHJ	P,GNE
	 ERROR	ERMSSG		;"AND" LAST, NOT WELL FORMED
	JRST	TO2		;IT MUST EXIST AND BE A VARIABLE


TOGET:	TRO	F,FLUSHF	;LOADING AN ALREADY DEFINED PROCEDURE
	MOVEM	A,TOPROD	;SO LET IT BE OPEN
	MOVEI	A,-1(A)
	MOVE	A,@RPA		;PROCEDURE NAME
	PUSHJ	P,PTOSS
	MOVEI	A,[ASCIZ / IS ALREADY DEFINED./]
	PUSHJ	P,PTOSSM
	PUSHJ	P,CRLF
	JRST	IFXFLS

TOA:	TLNN	F,TITLEF	;TITLE LINE NOW VALID
	JRST	TOB		;TO, NOT TITLE
	MOVE	A,TOPROD
	MOVNI	G,1
	EXCH	G,@RPA		;OLD PROCEDURE OPEN NOW UNDEFINED
	HRLI	G,-1(M)		;NO OF DUMMIES-1,LOC OF DIRECTORY
	MOVEI	A,(E)
	MOVEM	A,TOPROD	;NEW PROCEDURE NAME NOW OPEN
	MOVEM	G,@RPA		;AND DEFINED


	MOVEI	A,(G)
	MOVEM	M,@PSA		;NUMBER OF DUMMIES
	MOVEI	A,1(A)
	PUSHJ	P,TOLINJ
	JRST	SCOMEX

TOB:	MOVEM	E,TOPROD	;PROCEDURE NOW OPEN
	HRLZI	G,-1(M)
	HRR	G,PTOP		;NEW DIRECTORY GOES HERE
	MOVEI	A,(E)
	MOVEM	G,@RPA		;PROCEDURE DEFINED
	MOVEI	A,(M)
	PUSHJ	P,MOVEMP
	MOVEI	G,(A)		;SAVE PSA PTR TO COMPILE CODE PTR
	PUSHJ	P,MOVEMP	;MAKE A HOLE
	MOVEI	A,(G)		;GET PSA PTR BACK
	PUSHJ	P,TOLINJ
	JRST	SCOMEX

MOVEMP:	MOVE	D,PTOP
	MOVEM	A,@PSD
	AOS	A,PTOP
	MOVEI	B,@PSA
	CAML	B,PS+1
	EXPAND	PS
	SETZM	@PSA
	POPJ	P,


TOFLUS:	SETZM	CBOT
	POPJ	P,

TOLINE:	SKIPN	TOPROD	;IS THERE A PROCEDURE OPEN
	ERROR	INERR3	;LINE X OF WHAT PROCEDURE?
	TRNE	F,FLUSHF
	JRST	TOFLUS
	MOVEI	H,2	;SAVE THE LINE BUT NOT THE LINE NUMBER
	MOVE	A,CBOT
	MOVEI	A,1(A)		; POINT PAST OVERHEAD WORD
	MOVE	L,@WSA
	PUSHJ	P,DNM
	ERROR	IOPERR	;I AM IN TROOUBLE
	CAIL	M,^D100000
	ERROR	INERR2	;LINE NO TOO LARGE
	JUMPE	M,[ERROR INERR4]	;LINE NO =0

	HRRZ	A,TOPROD
	JSP	C,SRCHL1
	 CAIGE	B,(M)
	 JRST	TOLING

	CAIN	B,(M)		;LINE NO ALREADY EXISTS?
	AOJA	A,TOLINJ	;YES

TOLING:	SETOM	NXLINE	;CANNOT SAFELY ASSUME THAT THE PROCEDURE WE'RE IN
	MOVEI	B,2	; HAS NOT MOVED
	ADD	B,PTOP
	ADD	B,PS
	CAML	B,PS+1	;ROOM IN PS FOR ANOTHER 2 WORD ENTRY?
	EXPAND	PS	;NO

	MOVE	B,PTOP
	ADD	B,PS
	MOVEI	C,@PSA

	MOVE	D,(B)	;COPY EVERYTHING DOWN 2, UP TO PLACE TO INSERT
	MOVEM	D,2(B)
	CAIE	B,(C)
	SOJA	B,.-3

	MOVEI	B,2
	ADDM	B,PTOP

	MOVEM	M,(C)	;LINE NUMBER
	AOJA	A,TOLINJ


TOLINJ:	TLO	F,GCCSF	; WRONG PLACE FOR THIS FLAG
	MOVE	B,CBOT		; PTR TO OLD LENGTH
	CAIN	H,1	; ALL OF THE LINE BEING SAVED?
	JRST	TOLINQ	; YES
	PUSH	P,A	; PTR INTO PROCEDURE DIRECTORY
	MOVE	A,@WSB		; GET THE OLD LENGTH
	SUBI	A,-1(H)		; NEW LENGTH
	PUSHJ	P,MAKELM
	MOVE	A,CBOT
	ADDI	A,-1(H)
	HRLZI	C,@WSA
	HRRI	C,@WSB
	MOVEI	A,(C)
	AOBJN	C,.+1
	ADD	A,(A)
	BLT	C,(A)
	POP	P,A
	TLO	B,COMPOUND	; NEW ELEMENT IS COMPOUND
TOLINQ:	MOVEM	B,@PSA
	POPJ	P,	;R1 FROM COMPIL


TRACE:	PUSHJ	P,GNE	;TRACE WHAT?
	 ERROR	ERMSSG	;NOTHING
	TLNN	A,UPRF	;TRACE A USER PROCEDURE?
	 ERROR	WHATER	;CANNOT TRACE THAT
	MOVEI	A,-1(A)	;POINT AT FIRST WORD OF RP PAIR
	HRLZI	B,TRACEF
	IORM	B,@RPA	;MARK THIS PROCEDURE AS TRACED
	JRST	COMEX

UPROD:	MOVE	A,-1(P)
	POP	P,-1(P)
	MOVNI	B,1
	CAMN	B,@RPA	;IS THE PROCEDURE DEFINED?
	 ERROR	EVER3	;NO, X NEEDS A MEANING
	MOVEI	B,(A)
	CAMN	B,TOPROD	;IS THIS THE ONE BEING DEFINED?
	 ERROR	EVER4	;YES, X HAS NOT BEEN COMPLETELY DEFINED

	JSP	D,SAVEUP	;PUSH ALL THE GOOD STUFF
	HRRZM	A,PRODNM

	MOVE	B,DP	;LOC OF DUMMY ARG TABLE
	HLRE	C,A	; + NO OF ARGS AT SCAN TIME -1
	ADDI	C,1
	MOVEI	E,(C)
	ADDI	E,1(C)
	ADD	B,DTOP	;PART OF TABLE ALREADY USED
	ADDI	B,(E)	;AMT FOR DUMMY NAMES
	CAML	B,DP+1	;ALL FIT IN DP?
	EXPAND	E,DP	;EXPAND THE TABLE AT LEAST THIS MUCH

	ADDM	C,DTOP
	ADDM	C,DTOP	;UPDATE AMT USED



	TLO	F,NOBREAK	;INHIBIT BREAK FOR DURATION OF TRACE
	HRRZ	D,@RPA
	PUSH	P,BCHAR	;SAVE CURRENT STATE OF MARGIN
	MOVE	E,TRACEM	;MARGIN FOR TRACE
	ANDI	E,17		;INDENT AT MOST 14 SPACES
	MOVEM	E,BCHAR
	TRZ	F,TF	;IF SET WILL DENOTE THAT THIS PROC IS TRACED
	MOVEI	A,-1(A)
	MOVE	A,@RPA	;FETCH NAME OF PROCEDURE
	TLNE	A,TRACEF	; AND SKIP IF NOT TRACED
	TRO	F,TF
	PUSH	P,C
	TRNE	F,TF
	SKIPN	CHARNO
	SKIPA		;NOT TRACING OR AT MARGIN
	PUSHJ	P,CRLF	;NOT AT MARGIN, GET THERE
	TRNE	F,TF
	PUSHJ	P,INDENT
	POP	P,C
	TRNE	F,TF
	PUSHJ	P,CALPTS	;TRACED, TYPE OUT THE NAME

	MOVEI	D,1(D)	;POINT TO PTR TO TO LINE 
	MOVE	D,@PSD	;FETCH PTR TO TO LINE
	MOVN	E,C	;-N
	MOVEI	C,1(S)	;STACK LOCATION
	ADD	C,E	;LOCATION IN STACK OF FIRST DUMMY
	HRL	C,E	;-COUNT OF DUMMIES
	HRRZ	G,DP
	ADD	G,-3(P)	;OLD VALUE OF DTOP
			;BEWARE OF THIS INSTRUCTION WHEN CHANGING SAVEUP
	JUMPGE	C,UPDVCR	;NO DUMMIES, GET FIRST LINE

	MOVE	A,[POINT 7,[ASCIZ / OF /]]
UPDVCL:	TRNE	F,TF
	PUSHJ	P,CLPTS1	;IF TRACED, TYPE "OF" OR " AND "
	MOVEI	E,042		;TO QUOTE INPUTS IF TRACING
	MOVE	A,@WSD	;LOOP TO COPY INTO DP, GET DUMMY NAME
	TLNN	A,VARF	;IS IT A DUMMY NAME
	AOJA	D,.-2	;NO, TRY NEXT ELEMENT
	MOVEM	A,(G)	;INTO DUMMY VAR TABLE
	MOVE	B,(C)	; NEW BINDING
	EXCH	B,@VPA	; GOES INTO VP, OLD BINDING
	MOVEM	B,1(G)	; GOES INTO DP
	MOVE	A,(C)	; NEW BINDING AGAIN, FOR TRACING
	TRNE	F,TF		;TRACING?
	PUSHJ	P,CLPTS0	;YES, TYPE INPUT QUOTED
	MOVEI	G,2(G)
	POP	S,0(S)	;POP 1 THING OFF S STACK WITHOUT CLOBBERING
	MOVEI	D,1(D)
	MOVE	A,[POINT 7,[ASCIZ / AND /]]
	AOBJN	C,UPDVCL	;UPROD DUMMY VAR COPY LOOP
UPDVCR:	TRNE	F,TF		;BUT BEFORE EXECUTING
	PUSHJ	P,CRLF		;NEATEN UP
	MOVEI	A,2 
	TRZE	F,TF	;TRACING? ALSO, FLAG NO LONGER NEEDED
	ADDM	A,TRACEM	;NEXT TRACE CALL INDENTED TWO MORE SPACES


UPFRST:	PUSH	S,CBOT
	POP	P,BCHAR		;RESTORE OLD MARGIN
UPNEXT:	JSP	C,SRCHLN
	 CAMG	B,LINENO
	 JRST	OUTPTA
	MOVEM	A,NXLINE
UPNXT1:	MOVEM	B,LINENO	;FOUND A LINE NO .GTR. PREVIOUS LINE
	TLZ	F,NOBREAK	;SAFE TO ALLOW BREAK AGAIN
	AOS	A,NXLINE
	MOVE	A,@PSA		;GET COMPILE PTR & CHANGE NO
	MOVEM	A,CBOT
	MOVEM	A,CPP

	PUSHJ	P,EXECUTE

	AOS	A,NXLINE	;DONE WITH THE LINE, GET NEXT ONE
	JUMPE	A,UPNEXT	;DO IT THE LONG WAY IF ANY PROCEDURES CHANGED
	MOVE	B,@PSA		;FETCH THE LINE NO
	JUMPN	B,UPNXT1	;LINE NO .NE. 0_MORE TO DO
	JRST	OUTPTA		;NO MORE LINES, DONE WITH PROCEDURE

CLPTS0:	TRO	F,PREFIX!SUFFIX
CALPTS:	HRLI	A,010700+W	;WORKSPACE ELEMENT
CLPTS1:	PUSH	P,B
	PUSH	P,C
	PUSH	P,D
	PUSHJ	P,PTOS		;FINALLY!
	POP	P,D
	POP	P,C
	POP	P,B
	POPJ	P,

OUTPTA:	TLZ	F,NOBREAK	;REMEMBER TO PERMIT BREAK
	TLO	F,COMF	;DENOTE THAT THERE IS NO OUTPUT
	JRST	RETA

FOR SAVBRK,<
GO:	SKIPE	PRODNM
	ERROR	DIRERR	;"GO" ALONE CAN ONLY BE DIRECT
	SKIPG	GODEPTH
	ERROR	GOERR9	;NOPLACE TO GO
	JSP	H,GORE
	POPJ	P,

CANCEL:	SKIPE	PRODNM
	 ERROR	DIRERR	;DIRECT ONLY ERROR BEFORE NOTHING TO CANCEL
	SKIPG	GODEPTH
	 ERROR	CANER1	;NOTHING TO CANCEL
	JSP	H,GORE
	JRST	FLUSHM	;FLUSH TO BSP

GORE:	SOS	GODEPTH	;TEST ABOVE SO IT DOESN'T GO NEGATIVE
	MOVE	A,BSP
	JSP	C,SETPDL
	JSP	D,RESTOR
	POP	P,BSP
	JRST	(H) >


ESTOP:	TLO	F,COMF

OUTPUT:	SKIPN	PRODNM
	ERROR	STOERR		;OUTPUT AND STOP MUST BOTH BE STORED
	PUSHJ	P,SWITCH	; SAVED CBOT AND THE OUTPUT
RETURN:	JSP	D,COMEXR	;DO THE END OF LINE CHECKS
	POP	P,0(P)
RETA:	JSP	E,RESTOA	;RESTORE ALL BUT PRODNM
	TLNE	F,GOF		;IS THIS RETURN UNTRACEABLE?
	JRST	RETD		;YES IT IS, SKIP TRACE STUFF



	MOVE	A,PRODNM
	MOVEI	A,-1(A)	;DON'T CHANGE PRODNM, COMERU MAY NEED IT
	MOVE	A,@RPA	;ENDING A TRACED PROCEDURE?
	TLNN	A,TRACEF
	JRST	RETD	;NO
	MOVNI	B,2
	ADDB	B,TRACEM
	PUSH	P,BCHAR
	ANDI	B,17
	MOVEM	B,BCHAR
	PUSHJ	P,INDENT
	PUSHJ	P,PTOSS	;TYPE PROCEDURE NAME
	MOVEI	A,[ASCIZ / OUTPUTS /]
	TLNE	F,COMF	;DID IT OUTPUT?
	MOVEI	A,[ASCIZ / STOPS/]	;NO, IT STOPPED
	PUSHJ	P,PTOSSM

RETB:	TLNE	F,COMF
	JRST	RETC	;NO NEED TO TYPE WHAT IT OUTPUTTED, IT STOPPED
	TRO	F,PREFIX!SUFFIX
	MOVEI	E,042
	MOVE	A,(S)
	PUSHJ	P,PTOSS
RETC:	POP	P,BCHAR
	PUSHJ	P,CRLF


RETD:	POP	P,E	;OLD PRODNM INTO E
	ANDI	E,377777	;TRUTH BIT IS PACKED IN THIS HALFWORD
	EXCH	E,PRODNM	;SAVE NAME OF PR BEING EXITED IN E
	TLZE	F,COMF	;DID THE PROCEDURE OUTPUT?
	JSP	D,COMEXU	;NO, TREAT IT LIKE A COMMAND
	POPJ	P,

SRCHLN:	HRRZ	A,PRODNM	;THIS IS THE PROCEDURE TO SEARCH
SRCHL1:	HRRZ	A,@RPA	;GET RELATIVE PTR INTO PS SPACE
	CAIN	A,-1	;ERASED?
	ERROR	NOPROD	;THERE IS NO PROCEDURE X

SRCHLL:	MOVEI	A,2(A)	;POINT TO NEXT LINE NO
	HRRZ	B,@PSA	;GET THE LINE NO
	JUMPE	B,1(C)	;R1, FOUND END BEFORE THE LINE
	XCT	(C)	;ARG TO ROUTINE IS THE COMPARE
	JRST	SRCHLL	;CONTINUE ON NO COMPARE
	JRST	2,2(C)	;GOOD COMPARE, R2


GOTOLINE:
	SKIPN PRODNM	;IS THE COMMAND STORED?
	 ERROR STOERR	; NO, IS AN ERROR

	PUSHJ	P,GTL1

	JSP	C,SRCHLN	;IS THERE THAT LINE IN THIS PROCEDURE
	 CAIE	B,(M)
	 ERROR	GOERR3	;THERE IS NO LINE X
	MOVEI	A,-1(A)
	MOVEM	A,NXLINE	;SO STEP TO NEXT LINE WILL FIND THIS ONE WITHOUT SEARCHING
	JRST	COMEX

NUMEVL:	AOS	A,CPP
	MOVE	L,@WSA	;FETCH LINE NUMBER
	TLNE	L,LITF	;IF THIS, DOES NOT NEED TO BE EVALED
	JRST	GOTO1	;IS A LITERAL, COULD BE A NUMBER
	PUSHJ	P,EVAL	;NOT A LITERAL, MUST EVALUATE
GTL1:	POP	S,L	;VALUE RETURNED
GOTO1:	MOVEI	D,(L)	;SAVE POINTER FOR ERROR COMMENT
	TLNN	L,SENTF	; CANNOT BE A NUMBER IF IT IS A SENTENCE
	PUSHJ	P,DNM
	ERROR	GOERR2	;IS NOT A LINE NUMBER
	POPJ	P,

SAVEUP:	HRLZ	B,LINENO	;A MUST BE LEFT ALONE
	HRR	B,PRODNM
	MOVE	C,TRUTH
	ANDI	C,400000
	ORI	B,(C)		;PRODNM .LT. 2^17 BECAUSE THERE MUST BE A
				; NAME FOR EACH ONE 
	PUSH	P,B		;LINENO,,TRUTH+PRODNM
	PUSH	P,DTOP
	MOVE	B,CPP
	SUB	B,CBOT
	TLZE	F,GCCSF		;CODE PTRS PUSHED FROM HERE ON ARE VALID
	TLO	B,200000	;BUT IF SET, THOSE ABOVE ARE NOT
	TRNE	F,EELSEF	;ON A LINE STARTING WITH IF?
	TLO	B,100000	; YES, PUSH THAT FACT
	PUSH	P,B
	PUSH	P,SPP

	SETZM	TRUTH
	SETZM	LINENO
	SETZM	PRODNM
	HRRZI	B,(S)		;WHEN SAVEUP IS CALLED IS THE ONLY TIME
	SUB	B,SP		;THE DEPTH OF THE PUSHDOWN LISTS CHANGE
	HRLZM	B,SPP
	HRRZI	B,1(P)
	SUB	B,PP
	HRRM	B,SPP
	JRST	(D)

RESTOR:	JSP	E,RESTOA	;RESTORE ALL BUT PRODNM
	HRRZM	A,PRODNM
	POP	P,A	;FIX UP PDL
	JRST	(D)

RESTOA:	POP	S,CBOT
	POP	P,SPP
	TLNN	F,GCCSF		; HAS THERE BEEN A POSSIBLE LINE EDIT?
	JRST	RESTOC		; NO, NO NEED TO CHECK FOR ALTERATIONS
	HRRZ	A,-2(P)		;PRODNM BEING RETURNED TO
	ANDI	A,377777	;REMOVE TRUTH FLAG
	JUMPE	A,RESTOC	; SKIP TEST IF DIRECT LINE
	HLRZ	M,-2(P)		;LINE NO
	JSP	C,SRCHL1	;FIND THE LINE AGAIN
	 CAIE	B,(M)
	 ERROR	LNDERR		;LINE DELETED WHILE IN IT
	MOVEI	A,1(A)
	MOVE	A,@PSA		;SEQNO,,CODEPTR
	CAME	A,CBOT		;IS IT THE SAME LINE STILL?
	 ERROR	LNAERR		; NO, IT WAS EDITTED
RESTOC:	POP	P,A		; BITS,,CPP REL CBOT
	TLZE	A,200000	;WAS GCCSF SET AT THIS LEVEL GOING DOWN
	TLO	F,GCCSF		;THEN IT MUST BE SET ON WAY UP
	TLZE	A,100000	; LINE BEING POPPED STARTED WITH IF?
	TRO	F,EELSEF	; YES, THEN IT CAN END WITH ELSE
	ADD	A,CBOT
	MOVEM	A,CPP
	MOVE	C,DTOP		;FLUSH LOCAL BINDINGS THIS LEVEL
	POP	P,DTOP		; HAVING REMEMBERED OLD VALUE
RESTB1:	CAMN	C,DTOP		; ARE THERE ANY MORE BINDINGS?
	JRST	RESTB2		; NO
	MOVEI	C,-1(C)		; REL PTR TO VALUE
	MOVE	B,@DPC		; PICK UP VALUE
	MOVEI	C,-1(C)		; REL PTR TO REL PTR TO VP
	MOVE	A,@DPC
	MOVEM	B,@VPA		; STORE VALUE IN OBLIST
	JRST	RESTB1
RESTB2:	MOVE	A,(P)	;LINENO,,TRUTH+PRODNM
	MOVEI	B,0
	TRZE	A,400000
	HRROI	B,-1	;TRUTH=FALSE
	MOVEM	B,TRUTH
	HLRZM	A,LINENO
	SETOM	NXLINE
	JRST	(E)


FOR TURTLE,<
BACK:	SKIPA	B,[[BYTE (7) 16,177,177,177,177,177,177,0]]
FRONT:	MOVEI	B,[BYTE (7) 1,177,177,177,177,177,177,0]
	JRST	TURTEL

RIGHT:	SKIPA	B,[[BYTE (7) 10,177,177,177,177,0]]
LEFT:	MOVEI	B,[BYTE (7) 26,177,177,177,177,0]
	JRST	TURTEL

HORN:	MOVEI	B,[BYTE (7) 35,177,177,0]
TURTEL:	PUSHJ	P,TOSS		;DO IT
	JRST	COMEX

TOUCHE:	MOVEI	A,101	;TERMINAL OUTPUT
	DOBE		;WAIT FOR EMPTY BUFFER

	MOVEI	A,100	;TERMINAL INPUT
	SIBE		;INPUT BUFFER EMPTY
	JRST	TOUCH1	;NO
	MOVEI	A,200	;8*16MS GTR 1 CHAR TIME
	DISMS		;WAIT FOR A POSSIBLE CHAR

	MOVEI	A,100
	SIBE		;DID A CHAR COME IN?
	JRST	.+2	;YES
	JRST	ISFALSE	;DIDN'T TOUCH ANYTHING

TOUCH1:	BIN
	JRST (D)

TOUCHLEFT:
	JSP D,TOUCHE	;GET A TOUCH CHAR
	CAIE	B,20	;CONTROL P
	JRST	ISFALSE
	JRST	ISTRUE

TOUCHRIGHT:
	JSP D,TOUCHE
	CAIE	B,22	;CONTROL-R
	JRST	ISFALSE
	JRST	ISTRUE >

WAIT:	MOVE	L,(S)
	HRLI	L,010700+W
	PUSHJ	P,DNM
	 ERROR	ZERERR
	POP	S,L
FOR TENEX,<	MOVEI	1,^D1000
	IMUL	1,M
	DISMS >
FOR TEN50,<	MOVEI	A,^D3600
	IMULI	M,^D60
	CAIG	M,(A)
	JRST	.+4
	CALL	A,[SIXBIT /SLEEP/]
	SUBI	M,(A)
	JRST	.-4
	IDIVI	M,^D60
	CALL	M,[SIXBIT /SLEEP/] >
	JRST	COMEX

LINEFEED:
	SKIPA C,[012]
BELL:	MOVEI C,007
	JRST  COMCHR

FORMFEED:
	SKIPA C,[014]
CRETUN:	MOVEI C,015
COMCHR:	PUSHJ P,TYO
	JRST COMEX

SKIP:	PUSHJ P,CRLF
	JRST COMEX

                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                              ;TYPE IN STRING
;CHECK FOR CONTROL CHARACTERS,EDIT CHARS, ILLEGAL CHARS
;EDITF=1_EDIT MODE, THEN A CONTAINS BYTE POINTER TO BEGINNING OF STRING
;BYTE POINTER TO OUTPUT STRING IN B
;RETURN STRING POINTER TO INPUT ON S-LIST
;R1_FAILURE, TRY AGAIN, R2_SUCCESS.
TIS:	MOVEI	C,2	;SET CONTINUATION TO SPACE TWO CHARACTERS
	MOVEM	C,BCHAR
	TLZ	F,LDONF!FCHARF	;CLEAR LINE DONE FLAG FOR EDIT MODE
	TLNN	F,EDITF!GETF	;DON'T TYPE WEDGE OR _ IF EDITING
	PUSHJ	P,TOSS	;PRINT OUT COMMENT FROM B
FOR DRIBBLE,<
FOR TENEX,<
	PUSH	P,A
	SKIPN	A,DRIBFL		;IS THERE A DRIBBLE OPEN?
	JRST	TISAZ		;NO
	SETO	B,
	SETZ	D,
	ODCNV
	HRRZM	D,DRIBTM
TISAZ:	POP	P,A  >>
	PUSHJ	P,NEWSTR
	TLNN	F,EDITF	;ARE WE IN EDIT MODE?
	JRST	TISB	;NO
	PUSHJ	P,NEWSRC
	MOVEM	A,LINBOT	;FOR PURPOSES OF GARBAGE COLLECTION
	PUSHJ	P,TEDIT	;AND COPY IT TO THE INPUT STRING
TISB:	PUSHJ	P,TYI	;GET NEXT CHARACTER INTO C
FOR DRIBBLE,<
FOR TENEX,<
	PUSH	P,A
	SKIPN	A,DRIBFL
	JRST	TISB01
	TLOE	F,FCHARF	;FIRST CHAR ON LINE?
	JRST	TISB0		;NO
	MOVE	H,B		;TIME STAMP THE LINE
	MOVE	G,C
	SETO	B,
	SETZ	D,
	ODCNV
	HRRZI	D,(D)
	SUB	D,DRIBTM
	CAIGE	D,0
	ADDI	D,^D24*^D3600
	HRLZI	E,400001
	ODTNC
	MOVEI	B," "
	BOUT
	MOVE	B,H
	MOVE	C,G
TISB0:	EXCH	C,B
	BOUT
	EXCH	C,B
TISB01:	POP	P,A >>

TISB1:	CAIGE	C,40	;CONTROL CHAR?
	JRST	TISC	;YES
	CAIL	C,175	;RUBOUT OR EOM?
	JRST	TISO	;YES
	AOS	CHARNO	;PRINT CHARACTER SO COUNT
	CAIN	C,134	;BACKSLASH
	JRST	TISN	;YES
TISF:	IDPB	C,B
	JRST	TISB

TISC:	MOVEI	E,(C)	;SAVE CHAR IN C
	ROT	E,-1
	HRRZ	D,ITAB(E)
	JUMPL	E,.+2
	HLRZ	D,ITAB(E)
	JUMPN	D,(D)	;DISPATCH IF IT IS A LEGAL CONTROL CHAR
	JRST	TISF	;NOT A SPECIAL CONTROL CHAR


DEFINE CC (A,B,C,D)
<XWD A,B
 XWD C,D>

ITAB:
CC 0,0,0,0	;NUL ABC
CC 0,0,0,TISF	;DEFG
CC 0,0,TISI,0	;H TAB LF K
CC 0,TISJ,TISK,0	;L CR NO
NOTFOR TURTLE,<CC 0,0,TISL,TISDC3>	;TEN50 USES DC3 AS TERMINATOR IN FILES
FOR TURTLE,<CC TISB,0,TISL,TISDC3>
CC TISLIT,0,0,TISM	;TUVW
NOTFOR TURTLE,<CC 0,0,0,TISZ>	;XYZ ALTMODE
FOR TURTLE,<CC TISB,0,0,TISZ>
FOR TEN50,<CC 0,0,0,0>
FOR TENEX,<CC 0,0,0,TISEOL>	;END OF LINE IS 37, LF ALREADY FLUSHED


TISDC3:	TLNE	F,GETF	;COMING FROM A FILE?
	JRST	TISEOL	;YES, OK
	JRST	TISF
TISD:
FOR TEN50,<	TTCALL 11,>	;FLUSH ALL INPUT
FOR TENEX,<	MOVEI	A,100
	CFIBF>
	MOVEI	B,[ASCIZ "
MEANINGLESS CHARACTER"]
TISA:	PUSHJ	P,TOSS
	PUSHJ	P,CRLF
TISR1:
	TLZE	F,EDITF
	POP	S,A	;CLEAN UP THE PUSH-DOWN LISTS
	POPJ	P,	;GIVE R1
TISG:	MOVEI	C," "	;CONTROL B
	TLNN	F,GETF	;NO ECHO IF FROM FILE
	PUSHJ	P,TYO	;TYPE BACK A SPACE
	MOVEI	C,002	;AND PUT CONTROL B IN BUFFER
	JRST	TISF
TISH:	TLNN	F,GETF
	PUSHJ	P,PTAB		;IF NOT IN A FILE, ECHO SPACES
	MOVEI	C,011
	JRST	TISF


TISLIT:	TLNN	F,GETF	;DOING A GET?
	JRST	TISF	;NO, ^T NOT LEGAL FROM TERMINAL
TISLT1:	IDPB	C,B	;COPY TEXT BETWEEN ^T'S LITERALLY, NO INTERP
	PUSHJ	P,TYI
FOR DRIBBLE,<PUSH	P,A
	EXCH	C,B
	SKIPE	A,DRIBFL
	BOUT
	EXCH	C,B
	POP	P,A >
	CAIE	C,024	;LOOKING FOR MATCHING ^T
	JRST	TISLT1	;NOT FOUND
	JRST	TISF	;BACK TO NORMAL READING

TISI:	TLNE	F,GETF
	JRST	TISF		;COMING FROM FILE, LET IT THRU
	PUSH	P,B
	MOVEI	B,[BYTE (7) 15,15,40,40]
	PUSHJ	P,TOSS
	POP	P,B
	MOVEI	C," "	;AND TREAT LIKE A SPACE
	JRST	TISF

TISO:	CAIE	C,177	;RUBOUT?
	JRST	TISZ	;NO, EOM
	MOVEI	B,[BYTE (7) 15,177,134,0]
	JRST	TISA

TISZ:	PUSHJ	P,CRLF
	JRST	TISEOL
TISJ:	SETZM	CHARNO
	PUSHJ	P,TYI
	CAIE	C,12		;LINEFEED FOLLOWING CR?
	JRST	TISCR		;NO
TISEOL:	SETZB	C,CHARNO
	IDPB	C,B		;FINISH THIS STRING
	HRLZI	L,010700+W
	HRR	L,NEWBOT
	MOVEM	L,LINBOT
	MOVE	B,L		;SAME SRC AND DEST FOR WELL-FORMING

TISEL2:	ILDB	C,B
	JUMPE	C,TISEL3
	CAIL	C,40
	JRST	.-3
	CAIE	C,15
	CAIN	C,12
	JRST	TISEL2		; CR AND LF CAN OCCUR IN LITERALS
	CAIE	C,"G"-100
	CAIN	C,"I"-100
	JRST	TISEL2
	CAIN	C,"L"-100
	JRST	TISEL2
	CAIN	C,"T"-100
	TLNN	F,GETF
	JRST	TISD
	JRST	TISEL2

TISEL3:	MOVE	B,L
	MOVEI	D,0		;TERMINATE ON EOL
	PUSHJ	P,WEFS		;REMOVE EXTRA SPACES
	TLZE	F,EDITF	;CLEAR PDL+EDIT FLAG
	POP	S,-1(S)
CPOPJ1:	AOS	0(P)
CPOPJ:	POPJ	P,


TISCR:	MOVEI	A,15	;CR
	IDPB	A,B		;PUT IT INTO THE TEXT
	JRST	TISB1		;TREAT THE CHAR AFTER CR FOR WHAT IT IS

TISK:	TLNN	F,EDITF	;TYPE NEXT WORD IN EDIT MODE
	JRST	TISF	;NOT IN EDIT MODE
	PUSHJ	P,TEDIT	;COPY NEXT WORD TO TT AND BUFFER
	JRST	TISB
TISL:	TLNN	F,EDITF	;TYPE REST OF LINE IN EDIT MODE
	JRST	TISF	;NOT IN EDIT MODE
	PUSHJ	P,TEDIT
	TLNN	F,LDONF
	JRST	.-2
	JRST	TISB

TEDIT:	TLNE	F,LDONF	;IF LINE DONE JUST EXIT
	POPJ	P,
	PUSH	P,A	;SAVE POINTERS
	PUSH	P,B	;NO GARBAGE COLLECTION POSSIBLE
	PUSHJ	P,PWORD	;PRINT WORD
	POP	P,B
	POP	P,L
TEDITA:	CAMN	A,L	;HAVE WE CAUGHT UP?
	POPJ	P,
	ILDB	C,L	;COPY THE NEXT CHAR INTO THE INPUT STRING
	JUMPE	C,TEDITB	;IS IT A NULL
	IDPB	C,B
	JRST	TEDITA
TEDITB:	TLO	F,LDONF	;SET LINE DONE FLAG
	POPJ	P,	;AND EXIT
TBACK:	CAMN	B,NEWBOT	;ARE WE AT BEGINNING
	JRST	TISB	;OFF BEGINNING OF STRING
	UNDEX	B
	JRST	2,@D

TISN:	JSP	D,TBACK	;BACK UP ONE CHARACTER
	JRST	TISB

TISMA:	JSP	D,TBACK	;REMOVE ONE MORE CHARACTER
	MOVEI	C,134	;TYPE BACKSLASH
	PUSHJ	P,TYO
TISM:	CAMN	B,NEWBOT	;IS THERE A PREVIOUS CHAR
	JRST	TISB	;NO-SO QUIT
	LDB	C,B	;YES-REMOVE LEADINGS SPACES
	CAIN	C," "
	JRST	TISMA
TISMB:	CAMN	B,NEWBOT	;REMOVE NON-SPACES TIL A SPACE
	JRST	TISB	;OFF BEGINNING OF LINE SO QUIT
	LDB	C,B
	CAIN	C," "
	JRST	TISB
	JSP	D,TBACK
	MOVEI	C,134	;TYPE A BACKSLASH WHEN CHARACTERS REMOVED
	PUSHJ	P,TYO
	JRST	TISMB

;PRINT WORD
;ENTER WITH BYTE POINTER TO WORK-SPACE IN A
;PRINT WORD AND FOLLOWING SPACE IF EXISTS
;TYO UPDATES CHARNO AND TAKES INPUT IN C

PWORDH:	PUSHJ	P,TYO	;JUST THE CR, NO LF
PWORD:	MOVEI	D,0	;INITIALIZE COUNT OF PRINTING CHARACTERS
	MOVE	B,A	;COPY POINTER
	TRNE	F,PREFIX	;MUST WE PREFIX " OR / TO THIS WORD?
	MOVEI	D,1	;YES, COUNT THE EXTRA CHAR
PWORDB:	ILDB	C,B	;COUNT PRINTING CHARACTERS
	CAIE	C,002	;CONTROL-B IS A SPACING CHARACTER
	CAILE	C," "
	AOJA	D,PWORDB
	CAIE	C,015	;STOP IF CR
	CAIN	C," "	;OR END OF WORD
	JRST	PWORDA
	JUMPN	C,PWORDB	;CONTINUE ON NOT NULL
	TRNE	F,SUFFIX	;MUST WE SUFFIX " OR / TO END OF THIS STRING?
	MOVEI	D,1(D)	;YES, COUNT THE EXTRA CHAR AT END
PWORDA:	TLNE	F,SAVEF	;WHEN SAVING
	JRST	PWORDS		;THE WORD ALWAYS FITS ON A LINE
	MOVEI	C,^D72
	SUBI	C,(D)
	CAMGE	C,BCHAR
	JRST	PWORDC	;YES
	CAMGE	C,CHARNO	;COUNT+CHARNO .GTR. 72.?
	PUSHJ	P,LINE	;YES-CRLF AND SPACE TO BCHAR
PWORDS:	MOVEI	C,(E)	;PUT PREFIX IN C
	TRZE	F,PREFIX	;SHOULD WE TYPE A PREFIX?
PWORDE:	PUSHJ	P,TYO	;YES
	ILDB	C,A	;OTHERWISE PRINT WORD
	CAME	A,B	;HAVE WE CAUGHT UP
	JRST	PWORDE	;NO
PWORDF:	LDB	C,A	;WHAT WAS THE TERMINATING CHARACTER
	JUMPE	C,PWORDX
	CAIN	C,015
	JRST	PWORDH	;CR- HANDLE REST OF WORD JUST LIKE WORD
	CAIN	C," "
	JRST	PWORDD	;SPACE
	PUSHJ	P,LINE	;OTHER-MEANS WE ARE AT END OF LINE BUT NOT OWRD
	JRST	PWORD	;AND GO PROCESS REST AS WORD

PWORDX:	MOVEI	C,(E)	;AT EOM
	TRZE	F,SUFFIX	;MUST WE AFFIX A SUFFIX?
	PUSHJ	P,TYO	;YES, TYPE IT
	POPJ	P,

PWORDC:	MOVE	D,BCHAR	;IF BCHAR .NE. CHARNO CRLF AND SPACE
	CAMLE	D,CHARNO
	PUSHJ	P,LINE
	MOVEI	C,(E)
	TRZE	F,PREFIX
	PUSHJ	P,TYO
PWORDG:	MOVEI	C,110	;PRINT TILL CHARNO=72.
	CAMN	C,CHARNO
	JRST	PWORDF
	ILDB	C,A
	PUSHJ	P,TYO
	JRST	PWORDG
PWORDD:	MOVEI	C,110	;TYPE SPACE IF CHARNO .NE. 72.
	CAMN	C,CHARNO	;TYPE CRLF AND SPACES IF CHARNO=72.
	JRST	LINE
	MOVEI	C," "
	JRST	TYO


PTYO:	MOVEI	C,^D72
	CAMN	C,CHARNO
	PUSHJ	P,LINE
	MOVEI	C,(B)
	JRST	TYO

PSPACE:	PUSH	P,B
	MOVEI	B," "
	PUSHJ	P,PTYO
	POP	P,B
	POPJ	P,

PTAB:	HRRZ	B,CHARNO
	IDIVI	B,11		;TAB STOPS ARE NINE APART
	SUBI	C,11
	MOVE	B,C
	PUSHJ	P,PSPACE
	AOJL	B,.-1
	POPJ	P,

LINE:
	SETZM	CHARNO
	TLNE	F,SAVEF
	PJRST	PSPACE
	PUSHJ	P,CRLF	;TYPE CRLF AND SPACES USING C
INDENT:	MOVE	C,BCHAR
	CAMN	C,CHARNO
	POPJ	P,
	CAMG	C,CHARNO
	JRST	INDENT-1	;BCHAR .LT. CHARNO
	MOVEI	C," "
	PUSHJ	P,TYO
	JRST	INDENT

CRLF:	TLNN	F,SAVEF
	SETZM CHARNO
	MOVEI	C,015
	PUSHJ	P,TYO
	MOVEI	C,012
	JRST	TYO

REQUEST:	TLO	F,RQF
	MOVEI	B,[ASCIZ /*/]
	SKIPE	CHARNO	;AT LEFT MARGIN?
	MOVEI	B,[EXP 0]	;NO, DON'T TYPE ANYTHING
	PUSHJ	P,TIS
	JRST	REQUEST
	TLZ	F,RQF
	POPJ	P,

FOR TENEX,<
XBOUT:	PUSH	P,A
	EXCH	B,C
FOR NFILE,<MOVEI	A,101>
FOR OFILE,<MOVE	A,OUTFILE >
	BOUT
	EXCH	B,C
	POP	P,A
	POPJ	P,

XBIN:	PUSH	P,A
	MOVE	C,B
FOR NFILE,<MOVEI	A,100 >
FOR OFILE,<MOVE	A,INFILE >
	BIN
	EXCH	C,B
	POP	P,A
	POPJ	P,  >

FOR TENEX,<
ASK:	POP	S,L
	PUSHJ	P,DNM
	 ERROR	ZERERR
	MOVEI	C,"*"
	SKIPN	CHARNO		;AT LEFT MARGIN?
	PUSHJ	P,TYO		;YES

	MOVE	A,[XWD 15,1]
	ATI			;CR TO CH1 FOR ASK
	MOVE	A,[XWD 33,2]
	ATI			;ALT MODE TO CH 2 FOR ASK
	MOVE	A,[XWD 34,3]
	ATI			;RUBOUT TO CH 3 FOR ASK
	MOVEI	A,100
	RFMOD
	ANDCMI	B,6000
	IORI	B,2000		;CHANGE ECHO MODE TO IMMEDIATE
	SFMOD
	HRLZI	A,400000
	HRLZI	B,340000
	AIC	;ACTIVATE INTERRUPT CHANNELS FOR CR,ALTMODE, AND RUBOUT
	MOVEI	A,^D1000
	IMULI	A,(M)
	DISMS			;IN MILLISECONDS
	MOVEI	A,100
	CFIBF			;INCOMPLETE, FLUSH ALL OF IT

ASK1:	MOVEI	A,15		;CR
	DTI			;DEASSIGN TERMINAL INTERRUPT
	MOVEI	A,33		;EOM
	DTI
	MOVEI	A,34		;RUBOUT
	DTI
	HRLZI	A,400000
	HRLZI	B,340000
	DIC			;DEACTIVATE ASK CHANNELS
	MOVEI	A,100		;TERMINAL JFN
	RFMOD
	XORI	B,6000
	SFMOD
	MOVEI	B,37
	STI			;EOM FOR TIS
	MOVEI	B,[EXP 0]
	PUSHJ	P,TIS
	 ERROR	IOPERR
	POPJ	P,

ASKEOL:	MOVEI	A,ASK1
	MOVEM	A,LEVLTB+2
	DEBRK

ASKRUB:	MOVEI	A,100
	CFIBF
	MOVEI	B,[BYTE (7) 15,177,43,0]
	PUSHJ	P,TOSS
	PUSHJ	P,CRLF
	MOVEI	C,"*"
	PUSHJ	P,TYO
	DEBRK		>	;CONTINUE WAITING

TYO:	TLNE	F,NOBREAK
	JRST	.+3
	TLZE F,BREAKF
	 ERROR	BREAK
	TLNE	F,SAVEF	;GOING TO A FILE?
	JRST	TYO2	;YES, LET TAB AND ^B THROUGH UNTRANSLATED
	CAIE	C,11	;IS IT TAB
	JRST	TYO1	;NO
	PUSH	P,B
	PUSHJ	P,PTAB
	POP	P,B
	POPJ	P,

TYO1:	TLNE	F,TOF	; # GET CONVERTED ONLY ON PRINT OR TYPE
	CAIE C,"#"	;MULTIPLE SPACE CHAR
	CAIN	C,002
TYOSPC:	MOVEI	C,40		;SUBSTITUTE SPACE FOR CONTROL-B
TYO2:	TLNN	F,TOF
	JRST	.+3
	CAIN	C,015	;IS THE CHAR CR
	XCT	CHOUT	;YES, TYPE TWICE, NOT THE BEST WAY TO GET TIMING ON CR ALONE
	XCT	CHOUT
	CAIN	C,177
	JRST	.+3	;RUBOUTS DON'T COUNT
	CAIL	C," "	;IS IT A PRINTING CHARACTER
	AOS	CHARNO
	CAIN	C,015	;CR-CLEAR CHARNO
	SETZM	CHARNO
	POPJ	P,

TYI:	TLO	F,TIF	;SET TYPEIN FLAG
	TLNE	F,NOBREAK
	JRST	.+3
	TLZE F,BREAKF
	 ERROR	BREAK
	XCT	CHIN		;GET A CHARACTER
	TLZ	F,TIF
	POPJ	P,
PTOSSM:	HRLI	A,440700	;FOR MACHINE STRINGS
	JRST	PTOS
PTOSS:	HRLI	A,010700+W	;FOR GENERATED STRINGS
PTOS:	PUSHJ	P,PWORD
	LDB	C,A
	JUMPN	C,.-2
	POPJ	P,

TOSW:	HRLI	B,010700+W
	JRST	TOS
TOSS:	HRLI	B,440700
TOS:	ILDB	C,B	;BYTE POINTER IN B USE C
	JUMPE	C,CPOPJ
	PUSHJ	P,TYO
	JRST	TOS

NUMBRQ:	TLNE	A,EMPTYF	;IS IT EMPTY?
	JRST	CPOPJ	;YES
	TRZ	F,TF
	HRLI	A,010700+W

	ILDB	C,A
	JUMPE	C,CPOPJ	;EMPTY
	CAIE	C,"+"
	CAIN	C,"-"
NMBRQ1:	ILDB	C,A
	JUMPE	C,NMBRQ2
	TRO	F,TF	;HAVE SEEN A CHARACTER
	CAIL	C,"0"
	CAILE	C,"9"
	POPJ	P,	;NOT A DIGIT, FAIL
	JRST	NMBRQ1

NMBRQ2:	TRZE	F,TF	;ALL THE CHARS WE SAW WERE DIGITS, BUT WERE THERE ANY
	AOS	0(P)	;SAW SOME DIGITS
	POPJ	P,	;SAW NO CHARACTERS


PREDIQ:	MOVE	B,@A
	ADDI	A,1
	CAIE	B,1	;IS LENGTH OF TEXT 1?
	JRST	PREDQ1	;NO, CANNOT BE "TRUE"
	MOVE	B,@A	;GET TEXT OF ONE WORD ELEMENT
	CAME	B,[ASCIZ /TRUE/]
	POPJ	P,	;NOT "TRUE"
	JRST	CPOPJ1	;IS "TRUE"
PREDQ1:	CAIE	B,2	;FALSE MUST BE TWO WORDS LONG
	POPJ	P,	;NOT "FALSE"
	MOVE	B,@A
	ADDI	A,1
	CAMN	B,[ASCII /FALSE/]
	SKIPE	@A	;NEXT WORD MUST BE 0 TERMINATOR FOR "FALSE"
	POPJ	P,	;R1, IS NOT "FALSE"
	JRST	CPOPJ1	;R2


NUMRQS:	MOVE	A,(S)	;NUMBERS?
	PUSHJ	P,NUMBRQ
	 ERROR	SUMERR
	MOVE	A,-1(S)
	PUSHJ	P,NUMBRQ
	 ERROR	SUMERR
	POPJ	P,


DNMO:	MOVEI R,10
	JRST DNM0
DNM:	HRLI	L,010700+W	;ALL CALLS USE A COMPLETE STRING
	MOVEI	R,12
DNM0:	MOVEI	M,0
	TRZ	F,NNUMF
DNM1:	ILDB	C,L
	CAIL	C,"0"
	CAILE	C,"9"
	JRST	DNM2
	TRO	F,NNUMF
	IMULI	M,(R)
	ADDI	M,-60(C)
	JRST	DNM1
DNM2:	TRNN	F,NNUMF
	POPJ	P,
	JRST	CPOPJ1


DECPRT:	SKIPA	B,[EXP 12]
OCTPRT:	MOVEI	B,10
	MOVEM	B,RADIX
ANYPRT:	IDIV	A,RADIX
	HRLM	B,(P)
	SKIPE	A
	PUSHJ	P,ANYPRT
	HLRZ	C,(P)
	ADDI	C,"0"
	JRST	TYO

SNM:	PUSHJ	P,NEWSTR
SNM0:	SETZM	SRCBOT
SNMA:	IDIVI	M,12
	HRLM	N,(P)
	SKIPE	M
	PUSHJ	P,SNMA
	HLRZ	C,(P)
	ADDI	C,"0"
	IDPB	C,B
	POPJ	P,

COPYAB:	ILDB	C,A
	IDPB	C,B
	JUMPN	C,.-2
	POPJ	P,


SWITCH:	MOVE	A,(S)
	EXCH	A,-1(S)
	MOVEM	A,(S)
	POPJ	P,


;MAKE AN ARBITRARY SIZED WORKSPACE ELEMENT
; ENTER WITH LENGTH IN A
; RETURN WITH PTR TO LENGTH WORD IN B

MAKELM:	PUSHJ	P,NEWSTR	; GENERATING A STRING
	SETZM	SRCBOT		; WITH NO SOURCE
	MOVEM	A,@B		; SET UP LENGTH WORD
	ADD	B,A		; POINT B AT LAST DATA WORD
	SETZM	@B		; REFERENCE IT TO CAUSE ILL MEM REF
	MOVEM	B,WTOP		; GET HERE AFTER GC AND EXPAND WS
	SUB	B,A		; PROMISED THAT B POINT AT FIRST
	POPJ	P,


;COPY CONTENTS OF AN ELEMENT TO A NEW AND LARGER ONE
; ENTER WITH LENGTH INCREMENT IN A
; AND PTR TO OLD ELEMENT IN B
;RETURN PTR TO NEW ELEMENT IN B
; AND PTR TO OLD END IN NEW ELEMENT IN A

COPYUP:	PUSH	S,B		; SAVE OLD GUY
	ADD	A,@WSB		; NEW LENGTH
	PUSHJ	P,MAKELM	; MAKE IT
	POP	S,A
	HRLZI	C,@WSA		; FROM
	HRRI	C,@WSB		; TO
	AOBJN	C,.+1		; BUT SKIP THE WORD COUNT
	MOVE	A,@WSA		; OLD LENGTH
	ADD	A,B		; NEW BASE
	BLT	C,@WSA		; ENDING AT END OF OLD DATA
	POPJ	P,

;NEWOPS IS CALLED BY FIRST,LAST,BUTFIRST, AND BUTLAST
;ALL HAPPEN TO DO THE SAME THING

;NEWSTR IS USED BY ANYONE WHO GENERATES A NEW STRING

NEWOPS:	MOVE	A,0(S)	;GET ARG OFF S STACK WITHOUT DESTROYING IT
	TLNE	A,EMPTYF	;IS IT AN EMPTY STRING
	JRST	APOPJ		;YES EXIT FROM CALLING ROUTINE
	TLNE	A,WORDF
	AOS	0(P)	;SKIP JUMP TO SENT IF A WORD
	HRLI	A,010700+W	;IN ANY CASE, MAKE A STRING PTR
	MOVEM	A,SRCBOT
NEWSTR:	HRRZ	B,WTOP
	ADD	B,[XWD 010700+W,1]	;MUST BE EXACTLY THIS FOR TEST IN TBACK TO WORK
	MOVEM	B,NEWBOT
	POPJ	P,



;NEWSRC SETS UP A NEW SOURCE STRING FROM THE FIRST ARG ON THE PDL
;NEWSR1 DOES THE SAME FOR THE SECOND ARG BACK

NEWSR1:	SKIPA	A,-1(S)
NEWSRC:	MOVE	A,0(S)
NEWSR0:	HRLI	A,010700+W
	MOVEM	A,SRCBOT
	POPJ	P,


GNE:	AOS	A,CPP		;GET NEXT NON-COMMENT ELEMENT
	MOVE	A,@WSA
	JUMPE	A,CPOPJ		;END OF LINE, R1
	TLNE	A,COMMTF	;IS IT COMMENT
	JRST	GNE		;YES, SKIP IT
	JRST	CPOPJ1

ENDSTP:	POP	S,C	;FLUSH OLD INPUT FIRST BEFORE
ENDSTR:	MOVEI	C,0	;FINISH UP THE NEW STRING
	IDPB	C,B
ENDST1:	TLNE	B,760000
	JRST	.-2
	MOVEM	B,WTOP
	SUB	B,NEWBOT
	HRRZM	B,@NEWBOT
	HRLZI	B,W+WORDF
	HRR	B,NEWBOT
	TRZE	F,NWF
	TLC	B,WORDF!SENTF
	PUSH	S,B
	POPJ	P,
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                 
;THE NEXT SECTION (10-11 PAGES) CONTAINS STORAGE ALLOCATION
;AND GARBAGE COLLECTION ROUTINES FOR THE VARIOUS SPACES

;MOVE A PROCEDURE TO THE END OF THE PROCEDURE SPACE

MOVEPR:	SETOM	NXLINE	;POINTER INTO PS NO LONGER VALID
	HRRZ	D,@RPA	; A CONTAINS A PRODNM
	MOVEI	B,(D)	;B AND D HAVE REL PTR TO BEGINNING OF DIRECTORY

	MOVEI	D,2(D)	;TWO WORDS PER LINE IN PROCEDURE DIRECTORY
	SKIPE	@PSD	;LINE NO=0?
	JRST	.-2	;NO, NEXT LINE

	MOVEI	D,1(D)
	HRRZ	C,PTOP
	CAIL	D,(C)
	POPJ	P,	;ALREADY THE LAST ONE IN THE SPACE

	SUBI	D,(B)	;D IS NOW THE LENGTH OF THE PROCEDURE
	ADDI	C,@PSD
	CAML	C,PS+1
	EXPAND	D,PS

	HRRZ	E,PTOP
	HRRM	E,@RPA
	HRLI	E,(B)
	MOVE	C,PS
	HRLI	C,(C)
	ADDB	E,C
	MOVE	G,E
	HRLI	D,(D)
	ADD	G,D
	BLT	C,-1(G)
	HLR	G,E
	BLT	G,-1(E)

	MOVEI	A,0	;SEARCH RP FOR ALL THAT POINT AFTER MOVED DIR.
	MOVNI	D,(D)
MPRL:	SKIPN	@RPA
	POPJ	P,	;DONE
	MOVEI	A,1(A)
	HRRZ	C,@RPA
	CAIN	C,-1	;IS THIS ONE DEFINED?
	AOJA	A,MPRL	;NO, DON'T OFFSET IT
	CAIL	C,(B)	;B CONTAINS PTR TO OLD BEG OF MOVED PROC
	ADDM	D,@RPA	;OFFSET IT BY AMOUNT MOVED DOWN
	AOJA	A,MPRL

;GARBAGE COLLECTOR PASS ONE
;SEARCH ALL LISTS AND THINGS, REPLACING START OF ALL GOOD STRINGS

GARCOL:	MOVE	S,UUOACS+S
	SETZM	1(S)	;ALWAYS ROOM FOR ONE ZERO AFTER S LIST, BECAUSE
			; CS ALWAYS STARTS AT ONE
	TLZ	F,GCF	;INDICATE THAT GC WAS DONE FOR THIS K
	MOVEI	N,.WTOP-.WBASE	;BOTTOM OF FLUSHABLE STRINGS

	MOVE	B,VP
	JSP	S,GCMARK
	MOVN	B,DTOP
	HRLZI	B,(B)
	HRR	B,DP
	ADDI	B,1
	ADD	B,[XWD 2,2]
	JUMPGE	B,.+3
	JSP	L,MARKNEW
	JRST	.-3

	MOVE	B,SP
	MOVEI	B,1(B)	;STACKS SKIP THE FIRST CELL
	JSP	S,GCMARK
	MOVE	B,UA
	JSP	S,GCMARK	;USER ABBREVIATIONS ALSO

	HRLZI	A,GCSTAB-GCSTOP	;A TABLE OF PAIRS,LIKE UUOACS+L,LINBOT
	MOVE	B,GCSTAB(A)
	HLR	C,B
	MOVN	E,(B)		;LIKE MOVN E,LINBOT
	ADDM	E,(C)		;LIKE ADDM E,UUOACS+L , TO OFFSET AC
	AOBJN	A,.-4		;BACK FOR THE REST OF THE FUNNIES

	HRLZI	A,GCSTAB-GCSTOP
	HRRZ	B,GCSTAB(A)
	JSP	L,MARKNEW
	AOBJN	A,.-2


	MOVE	A,RP	;MARK PROCEDURES
GCMKP1:	MOVE	B,(A)	;PTR TO PROCEDURE NAME
	JUMPE	B,GCPSS2
	MOVEI	B,(A)
	JSP	L,MARK1	;MARK THE NAME

	MOVEI	A,1(A)		; POINT AT PTR TO DIRECTORY
	MOVE	M,(A)		; POSITION IN PS
	CAMN	M,[-1]		; IS THE PROCEDURE DEFINED?
	AOJA	A,GCMKP1	; NO, NOTHING TO COLLECT
	ADD	M,PS		; MAKE PTR ABSOLUTE
GCMKP2:	MOVEI	B,1(M)		; B NOW POINTS TO HEAD OF COMPOUND 
	JSP	L,MARKNEW
	MOVEI	M,2(M)		; NEXT LINE
	SKIPE	(M)		; IS IT END OF PROCEDURE?
	JRST	GCMKP2
	AOJA	A,GCMKP1	; YES, TRY NEXT PROCEDURE


GCMARK:	SKIPN	(B)
	JRST	(S)
	JSP	L,MARKNEW
	AOJA	B,GCMARK

REPEAT 0,<
MARK1:	MOVE	D,(B)	;CALLED WITH MOVEI B,ADDR OR EQUIVALENT
	CAME	D,[EXP -1]
	TLNN	D,W
	JRST	(L)
	CAILE	N,(D)	;IS THIS A FLUSHABLE STRING?
	JRST	(L)	;NO
	HLRZ	C,@WSD	;GET CURRENT BACK POINTER
	HRRM	C,(B)	;PUT IT INTO THIS REFERRER
	HRLM	B,@WSD	;PUT ADDR OF THIS REFERRER AS TOP OF BACK CHAIN
	JRST	(L)
>

;MARK MIXED LISTS
;B CONTAINS PTR TO HEAD
;C=0 IS THE END CONDITION

MARKNEW:
	SETZ	C,
MARKN0:	MOVE	D,(B)		; GET POINTER TO BE CHASED
	TLNN	D,IMMEDIATE	; IF TRUE, IT IS NOT A POINTER
	CAILE	N,(D)		; OR ONE OF THE PERMANENT STRINGS
	JRST	MARKN1		; DO PREVIOUS ONE IN THIS LIST

	ADDI	D,(W)		; MAKE RELATIVE PTR ABSOLUTE
	HLRZ	E,(D)		; GET HEAD OF BACKCHAIN
	HRLM	B,(D)		; AND MAKE BACKCHAIN POINT AT THIS PTR
	TLNE	D,COMPOUND	; IS IT ALSO A MIXED LIST?
	JUMPE	E,MARKN2	; AND NOT PREVIOUSLY CHASED
	HRRM	E,(B)		; NO TO EITHER, JUST FINISH BACKCHAIN

MARKN1:	JUMPE	C,(L)		; END OF TOP LEVEL?
	SUBI	B,1		; NO, DO PREVIOUS ONE ON THIS LEVEL
	SOJG	C,MARKN0	; IF NOT DONE WITH THIS LEVEL

	HLRZ	C,(B)		; GET HEAD OF BACKCHAIN,TAIL AT PREV LEV
	MOVE	B,C
	MOVE	C,(B)		; FETCH POSSIBLE COUNT
	TLZN	C,IMMEDIATE	; IS IT END OF BACKCHAIN?
	JRST	.-3		; NO, TRACE CHAIN SOME MORE

	HLLZM	C,(B)		; COMPLETE BACKCHAIN FOR PASS TWO
	MOVEI	C,(C)		; MAKE IT 0,,COUNT
	JRST	MARKN1		; AND GO BACK FOR MORE ON POPPED LEVEL

MARKN2:	HLL	C,D		; PUSH DOWN A LEVEL AND CHASE IT
	TLO	C,IMMEDIATE	; SO THE POP CAN BE UNAMBIGUOUS
	MOVEM	C,(B)		; THAT'S WHERE THE COUNT IS STORED
	MOVE	B,D		; POINT TO HEADER WORD NEXT LEVEL DOWN
	HRRZ	C,(B)		; GET THE COUNT FOR THIS LEV
	ADD	B,C		; START CHASING AT BACK END OF OBJ
	JRST	MARKN0		; AND START OVER


;GARBAGE COLLECTOR PASS TWO

GCPSS2:	MOVEI	R,GCPS2R	; PASS TWO SUBR TO CHASE BACKCHAINS
	JSP	L,GCPS2S
	MOVEI	R,GCPS3R	; PASS THREE SUBR TO COMPRESS
	JSP	L,GCPS2S
GCPS2C:	SUBI	E,@WSB	;MAKE E THE AMOUNT COLLECTED
	MOVNI	G,(E)		;USE G TO OFFSET WTOP,ETC
	HRRM	B,NEWBOT	;B IS THE RELATIVE DEST FOR THE STRING
	ADDM	G,UUOACS+B
	ADDM	G,WTOP

	HRRZ	D,JOBREL
	SUBI	D,(E)
	HRLI	B,(A)
	ADDI	B,(W)
	CAMG	A,JOBREL	;DON'T GET CAUGHT WITH ILL MEM REF
	BLT	B,(D)

GCPS2D:	CAIG	E,2000	;DID WE RECOVER MORE THAN A FULL K?
	JRST	GCPSS3	;NO
	MOVEI	A,(D)		;YES, REMOVE EXTRA K'S
FOR TEN50,<	CALL	A,[SIXBIT /CORE/]
	 HALT>

FOR TENEX,<	JSP	E,SETMEM>
	JRST	GCPSS3

 
GCPSS3:	MOVEI	D,2(D)
	CAMLE	D,JOBREL
	JRST	GCPS3B		;AVOID DOING BLT ON FULL ALLOCATION
	SETZM	-1(D)
	HRLI	D,-1(D)
	BLT	D,@JOBREL

GCPS3B:	HRLZI	A,GCSTAB-GCSTOP	;UNDO THE CRAZIES
	MOVE	B,GCSTAB(A)
	HLR	C,B
	MOVE	G,(B)
	ADDM	G,(C)
	AOBJN	A,.-4
	JRST	(P)


GCSTAB:	XWD UUOACS+L,LINBOT
	XWD UUOACS+A,SRCBOT
	XWD CPP,CBOT
FOR TEN50,<
	XWD BUFLOC,BUFADR
	XWD BUFLOC+1,BUFADR+1
	XWD FPTR,BUFADR+1
	XWD BUFLC2,BUFAD2
	XWD FPTR2,BUFAD2>
GCSTOP==.

	HRRZ	E,NEWBOT
	HRRZ	B,WTOP
	CAIG	E,(B)
	MOVEI	E,1(B)	;WTOP .GTR. NEWBOT, I.E. NO STRING IN PROGRESS
	ADDI	E,(W)	;FOR END TEST, QUIT BEFORE NEW STRING
	MOVEI	A,.WTOP-.WBASE(W)	;SOURCE IS ABSOLUTE
	MOVEI	B,.WTOP-.WBASE	;DESTINATION IS RELATIVE
GCPS2A:	CAIN	E,(A)	;IS THE NEXT STRING THE UNFINISHED ONE?
	JRST	(L)	; YES, DONE WITH THIS PASS

	MOVE	C,(A)	;GET THE LENGTH WORD
	TLNE	C,-1	;DOES IT HAVE A BACK CHAIN POINTER, IE MARKED?
	JSP	S,(R)	; YES, CHASE OR COMPRESS
GCPS2B:	ADDI	A,1(C)	;NEXT SOURCE
	JRST	GCPS2A


GCPS2R:GCPS2S:	HLRZ	D,C	;TRACE THE BACK CHAIN
	HRRZ	G,(D)
	HRRM	B,(D)	;REPLACE BACK CHAIN POINTER WITH NEW RELATIVE POINTER
	MOVEI	D,(G)	;PUT THE NEW BACK PTR IN D
	JUMPN	D,.-3	;THE CHAIN CONTINUES
	ADDI	B,1(C)	; AND POINT TO NEXT OBJ
	JRST	(S)

GCPS3R:	HRRZM	C,(A)	;CLEAR THE BACK CHAIN POINTER IN LENGTH WORD
	HRLZI	G,(A)	;BLT SOURCE
	HRRI	G,@WSB	;BLT DEST, MAKING IT ABSOLUTE
	ADDI	B,(C)	;DEST+LENGTH=END OF BLT
	BLT	G,@WSB	;MOVE IT
	ADDI	B,1	;MAKE B POINT BEYOND END OF THIS STR, AT BEG OF NEXT
	JRST	(S)

FOR TEN50,<
ALLOC:	EXCH	A,JOBCNI	;SAVE A AND GET REASON FOR TRAP
	TRNE	A,20000	;WAS IT AN ILL MEM REF?
	JRST	ALOCWS	;YES, WORKSPACE SPACE NEEDED
	EXCH	A,JOBCNI >	;CLEAN UP

ALLOCP:	JUMPG	S,ALOCSP	;PDL TRAP, WAS IT ARG STACK?
	JUMPL	P,[ERROR .]
	EXPAND	PP	;NO, CONTRO STACK
	HRL	P,ALOCTB+PP-RP
	TLC	P,-1
	ADD	P,[XWD 1,0]	;MAKE IT TWO'S COMPLEMENT
	JRST	ALOCAL

ALOCSP:	EXPAND	SP
	HRL	S,ALOCTB+SP-RP
	TLC	S,-1
	ADD	S,[XWD 1,0]
	JRST	ALOCAL

FOR TEN50,<
ALOCWS:	EXCH	A,JOBCNI
	EXCH A,JOBTPC
	TLNN A,20000	;IS BYTE INTERRUPT ON?
	SUBI A,1	;NO, MAKE PC POINT AT INSTRUCTION THAT FAILED
	EXCH A,JOBTPC
	EXPAND	WS

ALOCAL:	MOVEM	A,JOBCNI
	HRRZI	A,220000
	CALLI	A,16	;REENTER TRAPPING MODE
	MOVE	A,JOBCNI
	JRST	2,@JOBTPC >

FOR TENEX,<

ALOCWS:	EXPAND WS
ALOCAL:	DEBRK


LEVTAB:	EXP LEVLTB,LEVLTB+1,LEVLTB+2

DATAEX:	ERROR	. >

ALLOCATOR:
	LDB	C,[POINT 4,JOBUUO,12]	;GET THE AC NUMBER
	HRRZ	A,JOBUUO	;WHICH SPACE IS IT?
	HRRZ	B,ALOCTB-RP(A)	;HOW MUCH SPACE MUST WE ADD NOW?
	JUMPE	C,.+3	;SKIP NEXT TWO IF NO AC SPECIFIED
	CAMGE	B,UUOACS(C)	;IS THE SPECIFIED AMOUNT GREATER THAN THE NORMAK AMOUNT
	MOVE	B,UUOACS(C)	;REQUESTED AMOUNT GREATER THAN NORMAL
	MOVEM	B,GCTEM
	ADDI	B,@WTOP
	CAIN	A,WS	;IS THE REQUEST FOR MORE WORK SPACE?
	JRST	ALLOC0	;YES
	CAMG	B,JOBREL	;DOES THE REQUESTED AMOUNT FIT
	JRST	ALLOC1	;YES, DON'T ASK FOR MORE

	JSP	G,NEWMEM
	 ERROR	PUNT	;NO MORE CORE AVAILABLE,
	JRST	ALLOC1	;GOT ONE

ALLOC0:	HRRZ	A,JOBREL
	ADDI	A,1
	JFFO	A,.+2
	 ERROR	IOPERR
	ASH	A,(B)
	JUMPN	A,.+3	;ARE WE AT 2^N K BOUNDARY?
	TLOE	F,GCF	;YES, HAVE WE GC'ED THIS 2^N K YET?
	JRST	ALLC01	;NO, GO DO IT
	JSP	G,NEWMEM
	 ERROR	PUNT	;CAN'T GET MORE CORE AND CAN'T SAVE BY GC
	JRST	UUORET	;RETURN

ALLC01:	JSP	P,GARCOL
	HRRZ	A,JOBUUO
	CAIN	A,WS	;WAS IT WORKSPACE CALL?
	JRST	UUORET	;YES, NOW DONE
	MOVE	C,GCTEM
	ADDI	C,@WTOP
	CAMLE	C,JOBREL	;DID THE GC GIVE US ENOUGH ROOM?
	 ERROR	PUNT	;NO

ALLOC1:	HRRZ	A,JOBUUO
	MOVE	B,GCTEM
	MOVEI	G,(B)
	HRLI	G,(G)
	MOVE	H,1(A)
	HRRZ	C,JOBREL
	HRRZI	D,1(C)
	SUBI	D,(B)
	HRLZI	D,(D)
	HRRI	D,1(C)
	JRST	ALLOC3

ALLOC2:	MOVE	E,D
	BLT	E,(C)
	SUBI	C,(B)
ALLOC3:	SUB	D,G
	CAIGE	H,(D)
	JRST	ALLOC2

	HRLZI	D,(H)
	HRRI	D,1(H)
	SETZM	(H)
	ADDI	H,(B)
	BLT	D,-1(H)

	MOVEI	A,1(A)
	JSP	D,RPOINT

UUORET:	MOVE	17,[XWD UUOACS+1,1]	;DON'T RESTORE 0
	BLT	17,17
	JRST	2,@UUOTRP


RPOINT:	HLRZ	C,ALOCTB-RP(A)	;POINTER TO LIST OF WORDS TO UPDATE
	ADDM	B,(A)		;UPDATE THE BASE IN THE BASE TABLE
	JUMPE	C,ALLOC5	;NO SECONDARY BASE POINTERS
	MOVE	E,(C)		;GET AN ADDRESS OF A SECONDARY BASE PTR
	JUMPE	E,ALLOC5	;NO MORE IN THIS LIST
	ADDM	B,(E)		;UPDATE IT
	AOJA	C,.-3
ALLOC5:	CAIE	A,WS
	AOJA	A,RPOINT	;MORE TO DO IN ALOCTB
	JRST	(D)


FOR TENEX,<

SETMEM:	IORI	A,1777
	MOVEM	A,JOBREL
	MOVEI	A,1(A)
	MOVEI	C,(A)
	ASH	A,-11	;DIVIDE BY PAGE SIZE
	HRLI	A,400000
	RPACS		;DID I CREATE THIS PAGE BEFORE?
	TLNN	B,(1B5)	;IF SO, THEN THIS BIT SET
	MOVE	B,(C)	;IF NOT, CREATE IT
	MOVEI	B,0	;NO ACCESS
	SPACS
	SUBI	A,2
	HRLZI	B,160000
	SPACS		;ALL ACCESS FOR PREVIOUS K
	JRST	(E)


NEWMEM:	HRRZ	C,JOBREL
	ADDI	C,2000
	CAILE	C,<40*2000>-1
	JRST	(G)	;NOT AVAILABLE TO USER, R1
	MOVEI	A,(C)
	JSP	E,SETMEM	;SET NEW BOUND
	MOVNI	A,1777
	HRRZ	B,JOBREL
	ADDI	A,(B)
	HRLI	A,(A)
	SETZM	(A)
	ADDI	A,1
	BLT	A,(B)	;AND ZERO THE NEW MEMORY
	HRRZ	A,JOBUUO
	JRST	1(G) >

FOR TEN50,<

NEWMEM:	MOVE	A,JOBREL
	ADDI	A,1
	MOVEI	B,(A)		;FIRST ADDR OF NEW K IN A AND B
	CALL	A,[SIXBIT /CORE/]
	 JRST	(G)		;NONE AVAILABLE, IMMEDIATE R1
	ASH	B,-12		;DECIMAL K
	CAIGE	B,100		; GOT TOO MUCH?
	 JRST	1(G)		;NO, OK
	HRRZ	A,THISPR
	CAIN	A,SAVEL+1	;ARE WE DOING A SAVE?
	JRST	1(G)		;YES, OK TO USE LAST K
	MOVE	A,JOBREL
	SUBI	A,2000
	CALL	A,[SIXBIT /CORE/]
	 ERROR	IOPERR
	JRST	(G) >		;MUST RESERVE LAST K FOR PANIC SAVE


SHORTN:	HRRZ	B,ALOCTB(G)	;SHORTEN THIS SPACE IF IT NEEDS IT
	ADDI	A,(B)
	CAML	A,RP+1(G)	;MORE THAN MINIMUM ALLOC LEFT OVER?
	JRST	(L)		;NO
	HRL	A,RP+1(G)
	HLRZ	B,A	;EVEN IF A CHANGES
	SUBI	B,(A)	;THE TWO HALVES REMAIN RELATIVELY CONSTANT
	MOVNI	B,(B)
	ADD	W,B
	BLT	A,@WTOP
	MOVEI	A,RP+1(G)
	JSP	D,RPOINT
	MOVE	W,UUOACS+W	;FOR NEXT @WTOP
	JRST	(L)

;COMPRESS ALL AREAS TO BE AT MOST CURRENTLY USED PLUS INITIAL ALLOCATION

CMPRSS:	HRLZI	G,-3	;THREE PAIR TABLES
CMPRS1:	MOVE	A,RP(G)	;FETCH THE BASE OF THE TABLE
	SKIPN	(A)	;ANY MORE PAIRS?
	JRST	.+3	;NO
	MOVEI	A,2(A)
	JRST	.-3	;TRY NEXT PAIR
	JSP	L,SHORTN	;YES
	AOBJN	G,CMPRS1	;OTHER PAIR TABLES

	HRLI	G,-2	;THREE TABLES WITH TOP POINTERS
CMPRS2:	HRRZ	A,PTOP-3(G)	;PTOP FIRST IN ORDER
	ADD	A,RP(G)		;TOPS ARE RELATIVE, MAKE ABSOLUTE
	JSP	L,SHORTN
	AOBJN	G,CMPRS2	;OTHER TOP TABLES

	HRLI	G,-2	;TWO PDP'S
CMPRS3:	HRRZ	A,UUOACS+S+RP-SP(G)	;GET CURRENT TOP OF STACK
	JSP	L,SHORTN		;PDP'S ARE ALREADY ABSOLUTE
	HRRZ	A,UUOACS+S+RP-SP(G)
	SUB	A,RP+1(G)
	MOVEI	A,1(A)
	HRLM	A,UUOACS+S+RP-SP(G)	;FIX PDL END TEST
	AOBJN	G,CMPRS3	;OTHER PDP

	HRRZ	A,JOBREL
	SUBI	A,@WTOP	;DIFFERENCE BETWEEN LAST STRING AND TOP OF CORE
	CAIG	A,2000	;IS IT MORE THAN A K
	JRST	CMPRS4	;NO, DON'T CONTRACT
	MOVEI	A,@WTOP
FOR TEN50,<
	CALL	A,[SIXBIT /CORE/]	;ONLY HAVE TO BE ENOUGH FOR WHAT WE
	JRST	ILLUUO >	;BIG TROUBLE
FOR TENEX,<	JSP	E,SETMEM>

CMPRS4:	MOVEI	A,@WTOP
	HRLI	A,1(A)
	HRRI	A,2(A)
	SETZM	-1(A)
	MOVE	B,JOBREL
	CAILE	B,(A)
	BLT	A,(B)
	JRST	UUORET	;HAVE NOW TO FIT IN CORE

;STUFF TO BE COPIED TO UNSHARED CORE AT START OF RUN

DEFINE TT (A1,A2,A3)
<IFB <A3>,<EXP A2>
IFNB <A3>,<XWD A3,A2>>
ALOCTB:	TABLES

DEFINE MM (A1,A2)
<XWD A2,0>

SPFRST:	Z
	JRST	DOUUO
	POINTR	;ALL THE LH'S OF THE INDEXED POINTERS
	EXP 0,2	;PTOP,DTOP, IN THAT ORDER!
	XWD W,.WTOP-.WBASE-1
	XWD 525252,123457	;AN INITIAL RANDOM BITS, FIX LATER
				;SO THE STARTING POINT IS ALSO RANDOM
FOR TENEX,<	Z	;BITS FOR JFNTAB
	XWD 377777,377777	;NO INPUT OR OUTPUT FILE FOR STRING
	Z		;DEFAULT DEVICE IS DSK
	Z
	Z
	[ASCIZ /LGO/]		;EXTENSION
	Z
	Z
	XWD 1,BREAKY	;CH 0, PRIORITY  ABOVE ALL OTHERS
	XWD 3,ASKEOL	;CH 1 CR FOR ASK
	XWD 3,ASKEOL	;CH 2 ALT-MODE FOR ASK
	XWD 3,ASKRUB	;CH 3 RUBOUT FOR ASK
	REPEAT 5,<Z>	;CH 4-8 UNUSED
	XWD 2,ALLOCP	;CH 9, PDL TRAP
	XWD 2,DATAEX	;CH 10
	XWD 2,DATAEX	;CH 11
	REPEAT 4,<Z>	;CH 12-15 UNUSED
	XWD 2,ALOCWS >	;CH 16 ILLEGAL MEMORY READ
TERMIO:
FOR TENEX,<PUSHJ P,XBIN
	PUSHJ P,XBOUT>
FOR TEN50,<TTCALL	4,C
	TTCALL	1,C >
FOR OFILE,<FOR TENEX,<EXP 100,101>>
SPLLEN==.-SPFRST



DEFINE ELM1 (NAME,VAL)
<
NAME==.-.WBASE
EXP 1,VAL
>

.WBASE=.
ELM1 EMPTYV,0
ELM1 TRUEV,ASCIZ /TRUE/
FALSEV==.-.WBASE
EXP 2
ASCIZ /FALSE/
ELM1 LINEFV,012B6
ELM1 CARETV,015B6
ELM1 FORMFV,014B6
ELM1 BLANKV,002B6
ELM1 BELLV,007B6
ELM1 QUOTEV,042B6
ELM1 SKIPV,BYTE (7) 15,12
.WTOP==.



DEFINE MVM (A,B,C)
<EXP [ASCIZ "A"]
IFNB <C>,<XWD C,B>
 IFB <C>,<XWD W+WORDF,B>>


;MACHINE DEFINED VARIABLE NAMES

MV:
EXP [ASCIZ "EMPTY"]
XWD W+WORDF!SENTF!EMPTYF,EMPTYV
MVM LINE FEED,LINEFV
MVM CARRIAGE RETURN,CARETV
MVM FORM FEED,FORMFV
MVM BLANK,BLANKV
MVM BELL,BELLV
MVM QUOTE,QUOTEV
MVM SKIP,SKIPV
MVM CONTENTS,CCONTE,COMPUT
0



;MACHINE DEFINED PROCEDURES

DEFINE MPM (NAME,GOTO,NARG,BITS)
<IFB <BITS>,<XWD WORDF+2,[ASCIZ \NAME\]>
IFNB <BITS>,<XWD BITS+WORDF,[ASCIZ \NAME\]>
 XWD NARG-1,GOTO>

;DEFAULT PRECEDENCE IN LH OF FIRST WORD IS 2



DEFINE PAIR (A,B)
<XWD 400000,[ASCIZ "A"]
 EXP [ASCIZ "B"]>

DEFINE LETTAB (LET)
<IRPC LET
<MNT'LET>>

MNPT:
LETTAB ABCDEFGHIJKLMNOPQRSTUVWXYZ

;THE ENTRIES IN THIS TABLE NEED NOT BE IN ORDER OF DECREASING
;PROBABILITY OF OCCURANCE BECAUSE IT IS SEARCHED ONLY AT COMPILE
;TIME.  AT EXECUTE TIME THE ENTRIES ARE REFERENCED ONLY BY NUMBER.
;THE ABOVE STATEMENT IS FALSE

CMPT:
MNTA:	MPM ABBREVIATE,ABBREVIATE,0
ABREVL:	MPM ABBREVIATION,SPECWD,0
ABRVSL:	MPM ABBREVIATIONS,SPECWD,0
ALLL:	MPM ALL,SPECWD,0
ANDL:	MPM AND,SPECWD,0
ASL:	MPM AS,SPECWD,0
FOR TENEX,<	MPM ASK,ASK,1 >   ;DEMAND IN TIME
	PAIR ABB,ABBREVIATION
	PAIR ABBS,ABBREVIATIONS
	PAIR ABT,ABBREVIATE
	0
MNTB:
FOR TURTLE,<	MPM BACK,BACK,0	>
	MPM BELL,BELL,0
	MPM BOTH,BOTH,2
	MPM BUTFIRST,BUTFIRST,1
	MPM BUTLAST,BUTLAST,1
	PAIR B,BOTH
	PAIR BF,BUTFIRST
	PAIR BL,BUTLAST
	0
MNTC:
FOR SAVBRK,<	MPM CANCEL,CANCEL,0 >
	MPM CLOCK,CLOCK,0
CONTNL:	MPM CONTENTS,SPECWD,0
	MPM COPY,COPY,1;	;OTHER INPUTS ARE NOEVAL
	MPM COUNT,COUNT,1
	PAIR C,COUNT
	0
MNTD:	MPM DATE,DATE,0
	MPM DDT,CALDDT,0
	MPM DIFFERENCE,DIFF,2
	MPM DIVISION,DIVISION,2
	MPM DO,CALLDO,1
	PAIR DIFF,DIFFERENCE
	PAIR DIV,DIVISION
	0

MNTE:	MPM EDIT,EDIT,0
	MPM EITHER,EITHER,1
ELSEL:	MPM ELSE,[ERROR ELSERR]
	MPM EMPTYP,EMPTYP,1
	MPM END,ENND,0
ENTRYL:	MPM ENTRY,SPECWD,0
	MPM EQUALP,EQUALP,2
	MPM ERASE,ERASE,0
	MPM EXIT,EXIT,1	;THIS COMMAND IS USEFUL INSIDE HOARDED PROCEDURES
	PAIR EDL,EDIT LINE
	PAIR EDT,EDIT TITLE
FOR NFILE,<XWD 400000,[ASCIZ /EE/]
	EXP [ASCIZ /ERASE ENTRY/] > ;CAN'T USE  PAIR
	PAIR EI,EITHER
	PAIR EP,EMPTYP
	PAIR ER,ERASE
	PAIR ERL,ERASE LINE
	0
MNTF:
FOR NFILE,<FILEL:	MPM FILE,SPECWD,0 >
	MPM FIRST,FIRST,1
	MPM FORMFEED,FORMFEED,0
FROML:	MPM FROM,SPECWD
FOR TURTLE,<	MPM FRONT,FRONT,0 >
	PAIR F,FIRST
	0
MNTG:	MPM GET,GET,0
FOR SAVBRK,< MPM GO,GO,0 >
	MPM GOTOLINE,GOTOLINE,1
	MPM GOODBYE,GOODBYE,0
	MPM GREATERP,GRATRP,2
	PAIR GB,GOODBYE
	PAIR GP,GREATERP
	PAIR GTL,GOTOLINE
	0
MNTH:
FOR TURTLE,<	MPM HORN,HORN,0 >
	0
MNTI:	MPM IF,IF,1		;FOR IF THEN ELSE
	MPM IFFALSE,IFFALSE,0
	MPM IFTRUE,IFTRUE,0
	MPM IGNORE,IGNORE,1	;A COMMAND WHICH IGNORES ITS INPUT
	MPM IS,IS,2
	PAIR IFF,IFFALSE
	PAIR IFT,IFTRUE
MNTJ:
MNTK:	0

MNTL:	MPM LAST,LAST,1
	MPM LESSP,LESSP,2
LINELL:	MPM LINE,SPECWD,0
	MPM LINEFEED,LINEFEED,0
	MPM LINES,LINES,1
	MPM LIST,LIST,0
	MPM LOCAL,LOCAL,1
	PAIR L,LAST
	PAIR LC,LIST CONTENTS
	PAIR LE,LIST ENTRY
	PAIR LL,LIST LINE
	0


MNTM:	MPM MAKE,MAKE,2,200000	;SPECIAL TO COMPIL
	MPM MAXIMUM,MAXIM,2
	MPM MINIMUM,MINIM,2
	PAIR MAX,MAXIMUM
	PAIR MIN,MINIMUM
	0
MNTN:
NAMESL:	MPM NAMES,SPECWD,0
	MPM NOT,NOT,1
	MPM NUMBERP,NUMBRP,1
	PAIR NP,NUMBERP
	0
MNTO:
OFL:	MPM OF,SPECWD,0
ORL:	MPM OR,SPECWD
	MPM OUTPUT,OUTPUT,1
	PAIR OP,OUTPUT
	0
MNTP:	MPM PRINT,PRINT,1
PROCDL:	MPM PROCEDURES,SPECWD,0
	MPM PRODUCT,PRODUCT,2
	PAIR P,PRINT
	PAIR PROD,PRODUCT
	PAIR PRS,PROCEDURES
	0
MNTQ:	MPM QUOTIENT,QUOTIENT,2
	PAIR QUO,QUOTIENT
	0
MNTR:	MPM RANDOM,RANDOM,0
	MPM REMAINDER,REMAINDER,2
	MPM REQUEST,REQUEST,0
	MPM RESETCLOCK,RESETC,0
	MPM RETURN,CRETUN,0
	PAIR REM,REMAINDER
	PAIR RQ,REQUEST
	0

MNTS:
SAVEL:	MPM SAVE,SAVE,0
SENTCL:	MPM SENTENCE,SENTENCE,2
	MPM SENTENCES,SENTCS,0
	MPM SENTENCEP,SENTP,1
	MPM SKIP,SKIP,0
	MPM STOP,ESTOP,0
	MPM SUM,SUM,2
	PAIR S,SENTENCE
	PAIR SP,SENTENCEP
	PAIR SS,SENTENCES
	0


MNTT:	MPM TEST,TEST,1
	MPM TEXT,TEXT,2
THENL:	MPM THEN,SPECWD
	MPM THING,THING,1
	MPM TIME,TIME,0
TITLEL:	MPM TITLE,TITLE,0
TOL:	MPM TO,TO,0
FOR TURTLE,<	MPM TOUCHLEFT,TOUCHLEFT,0
	MPM TOUCHRIGHT,TOUCHRIGHT,0 >
TRACEL:	MPM TRACE,TRACE,0
TRACSL:	MPM TRACES,SPECWD,0
	MPM TYPE,TYPE,1
	MPM TYPEIN,TYPEIN,1
	PAIR T,TEST
MNTU:
MNTV:	0
MNTW:	MPM WAIT,WAIT,1
WORDL:	MPM WORD,WORD,2
	MPM WORDP,WORDP,1
	MPM WORDS,WORDS,0
	PAIR W,WORD
	PAIR WP,WORDP
	PAIR WS,WORDS
MNTX:
MNTY:	0
MNTZ:	MPM ZEROP,ZEROP,1
	PAIR ZP,ZEROP
	0		;ALL TABLE SEARCHES TERMINATE ON A 0


;PREFIX OPERATORS FOLLOW IN ORDER OF INCREASING PRECEDENCE,
;OPERATOR WITH HIGHEST PRECEDENCE GETS FIRST CRACK AT OPERAND
;NOTE WELL!! THERE IS A TEST IN THE TWOARG ERROR CODE
;WHICH RELIES ON THE FACT THAT THIS ENTIRE TABLE RESIDES ABOVE
;THE PREFIX OPERATOR TABLES TO DISTINGUISH INFIX FROM PREFIX OPS

RPREN:	MPM ),EXCTRP,0,1	;PARENTHESES ARE NOT INFIX OPERATORS BUT
LPREN:	MPM <(>,EXCTLP,0,1	;ARE INCLUDED HERE FOR CONVENIENCE
IMAKEL:	MPM _,MAKE,1,2
UPRODL:	MPM ,UPROD,1,2	;NO OF ARGS NEEDED IS A LIE
ILTHAN:	XWD 4,[ASCIZ \<\]
	XWD 1-1,LESSP
IEQUAL:	MPM =,EQUALP,1,4
IGRTR:	XWD 4,[ASCIZ \>\]
	XWD 1-1,GRATRP
IPLUS:	MPM +,SUM,1,6
IMINUS:	MPM -,DIFF,1,6
ITIMES:	MPM *,PRODUCT,1,10
IQUOT:	MPM /,QUOTIENT,1,10
INFUMN:	MPM -,COMPLM,1,12
INFUPL:	MPM +,UNPLUS,1,12
;	MPM ^,POWER,1,14	;FOR LATER EXPANSION

;BINARY OPERATORS IN TH TABLE ABOVE ARE INDICATED AS NEEDING ONE FEWER
;ARGUMENTS THAN THEY ACTUALLY DO.  THAT IS BECAUSE BY THE TIME
;THAT NUMBER OF ARGS NEEDED IS FETCHED FROM THE TABLE, ONE OF
;THE PROCEDURE'S INPUTS IS ALREADY ACCOUNTED FOR.

                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                    
;LOGO SYSTEM STUFF - LISTING, EDITING, AND ERASING

LIST:	TRZ	F,EABBRF!ECONTF!EENTRF!ENAMEF

	PUSH	P,[EXP LISTXT]	;SO ALL SUBRS CALLED MAY EXIT WITH POPJ
	PUSHJ	P,GNE
	 ERROR	WHATER
	TLNE	A,UPRF	;IS IT A USER PROCEDURE NAME?
	JRST	LISTPR	;YES, GO LIST IT
	TLZN	A,MPF
	ERROR	WHATER
	MOVEI	A,(A)
	CAIN	A,ALLL+1
	JRST	LALL
	CAIN	A,CONTNL+1	;IS IT "CONTENTS"?
	JRST	LISTCT	;GO LIST CONTENTS
	CAIN	A,TITLEL+1	;IS IT "TITLE"?
	JRST 	LISTTL
	CAIN	A,LINELL+1	;IS IT "LINE"?
	JRST	LISTLN
	CAIN	A,ENTRYL+1
	JRST	LISTEN
	CAIN	A,NAMESL+1
	JRST	LSTFNM
	CAIN	A,ABRVSL+1
	JRST	LSTFAB
FOR NFILE,<CAIN A,FILEL+1
	JRST	LFILE >
	ERROR	WHATER		;YOU CAN'T LIST THAT.

LALL:	PUSHJ	P,GNE
	JRST	LALLJ
	TLZN	A,MPF		;IS ARG A MACHINE NAME?
	ERROR	LSTER2		;NO, ALL THE REST MUST BE WELL KNOWN NAMES
	MOVEI	A,(A)
	CAIN	A,PROCDL+1	;ALL PROCEDURES?
	JRST	LISTAP	;YUP
	CAIN	A,NAMESL+1	;ALL NAMES?
	JRST	LISTAN	;YUP
	CAIN	A,ABRVSL+1	;ALL ABBREVIATIONS?
	JRST	LISTAA
	ERROR	LSTER2	;LIST ALL WHAT?

LALLJ:	PUSHJ	P,LISTAP
	PUSHJ	P,LISTAN
	PUSHJ	P,LISTAA
	SOS	CPP
	POPJ	P,


;LIST CONTENTS AND LIST ALL PROCEDURES

LISTCT:	PUSHJ	P,GNE	;IS IT LC FROM AN ENTRY?
	 JRST	LSTCT0
	SOS	CPP
	TRO	F,ECONTF
	JRST	LISTFL
LSTCT0:	MOVEI	A,LISTTO
	SOSA	CPP
LISTAP:	MOVEI	A,LISTPR
	PUSH	P,A
	PUSHJ	P,CRLF
	MOVEI	A,0	;FOR LOOP OVER ALL PROCEDURES
LSTCT1:	SKIPN	@RPA	;END OF PROCEDURE NAME LIST?
	JRST	APOPJ
	MOVEI	A,1(A)	;MAKE A LOOK LIKE UPROD COMPILE PTR
	MOVNI	B,1
	CAMN	B,@RPA
	AOJA	A,LSTCT1
	PUSH	P,A	;SAVE IT
	PUSHJ	P,@-1(P)	;CALL THE RIGHT ROUTINE
	JFCL		;DON'T CARE IF IT WASN'T DEFINED
	POP	P,A	;REMEMBER WHERE WE WERE IN LIST
	AOJA	A,LSTCT1	;GO GET NEXT PROD NAME


LISTTO:	MOVE	M,@RPA	;GET POINTER TO PROCEDURE DIRECTORY
	CAMN	M,[EXP -1]	;IS THIS PROCED DEFINED?
	POPJ	P,	;NO, R1 EXIT IMMEDIATELY
	MOVEI	A,-1(A)
	AOS	0(P)
	MOVE	A,@RPA		;IS THS PR TRACED?
	TLNN	A,TRACEF
	JRST	LSTLN1		;NO
	TLNE	F,SAVEF		;DOING A SAVE?
	JRST	.+4

	MOVEI	B,[ASCIZ /(TRACED)/]
	PUSHJ	P,TOSS		;TYPE "(TRACED) " IF IT IS
	JRST	LSTLN1

	MOVEI	B,[ASCIZ /TRACE /]
	PUSHJ	P,TOSS		;FOR A SAVE, TYPE "TRACE PRNAME"
	MOVEI	B,(A)
	PUSHJ	P,TOSW
	PUSHJ	P,CRLF
	JRST	LSTLN1



LISTTL:	MOVE	A,TOPROD	;LIST TITLE
	JUMPE	A,[ERROR NOPERR]	;MUST HAVE A PROCEDURE OPEN
	PUSHJ	P,LISTTO	;LIST IT
	ERROR	IOPERR	;BIG TROUBLE
	POPJ	P,

;LIST A PROCEDURE

LISTPR:	PUSHJ	P,CRLF	;ONE EXTRA IN FRONT
	PUSH	P,A	;SAVE WHICH PROCEDURE
	PUSHJ	P,LISTTO	;DO THE "TO" LINE
	 ERROR	EVER3	;X NEEDS A MEANING
	MOVEI	M,1(M)
	PUSHJ	P,LSTPLN	;DO CURRENT LINE
	AOJA	M,.-1	;DO NEXT LINE
	POP	P,A
	HRRZI	A,(A)
	CAMN	A,TOPROD	;SHOULD WE TYPE "END"? TESTING RH ONLY
	TLNE	F,SAVEF
	SKIPA
	POPJ	P,		;NO, PROCEDURE IS STILL OPEN
	MOVEI	B,[ASCIZ /END/]
	PUSHJ	P,TOSS
	JRST	CRLF


LISTLN:	PUSHJ	P,SNMEVL
	MOVEI	M,(A)	;SRCHLN USES A, LSTPLN USES M
	PUSHJ	P,LSTPLN
	POPJ	P,
	ERROR	IOPERR


SNMEVL:	SKIPN	TOPROD
	ERROR	NOPERR		;MUST HAVE A PROCEDURE OPEN
	PUSHJ	P,NUMEVL
	MOVE	A,TOPROD
	JSP	C,SRCHL1	;IS IT A LINE NO OF THE CURRENT PROCEDURE?
	 CAIE	B,(M)
	 ERROR	GOERR3
	POPJ	P,

SAVAA:	SKIPA	A,[EXP SAVAAR]
LISTAA:	MOVEI	A,LSTAAR
	PUSH	P,A	;SAVE NAME OF ROUTINE TO CALL
	MOVEI	A,2
	MOVEM	A,BCHAR
	PUSHJ	P,CRLF
	MOVE	G,UA	;POINT AT FIRST ABBREV NAME
LSTAAL:	SKIPN	A,(G)	;ANY MORE ABBREVIATIONS?
	JRST	APOPJ	;NO
	MOVNI	C,1
	CAME	C,1(G)	;IS THIS ONE DEFINED?
	PUSHJ	P,@(P)	;YES, OUTPUT IT
	ADDI	G,2
	JRST	LSTAAL

LSTAAR:	PUSHJ	P,PTOSS		;TYPE NAME
	MOVEI	B,[ASCIZ /=>/]
	PUSHJ	P,TOSS
	PUSHJ	P,PTAB
	MOVE	A,1(G)
	PUSHJ	P,PTOSS	;TYPE VALUE
	PUSHJ	P,CRLF	;LINE
	POPJ	P,

;LIST ONE LINE OF A PROCEDURE

LSTPLN:	HRRZ	A,@PSM	;GET LINE NO, M SET UP BY LISTTO
	JUMPE	A,CPOPJ1	;NO MORE LINES, GIVE R2
	PUSHJ	P,DECPRT	;TYPE LINE NO., MAX OF 5 CHARS
LSTLN1:	MOVEI	A,1	;ENTER HERE FOR LIST TITLE, DON'T TYPE LINE NO
	ADD	A,CHARNO
	MOVEM	A,BCHAR
	MOVEI	M,1(M)	;POINT AT PTR TO COMPILED CODE
	HRRZ	D,@PSM	;GET PTR TO COMPILED LINE
	MOVEI	D,1(D)
LSTPLL:	MOVE	A,@WSD	;GET NEXT ELEMENT OF THE LINE
	TLZ	A,COMPOUND!IMMEDIATE
	JFFO	A,.+2	;WHAT KIND OF ELEMENT?
	PJRST	CRLF	;EOL, TYPE CR
	PUSH	P,D	;REMEMBER WHERE WE ARE IN THE LINE
	TLNE	A,INFOP
	JRST	LSTIOP	;INFIX OP, MAY WANT TO SUPRESS SURROUNDING SPCS
LSTLL1:	SKIPE	CHARNO		;SO "TO" WON'T BE " TO"
	PUSHJ	P,PSPACE	;SPACE BEFORE EACH ELEMENT
LSTLL2:	JRST	@.-1(B)	;DISPATCH ON TYPE
	EXP LSTPMP,LSTPUP,LSTPV,LSTPL,LSTPMP,LSTPMV,LSTPCM

LSTPMP:	MOVE	A,-1(A)	;MACHINE PROCEDURE, GET PTR TO NAME
	HRLI	A,440700	;MAKE TEXT PTR
LSTPM1:	PUSHJ	P,PWORD	;TYPE ELEMENT
LSTPM2:	POP	P,D
	AOJA	D,LSTPLL	;GO BACK FOR NEXT ELEMENT ON THE LINE

LSTPUP:	MOVEI	A,-1(A)	;USER PROCEDURE, POINT AT NAME PTR
	MOVE	A,@RPA	;GET PTR TO NAME
	HRLI	A,010700+W	;MAKE TEXT PTR
	JRST	LSTPM1

LSTPMV:	MOVE	A,-1(A)
	MOVEI	E,":"
	TRO	F,PREFIX!SUFFIX
	PUSHJ	P,PTOSSM
	JRST	LSTPM2

LSTPV:	MOVEI	A,-1(A)		; POINT TO NAME WD, NOT VALUE WD
	MOVE	A,@VPA		; FETCH THE NAME PTR
	SKIPA	E,[":"]	;VARIABLE, AFFIX ":" MARKS
LSTPCM:	MOVEI	E,";"	;COMMENT
	TRO	F,PREFIX!SUFFIX
LSTPL1:	PUSHJ	P,PTOSS
	JRST	LSTPM2

LSTPL:	PUSH	S,A	;LITERAL
	PUSHJ	P,NUMBRQ	;SEE IF IT IS A NUMBER
	 TRO	F,PREFIX!SUFFIX	;IF NOT, QUOTE IT
	POP	S,A
	MOVEI	E,042
	JRST	LSTPL1

LSTIOP:	MOVEI	D,1(D)	;CHECK FOLLOWING ELEM FOR LIT OR VAR
	JSP	E,LSTIOT
	MOVEI	D,-2(D)	;PRECEDING ONE TOO
	JSP	E,LSTIOT
	JSP	H,ETMP	;BOTH PASS, SUPPRESS SPACE BEFORE AND AFTER
	AOS	D,0(P)
	MOVE	A,@WSD
	JFFO	A,LSTLL2	;NEVER FALLS THROUGH!
LSTIOT:	MOVE	C,@WSD
	TLNN	C,LITF
	TLNE	C,VARF
	JRST	(E)	;CANDIDATE FOR SPACE ELIMINATION
	JRST	LSTLL1

;LIST ALL NAMES

LISTAN:	MOVEI	A,2
	MOVEM	A,BCHAR
	PUSHJ	P,CRLF

	MOVE	A,VP+1		;DUMMIES FIRST, MAKE COPY OF VP
	SUB	A,VP		; SO IT CAN BE CLOBBERED WITH BINDINGS
	PUSHJ	P,MAKELM	; RETURNS IN B WS PTR TO LENGTH WORD
	HRLZ	A,VP		; SET UP TO BLT, FROM VP
	HRRI	A,@WSB		; TO NEW ELEMENT
	MOVE	G,A		; REMEMBER WHERE
	ADDI	A,1		; POINT BEYOND LEN WD
	BLT	A,@WTOP		; WHICH POINTS TO END OF ELEMENT

	HRLI	G,B		; ELEM(B)
	MOVE	H,DP
	ADD	H,DTOP		; AFTER LAST ENTRY IN DP
LSTAN3:	SUBI	H,2		; AT LAST ENTRY
	CAMG	H,DP		; NO MORE ENTRIES?
	AOJA	G,LSTANG		; DONE WITH DUMMIES, DO GLOBALS

	MOVE	B,(H)		; PTR TO PTR TO VALUE WD IN VP
	MOVEI	A,@G		; ABS PTR TO NAME WD IN VP COPY
	PUSHJ	P,LSTANR	; LIST TOPMOST BINDING

	MOVE	C,1(H)		; FETCH NEXT LEVEL BINDING
	MOVEM	C,1(A)		; AND MAKE IT TOP ONE IN COPY
	JRST	LSTAN3		; GO BACK FOR MORE


SAVAN:	MOVEI	A,SAVANR	; SAVE LISTING ROUTINE
	SKIPA	G,VP		;SAVE USES REAL VP

LSTANG:	MOVEI	A,LSTANR	;LISTING ROUTINE
	PUSH	P,A	;SAV ROUTINE TO CALL
	PUSHJ	P,CRLF
	MOVE	A,G
LSTANH:	SKIPN	(A)	;ANY MORE?
	JRST	APOPJ	;NO, DONE
	PUSHJ	P,@(P)	;NO, LIST THIS ONE
	ADDI	A,2
	JRST	LSTANH	;TRY NEXT ONE

LSTANR:	MOVSI	C,UNBOUN
	TDNE	C,1(A)		; IS THIS VALUE UNBOUND?
	POPJ	P,		; YES, DON'T TYPE IT
	PUSH	P,A	;TYPE ANY KIND OF VALUE
	MOVEI	E,":"	;SLASH THE NAME
	TRO	F,PREFIX!SUFFIX
	MOVE	A,(A)	;FETCH THE NAME
	PUSHJ	P,PTOSS
	MOVEI	A,[ASCIZ / IS /]
	PUSHJ	P,PTOSSM
	MOVEI	E,042	;QUOTE THE VALUE

LSTNR1:	MOVE	A,(P)
	TRO	F,PREFIX!SUFFIX
	MOVE	A,1(A)	;GET THE VALUE
	PUSHJ	P,PTOSS
	PUSHJ	P,CRLF
APOPJ:	POP	P,A
	POPJ	P,

;LIST X ENTRY NAME WHERE X CAN BE CONTENTS,NAMES,ABBREVIATIONS,ENTRY

LISTEN:	TRO	F,EENTRF
	JRST	LISTFL
LSTFNM:	TROA	F,ENAMEF
LSTFAB:	TRO	F,EABBRF

LISTFL:	TRZ	F,TF
FOR NFILE,<POP	P,A	;THE RETURN TO LISTXT
	JSP	H,FILEGO
	 ERROR	NOFILE
	PUSHJ	P,SEARCH
	 ERROR	NOENTRY
	MOVEI	D,4(G)
	MOVE	C,@E	;==	READ	@E,1
	MOVEI	E,1
	PUSHJ	P,.READ
	PUSHJ	P,READC
	CAIE	C,176	;SKIP OVER COPY OF NAME
	JRST	.-2
	MOVE	A,[PUSHJ P,READC]
	MOVEM	A,CHIN >

FOR OFILE,<PUSHJ	P,GENFLN
	MOVEI	A,LEOFIL	;WHERE TO GO ON EOF
	PUSHJ	P,OPENR >	;OPEN THE FILE FOR READING

LNLINE:	PUSHJ	P,TIS	;READ A LINE
	 ERROR		;SHOULD HAVE GOTTEN AN EOF INTERRUPT
	POP	S,A
	TRNE	F,EENTRF
	 JRST	LISTIT	;IF LIST ENTRY, LIST EVERY LINE
	PUSHJ	P,NEWSTR
	PUSHJ	P,NEWSR0
	PUSHJ	P,COPYWD
	 JRST	MBLIST	;NOTHING ON LINE, MAYBE TYPE CRLF
	POP	S,B
	MOVE	A,SRCBOT
	TRZE	F,STORED
	 JRST	LNLINE	;STORED LINES ONLY GET LISTED ON LIST ENTRY
	ADDI	B,1
	MOVE	C,@B	;FIRST WORD OF FIRST SYMBOL
	CAMN	C,[ASCIZ /TO/]
	TRNN	F,ECONTF
	SKIPA
	JRST	LISTIT	;TO LINE AND LIST CONTENETS
	CAMN	C,[ASCIZ /MAKE/]
	TRNN	F,ENAMEF
	SKIPA
	JRST	LISTIT	;NAME AND LIST NAMES
	CAME	C,[ASCII /ABBRE/]
	JRST	LNLINE	;NOT ABBREVIATE, SO NOTA
	ADDI	B,1
	MOVE	C,@B
	CAME	C,[ASCII /VIATE/]
	JRST	LNLINE	;AGAIN NOT ABBREVIATE
	ADDI	B,1
	SKIPN	@B
	TRNN	F,EABBRF
	JRST	LNLINE	;AND AGAIN
LISTIT:	PUSHJ	P,PTOSS	;LIST THE CURRENT LINE
	TROA	F,TF	;DENOTE THAT A LINE WAS TYPED SINCE LAST MBLIST

MBLIST:	TRZE	F,TF
	PUSHJ	P,CRLF
	JRST	LNLINE


COPYWD:	ILDB	C,A
	JUMPE	C,CPOPJ
	CAIE	C,024		;^T IS ALSO A TERMINATOR
	CAIN	C," "
	JRST	COPYW1
	IDPB	C,B
	JRST	COPYWD
COPYW1:	AOS	(P)
	JRST	ENDSTR

FOR OFILE,<FOR TENEX,<
LEOFIL:	CLOSF		;COME HERE ON EOF FOR LIST
	 ERROR
	POP	P,A
	POP	P,A
	POP	P,A	;RETURN FROM TYI
	POP	P,A	;RETURN FROM TIS
	POP	P,INFILE
	POP	P,LEVLTB+1	;TO FINISH LIST LINE
	SKIPN	INFILE
	TLZ	F,GETF
	DEBRK
>>


EDIT:	PUSHJ	P,GNE
	 ERROR	EDTER1
	TLNE	A,UPRF	;IS THAT WHAT A USER PROCEDURE?
	JRST	EDITPR	;YES
	CAMN	A,[XWD MPF!IMMEDIATE,LINELL+1]	;IS THAT WHAT "LINE"?
	JRST	EDITLN	;YES
	CAMN	A,[XWD MPF!IMMEDIATE,TITLEL+1]	;IS IT "TITLE"?
	JRST	EDITTL	;YES
	ERROR	EDTER1	;YOU CANNOT EDIT THAT

EDITPR:	SKIPE	TOPROD	;IS THERE A PROCEDURE OPEN?
	ERROR	EDTER2	;YES, YOU ARE ALREADY EDITING X
	MOVNI	B,1
	CAMN	B,@RPA		;IS THE PROCEDURE DEFINED?
	ERROR	EVER3		;CAN EDIT DEFINED PROCEDURES ONLY
	HRRZM	A,TOPROD	;THE PROCEDURE IS NOW OPEN
	PUSHJ	P,MOVEPR	;MAKE IT LAST
	SOS	PTOP		;PROCEDURE NO LONGER CLOSED
	JRST	COMEX

EDITLN:	PUSHJ	P,SNMEVL
	PUSHJ	P,LINGEN	;GENERATE THE TEXT OF THAT LINE FOR TIS

EDTLN1:	JSP	D,COMEXR	;DO END OF LINE CHECKING
	TLO	F,EDITF	;WE ARE NOW EDITING
	POPJ	P,


EDITTL:	SKIPN	M,TOPROD	;CURRENTLY IN A PROCEDURE?
	 ERROR	NOPERR
	PUSHJ	P,NEWSTR

	MOVEI	D,TITLEL+1	;"TITLE"
	PUSHJ	P,LNGMP		;COPY MACHINE PROCEDURE NAME
	MOVEI	A,(M)
	MOVE	D,@RPA
	MOVEI	D,1(D)
	MOVE	M,@PSD
	PUSHJ	P,LINGE0
	JRST	EDTLN1

DSPACE:	MOVEI	C," "
	IDPB	C,B
	POPJ	P,


LINGEN:	MOVEI	D,1(A)	;POINTER TO POINTER TO COMPILED CODE FOR LINE
	PUSHJ	P,SNM	;IS THE FIRST WORD OF THE GENERATED LINE
	MOVE	M,@PSD
LINGE0:	MOVEI	M,1(M)
LINGEA:	MOVE	D,@WSM		;GET NEXT ELEMENT
	TLZ	D,IMMEDIATE!COMPOUND
	JFFO	D,LINGE1	;DIPATCH ON TYPE
	JRST	ENDSTR		;WHICH WILL EXIT FROM LINGEN WITH POPJ

LINGE1:	TLNE	D,INFOP
	JRST	LNGFOP
LINGE2:	MOVEI	C," "
	IDPB	C,B	;BEFORE ALL BUT FIRST ELEMENT PUT A SPACE
LINGE3:	PUSHJ	P,@.(E)
	AOJA	M,LINGEA	;BACK FOR NEXT ELEMENT
	EXP	LNGMP,LNGUP,LNGV,LNGL,LNGMP,LNGMV,LNGCMT

LNGMV:	MOVEI	E,":"
	TRO	F,PREFIX!SUFFIX
LNGMP:	MOVE	A,-1(D)		;FETCH POINTER TO TEXT NAME OF PROCEDURE
	HRLI	A,440700
LNGALL:	MOVEM	A,SRCBOT
	TRZE	F,PREFIX
	IDPB	E,B		;AFFIX PREFIX IF THERE IS ONE
	ILDB	C,A		;COPY THE WHOLE ELEMENT
	JUMPE	C,LNGALE	;DONE IF EOM
	IDPB	C,B
	JRST	.-3		;NEXT CHAR
LNGALE:	TRZE	F,SUFFIX
	IDPB	E,B		;AFFIX SUFFIX IF ONE TOO
	POPJ	P,		;DONE WITH THIS ELEMENT

LNGUP:	MOVEI	A,-1(D)
	MOVE	A,@RPA	;GET NAME OUT OF TABLE
LNGUP1:	HRLI	A,010700+W	;IT IS A WORKSPACE ELEMENT
	JRST	LNGALL

LNGV:	MOVEI	A,-1(D)
	MOVE	D,@VPA
	SKIPA	E,[":"]
LNGCMT:	MOVEI	E,";"
	TRO	F,PREFIX!SUFFIX
LNGL1:	MOVEI	A,(D)
	JRST	LNGUP1

LNGL:	MOVEI	A,(D)	;NUMBRQ USES A FOR ARG
	PUSHJ	P,NUMBRQ
	TRO	F,PREFIX!SUFFIX
	MOVEI	E,042
	JRST	LNGL1

LNGFOP:	MOVEI	A,1(M)
	JSP	G,LNGIOT
	MOVEI	A,-1(M)
	JSP	G,LNGIOT
	XCT	LINGE3
	MOVEI	M,1(M)
	MOVE	D,@WSM
	JFFO	D,LINGE3
LNGIOT:	MOVE	C,@WSA
	TLNN	C,LITF
	TLNE	C,VARF
	JRST	(G)
	JRST	LINGE2

ERASE:	PUSH	P,[EXP ERASXT]
	PUSHJ	P,GNE
	 ERROR	WHATER
	TLNE	A,UPRF	;IS IT A UPROD
	JRST	ERASPR	;YES, ERASE ONE PROCEDURE
	TLZN	A,MPF	;ALL OTHER PARAMETERS MUST BE MACHINE NAMES
	 ERROR	WHATER
	MOVEI	A,(A)
	CAIN	A,LINELL+1	;IS IT "LINE"
	JRST	ERASLN	;YES, ERASE LINE
	CAIN	A,ALLL+1	;IS IT ALL?
	JRST	ERSALL
	CAIN	A,ABREVL+1
	JRST	ERSABB
	CAIN	A,TRACEL+1
	JRST	ERASTR	;ERASE TRACE
FOR NFILE,<CAIN A,ENTRYL+1
	JRST	ERASEN >
	ERROR	WHATER	;NONE OF THE ABOVE

ERASXT:	SQUEZE	;SEE IF WE CAN REDUCE THE STORAGE ALLOCATION
	JRST	COMEX


ERSALL:	PUSHJ	P,GNE	;ALL WHAT?
	 JRST	ERASAL	;ALL PERIOD
	TLZN	A,MPF	;MUST BE A MACHINE NAME
	 ERROR	WHATER
	MOVEI	A,(A)
	CAIN	A,NAMESL+1	;IS IT "NAMES"
	JRST	ERSALN
	CAIN	A,PROCDL+1	;IS IT "PROCEDURES"
	JRST	ERSALP
	CAIN	A,ABRVSL+1
	JRST	ERSALA	;ALL ABBREVIATIONS
	CAIN	A,TRACSL+1	;"TRACES"?
	JRST	ERSALT
	ERROR	WHATER

ERASAL:
FOR TEN50,<
	HLRZ	A,JOBSA	;GET FIRST FREE LOC
	ADDI	A,1777	;ONE MORE K
	CALL	A,[SIXBIT /CORE/]
	ERROR	IOPERR	>
FOR TENEX,<
	MOVEI	A,20000
	JSP	E,SETMEM	>
	MOVEI	F,TF	;DON'T TYPE STARTUP MESSAGE AGAIN
	JRST	LOGO+2	;OR DO THE RESET (WHICH CLOSES DRIBBLE FILE)

ERSALA:	SETZM	@UA
	POPJ	P,

ERSALP:	SETZM	@RP	;FLUSH ALL PROCEDURE NAMES
	SETZM	PTOP	;FLUSH ALL PROCEDURE DIRECTORIES
	SETZM	BSP	;CLEAR ALL SAVED BREAKS
	SETZM	GODEPT
	JRST	FLUSHM	;FLUSH PDLS AND ALL BOUND VARS

ERSALN:	MOVE	A,VP
	MOVE	H,[XWD W+WORDF!SENTF!EMPTYF!GLOBLF!UNBOUN,0]
ERSAN1:	SKIPN	(A)
	POPJ	P,
	PUSHJ	P,FINDAG	;FIND A GLOBAL VALUE
	MOVEM	H,1(C)		; AND RESET IT TO UNSET
	ADDI	A,2
	JRST ERSAN1

FINDAG:	MOVSI	D,GLOBLF
	MOVE	C,A
	TDNE	D,1(C)	;IS GLOBAL VALUE IN VP?
	POPJ	P,	;YES
	MOVEI	B,1(C)		;ABS PTR TO VALUE WD
	SUB	B,VP
	MOVN	C,DTOP
	HRLZI	C,(C)
	HRR	C,DP
FNDAG1:	ADD	C,[XWD 2,2]
	JUMPGE	C,CPOPJ1	;NO GLOBAL BINDING ANYWHERE
	CAME	B,(C)
	JRST FNDAG1
	POPJ	P,


ERSABB:	AOS	A,CPP
	PUSHJ	P,EVAL
	MOVE	A,(S)
	MOVE	B,UA
	PUSHJ	P,LOOKY
	 JRST	ERSAB1		;NOT A USER ABBREVIATION
	SKIPE	1(N)	;NO CHANGE IF AN ALREADY ERASED SYSTEM ABB
	SETOM	1(N)
	POP	S,A
NOSQZE:	POP	P,A
	JRST	COMEX

ERSAB1:	MOVE	A,(S)
	PUSHJ	P,SYSLKP	;LOOKUP BUILTIN FN OR ABBREVIATION
	 ERROR	WHATER
	SKIPL	(N)		;IS IT AN ABBREV. RATHER THAN PR NAME
	 ERROR	WHATER		;PR NAME
	PUSH	S,(S)
	SETZM	-1(S)
	POP	P,A
	JRST	ABBRV0		;SKIP OVER FETCHING INPUTS TO ABT


ERASTR:	PUSHJ	P,GNE	;ERASE TRACE ON WHAT PROCEDURE?
	 ERROR	WHATER
	TLNN	A,UPRF	;ONLY USER DEFINED PROCEDURES ARE TRACED
	 ERROR	WHATER
	MOVEI	A,-1(A)
	HRLZI	B,TRACEF
	ANDCAM	B,@RPA	;CLEAR THE APPROPRIATE BIT
	JRST	NOSQZE

ERSALT:	MOVE	A,RP	;ERASE ALL TRACES
ERSLT1:	HRLZI	B,TRACEF
	ANDCAB	B,(A)	;CLEAR THE BIT
	MOVEI	A,2(A)	;POINT AT NEXT ENTRY
	JUMPN	B,ERSLT1	;NAME NON-ZERO IF NOT AT END OF TABLE
	JRST	NOSQZE

ERASLN:	PUSHJ	P,SNMEVL	;ERASE LINE, WHAT LINE?
ERSLN1:	MOVEI	C,@PSA
	MOVE	B,2(C)	;COPY ALL THE REST OF THE LINES BACK TWO
	MOVEM	B,(C)
	MOVEI	C,1(C)
	JUMPN	B,.-3	;TERMINATE ON ZERO LINE NUMBER

	MOVNI	B,2
	ADDM	B,PTOP
	POPJ	P,

ERASPR:	MOVEI	A,-1(A)	;CLEAR LH OF A
	HRLZI	B,TRACEF
	ANDCAM	B,@RPA	;ERASE THE TRACE ON IT IF ANY
	MOVEI	A,1(A)

	MOVNI	B,1	;ERASE PROCEDURE
	CAMN	B,@RPA	;IS THE PROCEDURE DEFINED?
	 JRST ERPR1	;QUIT EARLY, NOT DEFINED
	SKIPE	TOPROD	;IS THERE A PROCEDURE OPEN?
	AOS	PTOP	;"CLOSE" IT TEMPORARILY
	CAMN	A,TOPROD	;IS IT THE PROCEDURE CURRENTLY BEING DEFINED
	SETZM	TOPROD	;YES, WHEN WE'RE DONE, NO PROCEDURE OPEN
	PUSH	P,TOPROD
	MOVEM	A,TOPROD
	PUSHJ	P,MOVEPR	;MAKE THIS ONE LAST TEMPORARILY
	MOVE	A,TOPROD
	MOVE	A,@RPA

	PUSHJ	P,ERSLN1	;ERASE ONE LINE
	SKIPE	@PSA		;ANY LEFT?
	JRST	.-2		;YES, ERASE ONE MORE

	MOVE	A,TOPROD
	MOVNI	B,1
	EXCH	B,@RPA	;MARK AS UNDEFINED AND FETCH PTR TO BEGINNING
	HRRM	B,PTOP	; OF THE STORAGE WE JUST FREED UP
	HRRZ B,-1(P)
	SKIPN PRODNM	;DON'T SAY IF STORED
	CAIE B,ERASXT	;OR NOT JUST ERASE PR
	JRST .+5
	JSP H,ETUP	;X
	MOVEI A,[ASCIZ / ERASED/]
	PUSHJ P,PTOSSM
	PUSHJ P,CRLF
	POP	P,TOPROD	;RESUME EDITING THE PROCEDURE THAT HAD BEEN
	SKIPE	TOPROD
	SOS	PTOP
	POPJ	P,

ERPR1:	HRRZ B,(P)
	SKIPN PRODNM
	CAIE B,ERASXT
	 POPJ P,
	ERROR NOPROD

                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                        
FOR NFILE,<


;SYSTEM INDEPENDENT FILE CODE


DEFINE READ (BLOCK,BUFFER)
<	MOVE	C,BLOCK
	MOVEI	E,BUFFER
	PUSHJ	P,.READ >

DEFINE FORCE (BUFFER)
<	MOVEI	E,BUFFER
	PUSHJ	P,.FORCE >

FOR TENEX,<
DEFINE ALTERD (BUFFER,AC)
<>> ;THE SYSTEM TAKES CARE OF IT
FOR TEN50,<
DEFINE ALTERD (BUFFER,AC)
<	MOVE	AC,LBUF+BUFFER
	SETOM	CHANGE(AC) >>


;FILE PARAMETERS

FOR TENEX,<NLOGB==4
PBLKL==1000
PBKLP2==11>
FOR TEN50,<NLOGB==1
PBLKL==200
PBKLP2==7>
LBLKL==200
DRENL==12
NENTRY==<LBLKL-10>/DRENL
BUFBIT==1



;DIRECTORY FORMAT
;OVERHEAD BLOCK - 8 WORDS
;WORD  CONTENTS
; 0 - TOP OF BLOCK FREE LIST
; 1 - NUMBER OF ENTRIES IN THIS DIRECTORY BLOCK
; 2 - TOP OF ENTRY BLOCK LIST IN THIS DIRECTORY
; 3 - TOP OF ENTRY BLOCK FREE LIST
; 4 - LINK TO NEXT DIRECTORY BLOCK, OR 0
; 5 - PASSWORD NUMBER FOR THIS FILE
; 6 - NUMBER OF BLOCKS ON THE FREELIST
; 7 - LAST REWRITE NUMBER USED IN FILE
;ENTRY BLOCK - DRENL WORDS LONG
; 0 TO 3 - FIRST 20 CHARACTERS OF THE ENTRY NAME
; 4 - LINK TO FIRST TEXT BLOCK OF ENTRY
; 5 - TIME AND DATE SAVED
; 6 - TIME AND DATE GOTTEN
; 7 - LOCKED, HOARDED, AND SIZE OF FILE IN BLOCKS
; 8 - REWRITE NUMBER FOR THIS ENTRY
; 9 - POINTER TO NEXT ENTRY BLOCK , OR 0



COPYGT:	SKIPA A,[GETCP0]
GET:	MOVEI A,GET4
	MOVEM A,GETDST		; CALLED BY GET OR BY COPY
	JSP	H,FILEGO	;GENERATE FILE NAME, SAVEUP, AND OPEN
	 ERROR	NOFILE		;MAYBE FILE IN USE?
	PUSHJ	P,SEARCH	;LOOK FOR ENTRY
	 ERROR	NOENTRY		;NO SUCH ENTRY

	MOVE	A,TANDD		;TIME AND DATE OF PROGRAM STARTUP
	MOVEI	D,6(G)		;CURRENT ENTRY +6
	MOVEM	A,@E		;UPDATE D-G

	MOVEI	D,4(G)		;ENTRY+4, POINTER TO DATA BLOCK
	READ	@E,1		;READ THE FIRST DATA BLOCK INTO BUF1

	PUSHJ	P,READC		;SKIP OVER COPY OF THE ENTRY NAME
	CAIE	C,176
	JRST	.-2

	FORCE	0		;WRITE THE MODIFIED DIRECTORY
	JRST @GETDST		; WHAT IS THE REST TO DO

GET4:	MOVE	A,[PUSHJ P,READC]	;MOST OF GET
	MOVEM	A,CHIN
	PUSHJ	P,TIS
	 ERROR	IOPERR		;CANNOT GET A RUBOUT FROM THE FILE
	MOVE	A,(S)
	HRLI	A,010700+W
	ILDB	C,A
	CAIE	C,";"		;IS THE FIRST LINE COMMENT?
	JRST	MAINL1		;NO, TREAT THE LINE NORMALLY
	POP	S,A
	PUSHJ	P,PTOSS		;TYPE THE COMMENT
	PUSHJ	P,CRLF
	JRST	MAINL		;PROCEED AS IF INPUT WERE FROM TERMINAL


ERASEN:	JSP	H,FILEGO
	 ERROR	NOFILE
	PUSHJ	P,SEARCH
	 ERROR	NOENTRY
	PUSHJ	P,ERENRO
	JSP	H,CLOSUP
	POP	P,A		;FLUSH THE RETURN TO ERASXT
	JRST	COMEX


GETCP0:	
FOR TENEX,< MOVE 1,COPJFN
	SKIPA
	PUSHJ P,READC1
GETCP1:	SOSGE CCOUNT
	JRST .-2
	ILDB 2,FPTR
	BOUT
	JUMPN 2,GETCP1
	CLOSF
	 JFCL
	JRST REDEOF		; AND CLOSE THE LOGO FILE
>

FOR TEN50,<
	SKIPA
	PUSHJ P,READC1
GETCP1:	SOSGE CCOUNT
	 JRST .-2
	ILDB 2,FPTR
	SOSG CCNT2
	JRST [	PUSHJ P,GTSVUG
		OUT 2,DMPLST
		SKIPA
		ERROR
		LDB 2,FPTR	; GET THE SAME CHARACTER AGAIN
		JRST .+1 ]
	IDPB 2,FPTR2
	JUMPN 2,GETCP1
	HRLZI 2,440700
	HRR 2,BUFLC2
	CAMN 2,FPTR2
	JRST GETCP2
	PUSHJ P,GTSVUP	;LAST BUFFER IS NOT EMPTY
	OUT 2,DMPLST
	SKIPA
	 ERROR
GETCP2:	CLOSE 2,
	RELEAS 2,
	SETZM BUFAD2
	JRST REDEOF

GTSVUG:	SKIPE CCNT2
	AOS (P)		;FIRST TIME THRU SKIP THE OUT
GTSVUP:	MOVEI 2,200*5
	MOVEM 2,CCNT2
	HRRZ 2,BUFLC2
	HRLI 2,440700
	MOVEM 2,FPTR2
	SUBI 2,1
	HRLI 2,-200
	MOVEM 2,DMPLST
	POPJ P,
>

SAVER:	PUSHJ	P,LISTAP
	PUSHJ	P,SAVAN
	PUSHJ	P,SAVAA

	SETZ	C,
	XCT	CHOUT		;INSERT AN END OF FILE
	POPJ	P,

FOR TENEX,<
SAVCOP:	MOVE 1,COPJFN
	BIN
	MOVE 3,2
	PUSHJ P,WRITEC
	JUMPN 3,.-3
	CLOSF
	 JFCL
	POPJ P,
>
FOR TEN50,<
SAVCOP:	SOSG CCNT2
	JRST [  PUSHJ P,GTSVUP
		IN 2,DMPLST
		SKIPA
		 ERROR
		JRST .+1 ]
	ILDB C,FPTR2
	PUSHJ P,WRITEC
	JUMPN C,SAVCOP
	CLOSE 2,
	RELEAS 2,
	SETZM BUFAD2
	POPJ P,
>


COPYSV:	SKIPA A,[SAVCOP]
SAVE:	MOVEI A,SAVER
	MOVEM A,SAVSRC
	JSP	H,FILEGO
	 JRST	SAVNEW		;NO FILE, MAKE ONE
	READ	[0],0		;READ BK 0 OF FILE, DIRECTORY INTO BUF0
	MOVE	A,@FPTR
	MOVEM	A,LABS		;FREE LIST IS FIRST WORD OF DIR

SAVE1:	TLO	F,SAVEF		;DENOTE SAVE IN PROGRESS
	MOVE	A,[PUSHJ P,WRITEC]
	MOVEM	A,CHOUT		;WHAT TO XCT WHEN OUTPUTTING CHARS

	MOVEI	A,1
	MOVEM	A,FILECT
	PUSHJ	P,WRITC2	;GET A NEW BLOCK
	MOVE	A,LBLOCK+1	;FIRST BLOCK OF FILE
	MOVEM	A,FILEAD
	MOVE	H,(S)		;COPY ENTRY NAME INTO FILE
	HRLI	H,010700+W
	ILDB	C,H
	XCT	CHOUT		;WRITE A CHAR INTO FILE
	JUMPN	C,.-2

	MOVEI	C,176
	DPB	C,FPTR		;OVERWRITE THE 0 EOM

	MOVE	A,CPP		;STORE COMMENT IN FILE IF ON SAVE LINE
	MOVEI	A,1(A)		;POINT AT NEXT ELEMENT
	SKIPE	A,@WSA		;IS IT NOT THERE?
	TLNN	A,COMMTF	;MAKE SURE IT IS A COMMENT
	JRST	SAVE2		;NOT A COMMENT
	MOVEI	C,";"
	PUSHJ	P,TYO
	PUSHJ	P,PTOSS
	PUSHJ	P,CRLF

SAVE2:	PUSHJ P,@SAVSRC	;CALL APROPRIATE ROUTINE
	TLZ	F,SAVEF
	MOVE	A,TERMIO+1	;TERMINAL OUTPUT INSTRUCTION
	MOVEM	A,CHOUT
	ALTERD	1,C

;MAKE DIRECTORY ENTRY

	MOVE	E,LBUF
	MOVE	E,BUFLOC(E)	;WHERE DIRECTORY IS
	SKIPE	3(E)
	JRST	SAVE4		;LIST OF AVAIL DIR ENTRIES NOT EMPTY

	PUSHJ	P,WRITC2	;NEED ANOTHER DIRECTORY BLOCK

	MOVE	E,LBUF
	MOVE	E,BUFLOC(E)	;DESTROYED E AGAIN
	HRLZI	B,(E)		;THE OLD DIRECTORY 0
	HRRZ	C,FPTR		;WHERE THE DATA IN IT WILL GO
	HRRI	B,(C)
	BLT	B,LBLKL-1(C)	;COPY THE FULL DIRECTORY 
	MOVE	B,LBLOCK+1
	MOVEM	B,4(E)		;CHAIN TO FULL DIRECTORY BLOCK
	ALTERD	1,D
	PUSHJ	P,CLRDIR
	JRST SAVE4

SAVNEW:	JSP	H,OPENW		;WRITING ONLY
	 ERROR	IOPERR			;DISASTER
	SETOM	LABS
	PUSHJ	P,WRITC2	;READ A NEW BLOCK INTO BUFFER 1
	MOVE	A,LBUF+1
	MOVEM	A,LBUF		;AND SWITCH IT INTO BUFFER 0
	MOVE	A,LBLOCK+1
	MOVEM	A,LBLOCK
	PUSHJ	P,CLRDIR
FOR TEN50,<FORCE 0>
	JRST	SAVE1

SAVE4:	MOVE	E,LBUF
	MOVE	E,BUFLOC(E)
	HRLI	E,D		;NOW FILL UP A DIRECTORY ENTRY
	MOVE	D,3(E)		;TOP OF LADES
	HRLI	D,-4		;NUMBER OF WORDS OF TEXT TO COPY MAX
	MOVEI	G,(D)		;EXTRA COPY OF PTR TO ENTRY

	MOVE	B,(S)
	MOVEI	B,1(B)		;FIRST TEXT WORD IN ENTRY NAME
	MOVE	C,@WSB
	MOVEM	C,@E
	TRNE	C,376		;END OF ENTRY NAME?
	AOBJN	D,.-4

	MOVEI	D,4(G)
	MOVE	C,FILEAD
	MOVEM	C,@E

	MOVEI	D,5(G)
	MOVE	C,TANDD
	MOVEM	C,@E

	MOVEI	D,6(G)
	SETOM	@E		;NEVER GOTTEN

	MOVEI	D,7(G)		;NO BLOCKS IN FILE
	MOVE	C,FILECT
	MOVEM	C,@E

	MOVEI	D,DRENL-1(G)
	EXCH	G,2(E)		;THIS BLOCK ONTO LUDES
	EXCH	G,@E		;OLD TOP OF LUDES INTO THIS BLOCK
	MOVEM	G,3(E)		;OLD LADES FP IN THIS BLOCK TO LADES

	AOS	1(E)		;A NEW ENTRY IS NOW COMPLETE
	ALTERD	0,C

	SETOM	LDIRNO		;SEARCH FOR ANOTHER ENTRY OF SAME NAME
	SETOM	LDIRL		;SET UP TO FALL INTO SEARCH
	MOVE	A,(S)
	ADDI	A,1(W)
	MOVE	G,2(E)		;LUDES
	PUSHJ	P,SERCH5	;ENTER AT CONTINUE WITH NEXT ENTRY
	 JRST	.+2		;NO OTHER ENTRY OF SAME NAME
	PUSHJ	P,ERENRO	;DELETE THAT OTHER ENTRY
	READ	[0],0
	MOVE	A,LBUF
	MOVE	B,LABS
	MOVEM	B,@BUFLOC(A)	;UPDATE LABS BEFORE CLOSING FILE
	ALTERD	0,C
	JSP	H,CLOSUP
	JRST	COMEX


COPY:	PUSHJ	P,GNE
	 ERROR ERMSSG
	MOVEI	G,COPYGT	;NEXT ELEMENT IS TO OR FROM
	CAMN	A,[XWD MPF!IMMEDI,FROML+1]
	JRST	COPY1
	CAME	A,[XWD MPF!IMMEDI,TOL+1]
	 ERROR
	MOVEI	G,COPYSV
COPY1:	POP	S,B	;STRING OF FILE DESIGNATOR
	HRLI	B,010700+W
FOR TENEX,<
	MOVSI	1,400001
	CAIE	G,COPYGT
	MOVSI	1,1
	GTJFN
	 ERROR TNXERR
	MOVEM	A,COPJFN
	MOVE	2,[XWD 70000,700000]
	OPENF
	 ERROR TNXERR
	JRST	(G)
>
FOR TEN50,<
	JRST COPY2

;CONVERT BEGINNING OF STRING POINTED TO BY B TO SIXBIT AND STORE
; RESULT IN E.  TERMINATORS ARE EOM : . [ AND MAYBE ]

R7TO6:	MOVEI	D,6
	MOVE	A,[POINT 6,E]
	SETZ	E,
R7TO6L:	ILDB	C,B
	CAIE	C,"."
	CAIN	C,":"
	POPJ	P,		;LEGAL TERMINATORS
	CAIE	C,"["
	CAIN	C,0
	POPJ	P,
FOR BBN50,<
	CAIN	C,"]"
	POPJ	P, >
	XORI	C,40
	SOJL	D,[ERROR]	;TOO MANY CHARS
	IDPB	C,A		;OK
	JRST	R7TO6L		; GET ANOTHER CHAR
>

FOR TEN50,<
COPY2:	MOVE H,[XWD OPNPAR,OPNPAR+1]
	SETZM OPNPAR
	BLT H,LUKDAT+3		; CLEAR FILE DESCRIPTOR BLOCKS
	MOVEI	H,LUKDAT
	PUSHJ	P,R7TO6		;GET DEVICE OR FILENAME
	JUMPE	C,FNDCD8	; FILENAME ONLY
	CAIN	C,"["		; FOO[PRJPRG]
	JRST FNDCD3
	CAIN	C,"."		; FILENAME
	JRST	FNDCD2
	CAIE	C,":"		; DEVICE DESIGNATOR
	 ERROR		;NOTA
	MOVEM	E,OPNPAR+1	; DEVICE NAME
	PUSHJ	P,R7TO6		; FILENAME OR NOTHING
	JUMPE	C,FNDCD8	; LAST FIELD IS FILE NAME
	CAIN	C,"["		; FOO:BAR[PRJPRG]
	JRST	FNDCD3
	CAIE	C,"."		; ONLY OTHER LEGAL TERMINATOR
	 ERROR
FNDCD2:	MOVEM	E,(H)		; FILE NAME OK
	MOVEI	H,LUKDAT+1
	MOVEI	D,3		; EXT CAN ONLY BE 3 CHARS LONG
	PUSHJ	P,R7TO6+1
	JUMPE	C,FNDCD8	; EXTENSION IS LAST
	CAIE	C,"["		; EXT CAN ONLY BE FOLLOWED BY PRJ-PRG
	 ERROR
FNDCD3:	MOVEM	E,(H)		; STORE FIELD BEFORE [PRJPRG]
FOR BBN50,<
	PUSHJ	P,R7TO6		; UP TO SIX CHAR IDCODE
	CAIE	C,"]"
	 ERROR			; MUST BE FOLLOWED BY RT BRK
	CALL	5,[SIXBIT /SQUOZE/] >
NOTFOR BBN50,<
	MOVE	L,B
	PUSHJ	P,DNMO		;OCTAL DNM
	 ERROR
	CAIE	C,","		;FIRST ONE MUST END IN ,
	 ERROR
	HRLZ	E,M		; FIRST IS PRJ NO
	PUSHJ	P,DNMO
	 ERROR
	CAIE	C,"]"		; SECOND DELIMITED BY RT BRK
	 ERROR
	HRR	E,M		; AND IS PRG NUMBER
>
	MOVEM	E,LUKDAT+3
	ILDB	C,B
	JUMPE	C,FNDCD9	; ALL OK
	ERROR			; SOMETHING EXTRA IN FILE DESIGNATOR

FNDCD8:	MOVEM	E,(H)		; STORE THE LAST FIELD
FNDCD9:	HRLZI 2,(SIXBIT /DSK/)
	SKIPN OPNPAR+1
	MOVEM 2,OPNPAR+1	; DEFAULT VALUE IS DSK
	MOVEI 2,17
	MOVEM 2,OPNPAR		; MODE IS DUMP
	OPEN	2,OPNPAR	; INIT THE DEVICE
	 ERROR
	MOVE	1,[ENTER 2,LUKDAT]
	CAIE	G,COPYGT
	HRLI	1,(LOOKUP 2,)
	XCT	1
	 ERROR
	SETZM	CCNT2
	MOVEI	A,200
	PUSHJ	P,MAKELM
	MOVEM	B,BUFAD2
	ADDI	B,1(W)
	HRRZM	B,BUFLC2
	JRST	(G) >


	PUSHJ	P,READC1
READC:	SOSGE	CCOUNT		;READ ONE CHARACTER
	JRST	.-2		;THIS BUFFER EMPTY
	ILDB	C,FPTR		;FETCH ONE
	JUMPN	C,CPOPJ		;END OF FILE?

REDEOF:	JSP	H,CLOSUP
	TLNN	F,GETF
	JRST	COMEX
	JRST	READC

READC1:	MOVNI	C,LBLKL-1
	ADD	C,FPTR
	SKIPN	C,@C		;FORWARD PTR IS IN FIRST WORD OF BLOCK
	JRST	REDEOF	;FILE BROKEN
	PUSH	P,A
	PUSH	P,B
	PUSH	P,D
	PUSH	P,E	;P, USEABLE ONLY BECAUSE NO GC POSSIBLE
	READ	C,1		;READ THE NEXT BLOCK
	POP	P,E
	POP	P,D
	POP	P,B
	POP	P,A
	POPJ	P,		;POINTER AND COUNT SET UP BY READ




WRITEC:	SOSGE	CCOUNT		;WRITE A CHARACTER INTO A FILE
	JRST	WRITC1		;BUFFER FULL
	IDPB	C,FPTR		;PUT IT AWAY
	POPJ	P,

WRITC1:	PUSH	P,A
	PUSH	P,B
	PUSH	P,C		;SAVE THE CHARACTER
	PUSH	P,D
	PUSH	P,E
	AOS	FILECT		;ANOTHER BLOCK USED FOR FILE
	PUSHJ	P,WRITC2
WRTC1A:	POP	P,E
	POP	P,D
	POP	P,C
	POP	P,B
	POP	P,A
	JRST	WRITEC

WRITC2:	TRZ	F,TF		;WHETHER LABS=0
	SKIPLE	C,LABS		;END OF LABS?
	JRST	WRITC3		;NO
	TRO	F,TF		;GENERATING MORE SPACE

;NO MORE LABS,LABSND LENGTH OF FILE AND CALL THAT MORE LABS

	MOVE	C,FILSIZ	;SET UP AT OPEN
	AOS	FILSIZ
	IMULI	C,NLOGB		;# OF PAGES IN C, MULTIPLY BY FUDGE
	MOVEM	C,LABS		;REMEMBER FOR A WHILE
WRITC3:	MOVE	B,LBUF+1
	HRRZ	A,(P)	;WHO CALLED?
	CAIN	A,WRTC1A	;WAS IT FOR A NEW DATA BLOCK?
	JRST [  MOVE C,LBLOCK+1	;YES, CHAIN NEW ONE TO LAST ONE
		IDIVI C,NLOGB	;  WHICH ONE REL TO BEG OF PBLOCK
		IMULI D,LBLKL	;BUT FIND BEGINNING OF BUFFER
		ADD D,BUFLOC(B)
		MOVE C,LABS
		MOVEM C,(D)
		JRST .+1 ]
	TRNN	F,TF		;NEED TO LENGTHEN FILE?
	JRST	WRITC4		;NO


FOR TENEX,< READ C,1		;OLD ONE GETS WRITTEN IF CHANGED
	MOVE	A,LBUF+1 >	;AND THE NEW ONE GETS ASSOCIATED

FOR TEN50,< 	MOVE	A,LBUF
	XOR	A,LBUF+1
	MOVE	B,[BUFBIT]	;HACK!!, BUFBIT=1 AND WE ARE INTERESTED IN LBUF 1
	TDNN	B,A
	XORM	B,LBUF+1
	PUSHJ	P,WRITIF
	MOVE	C,LABS
	MOVEM	C,LBLOCK+1	;NOW ASSOCIATE NEW BLOCK WITH BUFFER
	MOVE	A,LBUF+1
	MOVEM	C,PBLOCK(A)	;AND PHYSICAL BUFFER
	MOVEI	C,5*<LBLKL-1>
	MOVEM	C,CCOUNT
	MOVE	B,BUFLOC(A)
	HRLZI	C,(B)
	HRRI	C,1(B)
	SETZM	(B)
	BLT	C,LBLKL-1(B)
	HRLI	B,010700
	MOVEM	B,FPTR >	;


	;WHICH PHYS BUFFER
	MOVE	B,FPTR
	HRLZI	A,-NLOGB	;SET UP THE LABS CHAIN IN THE BUFFER
	HRR	A,LABS		;LOG ADDR OF THE FIRST BLOCK IN BUFFER
	AOBJP	A,.+4

	HRRZM	A,(B)		;THE FORWARD POINTER
	ADDI	B,LBLKL		;WHERE THE NEXT ONE SHOULD GO
	AOBJN	A,.-2		;THE REST

	SETZM	(B)		;THE LAST ONE TERMINATES THE CHAIN
	JRST	WRITC6

WRITC4:	READ	C,1		;READ THIS ONE FOR ITS FORWARD PTR
WRITC6:	MOVE	C,@FPTR
	MOVEM	C,LABS
	SETZM	@FPTR		;THIS ONE NOW THE END OF THE DATA CHAIN
	ALTERD	1,C	;NOTE THAT THIS BLOCK CHANGED (SOON)
	POPJ	P,

;SEARCH FOR AN ENTRY
;ENTRY NAME AT 0(S)

SEARCH:	SETOM	LDIRNO
	SETOM	LDIRL		;NO OF ENTRIES IN LAST DIRECTORY BLOCK
	READ	[0],0		;READ BLOCK 0 INTO BUFFER 0
				;READ RETURNS WITH BUFF ADR IN E

SERCH1:	HRRZ	E,FPTR		;PHYSICAL ADDR OF DIR BLOCK TO E
	MOVE	A,(S)		;FETCH PTR TO ENTRY NAME
	ADDI	A,1(W)
	MOVE	G,2(E)		;TOP OF ENTRY CHAIN, THIS DIRECTORY
	HRLI	E,D

SERCH2:	MOVEI	B,(A)		;ANOTHER POINTER TO FIRST WORD OF STRING
	HRLZI	D,-4		;FOUR WORDS OF TEXT IN DIRECTORY
	HRRI	D,(G)		;WHERE THE ENTRY BLOCK STARTS
SERCH3:	MOVE	C,(B)		;A WORD OF THE ENTRY NAME
	CAME	C,@E		;SAME AS IN THE ENTRY BLOCK?
	JRST	SERCH5		;NO, TRY THE NEXT ENTRY BLOCK
	TRNN	C,376		;IS IT THE END OF THE NAME STRING?
	JRST	CPOPJ1		;YES, FOUND
	MOVEI	B,1(B)		;NEXT WORD IN STRING
	AOBJN	D,SERCH3	;NEXT WORD IN ENTRY BLOCK

	PUSH	P,A		;IN CASE .READ IS CALLED
	PUSH	P,E
	READ	@E,1		;READ FIRST DATA BLOCK INTO BUF 1

	HRLZI	H,440700	;COMPARE FULL NAME
	HRR	H,-1(P)
	PUSHJ	P,READC
	ILDB	B,H
	JUMPE	B,SERCH4	;END OF INPUT STRING
	CAIN	B,(C)
	JRST	.-4		;STILL SAME, TRY ANOTHER
	SETZ	C,		;SO THE NEXT TEST WILL LOSE

SERCH4:	POP	P,E
	POP	P,A
	CAIN	C,176		;DID THE ENTRY NAME ALSO END?
	JRST	CPOPJ1		;YES, R2

SERCH5:	MOVEI	D,DRENL-1(G)	;POINT AT DIREXTORY ENTRY CHAIN
	SKIPE	G,@E		;IS IT IN THIS DIRECTORY BLOCK
	JRST	SERCH2		;YES
	SKIPN	A,4(E)		;IS THERE ANOTHER DIRECTORY BLOCK
	POPJ	P,		;NO, DIDN'T FIND THE ENTRY
	MOVE	B,LBLOCK
	MOVEM	B,LDIRNO
	MOVE	B,1(E)		;NUMBER OF ENTRIES TDB
	MOVEM	B,LDIRL
	READ	A,0		;READ THE NEXT DIRECTORY BLOCK
	JRST	SERCH1		;YES, PROCESS IT

;ERASE ENTRY ROUTINE - USED BY ERASE ENTRY AND SAVE
;AFTER SEARCH E CONTAINS D,,BLOCKAD
; AND G CONTAINS OFFSET OF ENTRY INTO BLOCK

ERENRO:	MOVEI	D,4(G)		;POINTER TO ADDR OF FIRST DATA BLOCK
	MOVE	C,@E		;GET IT
	MOVEM	C,FILEAD	;AND SAVE IT FOR PUTTING ON LABS

	MOVEI	D,DRENL-1(G)	;POINT TO FORWARD CHAIN IN OBJECT
	MOVEI	C,(G)		;OFFSET OF OBJECT ENTRY
	EXCH	C,3(E)		;PUT IT ON TOP OF FREE CHAIN
	EXCH	C,@E		;AND TOP OF FREE CHAIN INTO IT
				;ENDING WITH REST OF USED LIST IN C
	SKIPA	D,[EXP 2]	;FIRST ENTRY IN THIS BLOCK
EREN1:	MOVEI	D,DRENL-1(A)	;POINT AT NEXT CHAIN ADDRESS
	MOVE	A,@E
	CAME	A,G		;DID THIS CELL POINT AT DELETED CELL
	JRST	EREN1		;NO

	MOVEM	C,@E		;RECHAIN LUDES
	ALTERD	0,D
	SOSG	A,1(E)		;DECREMENT NUMBER IN LUDES
	JRST	FLUSHTHISONE

	SKIPG	B,LDIRL		;PREVIOUS DIR BLOCK?
	 JRST	TRYAFTER	;NO 
	ADDI	B,(A)		;TOTAL NO,TDIRL+LDIRL
	CAILE	B,NENTRY	;WILL FIT IN ONE BLOCK?
	 JRST	TRYAFT		;NO, TRY TDIRL+'NDIRL'

;COMPACT LDIR WITH TDIR, PUTTING TDIR INTO LDIR
;THIS ORDER SO THAT IF LDIR IS NO 0 RECHAIN ALWAYS WINS

	PUSH	P,E		;THE "FROM" DIRECTORY
	MOVE	B,LBUF		;TAKE THIS DIRECTORY
	MOVEM	B,LBUF+1		;AND PUT IT IN BUF1
	MOVE	B,LBLOCK
	MOVEM	B,LBLOCK+1
	READ	LDIRNO,0

	POP	P,E
	MOVE	H,A		;SECOND BUFFER POINTED TO BY H
COMPACT:	HRLI	H,G		;AND OFFSET BY CONTENTS OF G

	MOVE	A,2(H)		;TOP OF LIST IN LDIR
	MOVEI	G,DRENL-1(A)
	SKIPE	A,@H
	JRST	.-2		;FIND END OF PREVIOUS LIST
	MOVE	A,3(H)
	MOVEM	A,@H	;END OF OLD LIST WILL NOW POINT TO LADES

	MOVE	D,2(E)		;POINT AT FIRST ENTRY TO BE COPIED

ERENLP:	MOVEI	G,(A)
	HRLZI	A,@E		;FROM
	HRRI	A,@H		;THE TO FREELIST
	MOVEI	G,DRENL-2(G)
	BLT	A,@H		;COPY AN ENTRY

	MOVEI	G,1(G)		;POINT TO POINTER TO NEXT FREE TO
	MOVE	A,@H		;NEXT TO BUFFER ADDRESS TO A
	MOVEI	D,DRENL-1(D)
	SKIPE	D,@E		;NEXT FREE FROM, IS IT END OF LIST
	JRST	ERENLP	;NO

	SETZM	@H		;END OF GOOD CHAIN
	MOVEM	A,3(H)
	ALTERD	0,D		;NOTE THAT BUF0 CHANGED

	MOVE	A,LBLOCK+1
	EXCH	A,LABS
	MOVEM	A,(E)		;PUT THE DIR BLOCK JUST FREED ON TOP OF BLOCK FREELIST
	MOVE	A,4(E)
	MOVEM	A,4(H)		;UPDATE DIRECTORY CHAIN
	MOVE	A,1(E)
	ADDM	A,1(H)		;AND COUNT OF USED ENTRIES IN DIRECTORY

	ALTERD	1,D		;NOTE THAT BUF1 CHANGED


ERENB:	MOVE	A,FILEAD	;PUT FILE BLOCKS ONTO FREELIST

ERENR:	READ	A,1
	SKIPE	A,@FPTR		;FIND THE END OF THE DATA CHAIN
	JRST	ERENR

	MOVE	B,LABS
	MOVEM	B,@FPTR
	ALTERD	1,D

	READ	[0],0

	MOVE	B,FILEAD
	MOVEM	B,@FPTR
	MOVEM	B,LABS
	ALTERD	0,D
	POPJ	P,


TRYAFT:	SKIPN	A,4(E)		;IS THERE A NEXT DIRECTORY
	JRST	ERENB		;NO, JUST DELETE THE DATA BLOCKS
	PUSH	P,E
	READ	A,1		;READ IT
	POP	P,H		;IN THIS CASE, THE TO DIRECTORY
	MOVE	B,1(A)
	ADD	B,LDIRL
	CAILE	B,NENTRY
	JRST	ERENB		;TWO INTO ONE WON'T GO
	HRLZI	E,D
	HRRI	E,(A)		;THE AFTER IS THE "FROM" DIRECTORY
	JRST	COMPACT

FLUSHTHISONE:
	SKIPN	A,LBLOCK	;IS THIS DIRECTORY 0
	JRST	FLUSH0		;YES
	EXCH	A,LABS		;PUT IT ON LABS
	MOVEM	A,(E)
	MOVE	H,4(E)		;CHAIN TO NEXT DIRECTORY
	READ	LDIRNO,0
	MOVEM	H,4(A)	;CHAIN THIS ONE
	ALTERD	0,D
	SKIPE	1(A)		;IS THIS ONE EMPTY TOO?
	JRST	ERENB
	MOVEI	E,(A)		;IT MUST BE DIR BLOCK 0, DELETE FILE

FLUSH0:	SKIPE	4(E)		;ARE THERE ANY OTHER DIRECTORIES?
	JRST	ERENB		;YES, LET THIS ONE BE
FOR TENEX,<MOVE A,INFILE	;DELETE THE FILE
	DELF
	 ERROR	IOPERR>
FOR TEN50,<
FOR LEVELC,<CLOSE	1,
	STATZ	1,740000
	 ERROR	IOPERR >
	SETZM	LUKDAT
	RENAME	1,LUKDAT
	 ERROR	IOPERR >
	POP	P,G		;RETURN LOC
	JSP	H,CLOSUP+1	;REMOVE OPEN FILE JUNK FROM PDL
	JRST	1(G)		;SKIP OVER CLOSING OF FILE

CLRDIR:	MOVE	E,LBUF	;ADDR OF FIRST WORD OF BUFFER?
	MOVE	E,BUFLOC(E)
	SETZM	1(E)		;NUMBER OF DIRECTORY CELLS USED
	SETZM	2(E)		;LUDES - LIST USED DIRECTRY ENTRIES
	MOVEI	D,10		;REL PTR TO FIRST AVAILABLE DIR CELL
	MOVEM	D,3(E)		;LADES - LIST AVAIL DIR ENTRY CELLS
	MOVEI	E,10(E)		;POINT TO FIRST CELL
	MOVEI	D,DRENL(D)	;REL ADDR OF NEXT CELL
	MOVEM	D,DRENL-1(E)	;SET FORWARD PTR OF THIS CELL TO NEXT
	MOVEI	E,DRENL(E)	;POINT AT NEXT CELL
	CAIE	D,LBLKL-DRENL
	JRST	.-4
	SETZM	DRENL-1(E)
	POPJ	P,

WRITIF:	MOVE	B,LBUF(B)	;WHICH PHYSICAL BUFFER?
WRITF1:	SKIPE	CHANGE(B)	;IS THE DATA CHANGED?
	JRST	WRITIT		;NEEDS WRITING
	POPJ	P,

.FORCE:	MOVE	B,LBUF(E)	;PHYS BUF

WRITIT:	SETZM	CHANGE(B)	;WILL NO LONGER NEED REWRITING
FOR TEN50,< MOVE	A,PBLOCK(B)
	USETO	1,1(A)
	HRRZ	A,BUFLOC(B)
	SUB	A,[XWD PBLKL,1]	;REST OF IOWD
	MOVEM	A,DMPLST
	OUT	1,DMPLST		;DUMP MODE, CHANNEL 1,
	SKIPA
	 ERROR	IOPERR >
	POPJ	P,

FOR TEN50,<
.MARK:	MOVE	E,LBUF(E)
	SETOM	CHANGE(E)
	POPJ	P, >


OPENW:
FOR TENEX,<MOVE	A,[XWD 400000,1]
	MOVEM	A,JFNTAB
	MOVEI	A,JFNTAB
	GTJFN
	 JRST	(H)
	MOVE	G,[XWD 070000,700200]
	JRST	OPNRW1 >
FOR TEN50,<SETZM	FILSIZ
	JRST	OPNRW1>

CLOSIT:
FOR TEN50,<	MOVEI	B,0
	PUSHJ	P,WRITF1
	MOVEI	B,1
	PUSHJ	P,WRITF1
	CLOSE	1,
	RELEAS	1,
	SETZM	BUFLOC
	SETZM	BUFLOC+1	;DEALLOCATE THE BUFFERS
	SETZM	BUFADR
	SETZM	BUFADR+1
>
FOR TENEX,<HRRZ	A,INFILE
	CLOSF
	 ERROR	IOPERR
	HRLZI	E,-2		;REMOVE THE PAGES OF BUFFER
	MOVE	B,BUFLOC(E)	;FROM THE MAP OF FORK
	ASH	B,-11
	HRLI	B,400000
	SETO	A,
	PMAP
	AOBJN	E,.-5 >
	POPJ	P,

;READ A BLOCK INTO A BUFFER
;CALLED WITH LOGICAL BUFFER NUMBER IN E
; AND WITH LOGICAL BLOCK NUMBER IN C

.READ:	MOVEM	C,LBLOCK(E)	;THIS LOGICAL BUFFER WILL CONTAIN THIS BLOCK
	HRLZI	A,-2		;# OF PHYSICAL BUFFERS

	MOVEI	D,(C)		;FIRST CHECK IF LOGICAL BLOCK IS
	XOR	D,PBLOCK(A)	; IN ONE OF THE PHYSICAL BUFFERS
	TRNN	D,-NLOGB	;# OF LOGICAL BUFFERS IN ONE PHYS ONE
	JRST	.READ7		;ALREADY IN A BUFFER
	AOBJN	A,.-4		;TRY THE OTHER ONE

;THE LOGICAL BLOCK IS NOT IN EITHER PHYSICAL BUFFER
;THEREFORE THE PHYSICAL BUFFER USED BY THE REQUESTED LOGICAL ONE
; IS PREFERABLE UNLESS IT IS ALSO USED BY THE OTHER LOGICAL ONE TOO

	MOVE	A,LBUF		;THE PHYS BUFFER USED BY LOGICAL BUFFER 0
	XOR	A,LBUF+1
	XOR	A,[EXP BUFBIT]	;ARE BOTH USING THE SAME PHYS BUFFER?
	SKIPE	A		;NO
	XORM	A,LBUF(E)	;YES, SWITCH THE BUF THIS ONE USES

	MOVEI	B,(E)
	PUSHJ	P,WRITIF

	MOVE	B,LBUF(E)	;NOW READ THE REQUESTED BLOCK
	MOVE	A,LBLOCK(E)	;WHAT PART OF FILE TO READ

FOR TENEX,<	MOVE	B,BUFLOC(B)	;PHYSICAL BUFFER LOCATION
	ASH	A,-2	;MUST PARAMETERIZE LOG BASE 2 OF NLOGB
	ASH	B,-11		;2^9 WORDS PER PAGE
	HRLI	B,400000
	HRL	A,INFILE
	HRLZI	C,140000	;READ AND WRITE ACCESS PERMITTED
	PMAP		>
FOR TEN50,<USETI 1,1(A)
	HRRZ	A,BUFLOC(B)
	SUB	A,[XWD PBLKL,1]	;REST OF IOWD
	MOVEM	A,DMPLST
	IN	1,DMPLST
	SKIPA
	 ERROR	IOPERR >


.READ3:	MOVEI	A,NLOGB-1	;SET UP POINTER TO THE BLOCK
	AND	A,LBLOCK(E)
	IMULI	A,LBLKL		;NUMBER OF WORDS IN A LOGICAL BLOCK
	MOVE	C,LBUF(E)
	ADD	A,BUFLOC(C)
	HRLI	A,010700

	MOVEM	A,FPTR
	MOVEI	B,5*<LBLKL-1>
	MOVEM	B,CCOUNT
	MOVE	B,LBLOCK(E)
	MOVEM	B,PBLOCK(C)
	POPJ	P,

.READ7:	HRRZM	A,LBUF(E)	;MAKE THE LOGICAL BUFFER USE THAT PBUF
	JRST	.READ3		;SET UP POINTERS AND EXIT

FOR TENEX,<
FILEGO:	PUSH	S,CBOT
	PUSHJ	P,GETFLN
	PUSHJ	P,GETFLN
FILEGA:	MOVE	B,-1(S)
	MOVEI	A,10		;NUMBER OF WORDS FOR TENEX
	CAMGE	A,@WSB		;WILL THE NAME FIT?
	 ERROR	FILER9		;NOT IMPLEMENTED
	MOVEI	B,@WSB		;COMPUTE ABS ADDR OF STRING
	MOVEI	B,1(B)		;FIRST WORD OF TEXT
	MOVEM	B,JFNTAB+4	;FILE NAME SLOT

	MOVEI	B,[ASCIZ /LGO/]
	MOVEM	B,JFNTAB+5	;EXT

	MOVE	A,[XWD 100000,1]
	MOVEM	A,JFNTAB
	MOVEI	A,JFNTAB
	MOVEI	B,0
	GTJFN
	 JRST	(H)		;NO FILE
	MOVE	G,[XWD 070000,701000]
OPNRW1:	JSP	D,SAVEUP	;SAVE CURRENT STATE OF LOGO
	PUSH	P,INFILE	;SAVE NAME OF CURRENT OPEN FILE
	MOVEM	A,INFILE
	HRLZ	A,LBLOCK+1	;LOGICAL BLOCK NUMBER OF DATA BLOCK
	HRR	A,CCOUNT	;NUMBER OF CHARS REMAINING IN THAT BLOCK
	PUSH	P,A		;A POINTER INTO THE ENTRY
	PUSH	P,BSP
	MOVE	B,SPP
	MOVEM	B,BSP
	TLO	F,GETF
	MOVE	A,INFILE
	MOVE	B,G
	OPENF
	 JRST	(H)
	SIZEF
	 JRST	(H)
	MOVEM	C,FILSIZ
	MOVEI	A,400000
	MOVEM	A,BUFLOC
	MOVEI	A,401000
	MOVEM	A,BUFLOC+1
	SETOM	PBLOCK
	SETOM	PBLOCK+1	;NO PAGES IN THE BUFFERS
	JRST	1(H) >


FOR TEN50,<
FILEGO:	PUSH	S,CBOT
	PUSHJ	P,GETFLN
	PUSHJ	P,GETFLN
FILEGA:	MOVE	B,-1(S)
	HRLI	B,010700+W
	MOVE	A,[POINT 6,LUKDAT]
	SETZM	LUKDAT
	MOVEI	D,6
FILGO1:	ILDB	C,B
	JUMPE	C,FILGO2
	XORI	C,40		;7 TO 6 BIT CODE
	IDPB	C,A		;STORE IN LUKDAT
	SOJG	D,FILGO1
	ILDB	C,B
	JUMPN	C,[ERROR FILER9] ;FILE NAME TOO LONG, TEMP ERR

FILGO2:	INIT	1,17		;INIT IN DUMP MODE
	 SIXBIT	/DSK/
	 Z
	 ERROR	IOPERR

	HRLZI	C,(SIXBIT /LGO/)
	MOVEM	C,LUKDAT+1	;EXT
	SETZM	LUKDAT+2
	SETZM	LUKDAT+3

	LOOKUP	1,LUKDAT
	 JRST	(H)		;FILE BUSY OR SOMETHING
	HLRE	A,LUKDAT+3	;LENGTH OF FILE
	JUMPG	A,.+3		;ALREADY IN BLOCKS
	ASH	A,-7
	MOVNI	A,(A)		;MAKE -WORDS +BLOCKS
	HRRZM	A,FILSIZ
	SETZM	LUKDAT+3	;THIS PRJ PROG AGAIN

OPNRW1:	JSP	D,SAVEUP
	PUSH	P,INFILE
	MOVEM	A,INFILE
	HRLZ	A,LBLOCK+1
	HRR	A,CCOUNT

	PUSH	P,A
	PUSH	P,BSP
	MOVE	B,SPP
	MOVEM	B,BSP
	TLO	F,GETF

	ENTER	1,LUKDAT
	 JRST	(H)

	HRLZI	C,-2		; ASSIGN 2 BUFFERS IN WS
OPNRW2:	MOVEI	A,LBLKL
	PUSHJ	P,MAKELM
	MOVEM	B,BUFADR(C)
	MOVEI	B,@WSB
	MOVEI	B,1(B)
	MOVEM	B,BUFLOC(C)
	AOBJN	C,OPNRW2

	SETOM	PBLOCK
	SETOM	PBLOCK+1
	JRST	1(H) >

CLOSUP:	PUSHJ	P,CLOSIT
	MOVE	A,BSP
	ADD	A,[XWD -2,3]	;TWO OFF THE S, 3 XTRA LEFT ON P
	JSP	C,SETPDL

	POP	P,BSP
	POP	P,A
	HRRZM	A,CCOUNT
	HLRZM	A,LBLOCK+1
	POP	P,INFILE
	JSP	D,RESTOR
	SKIPE	INFILE	;IS THE POPPED FILE THE TERMINAL?
	JRST	(H)
	TLZ	F,GETF
	MOVE	A,TERMIO
	MOVEM	A,CHIN
	JRST	(H)

LFILE:	POP	P,A
	PUSH	S,CBOT
	PUSHJ	P,GETFLN	;ONLY ONE INPUT
	PUSH	S,(S)		;DUMMY UP ANOTHER
	JSP	H,FILEGA
	 ERROR	NOFILE
	SETZ	A,
LFILE1:	READ	A,0
	HRRZ	E,FPTR
	HRLI	E,D
	MOVE	D,2(E)		;TOP OF LIST OF USED ENTRY SLOTS

LFILE2:	HRLZI	G,440700
	HRRI	G,@E		;FOR THE FIRST 20 CHARS OF ENTRY NAME
	MOVNI	H,^D20
	ILDB	C,G
	JUMPE	C,LFILE9	;EOM IS 0 IN DIRECTORY
	XCT	CHOUT
	AOJN	H,.-3
	PUSH	P,E
	PUSH	P,D		;READ FILE FOR THE REST OF THE GOODIES
	READ	1(G),1
	MOVEI	A,4
	ADDM	A,FPTR		;SKIP TYPING FIRST TWENTY CHARS AGAIN
	MOVNI	A,4*5
	ADDM	A,CCOUNT
	PUSHJ	P,READC
	CAIN	C,176
	JRST	.+3
	XCT	CHOUT
	JRST	.-4
	POP	P,D
	POP	P,E

LFILE9:	PUSHJ	P,CRLF
	MOVEI	D,DRENL-1(D)
	SKIPE	D,@E
	JRST	LFILE2	;NEXT ENTRY IN THIS DIRECTORY
	SKIPE	A,4(E)
	JRST	LFILE1	;NEXT DIRECTORY BLOCK
	JRST	REDEOF
> ;CLOSE THE NFILE CONDITIONAL
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                     FOR OFILE,<

;FILE STUFF

GET:	PUSHJ	P,GENFLN	;GENERATE FILE NAME
	MOVEI	A,EOFILE	;WHERE TO GO ON INPUT EOF
	PUSHJ	P,OPENR		;OPEN IT FOR READING
	JSP	D,SAVEUP
	TLZ	F,STEPF		;FIRST LINE OF FILE MAY BE COMMENT
	PUSHJ	P,TIS
	 ERROR	IOPERR		;CANNOT GET A RUBOUT FROM THE FILE
	MOVE	A,(S)
	HRLI	A,010700+W
	ILDB	C,A
	CAIE	C,";"		;IS THE FIRST LINE COMMENT?
	JRST	MAINL1		;NO, TREAT THE LINE NORMALLY
	POP	S,A
	PUSHJ	P,PTOSS		;TYPE THE COMMENT
	PUSHJ	P,CRLF
	JRST	MAINL

FOR TENEX,<

EOFILE:	CAIN	A,100		;IS THE OPEN FILE THE TERMINAL?
	 DEBRK			;YES
	CLOSF
	 ERROR
	MOVE	A,SPP
	JSP	C,SETPDL
	JSP	D,RESTOR

	POP	P,A		;PREVIOUS FILE HANDLE
	MOVEM	A,INFILE
	CAIN	A,100
	TLZ	F,GETF		;NOT STILL GETTING
	MOVEI	A,COMEX
	MOVEM	A,LEVLTB+1
	DEBRK

OPENR:	HRRM	A,CHNTAB+12	;EOF ERROR DISPATCH LOCATION
	MOVE	A,[XWD 100000,1]
	MOVEM	A,JFNTAB
	MOVEI	A,JFNTAB
	MOVEI	B,0
	GTJFN
	 ERROR	NOFILE
	MOVE	B,[XWD 070000,600000]
	OPENF
	 ERROR	.
	POP	S,B		;FLUSH PTR TO FILE NAME

	TLO	F,GETF
	EXCH	A,INFILE
	EXCH	A,(P)
	JRST	(A)		;POPJ WITH AN INTERVENING PUSH  >

FOR TEN50,<
EOF1:
OPENR:
OPENW:
LEOFIL:
CLOSEF:
EOFILE:	POPJ	P,     >


FOR TENEX,<
GENFLN:	PUSHJ	P,GETFLN	;FILENAME
	PUSHJ	P,GETFLN	;ENTRYNAME
	PUSHJ	P,NEWSTR
	PUSHJ	P,NEWSR1
	PUSHJ	P,COPYAB
	MOVEI	C,"/"
	DPB	C,B
	PUSHJ	P,NEWSRC
	PUSHJ	P,COPYAB	;FILENAME/ENTRYNAME
	POP	S,A		;FLUSH AN ARG
	PUSHJ	P,ENDSTP	;FLUSH OTHER ARG AND PUT THIS ON STACK
SJNAME:	MOVE	B,(S)
	MOVEI	A,@WSB
	MOVEI	A,1(A)
	MOVEM	A,JFNTAB+4	;SLOT FOR NAME
	MOVEI	A,[ASCIZ /LGO/]
	MOVEM	A,JFNTAB+5
	POPJ	P, >



SAVE:	PUSHJ	P,GENFLN	;GENERATE FILE NAME
	PUSHJ	P,OPENW		;OPEN FILE FOR WRITING
	TLO	F,SAVEF		;DENOTE A SAVE IN PROGRESS
	MOVE	A,CPP		;STORE COMMENT IN FILE IF ON SAVE LINE
	MOVEI	A,1(A)		;POINT AT NEXT ELEMENT
	SKIPE	A,@CSA		;IS IT NOT THERE?
	TLNN	A,COMMTF	;MAKE SURE IT IS A COMMENT
	JRST	SAVE1		;NOT A COMMENT
	MOVEI	C,";"
	PUSHJ	P,TYO
	PUSHJ	P,PTOSS
	PUSHJ	P,CRLF

SAVE1:	PUSHJ	P,LISTAP	;TYPE ALL PROCEDURES TO FILE
	PUSHJ	P,SAVAN		;TYPE ALL GLOBAL NAMES TO FILE
	PUSHJ	P,SAVAA		;ALL ABBREVIATIONS TO FILE
	TLZ	F,SAVEF		;NO MORE TYPING IN THE SAVE
	PUSHJ	P,CLOSEF
	JRST	COMEX >

SAVANR:	PUSH	P,A
	PUSHJ	P,FINDAG
	SKIPA			;R1_FOUND ONE, R2_NO GLOBAL VAL
	JRST	APOPJ		; DON'T SAVE IF NO GLOBAL BINDING
	MOVSI	A,UNBOUN
	TDNE	A,1(C)		; OR IF UNBOUND
	JRST	APOPJ
	PUSH	P,C
	MOVEI	A,[ASCIZ /MAKE /]
	PUSHJ	P,PTOSSM
	MOVE	A,-1(P)	;WHAT NAME
	MOVE	A,(A)	;NAME OF THING
	MOVEI	E,024	;PSEUDO-QUOTE
	TRO	F,PREFIX!SUFFIX
	PUSHJ	P,PTOSS
	POP	P,-1(P)
	PUSHJ	P,PSPACE
	JRST	LSTNR1

SAVAAR:	MOVEI	A,[ASCIZ /ABBREVIATE /]
	PUSHJ	P,PTOSSM
	MOVEI	E,042
	TRO	F,PREFIX!SUFFIX
	MOVE	A,1(G)
	PUSHJ	P,PTOSS
	MOVEI	A,[ASCIZ / AS /]
	PUSHJ	P,PTOSSM
	MOVE	A,(G)
	TRO	F,PREFIX!SUFFIX		;QUOTE THIS ONE TOO
	PUSHJ	P,PTOSS
	JRST	CRLF



GETFLN:	PUSHJ	P,GNE	
	 ERROR	ERMSSG
	TLNN	A,UPRF	
	 ERROR	FILER1
	MOVEI	A,-1(A)
	PUSH	S,@RPA
	POPJ	P,


;OPENING AND CLOSING FILES FOR SAVE FOR TENEX

FOR OFILE,<FOR TENEX,<

OPENW:	HRLZI	A,400000
	MOVEM	A,JFNTAB
	MOVEI	A,JFNTAB
	MOVEI	B,0
	GTJFN
	 ERROR
	MOVE	B,[XWD 070000,500000]
	OPENF
	 ERROR
	EXCH	A,OUTFILE
	EXCH	A,(P)
	JRST	(A)

CLOSEF:	MOVE	A,OUTFILE
	HRLI	A,400000
	CLOSF
	 ERROR
	HRRZ	A,OUTFILE
	MOVE	B,[XWD 1,7]	;LH OF 7'TH WD OF FDB IS VERSION
	MOVEI	C,GCTEM
	GTFDB
	HLRZ	B,GCTEM
	CAIN	B,1		;IS THIS VERSION VERSION ONE
	JRST	CLOSW1		;YES, ALREADY PROPERLY NAMED
	MOVE	A,[XWD 0,1]
	MOVEM	A,JFNTAB
	PUSHJ	P,SJNAME

	MOVEI	A,JFNTAB
	MOVEI	B,0
	GTJFN
	 ERROR
	MOVEI	B,(A)	;NEW JFN IN B
	MOVE	A,OUTFILE	;OLD JFN IN A
	RNAMF		;MAKE SURE ALL CLSED FILES ARE VERSION 1
	 ERROR
	MOVEI	A,(B)
CLOSW1:	RLJFN
	 ERROR
	POP	S,A	;FLUSH POINTER TO NAME STRING
	POP	P,A	;RETURN
	POP	P,OUTFILE
	JRST	(A)  >
> ;CLOSE THE OFILE CONDITIONAL


ERRORR:	MOVE	P,[IOWD ERPDLL,ERRPDL]
	TRZ	F,PREFIX!SUFFIX
	MOVE	A,TERMIO+1
	TLZE	F,SAVEF
	MOVEM	A,CHOUT
	SKIPE	CHARNO
	PUSHJ	P,CRLF
	HRRZ	C,JOBUUO
	CAIL	C,MAXERR
	SETZ	C,
	HLRZ	A,ERRORS(C)	;GET TEXT MESSAGE
	HRLI	A,440700
	HRRZ	B,ERRORS(C)	;GET DISPATCH FOR COMPUTATIONAL ERRORS
	JUMPN	B,(B)	;DISPATCH IF A COMPUTATIONAL ERROR
ALLERP:	PUSHJ	P,PTOS	;TYPE GENERAL MESSAGE
ALLERR:	PUSHJ	P,CRLF
	SKIPN	PRODNM	;WERE WE INSIDE A USER PROCEDURE?
	JRST	ERROUT
	MOVEI	A,[ASCIZ /I WAS AT LINE /]
	PUSHJ	P,PTOSSM
	MOVE	A,LINENO
	PUSHJ	P,DECPRT
	MOVEI	A,[ASCIZ / IN /]
	PUSHJ	P,PTOSSM
	JSP	N,PUNAME	;PRINT PROCEDURE NAME
	PUSHJ	P,CRLF
	JRST	ERROUT


NONAME:	PUSHJ	P,PTOS
	HRRZ	A,UUOTRP
	SUBI	A,1
	PUSHJ	P,OCTPRT
	JRST	ALLERR

ERROUT:	TDZ	F,[XWD TIF!NOBREAK,MAKEF]
	MOVE	P,UUOACS+P	;SAFE TO USE REGULAR PDL NOW
	TLZE	F,BROKE		;WAS ERROR THE BREAK KEY
	SKIPN	PRODNM		;AND WAS A STORED LINE INTERRUPTED?
	JRST	FLUSHM



FOR SAVBRK,<
ERROT2:	TLZ	F,RQF		;WHICH WAY DO WE CALL MAINL
	TLZE	F,TOF		;IN A PRINT OR TYPE?
	JRST	ERROT4		;YES, GO PROCEEDS WITH THE NEXT LINE
	TLZN	F,GETF		;IN A GET?
	JRST	ERROT3	;NO, BREAK AT EXECUTE RESTARTS CURRENT LINE
FOR OFILE,<FOR TENEX,<	MOVEI	A,100	;CLOSE THE FILE AND RETURN TO TERMINAL
	EXCH	A,INFILE
	MOVEI	A,(A)
	CLOSF
	 ERROR	IOPERR >>
FOR NFILE,<JSP	H,CLOSUP>

	MOVE	A,BSP		;GETTING, CLOSE THE FILE AND
	JSP	C,SETPDL	;SET UP TO REDO THE GET ON A GO
	MOVE	A,TERMIO
	MOVEM	A,CHIN
	JSP	D,RESTOR	;RESTORE THE LINE THAT HAD THE GET

ERROT3:	SOS	LINENO	;SO UPNEXT WILL FIND THIS ONE AGAIN
ERROT4:	MOVE	A,SPP
	ADDI	A,1
	JSP	C,SETPDL	;GET TO BEGINNING OF THE LINE
	PUSH	P,BSP
	JSP	D,SAVEUP	;SAVEUP FOR GO
	AOS	GODEPTH		;ONE MORE GO ON THE STACK
	MOVE	A,SPP
	MOVEM	A,BSP		;NEW "TOP LEVEL"
FOR TENEX,<	MOVEI	A,101		;TERMINAL OUTPUT
	DOBE >			;DISMISS UNTIL ERROR MESSAGE IS
				;COMPLETELY OUT
FOR DRIBBLE,<TRO F,TF		;BUT DON'T ASK FOR INITIALS >
	JRST	RESET >		;CLEAR WATING BREAKS AND RESTART PSI


FLUSHM:	TLZN	F,GETF		;NO, BUT WERE WE GETTING FROM A FILE?
	JRST	ERROT1		;NO
FOR OFILE,<FOR TENEX,<	MOVEI	A,100
	EXCH	A,INFILE
	MOVEI	A,(A)		;LH 0
	CLOSF			;CLOSE THE FILE BEING GOTTEN FROM
	ERROR	IOPERR >>	;THIS HAS TO BE WRONG
FOR NFILE,<JSP	H,CLOSUP>

ERROT1:	HRRZ	G,BSP
	ADD	G,PP
FLUSHL:	MOVE	A,SPP
	ADDI	A,1
	JSP C,SETPDL
	CAIL	G,(P)		;SET BACK TO BSP?
	JRST	FLUSHE		;YES
	POP	P,A		;RETURN FROM EXECUTE
	JSP	D,RESTOR	;ONE MORE PDL LEVEL
	JRST	FLUSHL

FLUSHE:	GARBAG
FOR DRIBBLE,<TRO F,TF >		;DO NOT REOPEN DRIBBLE FILE
	JRST	RESET

MATCHG:	PUSHJ	P,PTOS
	MOVE	C,UUOACS+D	;THE OFFENDING TERMINATOR
	PUSHJ	P,TYO
MATCG1:	MOVEI	C,"?"
	PUSHJ	P,TYO
	JRST	ALLERR

ILLUUO:	MOVEI	B,[ASCIZ /HELP!!/]
	PUSHJ	P,TOSS
	JRST	ALLERR

.INER3:	PUSHJ	P,PTOS
	MOVE	A,CBOT
	ADDI	A,1
	MOVE	A,@WSA
	PUSHJ	P,PTOSS
	MOVE	A,[POINT 7,[ASCIZ / OF WHAT PROCEDURE?/],]
	JRST	ALLERP

.NMER3:	SKIPA	E,-1(S)
.TOER3:	MOVE	E,1(S)
	EXCH	E,A
	HRLI	A,010700+W
TYPAFT:	PUSHJ	P,PTOS
	MOVE	A,E
	JRST	ALLERP

CANTBS:	MOVE	E,A
	MOVE	A,(S)
	JSP	H,ETLIT
	JRST	CANTB0
CANTBA:	SKIPA	E,UUOACS+A
CANTBT:	MOVE	E,THISPR
	EXCH	A,E
	JSP	H,ETYPIT
CANTB0:	MOVEI	A,[ASCIZ / CAN'T BE A /]
CANTB1:	HRLI	A,440700
	JRST	TYPAFT

.GOER2:	MOVE	E,A
	MOVE	A,UUOACS+D
	JRST	TYPAFT-1

.GOER3:	PUSHJ	P,PTOS
	SKIPA	A,UUOACS+D
.XITER:	POP	S,A
.GOER4:	HRLI	A,010700+W
	JRST	ALLERP

.NOPRO:	PUSHJ	P,PTOS
	SOS	A,UUOACS+A
	HRRZ	A,@RPA
	JRST	.GOER4

.TNAME:	PUSHJ	P,PTOS
	JSP	N,TUNAME
	JRST	ALLERR
.PMNAM:	JSP	N,PMNAME
	JRST	ALLERP

.COMER:	TLZN	F,UPFF
	JRST	.PMNAM		;A SYSTEM COMMAND
	SKIPA	E,UUOACS+E	;A USER DEFINED COMMAND
UNDFND:	MOVE	E,UUOACS+A
	JSP	N,PUNAM1
	JRST	ALLERP

TWOARG:	PUSH	P,A
	HRRZ	B,THISPR
	CAIL	B,RPREN		;INFIX OPS ABOVE )
	JRST	.ITWOA		;TYPE "A" + "B"
	JSP	N,PMNAME
	MOVEI	B,[ASCIZ / OF /]
	PUSHJ	P,TOSS
	MOVE	A,-1(S)
	JSP	H,ETLIT	;ARG ONE OF TWO
	MOVEI	A,[ASCIZ / AND /]
TWARG9:	PUSHJ	P,PTOSSM
TWARG8:	MOVE	A,(S)	;GET SECOND ARG OF TWO OR ONLY ARG IF ONE
	JSP	H,ETLIT
	PUSHJ	P,CRLF
	POP	P,E
	JRST	TYPAFT+1

ONEARG:	PUSH	P,A
	HRRZ	B,THISPR
	CAIL	B,RPREN
	JRST	.IONEA
	JSP	N,PMNAME
	MOVEI	A,[ASCIZ / OF /]
	JRST TWARG9

.ITWOA:	MOVE	A,-1(S)
	JSP	H,ETLIT
	PUSHJ	P,PSPACE
.IONEA:	JSP	N,PMNAME
	PUSHJ	P,PSPACE
	JRST	TWARG8

.NOCMD:	PUSHJ	P,PTOS
	POP	S,A
	JSP	H,ETLIT
	JRST	MATCG1


TUNAME:	SKIPA	E,TOPROD
PUNAME:	MOVE	E,PRODNM
PUNAM1:	EXCH	A,E
	JSP	H,ETUP
	MOVE	A,E
	JRST	(N)

PMNAME:	MOVE	B,THISPR
	MOVE	B,-1(B)
	PUSHJ	P,TOSS
	JRST	(N)

.XTRER:	MOVE	E,A
	SKIPA	A,CPP		;START WITH THIS ELEMENT
.XTRR1:	AOS	A,CPP
	MOVE	A,@WSA		;NEXT ELEMENT
	JSP	H,ETYPIT
	JUMPE	A,TYPAFT+1
	PUSHJ	P,PSPACE	;SEPARATE ELEMENTS WITH SPACES
	JRST	.XTRR1		;BACK FOR NEXT ELEMENT

.MISSG:	PUSHJ	P,PTOS		;"THERE ARE "
	HRRZ	H,UUOACS+P
	HLRE	A,(H)
	ADDI	A,1
	PUSHJ	P,DECPRT	;N
	MOVEI	A,[ASCIZ / INPUTS MISSING FOR /]
	PUSHJ	P,PTOSSM
	HRRZ	A,(H)
	CAIE	A,UPRODL+1
	JRST	.MISG2
	MOVE	A,-1(H)
	JSP	H,ETUP
	JRST	.MISG9

.MISG1:	PUSHJ	P,PTOS
	MOVE	A,THISPR
.MISG2:	JSP	H,ETMP

.MISG9:	MOVEI	C,"."
	PUSHJ	P,TYO
	JRST	ALLERR



LNALND:	MOVE	E,A
	MOVEI	A,[ASCIZ /LINE /]
	PUSHJ	P,PTOSSM
	MOVE	A,LINENO
	PUSHJ	P,DECPRT
	MOVEI	A,[ASCIZ / OF /]
	PUSHJ	P,PTOSSM
	MOVE	A,PRODNM
	JSP	H,ETUP		;TYPE USER PROCEDURE NAME
	MOVEI	A,[ASCIZ / WAS /]
	PUSHJ	P,PTOSSM
	MOVE	A,E
	PUSHJ	P,PTOS
	MOVEI	A,[ASCIZ / DURING EXECUTION./]
	PUSHJ	P,PTOSSM
	JRST	ALLERP

ETYPA:	MOVE	E,UUOACS+A
	EXCH	A,E
	JSP	H,ETYPIT
	JRST	TYPAFT+1

BRAKER:	PUSHJ	P,PTOS
	TLO	F,BROKE
	JRST	ALLERR


SETPDL:	HLRZ	B,A
	ADD	B,SP
	HRRZI	S,(B)
	SUB	B,SP+1
	HRLI	S,1(B)

	ADD	A,PP
	HRRZI	P,-1(A)
	SUB	A,PP+1
	HRLI	P,1(A)
	JRST	(C)


ETYPIT:	JFFO	A,.+2
	JRST	(H)
	JRST	@.+1(B)
	 EXP	ETMP,ETUP,ETVAR,ETLIT,ETMP,ETMV,ETCOM

ETMV:	MOVEI	E,":"
	TRO	F,PREFIX!SUFFIX
ETMP:	HRLI	A,440700
	JRST	ETUP1
ETUP:	HRLI	A,010700+W
	ADD	A,RP
ETUP1:	HRR	A,-1(A)
	PUSHJ	P,PTOS
	JRST	(H)

ETCOM:	MOVEI	G,";"
	JRST	ETLIT1
ETVAR:	MOVEI	A,-1(A)
	MOVE	A,@VPA
	SKIPA	G,[EXP ":"]
ETLIT:	MOVEI	G,042
ETLIT1:	EXCH	G,E
	TRO	F,PREFIX!SUFFIX
	PUSHJ	P,PTOSS
	MOVE	E,G
	JRST	(H)

DEFINE EE (NAME,TEXT,CODE)
<NAME==.-ERRORS
XWD [ASCIZ *TEXT*],CODE>

ERRORS:
EE .BARF,ERROR ,NONAME
EE ABBER1,LOOP IN ABBREVIATION EXPANSION.
EE ABBER2,DON'T USE THE EMPTY THING AS AN ABBREVIATION.
EE CANER1,NOTHING TO CANCEL.
EE COMERR, CAN'T BE USED AS AN INPUT. IT DOES NOT OUTPUT.,.COMER
EE DIRERR, CAN ONLY BE DIRECT.,.PMNAM
EE DIVERR,DIVISION BY ZERO.
EE EDTER1,YOU CANNOT EDIT THAT.
EE EDTER2,YOU ARE ALREADY EDITING ,.TNAME
EE ELSERR,THEN CLAUSE MISSING.
EE ERXTRA,IS EXTRA.,.XTRER
EE ERMSG1,THERE ARE ,.MISSG
EE ERMSSG,SOMETHING MISSING FOR ,.MISG1
EE EVER3, NEEDS A MEANING.,UNDFND
EE EVER4, HAS NOT BEEN COMPLETELY DEFINED.,UNDFND
EE EVER5,PROCEDURE NAME.,CANTBT
EE FILER1,FILE NAME.,CANTBA
EE FILER9,FILE NAME TOO LONG.
EE GOERR1,GO WHERE?
EE GOERR2, IS NOT A LINE NUMBER.,.GOER2
EE GOERR3,THERE IS NO LINE ,.GOER3
EE GOERR9,NO PLACE TO GO.
EE INERR1,MATCHING ,MATCHG
EE INERR2,LINE NUMBER IS TOO LARGE.,
EE INERR3,LINE ,.INER3
EE INERR4,YOU CAN'T HAVE A LINE 0.,
EE INFERR,INFIX OPERATOR MUST BE PRECEDED BY AN INPUT
EE IOPERR,I AM IN TROUBLE. TELL YOUR TEACHER.
EE LCLERR,LOCAL NAME.,CANTBS
EE LNAERR,ALTERED,LNALND
EE LNDERR,DELETED,LNALND
EE LSTER2,LIST ALL WHAT?
EE NMERR3, IS USED BY LOGO.,.NMER3
EE NMERR5,DON'T USE THE EMPTY WORD FOR A NAME.,
EE NOCMD,THERE IS NO COMMAND FOR ,.NOCMD
EE NOENTRY,NO SUCH ENTRY.
EE NOFILE,NO SUCH FILE.
EE NOPERR, WHAT? YOU ARE NOT DEFINING ANYTHING.,.PMNAM
EE NOPROD,THERE IS NO PROCEDURE ,.NOPRO
EE PREDR1,INPUT MUST BE A PREDICATE.,ONEARG
EE PREDR2,INPUTS MUST BE PREDICATES.,TWOARG
EE PRNER1,MATCHING (?
EE PRNER2,MISSING )?
EE STOERR, CAN ONLY BE USED IN A PROCEDURE.,.PMNAM
EE SUMERR,INPUTS MUST BE NUMBERS.,TWOARG
EE TITER2,TITLE MUST BE FOLLOWED BY "TO".
EE TOERR1,USE "DO" WHEN "TO" IS STORED.
EE TOERR2,YOU NEED : MARKS AROUND EACH INPUT.,
EE TOERR3, IS USED BY LOGO.,.TOER3
EE TOERR4,DON'T TRY TO DEFINE ANOTHER PROCEDURE INSIDE THIS ONE.,
EE TOERR5,PROCEDURE NAME.,CANTBA
EE TOERR6, IS ALREADY DEFINED.,ETYPA
EE TOERR7,ONLY PRINTING CHARACTERS IN A PROCEDURE NAME.
EE TOOFUL,NO ROOM FOR ANOTHER GET; SAVE AND ERASE FIRST.
EE WHATER, WHAT?,.PMNAM
EE WRDERR,INPUTS TO WORD CANNOT BE SENTENCES.,TWOARG
EE XITERR,,.XITER  ;NO SYSTEM ERROR MESSAGE, ONLY THAT SUPPLIED BY USER
EE ZERERR,INPUT MUST BE A NUMBER.,ONEARG
EE BREAK,BREAK,BRAKER
EE PUNT,STORAGE FULL
BLOCK 5	;FOR PATCHING THIS LIST
MAXERR==.-ERRORS


FOO:	BLOCK 200
LIT



LOGEND:	END LOGO
                                                                                                                                                                                                                                                                                                       