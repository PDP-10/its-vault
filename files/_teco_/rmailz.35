!- QREG USAGE IN RMAILZ (NOTHING IS ASSUMED AT ENTRY):
  Q0 IS A BUFFER HOLDING THE COMMAND STRING FROM DDT, PERHAPS CANONICALIZED,
      OR SOME FINAL SEGMENT OF IT.
  Q1 IS A BUFFER HOLDING THE NAMES OF ALL EXISTING SLASHIFIED
      FILES THAT HAVE BEEN FOUND SO FAR, AND THE NAME OF THE RMAIL
      FILE IF ENCOUNTERED, WHETHER EXISTING OR NOT; ONE FILENAME
      PER LINE, WITH THE RMAIL FILE LAST.
  Q2 IS THE UPPERMOST BUFFER. IT HOLDS THE CONCATENATED CONTENTS
      OF ALL THE FILES READ SO FAR.
  Q6 IS THE DEFAULT FILENAME FOR THE "O" OPTION. SET UP HERE
      FOR USE BY RMAILX.
  Q7 IS INITIALLY 0; SET TO 1 WHEN AN RMAIL FILE IS SEEN.
      EVENTUALLY Q1 IS COPIED INTO Q7.
  QA HOLDS THE DEFAULT SNAME (A STRING).
  QB HOLDS THE DEFAULT UNAME (A STRING).
  QE IS 1 IFF THERE IS AN RMAIL FILE, ELSE 0. SET UP BEFORE THE
      MAIL SCAN OF THE COMMAND LINE. EVENTUALLY ZEROED IF THERE
      ARE NO FILES EXCEPT THE RMAIL FILE.
  QF IS A TEMP.
  QG IS THE CHARACTER THAT TERMINATED THE FILE BEING PROCESSED.
  QP IS LOADED WITH A MACRO THAT PARSES OUT STANDARD FIELDS OF A MESSAGE.

THE USEFUL VALUES ARE Q6, Q7, QA, QB, QE
!

!- WHEN RMAILZ IS CALLED, THE BUFFER SHOULD CONTAIN
   <DESIRED DEFAULT SNAME> AS THE FIRST LINE
   <DESIRED UNAME> AS THE SECOND LINE
   <COMMAND STRING> AS THE THIRD LINE
!


    0UE 0U7              !-ASSUME NO RMAIL FILE, NO AUTO-DELETE!
    1FSFNAMSY(
    J:FXA                !-INITIAL SNAME IN QA!
    K:FXB K              !-DESIRED UNAME IN QB; BUFFER HAS JUST THE COMMAND STRING!
    J :S "E ZJ'"#R' 0,.K      !-DELETE LEADING SPACES OF COMMAND!
    J <S;RD>          !-DELETE ALL LINEFEEDS!
    Z"N ZJ-D'            !-DELETE FINAL CR!
    Z"E IB;OMAIL,COMMON;,B/'   !-NULL COMMAND SAME AS ^A OMAIL,<UNAME>/ !
    0,0A-33."E 1UE'      !-SET QE IF THERE IS AN RMAIL FILE!
    ZJ15.IJ              !-PUT CR AT END TO STOP SEARCH!
    Q..OU0               !-GET COMMAND STRING, IN ITS OWN BUFFER!
    FS BCREATEW Q..OU1  !-GET ANOTHER BUFFER FOR THE LIST OF ALL FILES FOUND!
    32I -D               !-MAKE SOME ROOM; WE ARE GOING TO INSERT STUFF LATER!
    FS BCREATEW Q..OU2  !-GET A BUFFER TO HOLD CONTENTS OF FILES!
    Q0U..O               !-LOOK AT COMMAND STRING AGAIN!
    ETDSK:A;B MAIL      !-DEFAULT NAME FOR NEWMAIL FILES!
  !LP!
    S,/î  !-SEARCH FOR END OF NEXT FILENAME!
    0A-33."E             !-TERMINATED BY ALTMODE =>!
       ETDSK:A; B RMAIL !-USE SPECIAL RMAIL DEFAULT NAME!
       ZK '              !-IGNORE REST OF COMMAND LINE!
    0AUG   -D
    <0,1A-32"N 0;' D>    !-DELETE LEADING SPACES!
    0,.FXF               !-DELETE FILESPEC, PUT IN QF!
    ETF QG-/"E 1:<ERF;F MAIL>"L ETCOMMON;''
    1:< ER>UF           !-PROCESS THE FILENAME. DOES FILE EXIST? !
    QG-33."E OFO'       !-RMAIL FILE NAME MUST ALWAYS GO IN Q1!
    QF"E QE"N QG-/"E   !-IF THERE IS AN RMAIL FILE, ACCUMULATE NAMES!
 !FO!     Q1U..O         !-OF EXISTING SLASHED FILES IN Q1!
          ZJ .(EG)J 3K L ZK       !-APPEND NAME, WITH DEFAULTS INSERTED, TO Q1!
          QG-/"E
            1:< EDOMAIL>       !-RENAME FILE AS OMAIL THEN REOPEN!
            :0L 0XF
            1:< ENFOMAIL ER> '''
       Q2U..O ZJ .UF
       QG-33."N ^ A'     !-NEWMAIL FILES JUST ADD AT END!
             "# .,( FSUREAD"N 10FY 0FSIFACCW' .) F( F=*APPEND*
"E )K J .,.FSBOUNDW ^ A ZJ 0,FSZFSBOUNDW'
 "# )K ^ A J''
        '
    Q0U..O               !-LOOK AGAIN AT COMMAND STRING: ANYTHING LEFT? !
    Z-B-1"G OLP'        !-READ ANOTHER FILESPEC IF THERE IS ONE!
    [1 FSBKILL          !-THROW AWAY BUFFER WITH CMD STRING; LOOK AT FILE NAMES!
    QE"NQ1U7'            !-IF HAVE RMAIL FILE, PUT FILE NAMES IN Q7; ELSE Q7 IS 0!
    JL .-Z"E 0UE'        !-IF THERE'S ONLY 1 FILE, DON'T DO AUTODELETE!
    Q2U..O               !-SWITCH TO FILE CONTENTS!
    ETDSK:A;FOO >   !-GIVE DEFAULT FILENAME REASONABLE VALUE!
    :I6DSK:A;B XMAIL
    )FSFNAMSYW
