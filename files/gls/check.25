
TITLE COOKIE BEAR

A=1	;ACS FOR MAIN PROGRAM
AA=2
B=3
C=4
D=5
E=6
F=7
Q=11	;ACS FOR INTERRUPT HANDLING
R=12
S=13
T=14
U=15

TYIC=1	;TTY INPUT
TYOC=2	;TTY OUTPUT
CLIC=3	;CORE LINK INTERRUPT OUTPUT
CLAC=4	;CORE LINK ANSWER INPUT


LOC 42
	JSR INT		;TO MOBY INTERRUPT HANDLER

LOC 100

GO:	.TRANDL UNTRAN
	JFCL
	.OPEN TYIC,[0,,SIXBIT \   TTYFOOBARTYI   \]
	JSP AA,KILL
	.OPEN TYOC,[1,,SIXBIT \   TTYFOOBARTYO   \]
	JSP AA,KILL
	MOVEI F,50
READ:	.IOT TYOC,[^M]	;OUTPUT CR, LF, *
	.IOT TYOC,[^J]
	.IOT TYOC,["*]
	MOVEI A,6
	SETZ C,
	SKIPA B,[440600,,C]
IN:	IDPB D,B
	.IOT TYIC,D
	JUMPE D,.-1
	CAIN D,177
	JRST READ
	CAIL D,140
	SUBI D,40
	CAIL D,40
	CAILE D,137
	JRST DONE
	SUBI D,40
	SOJGE A,IN
DONE:	MOVEM C,HACKER
	.SUSET [.SSNAM,,[SIXBIT \.MSGS.\]]
	.SUSET [.SMASK,,[400000]]
	.SUSET [.SPICL,,[-1]]
	.SUSET [.RUNAM,,MYUNAM]
	MOVEI A,40.*30.
	MOVEI B,0
TRY:	.OPEN CLIC,CLIOPN
	JSP AA,KILLP
	MOVEI AA,6
	MOVE C,[440600,,MYUNAM]
UNMOUT:	ILDB D,C
	ADDI D,40
	.IOT CLIC,D
	CAIE D,40
	SOJG AA,UNMOUT
	.RTIME E,
	MOVEI AA,6
	MOVE C,[440600,,E]
TIMOUT:	ILDB D,C
	ADDI D,40
	.IOT CLIC,D
	TRNE AA,6
	TRNN AA,1
	JRST .+2
	.IOT CLIC,[":]
	SOJG AA,TIMOUT
	.IOT CLIC,[40]
	HRR C,LMSGS(B)
	HRLI C,440700
SHOVE:	ILDB D,C
	JUMPE D,ALLOUT
	.IOT CLIC,D
	JRST SHOVE
ALLOUT:	.CLOSE CLIC,
	CAIL B,NMSGS-1
	JSP AA,KILL
	MOVE AA,A
	.SLEEP AA,
	IMULI A,6
	IDIVI A,7
	CAIGE A,5*30.
	MOVEI A,5*30.
	AOJ B,
	CAIE B,NMSGS-1
	JRST TRY
	MOVEI AA,15.*30.
	.SLEEP AA,
	JRST TRY

KILLP:	SOJLE F,KILL
	MOVEI E,5*30.
	.SLEEP E,
	JRST -2(AA)
KILL:	.LOGOUT
	.VALUE

UNTRAN:	0
	SIXBIT \HACTRN\

MYUNAM:	0			;UNAME OF THIS PARTICULAR COOKIE BEAR

CLIOPN:	1,,'CLI			;CLI OPEN SPECS
HACKER:	BLOCK 1			;UNAME OF HACKER TO PESTER
HACTRN:	SIXBIT \HACTRN\		;WE :SEND MESSAGES TO HIS HACTRN

CLAOPN:	2,,'CLA			;CLA OPEN SPECS - TO RECEIVE MESSAGES FROM HIS HACTRN

LCLABF==8
CLABUF:	BLOCK LCLABF		;BUFFER FOR CLA INPUT

COOKIE:	IRPC X,,[COOKIE]
	"X
TERMIN


INT:	0			;MOBY INTERRUPT HANDLER
	0
	.OPEN CLAC,CLAOPN
	.DISMISS INT+1
	MOVE T,[-LCLABF,,CLABUF]
	.IOT CLAC,T
	MOVE T,[440700,,CLABUF+2]
	MOVE Q,CLABUF
	CAME Q,HACKER
	JRST YANK
	MOVE Q,CLABUF+1
	CAME Q,HACTRN
	JRST YANK
	MOVNI S,6
SKPTIM:	JSP Q,GETCLA
	CAIE R,":
	JRST SKPTIM
	JSP Q,GETCLA
	CAIE R,":
	JRST YANK
FIND:	JSP Q,GETCLA
	CAME R,COOKIE+6(S)
	JRST YANK
	AOJL S,FIND
	MOVEI U,THANX-LMSGS
	EXCH U,B
	MOVEI S,TRY
	EXCH S,INT+1
	JSP Q,GETCLA
	MOVEM S,INT+1
	MOVEM U,B
YANK:	JSP Q,GETCLA
	JRST YANK

GETCLA:	CAIE T,[010700,,CLABUF+LCLABF-1]
	JRST NXTCHR
	MOVE T,[-LCLABF,,CLABUF]
	.IOT CLAC,T
	MOVE T,[440700,,CLABUF]
NXTCHR:	ILDB R,T
	CAIN R,^C
	JRST ENDINT
	CAIN R,":
	JRST (Q)
	CAILE R,"Z
	SUBI R,40
	CAIG R,"Z
	CAIGE R,"A
	JRST GETCLA
	JRST (Q)
ENDINT:	.CLOSE CLAC,
	.DISMISS INT+1


LMSGS:
IRP QUUX,,[[COOKIE?
],[WILL YOU GIVE ME A COOKIE?
],[I WANT A COOKIE. PLEASE GIVE ME A COOKIE?
],[DON'T YOU WANT TO GIVE ME A COOKIE?
],[I LIKE COOKIES! PLEASE GIVE ME A COOKIE.
],[PLEASE GIVE ME A COOKIE. ANY KIND IS OK WITH ME.
],[AREN'T YOU GOING TO GIVE ME A COOKIE?
],[I WANT A COOKIE!!!!!!
],[COOKIE COOKIE COOKIE COOKIE COOKIE!!!!!
],[YOU OLD MEANY! COME ON, GIVE ME A COOKIE!
],[COOKIE!!!!!!!!!!!!!!!!
],[YOU'D BETTER GIVE ME A COOKIE, OR ELSE!
],[YOU MISERABLE HACKER! GIVE ME A COOKIE!
],[COOKIE, COOKIE, I WANNA COOKIE!
],[YOU BETTER GIVE ME A COOKIE, OR I'LL TELL MY
UNCLE GFR ON YOU! (UNCLE GFR = GR*M F*L* R**P*R)
],[
 CCC  OOO   OOO  K  K III EEEE !
C    O   O O   O K K   I  E    !
C    O   O O   O KK    I  EEE  !
C    O   O O   O K K   I  E
 CCC  OOO   OOO  K  K III EEEE !
],[&"%$"@*^@"&$%!!!! I WANNA COOKIE!!! GIMME COOKIE!!!
],[
(OR (GIVEP YOU ME 'COOKIE)
    (COND ((ZEROP (BOOLE 1 1 (RANDOM)))
           (GFR-DESTROY (DIRECTORY-OF YOU)))
          (T (GUN YOU 'HACTRN))))
],[YOU'VE GOT ONE LAST CHANCE TO GIVE ME A COOKIE!
],[OKAY FOR YOU! KEEP YOUR CRUMMY OLD COOKIES! SELFISH!
]]
	[ASCIZ \QUUX\]
TERMIN
NMSGS==.-LMSGS

THANX:	[ASCIZ \OH THANK YOU, THANK YOU FOR THE COOKIE! GOODBYE!
\]


END GO
