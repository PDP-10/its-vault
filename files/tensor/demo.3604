
title(exp):=block(clearscreen(),disp(dpart(exp)))$
(title("A Classical Textbook Example of Differentiation"))$
X^X^X;
DIFF(%,X);
(title("Differentiating a Nested Function"))$
ERF(TAN(ACOS(LOG(X))));
DIFF(%,X);
title("Expanding and Factoring a Univariate Polynomial")$
(X-1)*(2*X-1)*(3*X-1)^7;
EXPAND(%);
FACTOR(%);
title("Factoring Cyclotomic Polynomials ")$
X^600-1;
FACTOR(%);
title("Factoring a Multivariate Polynomial")$
(Y^3-X^2)^4*(X+Y+Z)^2;
EXPAND(%);
FACTOR(%);
title(" Algebraic Simplification")$
(SQRT(R^2+A^2)+A)*(SQRT(R^2+B^2)+B)/R^2
 -(SQRT(R^2+B^2)+SQRT(R^2+A^2)+B+A)/(SQRT(R^2+B^2)+SQRT(R^2+A^2)-B-A);
RATSIMP(%);
title(" Algebraic Simplification")$
(Z^5-Y*Z^4+X*Z^4-2*Y*Z^3-6*X*Z^3+2*Y^2*Z^2+4*X*Y*Z^2-6*X^2*Z^2+Y^2*Z+
6*X*Y*Z+9*X^2*Z-Y^3-5*X*Y^2-3*X^2*Y+9*X^3)/(-Z^2+Y+3*X)^2;
RATSIMP(%);
title("Integrating a Non-Tabulated Function")$
1/(X^3+A*X^2+X);
INTEGRATE(%,X);
DIFF(%,X),RATSIMP;
title("Logarithmic Subcase of Risch's Integration Algorithm")$
(LOG(X)-1)/(LOG(X)^2-X^2);
INTEGRATE(%,X);  
title("Taylor Series")$
SIN(X+A*X^3);   
TAYLOR(%,X,0,19);
title("Taylor Series - Puiseux")$
SQRT(LOG(1+X)+SIN(X));
TAYLOR(%,X,0,17); 
title("MACSYMA to FORTRAN Conversion")$
EXP:-GAMMA^5+DELTA*GAMMA^4+2*DELTA^2*GAMMA^3-ALPHA^3*GAMMA^3-3*ALPHA^2*GAMMA^3
-3*ALPHA*GAMMA^3-GAMMA^3-2*DELTA^3*GAMMA^2+3*ALPHA^3*DELTA*GAMMA^2
+9*ALPHA^2*DELTA*GAMMA^2+9*ALPHA*DELTA*GAMMA^2+3*DELTA*GAMMA^2
+ALPHA^2*GAMMA^2-2*ALPHA*GAMMA^2+GAMMA^2-DELTA^4*GAMMA-3*ALPHA^3*DELTA^2*GAMMA
-9*ALPHA^2*DELTA^2*GAMMA-9*ALPHA*DELTA^2*GAMMA-3*DELTA^2*GAMMA
+2*ALPHA^2*DELTA*GAMMA-4*ALPHA*DELTA*GAMMA+2*DELTA*GAMMA+DELTA^5
+ALPHA^3*DELTA^3+3*ALPHA^2*DELTA^3+3*ALPHA*DELTA^3+DELTA^3+ALPHA^2*DELTA^2
-2*ALPHA*DELTA^2+DELTA^2+ALPHA^5+ALPHA^4-2*ALPHA^3-2*ALPHA^2+ALPHA+1;
FORTRAN(%);
FACTORSUM(EXP);
FORTRAN(%);
title("Bignum Arithmetic and Arbitrary Precision Floating Point Arithmetic")$
6427752177035961102167848369364650410088811975131171341205503;
%^5;
2535301200456458802993406410751;
%TH(3)/%;
SQRT(%PI),NUMER;
FPPREC:50;
BFLOAT(SQRT(%PI));
title("Vandermond's Matrix")$
MAT1:MATRIX([1,X,X^2,X^3],[1,Y,Y^2,Y^3],[1,Z,Z^2,Z^3],[1,W,W^2,W^3]);
FACTOR(DETERMINANT(MAT1));
MINOR(MAT1,4,4);
%^^-1,FACTOR;
title("Solving Algebraic Equations")$
Q^2*X^2+P^2*Q*X-P*Q*X-P^3=0;
SOLVE(%,X);
title("Applying the Cubic Formula")$
(BREAKUP:FALSE,EQ:4*X^3+A*X+10*B= 0);
H:SOLVE(EQ,X)$
FIRST_ROOT:FIRST(H);
SECOND_ROOT:PART(H,2);
THIRD_ROOT:LAST(H);
title("Solving a Set of Simultaneous Linear Equations")$
[3*A+5*B+7*C+11*D+13*E=17*R,19*A+23*B+29*C+31*D+37*E=41*S,43*A+47*B+53*C+59*D+61*E=67*T,
71*A+73*B+79*C+83*D+89*E=97*X,101*A+103*B+107*C+109*D+113*E=127*Y];
SOLVE(%,[A,B,C,D,E]);
title("Solving a Set of Simultaneous Non-Linear Equations")$
[X*Y*Z = 42,-Z+Y+X = -2,-3*Z+2*Y+3*X = -9];
SOLVE(%);
title("Finding Eigenvalues")$
MATRIX([0,6,-10,-8],[6,0,8,10],[-10,8,15*A,6],[-8,10,6,15*A]);
SOLVE(CHARPOLY(%,L),L);
