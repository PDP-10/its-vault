TITLE HACK

TYIC==1
TYOC==2
DISC==3
INC==4

DISINI:	.OPEN DISC,OUTP
	.VALUE
	POPJ P,

OUTP:	SIXBIT /  !DIS/
	SIXBIT /HACK  OUT/

DTAB:	MOVEI CH,11
DISAD:	.IOT DISC,CH
	POPJ P,

TYI:	.IOT TYIC,A
	POPJ P,

TYO:	.IOT TYOC,A
	POPJ P,

START:	.GSNAM A,
	MOVEM A,RSYSNM
	MOVEI P,PDL
	.OPEN TYIC,[SIXBIT /   TTY/]
	.VALUE
	.OPEN TYOC,[SIXBIT /  !TTY/]
	.VALUE
	CLEARM SBLKSW
	PUSHJ P,CMD
	PUSHJ P,OPNRD
	JRST BEG

OPNRD:	MOVEI A,6
	HRLM A,DNAM
	MOVE A,ISYSNM
	.WSNAM A,
	.OPEN INC,DNAM
	.VALUE
	POPJ P,

;GET COMMAND
SIF=400000
COLF=200000
NAMF=100000
NAM2F=40000
DEVF=20000
SCOLF=10000
SYSNF=4000
RCHA:	ILDB A,CMPTR
	JUMPN A,CPOPJ
RCHA1:	MOVEI A,15
	MOVE B,[10700,,CMBUF-1]
	MOVEM B,CMPTR
RCHA2:	PUSHJ P,TYO
RCHA4:	PUSHJ P,TYI
	CAIN A,177
	JRST RCHA3
	IDPB A,B
	CAIE A,15
	JRST RCHA4
	MOVEI A,0
	IDPB A,B
	JRST RCHA
RCHA3:	CAMN B,[10700,,CMBUF-1]
	JRST RCHA1
	LDB A,B
	ADD B,[70000,,]
	SKIPGE B
	SUB B,[XOR 1]
	JRST RCHA2


CMD:	SETZB FF,CMPTR
CMDB:	CLEARM SUBNM
	MOVE TT,[10600,,SUBNM-1]
CMDL:	PUSHJ P,RCHA
	CAIN A,^Q
	JRST CMDQ
	CAIGE A,40
	JRST CMDE
	CAIN A,":
	JRST CMDCOL
	CAIN A,";
	JRST CMDSC
	CAIN A,"_
	JRST CMDLA
	CAIN A,40
	JRST CMDS
	CAIN A,"(
	JRST CMDSW
CMDL1:	SUBI A,40
	PUSHJ P,SYLEND
	TLNE TT,770000
	IDPB A,TT
	JRST CMDL
CMDQ:	PUSHJ P,RCHA
	JRST CMDL1
CMDSC:	TLOA FF,SCOLF
CMDCOL:	TLO FF,COLF
CMDS:	CAMN TT,[10600,,SUBNM-1]
	JRST CMDL
	TLO FF,SIF
	MOVE C,SUBNM
	JRST CMDB
SYLEND:	TLZN FF,SIF
CPOPJ:	POPJ P,
;DROPS THRU



SYLE2:	TLZE FF,COLF
	JRST SYLDV
	TLZE FF,SCOLF
	JRST SYLSN
	TLOE FF,NAMF
	JRST NAM2
	MOVEM C,FNAM1
	POPJ P,
NAM2:	TLON FF,NAM2F
	MOVEM C,FNAM2
	POPJ P,

SYLSN:	TLO FF,SYSNF
	MOVEM C,ISYSNM
	POPJ P,
SYLDV:	TLO FF,DEVF
	LDB T,[360600,,C]
	CAIG T,'9
	CAIGE T,'0
	JRST SYLDV1
	TLNN C,7777
	MOVSI C,(SIXBIT /UT/+T)
SYLDV1:	HLRZM C,DNAM
	POPJ P,

CMDLA:	PUSHJ P,SYLE1
	HLRZS FF
	MOVE T,[DNAM,,ONAM]
	BLT T,ONAM+3
	JRST CMDB

CMDE:	PUSHJ P,SYLE1
	TLNN FF,NAMF
	JRST CMDB
	TLNE FF,NAM2F
	JRST CMDE1
	MOVSI T,(SIXBIT /@/)
	EXCH T,FNAM1
	MOVEM T,FNAM2
CMDE1:	MOVEI T,(SIXBIT /DD0/)
	TLNN FF,DEVF
	MOVEM T,DNAM
	MOVE T,RSYSNM
	TLNN FF,SYSNF
	MOVEM T,ISYSNM
	TRNN FF,SYSNF
	MOVEM T,OSYSNM
	MOVE T,DNAM
	TRNN FF,DEVF
	MOVEM T,ONAM
	TRNE FF,NAM2F
	POPJ P,
	MOVE T,FNAM1
	TLNN FF,NAM2F
	MOVE T,FNAM2
	EXCH T,ONAM+1
	TRNN FF,NAMF
	MOVSI T,(SIXBIT /BIN/)
	MOVEM T,ONAM+2
	POPJ P,

SYLE1:	CAMN TT,[10600,,SUBNM-1]
	JRST SYLEND
	MOVE C,SUBNM
	JRST SYLE2

RSYSNM:	-1
CMBUF:	BLOCK 50
DNAM:	0
FNAM1:	0
FNAM2:	0
ISYSNM:	-1
ONAM:	BLOCK 3
OSYSNM:	-1
SUBNM:	0
CMPTR:	0
UTYOP:	0
UTYOCT:	0

CMDSW:	PUSHJ P,RCHA
	CAIE A,15
	CAIN A,12
	JRST CMDL1
	CAIN A,"S
	SETOM SBLKSW
	CAIN A,")
	JRST CMDL
	JRST CMDSW


Z:	5*6100

P=1
AA=3
A=4
B=5
C=6
D=7
F=14
T=10
TT=11
FF=12
LINK=13
CH=15

SBLKSW:	0	;-1 IF SBLKS
PDL:	BLOCK 40

BEG:	MOVEI A,BUFF
	HRRZM A,RPNTR'
	HRRM A,RPBB
	MOVE A,[-20000,,BUFF]
	.IOT INC,A
	HRRZM A,RPNTR
RET:	PUSHJ P,TYI
	CAIN A,"_
	JRST UP

CNTRLH:	PUSHJ P,DISINI
	CLEARM TCKS
	SKIPL SBLKSW
	JRST AURED
	HRRZ B,RPBB
	CAIN B,BUFF
	JRST AURED
SBHR1:	PUSHJ P,@AURED
	PUSHJ P,CNTH2
	CAME A,[JRST 1]
	JRST SBHR1
	JRST CNTRL2

AURED:	PUSHJ P,RPBB
	HLRO B,(A)
	MOVNS B
	SKIPL SBLKSW
	LDB B,[(220700)A]
	MOVEM B,TC
	JRST .+2
	PUSHJ P,@AURED
	PUSHJ P,CNTH2
	SOSL TC
	JRST .-3
	SKIPGE SBLKSW
	JRST CSB1
	SETCM T,TCKS
	PUSHJ P,@AURED
	EXCH T,A
	PUSHJ P,LOSADD
	EXCH T,A
	PUSHJ P,OPT
	PUSHJ P,DTAB
	MOVE A,T
	PUSHJ P,OPT
	AOSN TCKS
	JRST CNTRL2
CSB2:	PUSHJ P,DTAB
	MOVEI CH,"_
	PUSHJ P,DISAD
CNTRL2:	MOVEI CH,15
	PUSHJ P,DISAD
	JRST RET

CSB1:	PUSHJ P,@AURED
	PUSHJ P,OPT
	EXCH A,TCKS
	PUSHJ P,DTAB
	PUSHJ P,OPT
	CAME B,TCKS
	JRST CNTRL2
	JRST CSB2
OPT:	PUSH P,A
	MOVEI F,12.
	MOVE D,[(440300+P)]
	ILDB CH,D
	ADDI CH,60
	PUSHJ P,DISAD
	SOJG F,.-3
	POP P,A
	POPJ P,


SPT:	LDB CH,[(410400)A]
	ADDI CH,60
	PUSHJ P,DISAD
	LDB CH,[(400100)A]
	LSH CH,2
	ADDI CH,60
	PUSHJ P,DISAD
	MOVEI CH,40
	PUSHJ P,DISAD
	TLZ A,740000
SPT2:	IDIVI A,50
	HRLM A+1,(P)
	JUMPE A,.+2
	PUSHJ P,SPT2
	HLRZ CH,(P)
	ADDI CH,60-1
	CAILE CH,71
	ADDI CH,101-72
	CAILE CH,132
	SUBI CH,134-44
	CAIN CH,43
	MOVEI CH,56
	JRST DISAD

CNTH2:	PUSH P,A
	PUSHJ P,OPT
	PUSHJ P,DTAB
	MOVE A,(P)
	PUSHJ P,SPT
	POP P,T
	PUSHJ P,LOSADD
CNTH3:	MOVEI CH,15
	PUSHJ P,DISAD
	MOVEI CH,12
	JRST DISAD

TCKS:	0
TC:	0

RPBB:	MOVEI A,.
	CAMN A,RPNTR
	JRST UP3
	AOS RPBB
	MOVE A,(A)
	POPJ P,

LOSADD:	SKIPGE SBLKSW
	JRST SBLK1
	JFCL 4,.+1
	ADDM T,TCKS
	JFCL 4,.+2
	POPJ P,
	AOS TCKS
	POPJ P,


SBLK1:	EXCH T,TCKS
	ROT T,1
	ADDM T,TCKS
	POPJ P,


UP:	MOVEI A,BUFF
	MOVEM A,LST1
	MOVEM A,LST2
	HRRZ D,RPBB
	SKIPL SBLKSW
	JRST UP2
UP4:	MOVE B,(A)
	CAMN B,[JRST 1]
	AOJA A,UP2
	CAML A,D
	JRST UP1
	AOJA A,UP4

UP2:	CAML A,D
	JRST UP1
	HLRO C,(A)
	MOVNS C
	SKIPL SBLKSW
	LDB C,[(220700+A)]
	MOVE B,A
	ADDI A,2(C)
	EXCH B,LST2
	MOVEM B,LST1
	JRST UP2

UP1:	MOVE A,LST1
	HRRM A,RPBB
	JRST CNTRLH

LST1:	0
LST2:	0

UP3:	MOVE P,[(,-40)PDL-1]
	JRST UP

;DDEF:	MOVEI A,BUFF
;	LDB B,[(220700+A)]
;	LDB C,[(310700+A)]
;	CAIN C,10
;	JRST DDEF1
;	CAIN C,4
;	JRST CNTRLH
;	ADDI A,2(B)
;	JRST DDEF+1
;
;DDEF1:	MOVEI T,37044	;BUFF
;	MOVE TT,T
;	ADD TT,B
;	HRLI T,1(A)
;	BLT T,(TT)
;	ADDI A,2(B)
;	MOVNS B
;	HRLZS B
;	HRRI B,37043	;BUFF-1
;	MOVEM B,DDAC+3
;	JSR THERE
;	JRST 36266	;TABL1
;
;DDEF2:	JSR HERE
;	POP P,T
;	JRST DDEF+1
;
;DDPRNT:	JSR HERE
;	MOVE CH,DDAC+5
;	ANDI CH,177
;	PUSHJ P,DISAD
;	JSR THERE
;	POPJ P,
VARIAB
CONSTANTS
BUFF:
END START
ð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒ