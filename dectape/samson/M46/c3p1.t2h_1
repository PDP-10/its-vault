TITLE C3P1 T2H AUGUST 8, 4:10 AM

A=1
B=2
C=3
D=4
E=5
F=6
G=7
H=10
I=11
J=14
M=12
N=13
P=17

CRU:	MOVEI P,PDL-1
	PUSHJ P,CRLF
U:	JRST 4,AP

AP:	SETZB 0,LMB'
	CONO PTR,20
	CONO PTP,10
	MOVEI P,PDL-1
	SETOM BC'
	SETOM NL'
	MOVNI A,2
	MOVEM A,MJP

PF:	PUSHJ P,RCH9

XP:	MOVE E,[(700)INBUF-1
	SETOM TC'
	PUSHJ P,ERF

PFR:	SETZM 1U'
	SETZM 3U'
	SETOB I,IRL'
	SETZM TLL'
	MOVEI A,1
	MOVEM A,RB'
	MOVEM A,SID'
	SETZM ROB'
	SETZM GI'
	SETZM GIS'
	SETZM AO'
	SETZM TC'
	SETZM MM'
	SETZM SS'
	MOVEM A,TBC'
	MOVEI B,26
	MOVEM B,ST'

PUM:	PUSHJ P,PUN
	JRST S1
;PRINT OFFENDING MEASURE

ERF:	SETZM BLC'
	TLZ F,1
	MOVEI A,1
	MOVEM A,ETC'

ERR:	ILDB A,E
	JUMPE A,ET1
	CAIE A,175
	CAIN A,"/
	JRST EC0
	MOVE C,A
	PUSHJ P,TYO
	JRST ETR

ET1:	AOS A,ETC
	SUB A,TC'
	JUMPE A,ET2
	CAIE A,1
	JRST ET4
	MOVEI C,">
	PUSHJ P,TYO
	TLZ F,1
	JRST ET4

ET2:	MOVEI C,"<
	PUSHJ P,TYO
	TLO F,1
ET4:	MOVE A,BLC
	CAIGE A,100
	JRST ETS
	PUSHJ P,CRLF
	SETZM BLC
	JRST ERR

ETS:	MOVEI C,40
	PUSHJ P,TYO
ETR:	AOS BLC
ETI:	JRST ERR

EC0:	MOVEI C,">
	TLZE F,1
	PUSHJ P,TYO
	MOVEI C,"/
	PUSHJ P,TYO
	JRST CRLF


EC:	POPJ P,

S1Y:
S1Z:	PUSHJ P,ER1
	ASCII /TMF
@/
	JRST TE

S1X:	PUSHJ P,ER1
	ASCII /TFF
@/
	JRST TE

S1W:	PUSHJ P,ER1
	ASCII /UNC
@/
	JRST TE

S1V:	PUSHJ P,ER1
	ASCII /ERT
@/
	JRST TE

UNCH:	PUSHJ P,ER
	ASCII /UCH
@/
	JRST S20


S3X:	PUSHJ P,CRLF
	PUSHJ P,WRITE
	ASCII /TABLE OVERFLOW.  SUBDIVIDE SOURCE PROGRAM.@/
	JRST CRU

HAP:
RRZ:	PUSHJ P,WRITE
	ASCII /
MEASURE HAS TOO MANY CHARACTERS.  REARRANGE TAPE.
@/
	JRST CRU


PUN:	MOVE A,[(NT)KT
	BLT A,KT+43
PUE:	MOVE A,[(KT)MT
	BLT A,MT+43
	POPJ P,

NOSUCH:	PUSHJ P,ER1
	ASCII /NPS
@/
	JRST PSR

RCH1:	PUSHJ P,RDCH
	CAIE A,"]
	JRST RCH1
RCH0:	PUSHJ P,RDCH
	CAIN A,"[
	JRST RCH1
	CAIN A,15
	JRST RCH0
	CAMN E,[(700)INBUFE
	JRST RRZ
	CAIE A,12
	CAIN A,40
	MOVEI A,0
	CAIN A,11
	MOVEI A,0
	CAIE A,13
	CAIN A,14
	MOVEI A,0
	IDPB A,E
	CAME A,RCHP(J)
	MOVSI J,-4
	CAMN A,RCHP(J)
	AOBJP J,RCH2
	CAIE A,"/
	JRST RCH0
RCH2:	MOVEI A,175
	IDPB A,E
	MOVE E,[(700)INBUF-1
	MOVEM E,FL1'
	MOVEM E,FL2'

RCH:	ILDB A,E
	SKIPN A
	MOVEM E,FL1
	JUMPE A,.+2
	MOVE D,FL1
	MOVEM D,FL2
	CAIE A,175
	POPJ P,
RCH9:	MOVE E,[(700)INBUF-1
	MOVSI J,-4
	JRST RCH0

RCHP:	"E
	"N
	"D
	0

ER:	TDZA F,F
ER1:	MOVNI F,1
ERC:	MOVEM E,ERCE'

ERT:	MOVN A,MJP'
	CAIGE A,2
	JRST ERQ
	PUSHJ P,WRITE
	ASCII /
[ TO ERR IS HUMAN---TO FORGIVE, DIVINE. ]
@/

ERQ:	MOVE A,TJP'
	CAME A,TC
	JRST ERK
	MOVE A,MJP
	CAMN A,BC
	JRST EC3

ERK:	MOVE E,[(700)INBUF-1
	PUSHJ P,ERF

EC3:	MOVE A,BC
	MOVEM A,MJP
	MOVE A,TC
	MOVEM A,TJP
	MOVE E,ERCE

WRITE:	POP P,B
	HRLI B,700
	SUBI B,1

WRITE1:	ILDB C,B
	CAIN C,"@
	JRST 1(B)
	PUSHJ P,TYO
	JRST WRITE1


RDCH:	CONSO PTR,10	;RETURNS CHAR. IN A
	JRST .-1
	DATAI PTR,A
	TRNE A,777400
	JRST 4,.+1
	ANDI A,177
	JUMPE A,RDCH
	CAIL A,177
	JRST RDCH
	POPJ P,

FEED:	CLEARB C,D	;CHAR. IN C
PPA:	CONSZ PTP,20	;COUNT IN B  (SUPPRESSED IF D<0 )
	JRST .-1
	DATAI A
	ANDI A,700
	CAIN A,700
	POPJ P,
	DATAO PTP,C
	JUMPL D,.+2
	SOJG B,PPA
	POPJ P,

PBW:	HRL B,A	;ARG IN A (R.H.)
	MOVNI D,3
	MOVEI C,2
	ROTC B,6
	PUSHJ P,PPA
	AOJL D,.-3
	POPJ P,


CRLF:	MOVEI C,15
	PUSHJ P,TYO
	MOVEI C,12

TYO:	CONSZ TTY,20	;CHAR. IN C
	JRST TYO
	DATAO TTY,C
	POPJ P,
;R.H. OF F

LDL=1000
GCOUNT=2000
RCOUNT=4000
COMCNT=10000

S1:	SETZB M,PSI'
	SETZB F,CHI'
	MOVEI A,40
	MOVEM A,FC'
	MOVEI A,100
	MOVEM A,FU'

S10A:	MOVEI N,0
S10:	PUSHJ P,RCH
	CAIL A,60
	CAILE A,71
	JRST S11
	AOS PSI
	TRO F,LDL
	IMULI N,10.
	ADDI N,-60(A)
	JRST S10

S11:	TRZN F,LDL
	JRST S15
	JUMPN M,.+2
	MOVEM N,NUM+1
	CAIG M,1
	MOVEM N,NUM(M)
	ADDI M,1

S15:	JUMPE A,S18
	CAIN A,"/
	JRST S18
	AOS CHI
	AOS PSI
	CAIN A,".
	JRST S19
	CAIN A,"X
	JRST S1A
	CAIN A,"R
	TRO F,RCOUNT
	CAIN A,"G
	TRO F,GCOUNT
	CAIN A,",
	TRO F,COMCNT
	JRST S10A


S19:	MOVE A,FC
	MOVEM A,FU

S1A:	MOVE A,FC
	ASH A,-1
	MOVEM A,FC
	JRST S10A

S18:	AOS TC
	MOVEM A,TRM'
	SKIPN PSI
	JRST TE
	JUMPN M,.+3
	TRNN F,COMCNT
	JRST PC
	SKIPE CHI
	JRST S1E
	SKIPE AO
	POPJ P,
	MOVE A,TBC
	CAMN A,NUM
	JRST TE
	PUSHJ P,ER
	ASCII /BBL
@/
	JRST TE

S1E:	SKIPN AO
	JRST S1H
	SETZM AO
	PUSHJ P,ER
	ASCII /AGM
@/
S1H:	MOVEM M,B
	TRNE F,COMCNT
	ADDI M,1
	TRNE F,RCOUNT
	ADDI M,1
	CAILE M,2
	JRST S1Y	;TMF
	TRNE F,COMCNT
	JRST S1S
	TRNE F,GCOUNT
	ADDI M,1
	SUBI M,2
	JUMPL M,S1X	;TFF
	JUMPN M,S1T
	TRNN F,GCOUNT

S1T:	TDZA G,G	;NUM+1 TIME
	MOVEI G,1	;G TIME
SLI:	TRNE F,RCOUNT
	JRST S1K
	MOVEI H,0
	JUMPN B,S1L
	MOVEI H,2
	JRST S1L

S1S:	MOVEI G,2	;COMMA TIME
	JRST SLI

S1K:	MOVEI H,1	;REST
	SKIPE SID'
	JRST S1L
	MOVE A,NL'
	MOVE B,NOT(A)
	ANDI B,117777
	ADDI B,400000
	MOVEM B,NOT(A)

S1L:	JUMPE G,S1N
	CAIL G,2
	JRST S1M
	MOVEI I,2
	JRST S1O

S1M:	JUMPL I,S1W
	JRST S1O

S1N:	MOVEI I,100	;CONVERT NUM+1
	MOVEI B,1
	MOVE C,NUM+1
	JUMPE C,S1V
	CAIL C,100
	JRST S1V

S1Q:	ASH C,-1
	JUMPE C,S1P
	ASH I,-1
	ASH B,1
	MOVE A,FU
	ASH A,-1
	MOVEM A,FU
	JRST S1Q


S1P:	CAME B,NUM+1
	JRST S1V
	SKIPE FU
	JRST S1O
	PUSHJ P,ER
	ASCII /DTU
@/
S1O:	MOVE A,I
	ASH A,-1
	MOVEM A,FC
	JRST S2

S2:	SETZM FU
	SETZM SR'
	SETZM 3I'
	SETZM SI'
	SETZM ACI'
	SETZM ACC'
	SETZM ET'
	SETZM ETE'
	MOVE E,FL2'
	MOVEM E,FL1'

S20:	PUSHJ P,RCH
	CAIL A,140
	JRST UNCH
	MOVE B,ACI'

S21:	JRA A,@S21T(A)

S21T:	S2R
	UNCH
	UNCH
	UNCH
	S2R
	UNCH
	UNCH
	UNCH
	UNCH
	S2R
	S2R
	S2R
	S2R
	S2R
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH

	S2R
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	UNCH
	S2H
	S2J
	UNCH
	S2H
	S20
	S2I
	S2P
	S2R
	S20
	S20
	S20
	S20
	S20
	S20
	S20
	S20
	S20
	S20
	UNCH
	UNCH
	UNCH
	S2J
	UNCH
	UNCH

	UNCH
	S2E
	S2F
	S2G
	S2K
	S2D
	UNCH
	S20
	2SS
	UNCH
	UNCH
	UNCH
	S2C
	S2L
	S2M
	UNCH
	S38
	2SR
	S20
	S2B
	S20
	S2N
	UNCH
	S2O
	S2Q
	UNCH
	UNCH
	UNCH
	UNCH
	S20
	UNCH
	UNCH


S2B:	SUBI A,200000
S2C:	ADDI A,360000
2SR:	SUBI A,20000
2SS:	ADDI A,40000
S2D:	MOVEM A,SV
S2A:	AOS SI
	JRST S20

S2E:	ADDI A,30
S2F:	SUBI A,14
	ADDM A,SR'
	JRST S20

S2G:	MOVEI A,100000
	MOVEM A,3I
	JRST S20

S2H:	MOVEI A,2
S2I:	SUBI A,1
	JUMPL B,S24
	ADDM A,ACC'
	MOVMM A,ACI
	JRST S20

S24:	PUSHJ P,ER
	ASCII /NOR
@/
	JRST S20

S2J:	JUMPG B,S26
S27:	SETOM ACI
	JRST S20

S26:	SETOM ACI
	JRST S24

S38:	ADDI A,1
S2O:	ADDI A,1
S2N:	ADDI A,1
S2M:	ADDI A,1
S2L:	ADDI A,1
S2K:	ADDI A,1
S28:	MOVEM A,ET
	SKIPN ETE
	JRST S29
	PUSHJ P,ER
	ASCII /TME
@/
	MOVE A,ET

S29:	MOVEM A,ETE
	JRST S20
S2P:	MOVE A,FC
	ADDM A,FU
S2Q:	MOVE A,FC
	ASH A,-1
	MOVEM A,FC
	JRST S20
;SECOND SCAN COMPLETED

S2R:	MOVE A,SI
	JUMPE A,S51
	CAIG A,1
	JRST S52
	PUSHJ P,ER
	ASCII /TMS
@/

S51:	MOVE A,SS'	;SET NORMAL ARTIC. MODE
	MOVEM A,SV'

S52:	TRNE F,COMCNT
	JRST S69	;IF COMMA
	MOVE A,3I
	MOVEM A,CCC'	;SET TRIPLET INDIC.

2S3:	MOVE A,I
	ADD A,FU
	MOVEM A,NFT'	;TIME PART
	MOVEM A,NFP'
	TRNE F,GCOUNT
	JRST 2S1	;GRACE
	SETZM GI
	MOVE A,ROB
	JUMPE A,S2S
	MOVNS A
	ADDB A,NFT
	CAIGE A,2
	JRST 2S2

22S:	SETZM ROB
	JRST S2S

2S2:	SUB A,ROB
	JUMPGE A,22S
	MOVE A,NLS'
	MOVEM A,NL'
	SETZM ROB
	PUSHJ P,ER
	ASCII /ITG
@/
	JRST 2S3

2S1:	ADDM I,ROB
	MOVEI A,1
	MOVEM A,GI
	JRST S2S

S69:	MOVE A,NFP
	MOVEM A,NFT
	MOVE B,CCC
	SKIPN 3I
	MOVEM B,3I

S2T:	SKIPN FU
	JRST 2S4
	PUSHJ P,ER
	ASCII /TIC
@/
2S4:	TRNE F,GCOUNT
	JRST 2S1
	SETZM GI

S2S:	JUMPE H,S2U	;IF PITCH IS NUM
	CAILE H,1
	JRST SV2	;IF COMMA
	MOVE A,ACI	;IF REST
	ADD A,ETE
	JUMPE A,S2W
	PUSHJ P,ER
	ASCII /AIR
@/

S2W:	MOVEI A,200
	MOVEM A,TNE'
	JRST S70

S2U:	MOVE A,NUM
	ADD A,SR
	ADD A,ST
	MOVEM A,TON'
	JUMPL A,S40
	CAIGE A,44
	JRST S2V

S40:	PUSHJ P,ER
	ASCII /UAT
@/
	JRST S2W

SV2:	MOVE A,TNE'
	CAIN A,200
	JRST S70

S2V:	MOVE A,TON
	SKIPN ACI
	JRST S31
	MOVE B,NT(A)	;APPLY ACCIDENTAL
	ADD B,ACC'
	CAIGE B,2
	JRST S41
	CAIGE B,77
	JRST S42

S41:	PUSHJ P,ER
	ASCII /AOR
@/
	JRST S2W
S42:	MOVEM B,MT(A)
S31:	MOVE B,MT(A)
	ADD B,TLL
	CAIL B,2
	CAIL B,77
	JRST S40
	ASH B,7
	MOVEM B,TNE

S30:	SKIPN ETE
	JRST S70
	SKIPN 3I	;EMBELLISH
	JRST S71

S72:	PUSHJ P,ER
	ASCII /ETR
@/
	JRST S70

S71:	MOVE A,ETE
	MOVE A,EBL-1(A)
	SUB A,NFT
	MOVNS A

S99:	MOVEM A,EX'
	JUMPGE A,S73

S79:	PUSHJ P,ER
	ASCII /EIT
@/
	JRST S70
S73:	MOVE A,TON
	MOVE B,MT+1(A)
	ADD B,TLL
	CAIL B,2
	CAILE B,76
	JRST S39
	ASH B,7
	MOVEM B,TNF'
	MOVE B,MT-1(A)
	ADD B,TLL
	CAIL B,2
	CAILE B,76
	JRST S39
	ASH B,7
	MOVEM B,TND'
	MOVE A,ETE
	JRST @EBD-1(A)

EBL:	6	;D
	4	;M
	10	;N
	10	;U
	4	;W
	5	;P

EBD:	S81
	S82
	S83
	S84
	S85
	S86

S81:	MOVE B,TNE
	ADDI B,2
	PUSHJ P,CN
	MOVE B,TND
	ADDI B,2
	PUSHJ P,CN
	JRST S75

S82:	TDZA A,A
S83:	MOVEI A,4
	MOVEM A,CUT'
S76:	MOVE A,NFT
	TRNE A,3
	JRST S79
	MOVEM A,C


S77:	CAMG C,CUT
	JRST S78
	MOVEI B,2
	ADD B,TNF
	PUSHJ P,CN
	MOVEI B,2
	ADD B,TNE
	PUSHJ P,CN
	MOVEI B,4
	ADDM B,CUT
	JRST S77

S84:	MOVEI B,400002
	ADD B,TNF
	PUSHJ P,CN
	MOVEI B,400002
	ADD B,TNE
	PUSHJ P,CN
	MOVEI B,400002
	ADD B,TND
	PUSHJ P,CN
	JRST S75

S85:	MOVEI B,2
	ADD B,TNE
	PUSHJ P,CN
	JRST S83

S86:	MOVEI B,1
	ADD B,TNF
	PUSHJ P,CN
	MOVEI B,1
	ADD B,TNE
	PUSHJ P,CN
	MOVEI B,1
	ADD B,TNF
	PUSHJ P,CN

S75:	MOVE B,EX
	ADDI B,2
	ADD B,TNE
	ADD B,SV
	PUSHJ P,CN
	JRST S32


S78:	MOVE A,ETE
	JRST @EBE-1(A)

EBE:	S32
	S32
	S93
	S32
	S95
	S32

S93:	MOVEI B,2
	ADD B,TND
	PUSHJ P,CN
	MOVEI B,2
	ADD B,TNE
	PUSHJ P,CN
	JRST S32

S95:	MOVEI B,2
	ADD B,TNF
	PUSHJ P,CN
	JRST S32

S39:	PUSHJ P,ER
	ASCII /EOR
@/

S70:	MOVE B,NFT
	ADD B,TNE
	ADD B,SV
	ADD B,3I
	PUSHJ P,CN
	SKIPN GI
	JRST 2SA
	SKIPE GIS
	JRST 2SB
	MOVNI A,1
	ADD A,NL
	MOVEM A,NLS'
	AOSA GIS

2SA:	SETZM GIS
S32:
2SB:	MOVE A,NFT
	SKIPN 3I
	ASH A,1
	ADD A,NFT

S33:	MOVEM A,TU'
	ADDM A,MM
	MOVE A,SI
	MOVEM A,SID

PSR:	SETZM AO

TE:	SKIPN TRM
	JUMPE S1
	MOVEI A,1
	MOVEM A,SID
	SETZM TC
	MOVE A,3U
	ASH A,1
	CAMN A,MM
	JRST TE1
	SETZM TJP
	CAML A,MM
	JRST TE9
	PUSHJ P,ER
	ASCII /MTL
@/
	JRST TE1

TE9:	PUSHJ P,ER
	ASCII /MTS
@/

TE1:	SKIPN ROB
	JRST TE0
	PUSHJ P,ER
	ASCII /ITG
@/
	SETZM ROB
	SETZM GI
	SETZM GIS
	MOVE A,NLS
	MOVEM A,NL

TE0:	MOVEI B,600000
	PUSHJ P,CN

TEB:	PUSHJ P,SBC
	MOVE B,LMB'
	MOVEM B,BAR(A)
	MOVEI A,1
	ADD A,NL
	MOVEM A,LMB
	SETZM MM
	MOVEM E,MBH'
	SETZM AO
	PUSHJ P,PUE
	JRST S1
;LOOK UP PSEUDO

PC:	SKIPN AO
	JRST PC01
	SETZM AO
	PUSHJ P,ER
	ASCII /AGM
@/
PC01:	MOVE E,FL2
	MOVEM E,FL1
	MOVEI B,0

PC0:	PUSHJ P,RCH
	JUMPE A,PC1
	CAIN A,"/
	JRST PC1
	SUBI A,40
	ROT A,-6
	ROTC A,6
	JRST PC0

PC1:	HRLZI A,-NPI
	CAME B,PNM(A)
	AOBJN A,.-1
	MOVEI B,0
	JRST @PCD(A)

PNM:	SIXBIT /     S/
	SIXBIT /     L/
	SIXBIT /     E/
	SIXBIT /   END/
	SIXBIT /  BASS/
	SIXBIT /TREBLE/
	SIXBIT / TENOR/
	SIXBIT /  ALTO/
	SIXBIT / UNITS/
	SIXBIT /   KEY/
	SIXBIT /  REST/
	SIXBIT /  COPY/
	SIXBIT /    UP/
	SIXBIT /  DOWN/
	SIXBIT /     H/
	SIXBIT /     Q/
	SIXBIT / TEMPO/
	SIXBIT /  BEAT/


PCD:	PV1
	PV2
	PV3
	PV4
	PV5
	PV6
	PV7
	PV8
	PV9
	PVA
	PVB
	PVC
	PVD
	PVE
	PVF
	PVG
	PVH
	PVI
	NOSUCH


PV2:	ADDI B,200000	;L
PV1:	ADDI B,140000	;S
PVF:	ADDI B,20000	;H
PVG:	ADDI B,20000	;Q
PV3:	MOVEM B,SS	;E
	JRST PSR

PV6:	ADDI B,6	;TREBLE
PV8:	ADDI B,2	;ALTO
PV7:	ADDI B,4	;TENOR
PV5:	ADDI B,12	;BASS
	MOVEM B,ST
	JRST PSR

PV9:	MOVEI A,1	;UNITS
	MOVEM A,AO
	PUSHJ P,TE
	MOVE A,NUM
	MOVEM A,1U
	IMULI A,3
	MOVEM A,3U
	SETOM IRL
	JRST PSR

PV4:	PUSHJ P,SBC	;END
	MOVEI B,600000
	MOVEM B,BAR(A)
	MOVEI B,400
	PUSHJ P,FEED
	AOS A,NL
	PUSHJ P,PBW
	SETZB E,F
	JRST P410

P41:	MOVE A,NOT(E)
	ADD F,A
	CAIL F,-1
	SUBI F,-1
	PUSHJ P,PBW
	ADDI E,1
P410:	CAMGE E,NL
	JRST P41
	MOVE A,F
	PUSHJ P,PBW
	MOVEI B,6
	PUSHJ P,FEED
	AOS A,BC
	PUSHJ P,PBW
	SETZB E,F


P42:	MOVN D,E
	MOVE A,BAR(D)
	ADD F,A
	CAIL F,-1
	SUBI F,-1
	PUSHJ P,PBW
	ADDI E,1
	CAME E,BC
	JRST P42
	MOVE A,F
	PUSHJ P,PBW
	MOVEI B,300
	PUSHJ P,FEED
	JRST U


PVH:	AOS AO	;TEMPO
	PUSHJ P,TE
	MOVE B,NUM
	ANDI B,37777
	ADDI B,700000
	PUSHJ P,CN
	JRST PSR

PVA:	PUSHJ P,RCH	;KEY
	JUMPE A,PVA
	MOVEI B,1
	CAIE A,")
	CAIN A,"=
	JRST PUM
	CAIE A,"(
	CAIN A,"+
	JRST PUS
	CAIN A,"-
	JRST PUF
	PUSHJ P,ER
	ASCII /MYK
@/
	JRST S1

PUS:	MOVEM B,AO
	PUSHJ P,S1
	PUSHJ P,PUN

PUT:	MOVEI A,1
	MOVEI B,4
	MOVEI C,3
	JRST PUW

PUF:	MOVEM B,AO
	PUSHJ P,S1
	PUSHJ P,PUN

PUG:	MOVNI A,1
	MOVEI B,3
	MOVEI C,6


PUW:	MOVEM A,ACC
	MOVEM B,ACI
PUW1:	MOVEM C,TNE
	SKIPN NUM
	JRST PW2
	MOVE D,TNE

PW1:	MOVE A,NT(D)
	ADD A,ACC
	MOVEM A,KT(D)
	ADDI D,7
	CAIGE D,44
	JRST PW1
	SOS NUM
	MOVE C,TNE
	ADD C,B
	CAIL C,7
	SUBI C,7
	JRST PUW1

PW2:	PUSHJ P,PUE
	JRST PSR

PVD:	AOS AO	;UP
	PUSHJ P,TE
	MOVE A,NUM
	MOVEM A,TLL
	JRST PSR

PVE:	AOS AO	;DOWN
	MOVEM A,AO
	PUSHJ P,TE
	MOVN A,NUM
	MOVEM A,TLL
	JRST PSR

PVB:	AOS AO	;REST
	SKIPN MM
	JRST PB2
	PUSHJ P,ER1
	ASCII /ILR
@/
	JRST I1A

PB2:	PUSHJ P,TE
PB1:	SKIPN NUM
	JRST PSR
	SKIPL IRL
	JRST PB3
	PUSHJ P,SNL
	MOVE B,1U
	ADDI B,100
	ASH B,1
	MOVEM B,NOT(A)
	MOVEM A,IRL
	PUSHJ P,SNL
	MOVEI B,600000
	MOVEM B,NOT(A)
	ADDI A,1
	MOVEM A,LMB

PB3:	PUSHJ P,SBC
	MOVE B,IRL
	MOVEM B,BAR(A)
	SOSE NUM
	JRST PB3
	JRST PSR


PVC:	MOVEI A,2	;COPY
	ADDM A,AO
	SKIPN MM
	JRST COW
	PUSHJ P,ER1
	ASCII /ILC
@/
I2A:	PUSHJ P,TE
I1A:
CO1:	PUSHJ P,TE
	JRST PSR


COW:	PUSHJ P,TE
CO2:	MOVE A,NUM
	MOVEM A,CBH
	JUMPE A,CO3
	CAMGE A,TBC
	JRST CO4

CO3:	PUSHJ P,ER1
	ASCII /BLC
@/
CO4:	PUSHJ P,TE
CO5:	MOVE A,NUM
	CAML A,CBH'
	JRST CO7

CO6:	PUSHJ P,ER1
	ASCII /BRC
@/
	JRST PSR

CO7:	SOS A,CBH
CO8:	MOVNS A
	MOVE B,BAR(A)
	PUSHJ P,SBC
	MOVEM B,BAR(A)
	AOS A,CBH
	CAMGE A,NUM
	JRST CO8
	JRST PSR


PVI:	MOVEI A,2	;BEAT
	ADDM A,AO
	PUSHJ P,TE
	MOVE A,NUM
	MOVEM A,BEAT1'
	PUSHJ P,TE
	MOVE A,BEAT1'
	JUMPE A,.+3
	TRZN A,-200
	JRST PVI1
	PUSHJ P,ER1
	ASCII /BLB
@/
	JRST PSR

PVI1:	EXCH A,NUM
	JUMPE A,.+3
	TRZN A,-100
	JRST PVI2
	PUSHJ P,ER1
	ASCII /BRB
@/
	JRST PSR

PVI2:	ASH A,7
	ADD A,NUM
	MOVEI B,740000(A)
	PUSHJ P,CN
	JRST PSR

SBC:	AOS TBC
	AOS A,BC
	ADD A,NL
	CAIL A,ALL
	JRST S3X
	MOVN A,BC
	POPJ P,

SNL:	AOS A,NL
	ADD A,BC
	CAIL A,ALL
	JRST S3X
	MOVE A,NL
	POPJ P,


CN:	MOVEM B,NF'
	PUSHJ P,SNL
	MOVEM B,NOT(A)
	POPJ P,

NUM:	BLOCK 2

MT:	BLOCK 44
KT:	BLOCK 44
NT:	2
	4
	6
	7
	11
	13
	15
	16
	20
	22
	23
	25
	27
	31
	32
	34
	36
	37
	41
	43
	45
	46
	50
	52
	53
	55
	57
	61
	62
	64
	66
	67
	71
	73
	75
	76

PDL:	BLOCK 20
INBUF:	BLOCK 40
INBUFE:

VARIAB
CONSTA

NOT:
BAR=24000
ALL=BAR-NOT-1
NPI=PCD-PNM

END CRU
ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü 