TITLE C41   PDP-6 SYNTAX-DIRECTED COMPILER  P. SAMSON

EQUALS TAPE,NULL
EQUALS SUBTTL,TITLE
SUBTTL PART 1, MAR 12, 1965

;PDP-6 SYNTAX-DIRECTED COMPILER, P. SAMSON




Q=1
I=2
J=3
K=4
G=5
S=6
TS=7
R=10
PS=11
V=12
AL=13
T=14
C=15
P=17

INVSPC=150
INVTAB=151
INVCAR=152
INVLIF=153
NUMSGN=140
PCTSGN=45
DOLSGN=141
LEFTBR=142
RITEBR=143
PRIME=144
EXCLAM=145
BKSLSH=146
LEFTAR=147
ENDSYM=176
DIGIT0=60
DIGIT9=71
BELL=7
LETERA=101
LETERZ=132
LEFTSQ=133
RITESQ=135
LESSTN=74
GREATN=76
SPACE=40
DQUOTE=42
XOFF=23
RUBOUT=177
FORMF=14
LINEF=12
CARRET=15
QMARK=77

GOAL=177


MMV=300
NUNIV=16
PDLL=1000
LLENTH=106
LLENTG=16
PAGECT=70
LINECT=164
MUNIVS=100
NUNIVC=MUNIVS*5-1
NUNIVS=4
NUPP=10

TTY=120
PTR=104
PTP=100
ANELEX=124
CR=114
PRS=4
APRCHN=6

Q2=400000
Q3=200000
Q4=100000
Q5=40000
Q6=20000

TV:	JRST START
	JRST GLOB
	JRST GLOB2
	JRST KILLR
	JRST KILLRR
	JRST GLOB3


PASSN3:	CAIE I,10
	JRST ,PASSON
	CLEARM ,UNIV+4

UNGLB0:	CLEARM ,FFES
	CLEARM AFES
	MOVE T,UNIV+3
	CAIN T,1
	SETOM ,FFES
	CAIN T,2
	SETOM AFES

GLOB:	MOVE P,GLOBC3
	PUSHJ P,FORKS
	PUSHJ P,GULP
	SKIPE ,PASS1
	JRST ,PASS10

GLOB1:	MOVE P,GLOBC3
	PUSHJ P,FORKS
	MOVE J,GLOBC1
	LDB T,J
	HRRZ I,TRAN(T)
	JUMPE I,O1
	HRLI I,GOAL
	HRLI T,10
	HRRI T,R
	HRLZI K,400000
	CLEARM ,DEPTH
	JRST ,DIAGR

PASS10:	PUSHJ P,COPA
	JRST GLOB


O0:	DATAI 0,T
	TLNE T,200000
	JRST ,O00
	HRRZM J,PTRSX
	MOVEM J,PTRSY
O01:	ILDB C,J
	CAMN J,GULPT
	JRST ,O00
	CAIN C,QMARK
	JRST ,O01
	CAIL C,40
	CAILE C,140
	JRST ,O01
	PUSHJ P,COMMQ1
	PUSHJ P,COPA
	PUSHJ P,COMMQ1
	MOVE T,PTRSX
	SUBI T,INPUT
	IDIVI T,LLENTG
	MOVEM T,PTRSZ
	IMULI T,LLENTG
	EXCH T,PTRSY
	SUBM T,PTRSY
	MOVEI C,SPACE
	IBP ,PTRSY
	SKIPA S,GLOBC1

O11:	IBP ,S
	CAMN S,PTRSY
	JRST ,O12
	PUSHJ P,(Q)
	JRST ,O11

O12:	MOVEI C,136
	PUSHJ P,(Q)
	MOVE C,PTRSZ
	ADDI C,DIGIT0+1
	CAIE C,DIGIT0+1
	PUSHJ P,(Q)
	PUSHJ P,CRLFP1


O00:	HRLI R,1
	MOVE V,GLOBC2
	MOVE TS,OTP(R)
	TLO TS,350700
	CLEARB PS,T
	MOVE AL,ALPHA
	MOVEI J,1
	MOVEM J,Q1
	SETZM ,F26IS
	SETZM ,F27IS
	SETZM ,F34IS
	PUSHJ P,TRANS1
	HRRZM AL,ALPHA

UNGLOB:	MOVE T,UNIV+4
	JUMPE T,UNGLB0
	CAIN T,1
	JRST ,FINISH


PASSON:	MOVEI I,1
PASSN2:	PUSHJ P,INCH
	PUSHJ P,OUCH1
	CAIN C,SPACE
	JRST ,PASSN2
	CAIE C,CARRET
	CAIN C,LINEF
	JRST ,PASSN3
	CAME C,PASSNC(I)
	SETOI I,
	AOJA I,PASSN2


ALPHA:	OCT 1750          ;R.I.P. J.S.B.

PASSNC:	OCT 40
	OCT 103
	OCT 117
	OCT 115
	OCT 120
	OCT 111
	OCT 114
	OCT 105
	OCT 40

FFES:	OCT 0
AFES:	0
ASSMS:	OCT 0
TYOS:	OCT 0

PTRSX:	OCT 0
PTRSY:	OCT 0
PTRSZ:	OCT 0
PASS1:	OCT 0


GLOB2A:	SETOM, READS
	JRST ,GLOB2B

GLOB2:	MOVE P,GLOBC3
	PUSHJ P,FORKS
GLOB2B:	MOVNI J,1
	PUSHJ P,GULPS
	JRST FINIS1

KILLRR:	TROA S,1
KILLR:	MOVEI S,0
	CLEARB T,NMV
	MOVE P,GLOBC3
	PUSHJ P,INOUTI
	HRLZI Q,-200
	CLEARM ,SUCCR1
	MOVE C,KILLC1
	BLT C,SUCCRT-1
	HRRM T,TRAN(Q)
	AOBJN Q,.-1
	MOVEI T,FREE
	MOVEM T,RFRE
	JUMPN S,GLOB2A
	CONO PRS,400
	JRST 4,GLOB2

O1:	MOVEI J,1
	PUSHJ P,COMMAQ
	MOVEI C,136
	PUSHJ P,OUCHB
	PUSHJ P,OUCHB
	PUSHJ P,OUCHB
	PUSHJ P,CRLF
	JRST ,UNGLOB

O2:	JRST 4,GLOB

GLOBC0:	XWD 010700,INPUT-1
GLOBC1:	XWD 350700,INPUT
GLOBC2:	XWD 350700,OTPX
GLOBC3:	XWD -PDLL,PDL
GLOBC4:	XWD 010700,OTPX-1
Q1:	OCT 0
KILLC1:	XWD SUCCR1,SUCCR1+1


FINISH:	MOVEI T,440
	SKIPN ,PUNCHS
	SKIPE ,ASSMS
	PUSHJ P,FEED
FINIS1:	SKIPE ,TYOS
	PUSHJ P,CRLF
	CONO PRS,400
	DATAI 0,FINISC
	SKIPGE ,FINISC
	PUSHJ P,STATS
	PUSHJ P,LISTFE
	JRST 4,START

FINISC:	OCT 0


FORKS:	DATAI 0,T
	CLEARM ,ASSMS
	TRNE T,4
	SETOM ,ASSMS
	CLEARM ,TYOS
	TRNE T,2
	SETOM ,TYOS
	CLEARM ,LISTS
	TRNE T,1
	SETOM ,LISTS
	SETOM ,PUNCHS
	SKIPN ,ASSMS
	TRNN T,10
	CLEARM ,PUNCHS
	CLEARM ,READS
	TRNN T,60
	SETOM ,READS
	CLEARM ,CARDS
	TRNE T,40
	SETOM ,CARDS
	CLEARM ,SPEAKS
	TRNE T,1
	SETOM ,SPEAKS
	TRNN T,2
	JRST ,FORKSR
	TRNN T,20
	SETOM ,SPEAKS

FORKSR:	ANDI T,77
	MOVEM T,FORKSM
	POPJ P,

FORKSM:	OCT 0


START:	MOVE P,GLOBC3
	PUSHJ P,INOUTI
	MOVEI T,1750
	MOVEM T,ALPHA
	CLEARM ,UNIV+4
	CLEARM DEPTHR
	HRRZ I,TRAN+GOAL
	PUSHJ P,FORKS
	MOVEI T,400
	SKIPE ,PUNCHS
	PUSHJ P,FEED
	SKIPA P,GLOBC3

START2:	HRRZ I,(I)

START3:	JUMPE I,UNGLOB
	HLRZ T,(I)
	CAIE T,GOAL
	JRST ,START2
	HLRZ T,1(I)
	JUMPN T,START2
	HRRZ T,1(I)
	MOVEM T,OTP
	MOVEI R,0
	JRST ,O00


DING:	MOVEI C,BELL
	JRST ,OUCH

DINGE:	MOVEI C,BELL
	JRST ,OUCHE


PRINMV:	MOVEI T,0
	CAIN G,1
	JRST ,PMV1+1
	CAIL G,177
	JRST ,PMV1
	MOVEI C,DQUOTE
	CAIN G,LESSTN
	PUSHJ P,OUCHQ
	CAIE G,DQUOTE
	CAIN G,LEFTSQ
	PUSHJ P,OUCHQ
	MOVE C,G
	JRST ,OUCHQ

PMV1:	MOVE T,NAME-177(G)
	MOVEI C,LESSTN

PMV1A:	PUSHJ P,OUCHQ
	JUMPE T,PMV2
	MOVEI C,0
	ROTC T,6
	JUMPE C,.-1
	ADDI C,40
	JRST ,PMV1A

PMV2:	MOVEI C,GREATN
	JRST ,OUCHQ


GLOB3:	MOVE P,GLOBC3
	PUSHJ P,INOUTI
	CLEARB G,G3Y
	PUSHJ P,FORKS
	TRNE T,2
	CLEARM ,PUNCHS
	CLEARM ,ASSMS
	PUSHJ P,LISTRS
	MOVEI T,100
	PUSHJ P,FEED

G3AA1:	MOVNI Q,1
	JRST ,G3AA


G3ZZ:	AOS ,OTPX(R)
	MOVE Q,R
	MOVE G,G3Y

G3AA:	HRRZ I,TRAN(G)
	JUMPE I,G3BB
	MOVNI R,1
	CAIE G,LEFTSQ
	CAIN G,LESSTN
	JRST ,G3AB
	MOVEI C,SPACE
	CAIL G,177
	JRST ,G3AB1
	CAIL G,41
	CAIL G,140
	MOVEI C,DQUOTE
G3AB1:	PUSHJ P,OUCHQ

G3AB:	PUSHJ P,PRINMV
G3AY:	HLRZ G,(I)
	JUMPE G,G3CC

G3AZ:	HRRZ T,(I)
	JUMPE T,G3AC
	CAMGE R,Q
	AOJA R,G3AD
	AOS Q,R
	CLEARM ,OTPX(R)

G3AD:	SKIPN ,OTPX(R)
G3AC:	AOJA I,G3AB
	MOVE I,T
	JRST ,G3AY


G3CC:	MOVEI C,LEFTSQ
	PUSHJ P,OUCHQ
	MOVEI S,0
	HRRZ K,(I)
	TLO K,350700
	JRST 2,@.+1
	XWD 20000,G3CA

G3CA:	LDBI G,K
	JUMPE G,G3CA
	CAIN G,DQUOTE
	JRST ,G3CAB
	CAIN G,LEFTBR
	AOJA S,G3CB
	CAIN G,RITEBR
	SOJA S,G3CB
	LDB T,G3C1
G3CAB:	MOVEI C,DQUOTE
	CAME G,T
	PUSHJ P,OUCHQ

G3CB:	LDB C,G3C2
	PUSHJ P,OUCHQ
	JUMPGE S,G3CA
	PUSHJ P,CRLF
	PUSHJ P,OUCHQA
	JUMPL R,G3BB
	SKIPN ,OTPX(R)
	JRST ,G3ZZ
	SOJGE R,.-2

G3BB:	MOVEI G,1
	ADDB G,G3Y
	MOVE T,NMV
	CAIG G,177(T)
	JRST ,G3AA1
	PUSHJ P,LISTRS
	MOVEI C,XOFF
	PUSHJ P,OUCH
	MOVEI T,300
	PUSHJ P,FEED
	JRST 4,GLOB


G3Y:	OCT 0
G3C1:	XWD G+220700,TRAN
G3C2:	XWD G+330700,TRAN


DIAGR:	PUSH P,T
	PUSH P,I
	PUSH P,J
	AOBJN P,.+1
	PUSH P,I
	AOS T,DEPTH
	CAMLE T,DEPTHR
	MOVEM T,DEPTHR
	HRLM K,(P)

G0:	HLRZ T,1(I)
	JUMPE T,T1
	HRRM I,-3(P)
	HLR T,-4(P)
	TRNN T,10
	JRST ,G1

P1:	ADDI I,1
	HRRZ T,(I)
	JUMPN T,G10
	HLRZ T,1(I)
	JUMPN T,P1
	HLRZ G,-3(P)
	HLRZ T,(I)
	CAMN T,G
	JRST ,G10A
	LDB S,SUCCR(T)
	JUMPN S,G10A
	MOVE I,-3(P)
	JRST, T1

G10A:	HRLOI T,777767
	ANDM T,-4(P)

G10:	MOVE I,-3(P)

G1:	LDBI T,J
	HLRZ G,(I)
	CAIGE G,177
	JRST ,H1
	LDB S,SUCCR(T)
	JUMPE S,H1
	HLL I,(I)
	HRR I,TRAN(T)
	MOVEI T,-1(P)
	TLO T,11
	JRST,DIAGR

DEPTH:	OCT 0
DEPTHR:	OCT 0


H10:	HLRZ G,(I)
	LDB T,J

H1:	CAME T,G
	JRST ,H3

H2:	MOVE I,-3(P)
	MOVE T,-4(P)
	TLZ T,777767
	TLO T,2
	AOJA I,DIAGR


H3:	MOVE J,-2(P)
	HLR K,(P)

T1:	HRRZ I,(I)
	JUMPN I,T1A

T2:	HRR I,(P)

T4:	HLRZ T,1(I)
	JUMPN T,T3


Q0:	HRR T,1(I)
	HRRZM T,-1(P)
	HLRZ T,(I)
	HLRZ G,-3(P)
	CAME T,G
	JRST ,F1
	HRRZ S,-4(P)
	MOVEM K,(S)

F1:	LDB S,SUCCR(T)
	JUMPE S,F2
	HRRM I,-3(P)
	HRR I,TRAN(T)
	HLL I,-3(P)
	HRRZ T,-4(P)
	TLO T,13
	JRST ,DIAGR


T1A:	HRLZI T,10
	IORM T,-4(P)
	JRST ,G0

F20:	HLRZ G,-3(P)
	HLRZ T,(I)

F2:	CAME T,G
	JRST ,T3

DQ1:	MOVE T,-1(P)
	MOVEM T,OTP(K)
	HLR T,-4(P)
	ANDI T,7
	SUB P,DQ1C1
	SOS ,DEPTH
	CAML K,DQ1C2
	PUSHJ P,OFLOY
	AOJA K,@D0(T)

DQ1C1:	XWD 5,5
DQ1C2:	XWD 400000,OTPE-OTP


T3:	HRRZ I,(I)
	JUMPN I,T4
	MOVE J,-2(P)
	HLR K,(P)
	HLR T,-4(P)
	ANDI T,7
	SUB P,DQ1C1
	HRRZ I,-3(P)
	SOS ,DEPTH
	JRST ,@D1(T)


D0:	XWD 0,O0
	XWD 0,H2
	XWD 0,DQ1
	XWD 0,DQ1
	XWD 0,O2
	XWD 0,O2
	XWD 0,O2
	XWD 0,O2

D1:	XWD 0,O1
	XWD 0,H10
	XWD 0,H3
	XWD 0,F20
	XWD 0,O2
	XWD 0,O2
	XWD 0,O2
	XWD 0,O2


SUBTTL PART 2, MAR 12, 1965

;PDP-6 SYNTAX-DIRECTED COMPILER  P. SAMSON

TRANS:	MOVE T,-1(P)
TRANS1:	PUSH P,TS
	PUSH P,TS
	PUSH P,R
	PUSH P,PS
	HRLZS ,(P)
	PUSH P,V
	PUSH P,C
	PUSH P,T

TRO0:	LDB T,-5(P)
	JUMPE T,TRO2
	CAIN T,NUMSGN
	JRST, TRW3
	CAIN T,DOLSGN
	JRST ,TRW4
	CAIN T,PRIME
	JRST ,TRP1
	CAIN T,LEFTBR
	AOJA PS,TRO1
	CAIN T,RITEBR
	JRST ,TRB1
	CAIE T,BKSLSH
	CAIN T,EXCLAM
	JRST ,TRR1

TRO1:	PUSHJ P,OUTSUB
TRO2:	IBP ,-5(P)
	JRST, TRO0

TRB1:	JUMPE PS,TRR1
	SOJA PS,TRO1

TRR1:	SUB P,TRR1C1
	POPJ P,

TRR1C1:	XWD 7,7


TRO2A:	HLRZ PS,-3(P)
	JRST ,TRO2


TRP1:	MOVEI Q,1
TRP1A:	LDBI T,-5(P)
	CAIN T,PRIME
	AOJA Q,TRP1A
	CAME Q,PS
	JRST, TRP2
	CAIN T,NUMSGN
	JRST ,TRW3A
	CAIN T,DOLSGN
	JRST ,TRW4A
	PUSHJ P,ERROR
TRP2:	MOVEI C,PRIME
	PUSHJ P,OUTPUT
	SOJG Q,.-1
TRP3:	PUSHJ P,OUTSUB
	ILDB T,-5(P)

TRP3A:	CAIN T,EXCLAM
	JRST ,TRP4
	CAIN T,BKSLSH
	JRST ,TRP5
	CAIL T,DIGIT0
	CAILE T,DIGIT9
	PUSHJ P,ERROR
	JRST ,TRP3

TRP4:	PUSHJ P,OUTSUB
	JRST ,TRO2

TRP5:	PUSHJ P,OUTSUB
	IBP ,-5(P)
	MOVE TS,-5(P)
	MOVE R,-4(P)
	HRRZ C,-1(P)
	PUSHJ P,TRANS
	MOVE T,3(P)
	MOVEM T,-5(P)
	LDB T,-5(P)
	JRST TRP3A

TRW4:	JUMPN PS,TRP3
TRW4A:	HRLM PS,-3(P)
	MOVEM V,-2(P)
TRW4B:	PUSHJ P,TROCT
	HLLZS ,-3(P)
	HRLM C,-1(P)
	CAIN T,BKSLSH
	JRST ,TRD1
	CAIE T,EXCLAM
	PUSHJ P,ERROR

TRD2:	MOVEI PS,-3(P)
	MOVE K,-2(P)
	MOVE V,K
	MOVEM K,FUNCTA
	CAMN P,F27IS
	SETZM ,F27IS
	CAMN P,F26IS
	SETZM ,F26IS
	HLRZ T,-1(P)
	CAIL T,FUNCTA-FUNCT
	JSR ,ERROR1
	PUSHJ P,@FUNCT(T)
	JRST ,TRO2A


TRD1:	CAIE C,36
	JRST ,.+3
	SKIPN ,F26IS
	MOVEM P,F26IS
	CAIE C,37
	JRST ,TRD1A
	SKIPN ,F27IS
	MOVEM P,F27IS
	
TRD1A:	IBP ,-5(P)
	MOVE TS,-5(P)
	HRRZ R,-4(P)
	CLEARM ,Q1
	HRRZ C,-1(P)
	PUSHJ P,TRANS
	MOVE T,3(P)
	MOVEM T,-5(P)
	HLRZ T,-4(P)
	MOVEM T,Q1
	MOVEI C,ENDSYM
	PUSHJ P,OTPTN
	AOS ,-3(P)
	LDB T,-5(P)
	CAIN T,BKSLSH
	JRST ,TRD1A
	JRST ,TRD2


TRW3:	JUMPN PS, TRP3
TRW3A:	HRLM PS,-3(P)
	PUSHJ P,TROCT
	ADD C,-4(P)
	HRLM C,-1(P)
	CAIN T,BKSLSH
	JRST ,TRW5
	CAIE T,EXCLAM
	PUSHJ P,ERROR

TRW6:	IBP ,-5(P)
	MOVE TS,OTP(C)
	JUMPGE TS,.+3
	MOVE C,TS
	MOVE TS,OTP(TS)
	TLO TS,350700
	HLL C,-4(P)
	MOVE R,C
	HRRZ C,-1(P)
	MOVEI PS,
	PUSHJ P,TRANS

TRO3:	HLRZ PS,-3(P)
	MOVE T,-10(P)
	MOVEM T,(P)
	JRST ,TRO0


TRW5:	MOVEM V,-2(P)
	LDBI C,-5(P)
	PUSHJ P,OTPTN
	LDBI T,-5(P)
	CAIE T,LEFTAR
	PUSHJ P,ERROR
	IBP ,-5(P)
	MOVE TS,-5(P)
	HRRZ R,-4(P)
	CLEARM, Q1
	HRRZ C,-1(P)
	PUSHJ P,TRANS
	HLRZ T,-4(P)
	MOVEM T,Q1
	MOVE T,3(P)
	MOVEM T,-5(P)
	MOVEI C,ENDSYM
	PUSHJ P,OTPTN
	MOVE Q,-2(P)
	IBP ,Q
	HLRZ C,(P)
	LDB T,Q
TRW5A:	MOVEM T,TRSUBB(C)
	LDBI T,Q
	CAME Q,V
	AOJA C,TRW5A
	AOS ,(P)
	LDB T,-2(P)
	HRRZ C,(P)
	MOVEM T,TRSUBS(C)
	HLRZ T,(P)
	MOVEM T,TRSUBA(C)

TRW5W:	HRLM C,(P)
	MOVE V,-2(P)
	LDB T,-5(P)
	CAIN T,BKSLSH
	JRST ,TRW5
	HLRZ C,-1(P)
	JRST ,TRW6


TROCT:	MOVEI C,0
	LDBI T,-6(P)
	CAIL T,DIGIT0
	CAILE T,DIGIT9
TROCTR:	POPJ P,
	IMULI C,12
	SUBI T,DIGIT0
	ADD C,T
	JRST ,TROCT+1


OUTDIG:	ADDI C,DIGIT0

OUTPUT:	SKIPE ,Q1
	JRST ,OUCHB
OTPTN:	DPB C,V
	IBP ,V
	CAMN V,OTPXC1
	PUSHJ P,OFLOX
	POPJ P,

OUTSUB:	HRRZ Q,-1(P)
	LDB C,-6(P)
	JUMPE Q,OUTPUT
	CAME C,TRSUBS(Q)
	SOJA Q,.-2
	MOVE Q,TRSUBA(Q)
OUTSU1:	MOVE C,TRSUBB(Q)
	CAIN C,ENDSYM
	POPJ P,
	PUSHJ P,OUTPUT
	AOJA Q,OUTSU1



CRLF:	MOVEI C,15
	PUSHJ P,OUCHB
CRLF1:	MOVEI C,12

OUCHB:	PUSHJ P,LIST
OUCHI:	SKIPN ,TYOS
	JRST ,OUCHP
OUCH:	PUSHJ P,WTYO
	DATAO TTY,C
OUCHP:	SKIPN ,PUNCHS
	JRST ,ASSEM
	PUSHJ P,WPTP
	IORI C,200
	DATAO PTP,C
	ANDCMI C,200
ASSEM:	POPJ P,


PUNCHS:	OCT 0

OTPXC1:	XWD 350700,OTPXE

OUCHQ:	PUSHJ P,OUCHB
	CAIN C,LINEF

OUCHQA:	AOSE ,PAGEPT
	POPJ P,
	MOVNI C,PAGECT
	MOVEM C,PAGEPT
	MOVEI C,FORMF
	PUSHJ P,OUCHB
	MOVEI C,LINEF
	POPJ P,

PAGEPT:	OCT 0

LISTRS:	SKIPN ,LISTS
	JRST ,CRLF

LISTFE:	MOVEI C,CARRET
	PUSHJ P,LIST
	MOVEI C,FORMF

LIST:	SKIPN ,LISTS
	POPJ P,
	ROT C,1
	PUSHJ P,WLPT
	DATAO ANELEX,C
	ROT C,-1
	CAIE C,CARRET
	JRST ,.+4
	MOVEM C,LISTC
	MOVEI C,LINECT+1
	EXCH C,LISTC
	SOSLE ,LISTC
	POPJ P,
	MOVEM C,LISTCC
	MOVEI C,CARRET
	PUSHJ P,LIST
	MOVEI C,LINEF
	PUSHJ P,LIST
	MOVE C,LISTCC
	POPJ P,

LISTS:	OCT 0
LISTC:	OCT 0
LISTCC:	OCT 0


CRLFE:	MOVEI C,15
	PUSHJ P,OUCHE
	MOVEI C,12

OUCHE:	SKIPN ,LISTS
	JRST ,OUCH
	JRST ,LIST


FEED:	SKIPN ,PUNCHS
	POPJ P,
	MOVEI C,0
	PUSHJ P,WPTP
	DATAO PTP,C
	SOJG T,.-2
	POPJ P,


CRLF1C:	MOVNI C,LLENTH
	MOVEM C,BELLCT
CRLFP1:	CAIN Q,OUCHB
	PUSHJ P,CRLF
	CAIE Q,OUCHB
	PUSHJ P,CRLFO1
	JRST ,COMMAQ
CRLFO1:	MOVEI C,CARRET
COUCH1:	PUSHJ P,OUCH1
	MOVEI C,LINEF
OUCH1:	PUSHJ P,LIST
	SKIPN ,READS
	SKIPE ,CARDS
	JRST ,OUCHI
	JRST ,OUCHP

OUCHT:	PUSHJ P,LIST
OUCHO:	PUSHJ P,WTYO
	DATAO TTY,C
	POPJ P,


FUNCT:	XWD 0,ERROR
	XWD 0,FUNCT1
	XWD 0,FUNCT2
	XWD 0,FUNCT3
	XWD 0,FUNCT4
	XWD 0,FUNCT5
	XWD 0,FUNCT6
	XWD 0,ERROR

	XWD 0,FUNCT8
	XWD 0,FUNCT9
	XWD 0,FUNC10
	XWD 0,FUNC11
	XWD 0,FUNC12
	XWD 0,FUNC13
	XWD 0,FUNC14
	XWD 0,FUNC15

	XWD 0,FUNC16
	XWD 0,FUNC17
	XWD 0,FUNC18
	XWD 0,FUNC19
	XWD 0,FUNC20
	XWD 0,FUNC21
	XWD 0,FUNC22
	XWD 0,FUNC23

	XWD 0,FUNC24
	XWD 0,FUNC25
	XWD 0,FUNC26
	XWD 0,FUNC27
	XWD 0,FUNC28
	XWD 0,FUNC29
	XWD 0,FUNC29
	XWD 0,FUNC28

	XWD 0,FUNC32
	XWD 0,FUNC33
	XWD 0,FUNC34
	XWD 0,FUNC35
	XWD 0,FUNC36
	XWD 0,FUNC37
	XWD 0,FUNC38
	XWD 0,FUNC39
	XWD 0,FUNC40

FUNCTA:	OCT 0

FUNC35:	POPJ P,


FUNC36:	TROA Q,1

FUNCT1:	TRZ Q,1
	HRRZ T,(PS)
	JUMPLE T,.+4
	PUSHJ P,TROCT1
	MOVEM AL,2(PS)
	ADD AL,C
	HRRZ T,2(PS)
	TRNN Q,1
	MOVEI C,1
	ADDM C,2(PS)
	TRNE Q,1
	POPJ P,
	JRST ,OUTNUM

FUNCT2:	PUSHJ P,TROCT1
	HRRZ T,2(PS)
	SUB T,C
OUTNUM:	IDIVI T,12
	JUMPE T,OUTDIG
	PUSH P,C
	PUSHJ P,OUTNUM
	POP P,C
	JRST ,OUTDIG

FUNC22:	PUSHJ P,TROCT1
	MOVNS ,C
	HRRM C,F22A
	HRLZI T,400000
F22A:	ROT T,
	JRST ,OUTNUM

FUNC39:	MOVEI C,10
	PUSHJ P,TROC1X
	MOVE T,C
	JRST OUTNUM


OUTNM1:	EXCH J,Q1
	PUSHJ P,OUTNUM
	EXCH J,Q1
	POPJ P,

TROCT1:	MOVEI C,12
TROC1X:	HRRM C,TROC1E
	HRRZ T,(PS)
	SOJL T,TROCTU
	HRRM T,(PS)
	MOVEI C,0
TROC1A:	LDB T,FUNCTA
	CAIL T,DIGIT0
	CAILE T,DIGIT9
	POPJ P,
TROC1E:	IMULI C,1
	SUBI T,DIGIT0
	ADD C,T
	IBP ,FUNCTA
	JRST ,TROC1A

TROCTU:	MOVEI C,1
	MOVEI T,ENDSYM
	POPJ P,

FUNC28:	PUSHJ P,F28CO
F28T0:	ILDB C,FUNCTA
	CAIE C,ENDSYM
	CAMN C,F28C
	POPJ P,
	PUSHJ P,OUTPUT
	JRST ,F28T0

FUNC29:	PUSHJ P,F28CO
F29T0:	ILDB C,FUNCTA
	CAIN C,ENDSYM
	POPJ P,
	CAME C,F28C
	JRST ,F29T0

F29T1:	ILDB C,FUNCTA
	CAIN C,ENDSYM
	POPJ P,
	PUSHJ P,OUTPUT
	JRST ,F29T1

F28CO:	LDB C,FUNCTA
	MOVEM C,F28C
	ILDB C,FUNCTA
	CAIE C,ENDSYM
	JRST ,.-2
	POPJ P,

F28C:	OCT 0

FUNC20:	TLO Q,Q3+Q4
	JRST ,FUNCWP

FUNCT3:	TLOA Q,Q4
FUNCT5:	TLZ Q,Q4
	TLZ Q,Q3

FUNCWP:	HLRZ T,(PS)
	JUMPN T,FUNCWQ
	MOVE T,-2(PS)
	LDBI T,T
	CAIE T,RITEBR

FUNCWQ:	TLZ Q,Q4
	MOVE T,-3(PS)
	MOVEI C,0
	JRST 2,@.+1
	XWD 20000,WIPE2

WIPE2:	DPBI C,T
WIPE1:	CAME T,-2(PS)
	JRST ,WIPE2
	TLNN Q,Q4
	JRST ,WIPE3
	HRRZ T,-3(PS)
	MOVEI C,FREE
	CAME T,(C)
	AOJA C,.-1
	MOVEI T,1
	HRLM T,-1(C)

WIPE3:	TLNE Q,Q3
FUNC21:	SUB P,WIPEC1
FPOPJ:	POPJ P,

WIPEC1:	XWD 10,10

FUNC23:	MOVE T,FUNCTA
	MOVEM T,RKPTR
	PUSHJ P,RLTX1
	JUMPGE C,F23E
	SKIPA C,TRAN(C)

F23A:	MOVE C,(C)
	HLRZ T,(C)
	JUMPN T,F23A
	MOVE R,-1(PS)
	HRRZ TS,(C)
	TLO TS,350700
	MOVEI PS,0
	JRST ,TRANS

F23E:	PUSHJ P,ERROR
	POPJ P,


FUNC34:	SKIPE ,F34IS
	POPJ P,

FUNCT4:	TLZA Q,Q4
FUNCT6:	TLO Q,Q4
	TLZ Q,Q2+Q3+Q5+Q6
	CLEARM ,RISAVE
	MOVEM K,RKPTR
	MOVE K,RFRE
	CLEARM ,RXX
	JUMPL J,.+3
	HRRZ T,(PS)
	CAIG T,1
	JRST 2,@RJJ1
	LDB C,RKPTR
	CAIE C,ENDSYM
	MOVEM C,RXX
	ILDB C,RKPTR
	CAIE C,ENDSYM
	JRST ,.-2
	JRST ,RJJ+1

RJJ1:	XWD 20000,RJJ+1

RJJ:	TLO Q,Q3
	LDBI C,RKPTR
	CAIN C,LESSTN
	JRST ,RLT
	CAIE C,LEFTBR
	CAIN C,LEFTSQ
	JRST ,RLQ
	CAIN C,DQUOTE
	LDBI C,RKPTR

RT1A:	HRRZ T,C

RT1:	TLNN Q,Q3
	JRST ,RT1F
	TLO Q,Q6
RT2:	TLNE Q,Q2
	JRST ,RT9
RT2F:	HLRZ C,(I)
	CAMN C,T
	JRST ,RT3Y
	JUMPE C,RT2AL
	HRRZ C,(I)
	JUMPE C,RT2T
	MOVE I,C
	MOVEM I,RX
	JRST ,RT2F

RT3Y:	JUMPE T,RMM
	AOJA I,RJJ

F34IS:	OCT 0

FUNC40:	SKIPN F34IS
	POPJ P,
	JRST FUNCT4

RT1FB:	MOVE T,C
RT1F:	CAIN T,1
	JRST ,RJJ+1
	MOVE G,T
	HRRZ I,TRAN(T)
	MOVEM I,RX
	JUMPE I,RT1FA
	JUMPL J,RJJ
	TLNE Q,Q4
	JRST ,RJJ
	HRRZM I,RISAVE

RT1FA:	HRRM K,TRAN(T)
	JRST ,RT9A


RT2T:	HRRM K,(I)

RT9:	HRRZ I,K
	MOVEI C,0
	EXCH C,RISAVE
	HRL C,T
	MOVEM C,(I)
	ADDI K,1
	CAILE K,FRETOP
	PUSHJ P,OFLOF

RT9A:	TLO Q,Q2
	TLZ Q,Q4
	JUMPN T,RJJ

RMM:	HRRZ S,(I)
	ADD S,RMMC1
	HRRM K,(I)
	CLEARB I,RIXX
	MOVE C,RKPTR
	MOVEM C,RZZ
	TLNN Q,Q4
	CLEARM ,RXX
	SKIPN ,RXX
	JRST ,RM1
	MOVEM S,RKPTR

RM1:	MOVE T,RTPTRC
	MOVEM T,RTPTR
	MOVEI T,0
	MOVEI G,5


RM2:	LDBI C,RKPTR
	JUMPE C,.-1
	JUMPGE J,RM3A
	CAIN C,DQUOTE
	JRST ,RM20
	HLR C,TRAN(C)
	ANDI C,177
RM3A:	CAIN C,LEFTBR
	AOJA I,RM3
	CAIN C,RITEBR
	JRST ,RM4

RM3:	TLNN Q,Q5
	DPBI C,RTPTR
	CAMN C,RXX
	JRST ,RM9
	TLNE Q,Q5
	JRST ,RM2
RM3B:	SOJG G,RM2
	MOVEM T,(K)
	CAIL K,FRETOP
	PUSHJ P,OFLOF
	AOJA K,RM1

RM9:	TLCE Q,Q5
	JRST ,RM8
	MOVE S,RZZ
	EXCH S,RKPTR
	MOVEM I,RIXX
	MOVEI I,0
	JRST ,RM3B

RM8:	CLEARM ,RXX
	JRST ,RM2

RM4:	SOJGE I,RM3
	TLZN Q,Q4
	JRST ,RM5
	MOVEM S,RKPTR
	MOVE I,RIXX
	JRST ,RM2

RM5:	DPBI C,RTPTR
	MOVEM T,(K)
	AOJ K,
	CAILE K,FRETOP
	PUSHJ P,OFLOF
	MOVEM K,RFRE
	SKIPE ,READS
	JUMPL J,.+2
	JRST ,DING
	POPJ P,


RT2AL:	MOVEM I,RISAVE
	MOVE I,RX

RT2AM:	HRRM K,(I)
	JRST ,RT9

RKPTR:	OCT 0
RTPTR:	OCT 0
RTPTRC:	XWD 10700,T-1
RFRE:	OCT 0
RISAVE:	OCT 0
RXX:	OCT 0
RIXX:	OCT 0
RZZ:	OCT 0
RMMC1:	XWD 10677,-1
RX:	OCT 0


RLQ:	TLNN Q,Q3
	JRST ,RT1FB
	TLNE Q,Q6
	CAIN G,GOAL
	JRST ,RLQ0
	EXCH G,T
	PUSHJ P,ADDBIT

RLQ0:	MOVEI T,0
	JRST ,RT2


RM20:	LDBI C,RKPTR
	JRST ,RM3


F2ARG:	TLZA Q,Q4
	IBP ,FUNCTA
	PUSHJ P,TROCT1
	TLNN Q,Q4
	MOVE S,C
	CAIE T,ENDSYM
	PUSHJ P,ERROR
	TLON Q,Q4
	JRST ,F2ARG+1
	POPJ P,


RLTX1:	JRST 2,@.+1
	XWD 20000,RLTX

RLTX:	MOVEI T,0
	JRST ,RLT2

RLT1:	TRC C,40
	ROT C,-6
	ROTC T,6

RLT2:	LDBI C,RKPTR
	CAIN C,SPACE
	JRST ,.-2
	JUMPE T,RLT1
	CAIE C,GREATN
	CAIN C,ENDSYM
	JRST ,RLTE
	JRST ,RLT1

RLTE:	HRLO C,NMV
	EQVI C,177
	CAME T,NAME-177(C)
	AOBJN C,.-1
	POPJ P,

RLT:	PUSHJ P,RLTX
	JUMPL C,RT1A

RLTF:	MOVE S,T
	MOVEI T,1
	ADDB T,NMV
	CAILE T,MMV
	PUSHJ P,OFLOM
	ADDI T,177
	MOVEM S,NAME-177(T)
	HLLZS ,TRAN(T)
	MOVE S,SUCCR-1(T)
	IBP ,S
	MOVEM S,SUCCR(T)
	MOVEI S,@S
	CAIN S,@SUCCR-1(T)
	JRST ,RT1
	MOVEI S,MMV-1
	ADDB S,SUCCR(T)
	ANDI S,-1
	CAILE S,SUCCRT-MMV
	PUSHJ P,OFLOS
	JRST ,RT1


TAPE,
SUBTTL PART 3, MAR 9, 1965

;PDP-6 SYNTAX-DIRECTED COMPILER  P. SAMSON


ADDBIT:	LDB S,SUCCR(T)
	JUMPN S,ADDBP
	MOVEI S,1
	DPB S,SUCCR(T)
	HRLM C,(P)
	CAIGE T,177
	JRST ,ADDB1
	MOVEI C,0
	EXCH G,T
	JRST ,ADDB2+1

ADDB2:	EXCH G,C
	LDB S,SUCCR(C)
	JUMPE S,ADDB3
	EXCH C,T
	EXCH C,G
	PUSHJ P,ADDBIT
	EXCH T,G
	JRST ,ADDB3+1

ADDB3:	EXCH C,G
	CAIL G,177
	JRST ,ADDB4

ADDB6:	MOVE S,NMV
	CAIGE G,177(S)
	AOJA G,ADDB5
	MOVE G,T
	MOVE T,C
	HLRZ C,(P)
ADDBP:	POPJ P,

ADDB5:	CAIL C,177
	JRST ,ADDB2

ADDB4:	LDB S,SUCCR(T)
	JUMPE S,ADDB6
	EXCH C,T
	PUSHJ P,ADDBIT
	EXCH C,T
	JRST ,ADDB6

ADDB1:	EXCH G,T
	EXCH C,G
	MOVEI G,177
	JRST ,ADDB4


GULP:	
GULPA:	MOVE S,GLOBC0
	CLEARB J,PASS1
	SETOM ,GULPDS
	SETZM GULPRS
	CLEARB I,GULPMS

GULP3:	MOVEM S,GULPT
GULP2:	PUSHJ P,INCH
	SKIPE AFES
	JRST GULP20
	SKIPN ,GULPRS
	SKIPN ,FFES
	JRST ,GULP5
	CAIN C,134
	SETOM ,GULPMS
	CAIN C,134
	JRST ,GULP2
	JUMPL I,GULP5
GULP22:	CAIE C,SPACE
	CAIN C,11
	JRST ,GULP2B

GULP5:	LDB T,GULPP2
	JUMPN T,GULP1
	CAIN C,RUBOUT
	JRST ,GULP3
	CLEARM ,GULPDS
	CAIN C,72
	JRST GULP2C
GULP51:	ADDI J,1
	MOVE S,GULPT
GULP50:	SKIPE FFES
	SKIPE ,GULPRS
	JRST ,GULP5A
	JUMPL I,GULP5A
	MOVEI T,QMARK
	CAIE C,RITESQ
	CAIN C,51
	DPBI T,GULPT
	CAIN C,54
	DPBI T,GULPT
GULP5A:	DPBI C,GULPT
	MOVEM C,OLDCHR
	SKIPE FFES
	SKIPE GULPRS
	JRST GULP2
	JUMPN I,GULPH3

GULP7:	CAIL C,DIGIT0
	CAILE C,DIGIT9
	JRST ,GULP4
	IMULI I,12
	ADD I,C
	SUBI I,DIGIT0
	JRST ,GULP2

GULP20:	CAIE C,CARRET
	CAIN C,LINEF
	JRST GULP21
	JRST GULP22

GULP21:	MOVEI I,0
	CAIE C,CARET
	MOVEI C,INVLIF-INVCAR+CARRET
	ADDI C,INVCAR-CARRET
	JRST GULP23


GULP4:	MOVNI I,1
GULPH2:	AOJA I,GULP2


GULPH3:	JUMPL I,GULPH2
	CAIE C,110
	JRST ,GULP7
GULPH:	MOVNS ,I
	JRST GULP2


GULP2A:	SETOM ,GULPDS
	JRST ,GULP2

GULP1:	SKIPN ,GULPDS
	AOSG ,GULPMS
	JRST ,GULP2A
	MOVE T,C
	JUMPGE I,GULP9
	MOVEI C,SPACE
	CAIL I,10
	MOVEI I,10
	DPBI C,GULPT
	SOJG I,.-1

GULP9:	MOVEI C,QMARK
	DPBI C,GULPT
	IDPB C,GULPT
	MOVEI C,CARRET
	DPBI C,GULPT
	MOVEI C,LINEF
	DPBI C,GULPT
	SKIPN ,READS
	SKIPE ,CARDS
	JRST ,COPYO
	MOVEI C,CARRET
	CAIE T,CARRET
	PUSHJ P,OUCH
	PUSHJ P,CRLF1


COPYO:	PUSHJ P,COP
	SKIPE PASS1
	POPJ P,
	JUMPE J,GULPA
	SKIPE ,GULPRS
	JRST ,GULPA
	POPJ P,

COP:	SKIPN ,SPEAKS
	POPJ P,
	PUSHJ P,COMMAQ
	SKIPA Q,COUCH1

COPA:	MOVEI Q,OUCHB
	MOVE S,GLOBC0
	MOVE T,S
	MOVNI C,LLENTH
	MOVEM C,BELLCT

COPYO1:	LDBI C,S
	CAIE C,INVSPC
	CAIN C,INVTAB
	JRST ,COPYO2
	CAIE C,INVLIF
	CAIN C,INVCAR
	JRST COPYO9
	DPBI C,T
	CAIN Q,OUCHB
	JRST ,COPYO3
	CAIN C,QMARK
	SKIPN ,FFES
	JRST ,COPYO3
	JRST ,COPYO4



COPYO9:	CAIE C,INVCAR
	MOVEI C,LINEF+INVCAR-CARRET
	SUBI C,INVCAR-CARRET+11-INVTAB
COPYO2:	CAIE C,INVSPC
	ADDI C,INVSPC-SPACE+11-INVTAB
	SUBI C,INVSPC-SPACE

COPYO3:	PUSHJ P,(Q)
	AOSL ,BELLCT
	PUSHJ P,CRLF1C
COPYO4:	CAME S,GULPT
	JRST ,COPYO1
	MOVEM T,GULPT
	POPJ P,

GULP2D:	CAIE J,1
	JRST GULP51
	MOVE T,OLDCHR
	CAIN T,103
	JRST GULP2W
	CAIE T,123
	JRST GULP51
	SETOM PASS1
	SETOM GULPRS
	JRST GULP51

OLDCHR:	OCT 0


GULP2B:	MOVEI I,0
	CAIE C,SPACE
	MOVEI C,INVTAB-INVSPC+SPACE
	ADDI C,INVSPC-SPACE
GULP23:	SKIPE ,SPEAKS
	JRST ,GULP5
	JRST ,GULP2

GULP2C:	SKIPE AFES
	JRST GULP51
	JUMPN J,GULP2D
GULP2W:	SETOM ,GULPRS
	JRST ,GULP2

GULPRS:	OCT 0
GULPMS:	OCT 0
GULPDS:	OCT 0
GULPT:	OCT 0
GULPP1:	XWD 320100+C,TRAN
GULPP2:	XWD 310100+C,TRAN
BELLCT:	OCT 0

COMMAQ:	SKIPG ,J
	POPJ P,
COMMQ1:	MOVEI C,73
	CAIN Q,OUCHB
	JRST ,OUCHB
	JRST ,OUCH1


MESSAG:	HRRZ T,(P)
	ADD T,MESSC1

MESSAX:	LDBI C,T
	JUMPE C,MESSR
	PUSHJ P,OUCHT
	JRST ,MESSAX

MESSR:	ADDI T,1
	HRRM T,(P)
	POPJ P,


MESSC1:	XWD 10677,-1


FUNCT8:	PUSHJ P,F2ARG
	CAILE S,NUNIV
	JSR ,ERROR1
	MOVEM C,UNIV(S)
	POPJ P,

FUNCT9:	PUSHJ P,TROCT1
	CAILE C,NUNIV
	PUSHJ P,ERROR
	MOVE T,UNIV(C)
	JRST ,OUTNUM

FUNC10:	PUSHJ P,F2ARG
	ADD S,C
	MOVE T,S
	JRST ,OUTNUM

FUNC11:	PUSHJ P,F2ARG
	SUB S,C
	MOVE T,S
	JRST ,OUTNUM

FUNC37:	PUSHJ P,F2ARG
	IMUL S,C
	MOVE T,S
	JRST OUTNUM

FUNC38:	SKIPN F34IS
	POPJ P,
	LDB T,FUNCTA
	JRST FUOUT+1

FUNC12:	PUSHJ P,F2ARG
	CAML S,C
	POPJ P,
	JRST ,FUOUT

FUNC13:	PUSHJ P,F2ARG
	CAME S,C
	POPJ P,

FUOUT:	LDBI T,FUNCTA
	CAIN T,ENDSYM
	POPJ P,
	MOVE C,T
	PUSHJ P,OUTPUT
	JRST ,FUOUT

FUNC14:	PUSHJ P,F2ARG
	CAMLE S,C
	POPJ P,
	JRST ,FUOUT

FUNC15:	PUSHJ P,F2ARG
	ADDM C,UNIV(S)
	POPJ P,

FUNC16:	PUSHJ P,F2ARG
	SUB C,UNIV(S)
	MOVNM C,UNIV(S)
	POPJ P,


FUNC17:	PUSHJ P,F2ARG
	CAMN S,C
	POPJ P,
	JRST ,FUOUT

FUNC18:	PUSHJ P,STREQ
	JUMPE S,FUOUT
	POPJ P,

FUNC19:	PUSHJ P,STREQ
	JUMPN S,FUOUT
	POPJ P,



STREQ:	MOVEI S,1
	MOVE T,FUNCTA
	MOVEM T,STREQA

STREQ1:	LDB T,FUNCTA
	IBP ,FUNCTA
	CAIE T,ENDSYM
	JRST ,STREQ1

STREQ2:	LDB C,STREQA
	LDB T,FUNCTA
	IBP ,STREQA
	CAIN T,ENDSYM
	JRST ,STREQ4
	IBP, FUNCTA
	CAMN T,C
	JRST ,STREQ2

STREQ5:	LDB T,FUNCTA
	CAIN T,ENDSYM
	POPJ P,
	IBP ,FUNCTA
	JRST ,STREQ5

STREQ4:	CAMN T,C
	MOVEI S,0
	POPJ P,

STREQA:	OCT 0


OFLOF:	PUSHJ P,MESSAG
	ASCII .
RULE STORAGE.
	JRST ,OFLO

OFLOS:	PUSHJ P,MESSAG
	ASCII .
SUCCESSOR TABLE.
	JRST ,OFLO

OFLOM:	PUSHJ P,MESSAG
	ASCII .
META-VARIABLE TABLE.

OFLO:	PUSHJ P,MESSAG
	ASCII . OVERFLOW
.
	JRST 4,KILLR


OFLOP:	PUSHJ P,MESSAG
	ASCII .
PUSHDOWN DEPTH EXCESSIVE
.
	JRST 4,START

OFLOX:	PUSHJ P,MESSAG
	ASCII .
ARGUMENT STORAGE OVERFLOW
.
	JRST 4,START


OFLOY:	PUSHJ P,MESSAG
	ASCII .PARSE TABLE OVERFLOW
.
	JRST 4,START

OFLOA:	PUSHJ P,MESSAG
	ASCII .ASSEMBLER SYMBOL TABLE OVERFLOW
.
	JRST 4,START

INCH:	SKIPE ,READS
	JRST ,INTAPE
	SKIPE ,CARDS
	JRST ,CARD
	PUSHJ P,WTYI
	DATAI TTY,C
	TRZ C,200
	JRST ,OUCHO

FUNC24:	PUSHJ P,TROCT1
	MOVE C,UNIV(C)
	SKIPL C
	CAILE C,NUNIVS
	JSR ,ERROR1
	MOVE S,C
	ASH S,1
	IMULI C,MUNIVS
	ADD C,F24C1
	MOVEM C,F24P
	MOVNI C,NUNIVC
	MOVEI T,ENDSYM
	SKIPE ,C
	LDBI T,FUNCTA
F24E:	IDPB T,F24P
	CAIE T,ENDSYM
	AOJLE C,.-3
	MOVEM C,UNIVSP+1(S)
	MOVE C,F24P
	MOVEM C,UNIVSP(S)
F24POP:	POPJ P,

F24C1:	XWD 700,UNIVS-1
F24P:	OCT 0


FUNC33:	PUSHJ P,TROCT1
	MOVE C,UNIV(C)
	SKIPL C
	CAILE C,NUNIVS
	JSR ,ERROR1
	MOVE S,C
	ASH S,1
	MOVE C,UNIVSP(S)
	MOVEM C,F24P
	MOVE C,UNIVSP+1(S)
	JUMPE C,F24POP
	ILDB T,FUNCTA
	JRST 2,@.+1
	XWD 20000,F24E


FUNC25:	PUSHJ P,TROCT1
	MOVE C,UNIV(C)
	SKIPL C
	CAILE C,NUNIVS
	PUSHJ P,ERROR
	IMULI C,MUNIVS
	ADD C,F24C1
	SKIPA T,C
	PUSHJ P,OUTPUT
	ILDB C,T
	CAIE C,ENDSYM
	JRST ,.-3
	POPJ P,


INTP1:	CONO PTR,20
INTAPE:	PUSHJ P,WPTR
	CONSO PTR,10
	JRST ,INTP1
	DATAI PTR,C
	TRZ C,200
	CAIN C,177
	JRST, INTAPE
	JUMPE C,INTAPE
	POPJ P,

READS:	OCT 0
SPEAKS:	OCT 0

FUNC26:	SKIPE ,F26IS
	POPJ P,
	PUSHJ P,F2ARG
	CAILE C,NUNIV
	PUSHJ P,ERROR
	CAILE S,NUNIV
	PUSHJ P,ERROR
	MOVE T,UNIV(C)
	AOS C,UNIV(S)
	CAILE C,NUPP
	PUSHJ P,ERROR
	MOVEM T,UPP(C)
	POPJ P,

FUNC27:	SKIPE ,F27IS
	POPJ P,
	PUSHJ P,F2ARG
	CAILE C,NUNIV
	PUSHJ P,ERROR
	CAILE S,NUNIV
	PUSHJ P,ERROR
	SOS T,UNIV(S)
	MOVE S,UPP+1(T)
	MOVEM S,UNIV(C)
	POPJ P,

F26IS:	OCT 0
F27IS:	OCT 0




FUNC32:	MOVE T,F32P
	MOVEM T,F32R
	MOVNI S,2
	SETOM ,F34IS
	JRST ,F32C

F32A:	MOVEM T,F32T
F32B:	IBP ,FUNCTA
F32C:	LDB C,FUNCTA
	IDPB C,T
	CAIE C,ENDSYM
	JRST ,F32B
	AOJL S,F32A

F32W:	ILDB T,F32R
	MOVEM T,F32Q

F32V:	ILDB C,FUNCTA
	CAIN C,ENDSYM
	POPJ P,
	CAMN C,F32Q
	JRST ,F32E

F32U:	PUSHJ P,OUTPUT
	JRST ,F32V

F32E:	MOVE S,FUNCTA
	MOVEM S,F32Z
	MOVE T,F32R

F32J:	ILDB C,T
	CAIN C,ENDSYM
	JRST ,F32K
	ILDB S,FUNCTA
	CAMN S,C
	JRST ,F32J
	MOVE S,F32Z
	MOVEM S,FUNCTA
	LDB C,S
	JRST ,F32U

F32L:	PUSHJ P,OUTPUT
F32K:	ILDB C,F32T
	CAIE C,ENDSYM
	JRST ,F32L
	CLEARM ,F32Q
	SETZM ,F34IS
	JRST ,F32V

F32P:	XWD 10700,F32BU-1
F32R:	OCT 0
F32T:	OCT 0
F32Q:	OCT 0
F32Z:	OCT 0
F32BU:	BLOCK 20


WAIT:
WCR:	CONSO CR,10
	JRST ,WCR
	POPJ P,

WCR1:	CONSZ CR,400
	CONSZ CR,4020
	JRST ,WCR1
	POPJ P,

WTYO:	CONSZ TTY,20
	JRST ,WTYO
	POPJ P,

WTYI:	CONSO TTY,40
	JRST ,WTYI
	POPJ P,

WPTP:	CONSZ PTP,20
	JRST ,WPTP
	POPJ P,

WLPT:	CONSZ ANELEX,200
	JRST ,WLPT
	POPJ P,

WPTR:	CONSZ PTR,20
	JRST ,WPTR
	POPJ P,


INOUTI:	MOVE 0,INOUT1
	MOVEM 0,40+APRCHN+APRCHN
	CLEARM ,APRPT
	CLEARM ,APRIOT
	CONO ANELEX,10*APRCHN+100
	CONO 0,633440+APRCHN
	CONO PRS,12202
	CLEARM ,CARDBX
	MOVNI C,PAGECT
	MOVEM C,PAGEPT
	MOVEI C,LINECT
	MOVEM C,LISTC
	POPJ P,

INOUT1:	JSR ,APRBK


APRBK:	0
	CONSO 0,1000
	JRST ,APRBKA
	CONO 0,1000+APRCHN
	HRRZ 0,APRBK
	CAIL 0,WAIT
	CAIL 0,INOUTI
	AOSA ,APRPT
	AOS ,APRIOT
	JRST 12,@APRBK

APRBKA:	CONSO 0,200000
	JRST ,APRBKB
	MOVEI 0,OFLOP
	HRRM 0,APRBK
	CONO 0,400000+APRCHN
	JRST 12,@APRBK

APRBKB:	CONSO 0,30000
	JRST ,APRBKC
	JRST 4,.+1
	CONO 0,633440+APRCHN
	JRST 12,@APRBK

APRBKC:	CONSZ ANELEX,1400
	PUSHJ P,LISTFE
	JRST 12,@APRBK

APRPT:	OCT 0
APRIOT:	OCT 0

STATS:	CLEARM ,PUNCHS
	CLEARM ,ASSMS
	PUSHJ P,MESSAG
	ASCII /
I-O WAIT: /
	SETOM ,Q1
	SETOM ,TYOS
	MOVE T,APRIOT
	PUSHJ P,STATSN
	PUSHJ P,MESSAG
	ASCII / SEC.
OTHER:    /
	MOVE T,APRPT
	PUSHJ P,STATSN
	JRST ,CRLF


STATSN:	ADDI T,3
	IDIVI T,74
	MOVEM C,STATSX
	PUSHJ P,OUTNUM
	MOVEI C,56
	PUSHJ P,OUCHB
	MOVE T,STATSX
	IDIVI T,6
	JRST ,OUTNUM

STATSX:	OCT 0


GULPS:	PUSHJ P,INOUTI
	JRST ,GULPSC

GULPS0:	JRST 2,@.+1
	XWD 20000,GULPS1

GULPS4:	PUSHJ P,INCH
	CAIN C,XOFF
	POPJ P,
	CAIE C,RITESQ
	JRST ,GULPS4
	JRST ,GULPSC

GULPS1:	PUSHJ P,INCH
GULPS9:	CAIE C,DQUOTE
	JRST ,GULPS2
	DPBI C,K
	PUSHJ P,INCH
	JRST ,GULPS3

GULPS2:	JUMPN T,.+3
	CAIN C,LEFTSQ
	JRST ,GULPS4
	CAIN C,FORMF
	JRST ,GULPS1
	CAIN C,LEFTSQ
	AOS ,GULPSQ
	CAIN C,RITESQ
	SKIPG ,GULPSQ
	JRST ,GULPS3
	SOSN ,GULPSQ
	JRST ,GULPSB

GULPS3:	CAIN C,XOFF
	POPJ P,
	CAIN C,RUBOUT
	JRST ,GULPS0
	DPBI C,K
	AOJA T,GULPS1


GULPSB:	DPBI C,K
	MOVE K,GLOBC2
	PUSHJ P,FUNCT4
GULPSC:	PUSHJ P,INCH
	CAIN C,XOFF
	POPJ P,
	CAIL C,41
	CAIL C,140
	JRST ,GULPSC
	CLEARB T,GULPSQ
	MOVE K,GLOBC2
	JRST 2,@.+1
	XWD 20000,GULPS9

GULPSQ:	OCT 0


ERROR:	PUSHJ P,ERRORX
	HRRZ T,(P)
ERRORE:	SUBI T,1
	PUSHJ P,OUTNM1
	MOVE T,ERRORT
	MOVE C,ERRORC
	POPJ P,

ERROR1:	0
	PUSHJ P,ERRORX
	HRRZ T,ERROR1
	JRST ,ERRORE

ERRORX:	MOVEM T,ERRORT
	MOVEM C,ERRORC
	PUSHJ P,MESSAG
	ASCII .
SYNTAX RULES ERROR AT .
	POPJ P,

ERRORC:	OCT 0
ERRORT:	OCT 0

TAPE,
SUBTTL PART 4, FEB 27, 1965

;PDP-6 SYNTAX-DIRECTED COMPILER  P. SAMSON

CARD:	LDBI C,CARDP1
	SKIPE ,CARDBX
	JRST ,CARDQ
	PUSHJ P,CARDX
	CLEARM ,CARDCX
	PUSHJ P,WCR1
	CONO CR,120

CARD1:	SKIPGE ,CARDBX
	JRST ,CARD1X
	PUSHJ P,WCR
	DATAI CR,C
CARD6:	SKIPE ,CARDRS
	JRST ,.+3
	CAIN T,5
	AOJA T,CARD1A
	HLRZ C,TRANMV(C)
	CAIE C,SPACE
	CAIGE T,6
	JRST ,CARD5
	CAME C,CARDCT(K)
	TRZA K,-1
	ADDI K,1

CARD5:	CAIN C,SPACE
	JRST ,CARD4
	CAIL C,DIGIT0
	CAILE C,DIGIT9
	JRST ,CARD3
	JUMPN I,CARD4
	TROA I,1

CARD3:	MOVNI I,1
	JUMPN T,CARD4
	CAIE C,52
	CAIN C,103
	MOVEI C,0
	SKIPN ,C
	SETOM ,CARDRS

CARD4:	AOJA T,CARD2


CARD2:	CAIL T,111
	JRST ,CARD7
	JUMPE C,CARD2A
	DPBI C,CARDP2
	CAIL C,LETERA
	CAILE C,LETERZ
	JRST ,CARD1
	CAIE T,1
	JRST ,CARD1

CARD2A:	MOVEI C,72
	IDPB C,CARDP2
	JRST CARD1


CARD7:	MOVEI C,CARRET
	DPBI C,CARDP2
	MOVEM C,CARDBX
	CAIE K,4
	JRST ,CARD8

CARDV:	MOVE K,CARDK
	EXCH T,CARDC
	EXCH I,CARDI
	JRST ,CARD

CARDQ:	SKIPE ,CARDCX
	JRST ,CARDN
	CAIE C,CARRET
	POPJ P,
	SKIPGE ,CARDBX
	JRST ,.+3
	CLEARM ,CARDBX
	POPJ P,
	LDB C,CARD8R
	CAIN C,63
	JRST .+4
	LDB C,CARD8P
	CAIE C,20
	JRST ,CARD1Y
	MOVEI C,CARRET
	MOVEM C,CARDCX
	POPJ P,


CARD8:	PUSHJ P,WCR1
	CONO CR,20
	MOVE C,CARD8P
	MOVEI T,0
	MOVEM C,CARDXP

CARD8A:	PUSHJ P,WCR
	DATAI CR,C
	MOVEM C,CARDIB(T)
	CONSO CR,400
	AOJA T, CARD8A
	SETOM ,CARDBX
	JRST ,CARDV

CARD1A:	JUMPLE I,CARD1
	MOVEI C,72
	DPBI C,CARDP2
	JRST ,CARD1

CARD1Y:	PUSHJ P,CARDX
	MOVEI T,6

CARD1X:	LDBI C,CARDXP
	JRST ,CARD6


CARDN:	CLEARM ,CARDCX
	PUSHJ P,CARDX
	MOVE C,CARD8Q
	MOVEM C,CARDXP
	JRST ,CARD1X


CARDCT:	OCT 40
	 105
	 116
	 104
	 40


CARDXP:	OCT 0
CARDYP:	OCT 0
CARD8P:	XWD 600,CARDIB
CARD8Q:	XWD 600,CARDIB-1
CARD8R:	XWD 360600,CARDIB

CARDC:	OCT 0
CARDP1:	OCT 0
CARDP2:	OCT 0
CARDS:	OCT 0
CARDC1:	XWD 010700,CARDIT-1
CARDI:	OCT 0
CARDBX:	OCT 0
CARDCX:	OCT 0
	CARDRS:	OCT 0
CARDK:	OCT 0

CARDX:	MOVEM T,CARDC
	MOVEM I,CARDI
	MOVEM K,CARDK
	MOVE C,CARDC1
	MOVEM C,CARDP1
	MOVEM C,CARDP2
	CLEARM ,CARDRS
	CLEARB T,I
	MOVEI K,1
	POPJ P,


TRAN:	XWD 0,0
	XWD 01001,0
	XWD 02002,0
	XWD 3003,0
	XWD 4004,0
	XWD 5005,0
	XWD 6006,0
	XWD 7007,0

	XWD 10010,0
	XWD 11011,0
	XWD 12012,0
	XWD 13013,0
	XWD 14014,0
	XWD 15015,0
	XWD 16016,0
	XWD 17017,0
	XWD 20020,0
	XWD 21021,0
	XWD 22022,0
	XWD 23223,0
	XWD 24024,0
	XWD 25025,0
	XWD 26026,0
	XWD 27027,0

	XWD 30030,0
	XWD 31031,0
	XWD 32032,0
	XWD 33033,0
	XWD 34034,0
	XWD 35035,0
	XWD 36036,0
	XWD 37037,0


	XWD 40040,0
	XWD 41000+EXCLAM,0
	XWD 42042,0
	XWD 43000+NUMSGN,0
	XWD 44000+DOLSGN,0
	XWD 45045,0
	XWD 46046,0
	XWD 47000+PRIME,0

	XWD 50450,0
	XWD 51451,0
	XWD 52452,0
	XWD 53453,0
	XWD 54454,0
	XWD 55455,0
	XWD 56056,0
	XWD 57457,0

	XWD 60060,0
	XWD 61061,0
	XWD 62062,0
	XWD 63063,0
	XWD 64064,0
	XWD 65065,0
	XWD 66066,0
	XWD 67067,0

	XWD 70070,0
	XWD 71071,0
	XWD 72072,0
	XWD 73073,0
	XWD 74074,0
	XWD 75475,0
	XWD 76076,0
	XWD 77077,0


	XWD 100100,0
	XWD 101101,0
	XWD 102102,0
	XWD 103103,0
	XWD 104104,0
	XWD 105105,0
	XWD 106106,0
	XWD 107107,0

	XWD 110110,0
	XWD 111111,0
	XWD 112112,0
	XWD 113113,0
	XWD 114114,0
	XWD 115115,0
	XWD 116116,0
	XWD 117117,0

	XWD 120120,0
	XWD 121121,0
	XWD 122122,0
	XWD 123123,0
	XWD 124124,0
	XWD 125125,0
	XWD 126126,0
	XWD 127127,0

	XWD 130130,0
	XWD 131131,0
	XWD 132132,0
	XWD 133400+LEFTBR,0
	XWD 134000+BKSLSH,0
	XWD 135400+RITEBR,0
	XWD 136536,0
	XWD 137400+LEFTAR,0


	XWD 43140,0
	XWD 44141,0
	XWD 133142,0
	XWD 135143,0
	XWD 47144,0
	XWD 41145,0
	XWD 134146,0
	XWD 137147,0

	XWD 150150,0
	XWD 151151,0
	XWD 152152,0
	XWD 153153,0
	XWD 154154,0
	XWD 155155,0
	XWD 156156,0
	XWD 157157,0

	XWD 160160,0
	XWD 161161,0
	XWD 162162,0
	XWD 163163,0
	XWD 164164,0
	XWD 165165,0
	XWD 166166,0
	XWD 167167,0

	XWD 170170,0
	XWD 171171,0
	XWD 172172,0
	XWD 173713,0
	XWD 174174,0
	XWD 175175,0
	XWD 176176,0


TRANMV:	XWD 40,0
	XWD 61,0
	XWD 62,0
	XWD 63,0
	XWD 64,0
	XWD 65,0
	XWD 66,0
	XWD 67,0

	XWD 70,0
	XWD 71,0
	XWD 60,0
	XWD 75,0
	XWD 47,0
	XWD 136,0
	XWD 100,0
	XWD 134,0

	XWD 40,0
	XWD 57,0
	XWD 123,0
	XWD 124,0
	XWD 125,0
	XWD 126,0
	XWD 127,0
	XWD 130,0

	XWD 131,0
	XWD 132,0
	XWD 73,0
	XWD 54,0
	XWD 50,0
	XWD 42,0
	XWD 43,0
	XWD 45,0


	XWD 55,0
	XWD 112,0
	XWD 113,0
	XWD 114,0
	XWD 115,0
	XWD 116,0
	XWD 117,0
	XWD 120,0

	XWD 121,0
	XWD 122,0
	XWD 72,0
	XWD 44,0
	XWD 52,0
	XWD 133,0
	XWD 76,0
	XWD 46,0

	XWD 53,0
	XWD 101,0
	XWD 102,0
	XWD 103,0
	XWD 104,0
	XWD 105,0
	XWD 106,0
	XWD 107,0

	XWD 110,0
	XWD 111,0
	XWD 77,0
	XWD 56,0
	XWD 51,0
	XWD 135,0
	XWD 74,0
	XWD 41,0


	BLOCK MMV-77
NMV:	OCT 0
UNIV:	BLOCK NUNIV+1
UNIVSP:	BLOCK NUNIVS*2
UNIVS:	BLOCK NUNIVS*MUNIVS
TRSUBA:	BLOCK 20
TRSUBB:	BLOCK 20
TRSUBS:	BLOCK 40
OTP:	BLOCK 2000
OTPE:
OTPX:	BLOCK 400
INPUT:	BLOCK 300
OTPXE:
CARDIT:	BLOCK 20
CARDIB:	BLOCK 20
NAME:	OCT 1
	BLOCK MMV+1

DEFINE SB LE,NU
INDEX=430000
REPEAT LE,[
XWD INDEX+100+G,SUCCR!NU
INDEX=INDEX-10000]
INDEX=-1
TERMIN

SUCCR:	SB 44,1
	SB 44,2
	SB 44,3
	SB 24,4



SUCCMV:	BLOCK MMV-176
SUCCR1:	BLOCK MMV
SUCCR2:	BLOCK MMV
SUCCR3:	BLOCK MMV
SUCCR4:	BLOCK MMV+MMV+MMV+MMV+MMV+177
SUCCRT:	

PDL:	BLOCK PDLL
UPP:	BLOCK NUPP
FREE:	BLOCK 12000
FRETOP:
THEEND:	OCT 0

END KILLR
aaaaÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü ÿÿü 